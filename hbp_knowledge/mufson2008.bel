#######################
# Document Properties #
#######################

SET DOCUMENT Name = "Cholinergic system during the progression of Alzheimer’s disease: therapeutic implications"
SET DOCUMENT Version = "1.0.0"
SET DOCUMENT Description = "This file encodes the article Cholinergic system during the progression of Alzheimer’s disease: therapeutic implications by Mufson, 2008"
SET DOCUMENT Copyright = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Authors = "Rana Aldisi"
SET DOCUMENT Licenses = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT ContactInfo = "rana.al.disi@scai.fraunhofer.de"

###############
# Definitions #
###############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------

DEFINE NAMESPACE MESH  AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh.belns"
DEFINE NAMESPACE CHEBI AS URL "https://raw.githubusercontent.com/pharmacome/terminology/1b20f0637c395f8aa89c2e2e342d7b704062c242/external/chebi-names.belns"
DEFINE NAMESPACE HGNC  AS URL "https://raw.githubusercontent.com/pharmacome/terminology/1b20f0637c395f8aa89c2e2e342d7b704062c242/external/hgnc-symbols.belns"
DEFINE NAMESPACE GO    AS URL "https://raw.githubusercontent.com/pharmacome/terminology/1b20f0637c395f8aa89c2e2e342d7b704062c242/external/go.belns"
DEFINE NAMESPACE HBP   AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX  AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/1b7e14ec0fd02ee7ed71514c6e267f57d5641a4b/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel
# --------------------------------------------------------------------
DEFINE NAMESPACE GFAM  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-gene-families/hgnc-gene-families-20170710.belns"
DEFINE NAMESPACE MESHC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-chemicals/mesh-chemicals-20170725.belns"
DEFINE NAMESPACE MGI   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170725.belns"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION CellLine AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION Disease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION MeSHAnatomy AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"

# Annotations defined as lists
# ----------------------------

DEFINE ANNOTATION Species AS PATTERN "\d+"

#####################################################################################
#Statements
#####################################################################################

SET Citation={"PubMed","18986241"}

SET Evidence="Alzheimer’s disease (AD) is a progressive and
fatal neurodegenerative disorder manifested by
cognitive and memory decline and progressive
impairment of activities of daily living, as well
as a variety of neuropsychiatric symptoms and
behavioral dysfunctions."

#D000544 = Alzheimer Disease, D019636 = Neurodegenerative Diseases
path(MESH:D000544) -- path(MESH:D019636)
#D008568 = Memory
path(MESH:D000544) -- path(MESH:D008568)
#D003071 = Cognition
path(MESH:D000544) -- path(MESH:D003071)
#D001526 = Behavioral Symptoms
path(MESH:D000544) -- path(MESH:D001526)

SET Evidence="People with a clinical diagnosis of
MCI comprise a heterogeneous cohort of which
those with memory deficits only are classified as
amnestic MCI (aMCI) and those with impairment
in other cognitive domains lacking a clinical
diagnosis of dementia are designated multidomain
MCI (mdMCI) [3,4]. Many individuals
characterized with aMCI progress steadily to
greater stages of dementia severity, and in many
instances exhibit the neuropathologic, molecular
and biochemical hallmarks of AD [5–9].
These clinical pathobiologic studies suggest that MCI,
in general, represents a prodromal or preclinical
stage of AD."

#D060825= Cognitive Dysfunction, D008569=Memory Disorders
path(HBP:aMCI) -- path(MESH:D008569)
path(HBP:mdMCI) -- path(MESH:D060825)
#D003704 = Dementia
path(HBP:aMCI) -- path(MESH:D003704)
path(HBP:aMCI) -- path(MESH:D000544)

SET Disease="Alzheimer's disease"
#D060825= Cognitive Dysfunction, D062706 = Prodormal Symptoms
path(MESH:D060825) -- path(MESH:D062706)

UNSET Disease

SET Evidence="Although there is a widespread decline in various neurotransmitter
containing cell bodies in end-stage AD, the most consistent losses
throughout the progression of AD are seen in long projection neurons,
including cholinergic neurons of the basal forebrain [11–13]"

SET Disease="Alzheimer's disease"
SET MeSHAnatomy="Basal Forebrain"
#D059329 = Cholinergic Neurons
path(MESH:D000544) -| a(MESH:D059329)

UNSET MeSHAnatomy
UNSET Disease

SET Evidence="Cholinergic neurons within the nucleus basalis (NB) and the septal
diagonal band complex provide the major source of cholinergic
innervation to the cerebral cortex and hippocampus, respectively,
and play a key role in memory and attentional function [11–13]."

SET MeSHAnatomy={"Basal Nucleus of Meynert", "Cerebral Cortex", "Hippocampus"}
#GO:0060384 = innervation
a(MESH:D059329) => bp(GO:"GO:0060384")
a(MESH:D059329) -- path(MESH:D008568)
#D001288 = Attention
a(MESH:D059329) -- path(MESH:D001288)

UNSET MeSHAnatomy

SET Evidence="Cholinergic basal forebrain (CBF) cortical projection neurons contain
the pathological AD hallmark, neurofibrillary tangles (NFTs),
and undergo chemical phenotypic alterations during the progression
of AD, making them an excellent natural model for studying
mechanisms of cell death, survival and treatment approaches
both in vitro and in vivo, including relevant animal models of
neurodegeneration as well as human postmortem clinical pathological
tissue studies [14]."

SET MeSHAnatomy="Basal Forebrain"
SET Disease="Alzheimer's disease"

#D016874 = Neurofibrillary Tangles
a(MESH:D059329) -- a(MESH:D016874)
a(MESH:D016874) -- path(MESH:D000544)
a(MESH:D059329) -- path(MESH:D000544)

UNSET MeSHAnatomy
UNSET Disease

SET Evidence=" The regions of the forebrain that contain
cholinobasal and septohippocampal CBF neurons also display various
non-cholinergic neurons, including GABAergic interneurons
that innervate cholinergic perikarya [11]. In addition, neuropeptides
often co-localize with CBF neurons. For example, the inhibitory
neuropeptide galanin (GAL) is found in septohippocampal and
NB neurons in rodents, but not in higher apes or humans where
GAL fibers innvervate CBF neurons. [15]."

SET MeSHAnatomy="Basal Forebrain"

#D009479 = Neuropeptides
a(MESH:D059329) -- a(MESH:D009479)
#D059330 = GABAergic Neurons
a(MESH:D059329) -- a(MESH:D059330)

SET Species="10116"

a(MESH:D059329) -- p(RGD:Gal)

UNSET Species
UNSET MeSHAnatomy

SET Evidence="Specifically, progressive phenotypic
downregulation of markers within CBF neurons as well as frank
CBF cell loss has been observed consistently, along with an associated
reduction of choline acetyltransferase (ChAT) and acetylcholinesterase
(AChE) activity within the cortex in AD [16]. Most
researchers presumed that progressive disruption of cholinergic
function underlies much of the short-term memory loss seen in AD"

path(MESH:D000544) -| a(MESH:D059329)
path(MESH:D000544) -| p(HGNC:CHAT)
path(MESH:D000544) -| p(HGNC:ACHE)
#D008570 = Memory, Short-Term
a(MESH:D059329) -- path(MESH:D008570)

SET Evidence="Delaying or preventing cholinergic neurodegeneration or
minimizing its consequences is the mechanism of action for most
currently available US FDA-approved drugs for the treatment of
cognitive dysfunction observed in AD [17]"

SET MeSHAnatomy="Cholinergic Neurons"

bp(HBP:Neurodegeneration) -- path(MESH:D060825)

UNSET MeSHAnatomy

SET Evidence=" Notably, reports that
physostigmine and oral anticholinesterases have beneficial effects
for patients with AD suggest that the CBF system is somewhat
preserved during the progression of dementia, despite well-documented
loss of cholinergic biosynthetic machinery (including
ChAT and AChE enzyme deficits) in patients with this disease.
Interestingly, recent studies have shown that ChAT activity,
which results in acetylcholine (ACh) synthesis, is preserved in
the neocortex of people with MCI [18,19]."

SET Disease="Alzheimer's disease"
a(CHEBI:physostigmine) -| path(MESH:D000544)
#D002800 = Cholinesterase Inhibitors
a(MESH:D002800) -| path(MESH:D000544)
a(MESH:D059329) -- path(MESH:D003704)

UNSET Disease

SET MeSHDisease="Cognitive Dysfunction"
SET MeSHAnatomy="Neocortex"

path(MESH:D060825) causesNoChange act(p(HGNC:CHAT))
act(p(HGNC:CHAT)) => a(CHEBI:acetylcholine)

UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence="In
fact, our group found elevated ChAT activity in the hippocampus
and frontal cortex of subjects with MCI [19,20]. These results
suggest that cognitive deficits in MCI and early AD are not associated
with a reduction in ChAT activity. Moreover, these data
indicate that select components of the hippocampal and cortical
cholinergic projection system are capable of compensatory and/
or neuroplasticity responses during the early stages of AD."

SET MeSHAnatomy={"Hippocampus","Frontal Lobe"}

path(MESH:D060825) -> act(p(HGNC:CHAT))

UNSET MeSHAnatomy

SET Evidence="In
MCI, increased hippocampal and frontal cortex ChAT tone
may be important for promoting biochemical activity or compensating
for neurodegenerative defects, which may delay the
transition of these subjects to frank AD. Hippocampal ChAT
activity was increased selectively in MCI cases with high Braak
scores (Braak III/IV stage) indicative of advanced disease [19],
suggesting that a compensatory upregulation of ChAT may
be due, at least in part, to the disconnection of glutamatergic
entorhinal cortex input to the hippocampus which occurs early
in the disease process [21–23]. In this scenario, upregulation of
hippocampal ChAT activity may be due to reactive synaptogenesis,
the filling in of denervated glutamatergic synapses by
cholinergic input arising from the septum [24]."

SET MeSHDisease="Cognitive Dysfunction"
SET MeSHAnatomy={"Hippocampus","Frontal Lobe"}

act(p(HGNC:CHAT)) -| bp(HBP:Neurodegeneration)
#Synapses
path(MESH:D060825) -| a(MESH:D013569)
path(MESH:D060825) -> act(p(HGNC:CHAT))

UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence="The enzyme that hydrolyzes ACh at the synapse, AChE, does
not show a decline in cortical areas until at least moderately severe
levels of dementia [28]. "

path(MESH:D003704) -| p(HGNC:ACHE)

SET Evidence="A recent functional MRI study demonstrated that people with
MCI treated with the FDA-approved anticholinesterase donepezil
demonstrated increased frontal cortex activation relative
to untreated controls, which was positively correlated with task
performance [31]."

SET MeSHDisease="Cognitive Dysfunction"

#D005625 = Frontal Lobe
a(CHEBI:donepezil) -> act(a(MESH:D005625))
#D011597 = Psychomotor Performance
a(MESH:D005625) pos path(MESH:D011597)

SET Evidence="Butyrylcholinesterase (BChE) is a serine hydrolase similar to
AChE that is widely distributed throughout the CNS and also
catalyzes the hydrolysis of ACh. BChE is localized to neurons and
glia, and is associated with NFTs and senile plaques (SPs) in AD
brain [32]. Interestingly, population-based genetic studies of AD
have identified a point mutation that changes Ala539 to threonine
in the K variant of BChE, which effectively reduces serum BChE
concentrations, and may be associated with cognitive decline [33].
BChE activity also increases in AD brain whereas AChE activity
remains unchanged or declines [34,35]."

SET MeSHAnatomy={"Neurons", "Neuroglia"}
SET Disease="Alzheimer's disease"

act(p(HGNC:BCHE), ma(cat)) => deg(a(CHEBI:acetylcholine))
p(HGNC:BCHE) -- a(MESH:D016874)
#D058225 = Plaque, Amyloid
p(HGNC:BCHE) -- path(MESH:D058225)
p(HGNC:BCHE, var(p.Ala539Thr)) -| p(HGNC:BCHE)
p(HGNC:BCHE) -- path(MESH:D003071)
path(MESH:D000544) -> act(p(HGNC:BCHE))
path(MESH:D000544) causesNoChange act(p(HGNC:ACHE))
path(MESH:D000544) -| act(p(HGNC:ACHE))

UNSET MeSHAnatomy
UNSET Disease

SET Evidence="For example,
the vesicular ACh transporter (VAChT), which is co-expressed
with ChAT in human CBF neurons and participates in loading
ACh into synaptic vesicles in cholinergic terminals, is not
severely altered in AD [40]. In this regard, pharmacological
studies of VAChT in postmortem AD tissue or in vivo imaging
studies using vesamicol and its analogs, suggest that VAChT
levels remain steady or are minimally decreased coincident with
a severe decline in ChAT activity in cortical areas [41]."

#SLC18A3 = VAChT
path(MESH:D000544) causesNoChange p(HGNC:SLC18A3)
path(MESH:D000544) -| act(p(HGNC:ACHE))

SET Evidence="Moreover, there is evidence
from experimental lesions in animals [43–45] and from postmortem
human brain studies [46,47] suggesting that many cholinergic neurons
shrink, are depleted of phenotypic markers, and/or persist
in an atrophic state after injury or during the pathological process,
rather than degenerate."

SET MeSHAnatomy="Cholinergic Neurons"

#D001284 = Atrophy
path(MESH:D000544) -> path(MESH:D001284)

UNSET MeSHAnatomy

SET Evidence="NGF is synthesized as a precursor (proNGF) molecule that
is proteolytically cleaved to a mature biologically active neurotrophin
peptide [49]. Mature NGF binds to the TrkA receptor, which
stimulates signal transduction pathways mediating the majority
of the survival and growth effects of NGF [50], and to the p75NTR
receptor, which is a positive modulator of NGF/TrkA binding [50].
However, p75NTR has multiple functions, including apoptotic or cell
death actions [51–56], which are dependent upon its interaction with
various receptor chaperones [57–59]."

#D009414 = Nerve Growth Factors
p(HGNC:NGF) isA a(MESH:D009414)
#GO:0038180 = nerve growth factor signaling pathway
complex(p(HGNC:NGF), p(HGNC:NTRK1)) -> bp(GO:"GO:0038180")
#GO:1990089 = response to nerve growth factor
bp(GO:"GO:0038180") -> bp(GO:"GO:1990089")
#NGFR = p75NTR
complex(p(HGNC:NGF),p(HGNC:NGFR)) -> complex(p(HGNC:NGF), p(HGNC:NTRK1))
#GO:1901214 = regulation of neuron death
p(HGNC:NGFR) -- bp(GO:"GO:1901214")

SET Evidence=" In this regard, immunoblotting studies demonstrated
that proNGF is the predominant form of NGF present in
the cortex of aged cognitively intact humans [60]. ProNGF levels
are increased in the cortex of subjects diagnosed with MCI or mild
AD compared to those with NCI [61]. The biological consequences
of proNGF are controversial, as is the function of its accumulation
in the cortex during the prodromal stages of AD. Emerging
literature suggests that recombinant proNGF binds TrkA and promotes
neuronal survival and neurite outgrowth similar to mature
NGF, but is approximately fivefold less active than the mature
NGF peptide [62,63]."

SET MeSHAnatomy="Cerebral Cortex"
path(MESH:D060825) -> a(MESHC:"pro-nerve growth factor, human")
path(MESH:D000544) -> a(MESHC:"pro-nerve growth factor, human")
#GO:0031175 = neuron projection development
complex(a(MESHC:"pro-nerve growth factor, human"), p(HGNC:NTRK1)) -> bp(GO:"GO:0031175")
complex(p(HGNC:NGF), p(HGNC:NTRK1)) -> bp(GO:"GO:0031175")

UNSET MeSHAnatomy

SET Evidence="Significantly, while we have shown
that reduced TrkA levels in the cortex were positively associated
with lower cognitive performance as assessed by Mini-Mental State
Exam (MMSE) test scores [64], increased cortical proNGF levels
were negatively correlated with MMSE performance [61]."

SET MeSHAnatomy="Cerebral Cortex"
SET Disease="Alzheimer's disease"

path(MESH:D000544) -| p(HGNC:NTRK1)
#D003072 = Cognition Disorders
p(HGNC:NTRK1) neg path(MESH:D003072)
a(MESHC:"pro-nerve growth factor, human") pos path(MESH:D003072)

UNSET Disease
UNSET MeSHAnatomy

SET Evidence="Thus, the
concomitant reduction of TrkA and accumulation of proNGF in
the cortex may be an early pathobiological marker for the onset of
AD (Figure 1A). In fact, significantly increased cerebrospinal fluid
(CSF) levels of NGF are detectable in AD [65], demonstrating the
potential utility of NGF as a diagnostic biomarker."

p(HGNC:NTRK1) biomarkerFor path(MESH:D000544)
a(MESHC:"pro-nerve growth factor, human") biomarkerFor path(MESH:D000544)

SET MeSHAnatomy="Cerebrospinal Fluid"

path(MESH:D000544) -> p(HGNC:NGF)

UNSET MeSHAnatomy

SET Evidence="By contrast,
several studies indicate that increases in cortical proNGF may result
in proapoptotic signaling through binding to the p75NTR receptor.
In support of this, a different form of recombinant proNGF was
shown to bind p75NTR with high affinity and promote neuronal
apoptosis [55]."

#GO:0006915 = apoptotic process
complex(a(MESHC:"pro-nerve growth factor, human"), p(HGNC:NGFR)) -> bp(GO:"GO:0006915")

SET Evidence="It is also important to note that TrkA
reduces and p75NTR activates β-secretase strike (BACE) cleavage of
the amyloid precursor protein (APP), which requires NGF binding
and activation of the second messenger ceramide [66]. Aging may
activate beta-amyloid (Ab) generation in the brain by ‘switching’ from
TrkA to p75NTR, suggesting that NGF receptor balance is a molecular
link between normal aging of the brain and AD in relation to
amyloid processing."

SET Disease="Alzheimer's disease"

act(p(HGNC:BACE1)) -> a(CHEBI:"amyloid-beta")
p(HGNC:NTRK1) -| act(p(HGNC:BACE1))
p(HGNC:NGFR) -> act(p(HGNC:BACE1))
#D000375 = Aging
bp(MESH:D000375) -> p(HGNC:NGFR)
p(HGNC:NGFR) -> a(CHEBI:"amyloid-beta")

UNSET Disease

SET Evidence="Recent findings indicate that the putative proapoptotic effect(s)
of p75NTR-mediated proNGF signaling is dependent on interactions
between p75NTR and the neurotensin receptor sortilin, a
Vps10p domain trafficking protein that acts as a cell surface coreceptor
with p75NTR to mediate proNGF-induced cell death. This
family of receptors is acquiring increasing importance owing to
its potential involvement in AD [68]. A recent study provided
genetic information for a role of the proneurotrophin receptor
complex comprising sortilin and p75NTR, in the mediation of
neuronal viability in vivo [69]. Sortilin expression is required for
p75NTR-mediated apoptosis following proNGF treatment [57],
suggesting that sortilin is a p75NTR binding partner associated
with the initiation of cell death [51,58]"

composite(complex(p(HGNC:NGFR), p(HGNC:SORT1)), a(MESHC:"pro-nerve growth factor, human")) -> bp(GO:"GO:0006915")
path(MESH:D000544) -- p(HGNC:SORT1)

SET Evidence="Bruno and Cuello reported
that a protease cascade which converts proNGF to mature NGF
(mNGF) and degrades mNGF in the extracellular space by the
coordinated activity of plasminogen, tissue plasminogen activator
(tPA), neuroserpin, matrix metalloproteinase (MMP)-9 and
tissue inhibitor of MMP (TIMP)-1 may be defective in AD,
resulting in NGF dysfunction [74]."

#Since the protease invloved is not known or mentioned, not much can be encoded here
#GO:0032455 = nerve growth factor processing
path(MESH:D000544) -| bp(GO:"GO:0032455")

SET Evidence="We found an upregulation of
MMP-9 protein levels and activity in both AD and MCI brains,
which correlated inversely with cognitive status (Figure 1B) [75].
Since tissue alterations are often reflected in bodily fluids, determination
of MMPs in blood, urine and CSF has been recommended
as potential biomarkers to act as diagnostic measures to
characterize the disease process that occurs in the brain [76–78].
Interestingly, plasma MMP-9 was increased in MCI and AD
[77]."

SET MeSHAnatomy={"Brain", "Plasma"}

path(MESH:D000544) -> act(p(HGNC:MMP9))
path(MESH:D060825) -> act(p(HGNC:MMP9))
act(p(HGNC:MMP9)) pos path(MESH:D003072)

UNSET MeSHAnatomy

SET Evidence="These studies have demonstrated a significant
downregulation of trkA, trkB and trkC gene expression
during the development of AD [7]. An intermediate reduction
was observed in MCI, with the greatest decrement in mild AD
compared to aged controls. Moreover, two separate expressed
sequence tag (EST) cDNAs for each trk gene (e.g., ESTs targeted
to the extracellular domain [ECD] and tyrosine kinase
[TK]) domains were downregulated. By contrast, there was a
lack of regulation of p75NTR expression [7] in CBF neurons. A
‘step down’ dysregulation of trk expression may, in part, underlie
CBF neuron demise associated with the clinical presentation of
AD. Supporting this concept is the finding that trk downregulation
is associated with measures of cognitive decline [7,14]"

SET MeSHAnatomy={"Basal Forebrain", "Cholinergic Neurons"}

path(MESH:D000544) -| g(HGNC:NTRK1)
path(MESH:D000544) -| g(HGNC:NTRK2)
path(MESH:D000544) -| g(HGNC:NTRK3)

path(MESH:D000544) -> g(HGNC:NGFR)

g(HGNC:NTRK1) --  path(MESH:D003071)
g(HGNC:NTRK2) --  path(MESH:D003071)
g(HGNC:NTRK3) --  path(MESH:D003071)

UNSET MeSHAnatomy

SET Evidence=" For example,
NGF signal transduction activates the MAPK pathway, which
participates in a wide array of biologic functions, including cell
survival, differentiation and apoptosis [82–84]. MAPK is a serine/
threonine protein kinase which becomes activated upon phosphorylation
and affects a wide variety of transcription factors [85]."

#GO:0000165 = MAPK cascade
act(p(HGNC:NGF)) -> bp(GO:"GO:0000165")
bp(GO:"GO:0000165") -- bp(GO:"GO:0006915")
#GO:0030182 = neuron differentiation
bp(GO:"GO:0000165") -- bp(GO:"GO:0030182")
p(GFAM:"Mitogen-activated protein kinases", pmod(Ph)) -> act(p(GFAM:"Mitogen-activated protein kinases"))

SET Evidence="The
classic MAPK cascade involves activation of the small GTPase
Ras, and the kinases Raf and MEK [86,87]. Downstream consequences
of MAPK activation include activation of the ribosomal
S6 kinases (Rsk) and the MAPK-activated protein kinase 2
(MAPKAP2), which phosphorylates several transcription factors
including Elk-1 and cAMP-regulated response element binding
protein (CREB) [85]. The physiological significance of this elaborate
NGF-induced network remains unclear, but the sustained
activation of MAPK is linked to neurotrophin-mediated neurite
outgrowth [88,89]"

bp(GO:"GO:0000165") -> act(p(FPLX:RAS))
bp(GO:"GO:0000165") -> act(p(FPLX:RAF))
bp(GO:"GO:0000165") -> act(p(FPLX:MEK))
bp(GO:"GO:0000165") -> act(p(FPLX:P90RSK)) #RSK
bp(GO:"GO:0000165") -> act(p(HGNC:MAPKAPK2))
act(p(HGNC:MAPKAPK2)) -> p(HGNC:ELK1, pmod(Ph))
act(p(HGNC:MAPKAPK2)) -> p(HGNC:CREB1, pmod(Ph))
act(p(GFAM:"Mitogen-activated protein kinases")) -- bp(GO:"GO:0031175")

SET Evidence="A second downstream pathway is the PI3K/Akt
pathway that regulates neurotrophin-mediated survival responses
in PC12 cells [90,91]. Regulation of this pathway involves upstream
elements including Ras/Gab1/IRS1 [92,93]."

SET CellLine="PC12"
#GO:0014065 = phosphatidylinositol 3-kinase signaling
#GO:0043491 = protein kinase B signaling
#D002470 = Cell Survival
bp(GO:"GO:0014065") reg bp(MESH:D002470)
bp(GO:"GO:0043491") reg bp(MESH:D002470)
bp(GO:"GO:0014065") -- p(FPLX:RAS)
bp(GO:"GO:0043491") -- p(FPLX:RAS)
bp(GO:"GO:0014065") -- p(HGNC:IRS1)
bp(GO:"GO:0043491") -- p(HGNC:IRS1)
bp(GO:"GO:0014065") -- p(HGNC:GAB1)
bp(GO:"GO:0043491") -- p(HGNC:GAB1)

UNSET CellLine

SET Evidence=" A third downstream
mediator is the PLCgamma pathway [94,95]. PLCgamma pathway dysfunction
may play a role in Ca2+-mediated cellular degeneration in aging
and disease by affecting oxidative stress systems [96]."

#D018384 = Oxidative Stress
act(p(HGNC:PLCG1)) -- bp(MESH:D018384)
bp(MESH:D018384) -- path(MESH:D000375)
#D009410 = Nerve Degeneration
bp(MESH:D018384) -- path(MESH:D009410)

SET Evidence="Recent evidence supports the notion that p75NTR has an intrinsic
signaling capacity including:
•	 Sphingolipid metabolism [98–100]
•	 Activation of the JNK pathway [52,54,56,101–103]
•	 Activation of the NF-kappaB pathway [104–109]
•	 Activation of the Akt pathway [110]
•	 Activation of the MAPK pathway [97,111]"

#GO:0006665 = sphingolipid metabolic process
act(p(HGNC:NGFR)) -- bp(GO:"GO:0006665")
#GO:0007254 = JNK cascade
act(p(HGNC:NGFR)) -> bp(GO:"GO:0007254")
#GO:0038061 = NIK/NF-kappaB signaling
act(p(HGNC:NGFR)) -> bp(GO:"GO:0038061")
act(p(HGNC:NGFR)) -> bp(GO:"GO:0043491")
act(p(HGNC:NGFR)) -> bp(GO:"GO:0000165")

SET Evidence="Gambogic amide binds selectively to
TrkA (but not TrkB or TrkC), phosphorylates TrkA tyrosine
residues, and activates the Akt and MAPK TrkA-mediated NGF
signaling pathways. Gambogic amide has been demonstrated to
ameliorate excitotoxic damage and promote neurite outgrowth
in PC12 cells [116], making this a potential lead compound for
chemical modification and clinical trial assessment."

#C545053 = gambogic amide
complex(a(MESHC:"gambogic amide"), p(HGNC:NTRK1)) => p(HGNC:NTRK1, pmod(Ph))
p(HGNC:NTRK1, pmod(Ph)) -> bp(GO:"GO:0043491")
p(HGNC:NTRK1, pmod(Ph)) -> bp(GO:"GO:0000165")

SET CellLine="PC12"

a(MESHC:"gambogic amide") -| bp(HBP:Excitotoxicity)
a(MESHC:"gambogic amide") -> bp(GO:"GO:0031175")

UNSET CellLine

SET Evidence="The ionotropic nicotinic ACh
receptor (nAChR) is a pentameric membrane protein composed
of four polypeptide subunits designated nAChRs alpha, beta, delta and gamma,
which are expressed at varying levels throughout the CNS [117].
The muscarinic ACh receptor (mAChR) family includes five
members, M1, M2, M3, M4 and M5, which are also topographically
distributed within the CNS [118]"

SET MeSHAnatomy="Central Nervous System"

p(GFAM:"Cholinergic receptors nicotinic subunits") hasMembers list(p(HGNC:CHRNA1)\
, p(HGNC:CHRNA2), p(HGNC:CHRNA3), p(HGNC:CHRNA4), p(HGNC:CHRNA5), p(HGNC:CHRNA6), \
p(HGNC:CHRNA7), p(HGNC:CHRNA9), p(HGNC:CHRNA10), p(HGNC:CHRNB1), p(HGNC:CHRNB2),\
p(HGNC:CHRNB3), p(HGNC:CHRNB4), p(HGNC:CHRND), p(HGNC:CHRNG))

p(GFAM:"Cholinergic receptors muscarinic") hasMembers list(p(HGNC:CHRM1), p(HGNC:CHRM2),\
p(HGNC:CHRM3), p(HGNC:CHRM4), p(HGNC:CHRM5))

UNSET MeSHAnatomy

SET Evidence="Single cell expression via
microarray analysis was used to determine whether expression
levels for nAChR and mAChR receptors, as well as ChAT, were
differentially regulated within individual CBF neurons harvested
from NCI, MCI and AD cases. ChAT mRNA expression levels
did not differ across clinical conditions (Table 1). However, there
was a significant upregulation of alpha7 nAChR subunit expression
in AD compared with NCI and MCI."

SET MeSHAnatomy="Cholinergic Neurons"

path(MESH:D000544) causesNoChange r(HGNC:CHAT)
path(MESH:D060825) causesNoChange r(HGNC:CHAT)
path(MESH:D000544) -> r(HGNC:CHRNA7)

UNSET MeSHAnatomy

SET Evidence="This increase in alpha7 nAChR expression levels within CBF
neurons was inversely associated with cognitive performance.
Increased alpha7 nAChR expression in CBF neurons may signal
a compensatory response to maintain basocortical cholinergic
activity during the onset of AD. Upregulation of the alpha7 nAChR
within individual CBF neurons is also consistent with reports of
increased alpha7 nAChR mRNA and protein expression levels in hippocampal
neurons, astrocytes and peripheral blood leukocytes in
AD [120–122]. The observed increase in alpha7 nAChR in early AD
may regulate basocortical cholinergic tone through pre- and/or
postsynaptic mechanisms within cholinergic NB neurons prior
to their frank degeneration in the later stages of AD."

SET Disease="Alzheimer's disease"
SET MeSHAnatomy={"Hippocampus","Astrocytes","Leukocytes"}

path(MESH:D000544) -> r(HGNC:CHRNA7)
r(HGNC:CHRNA7) neg path(MESH:D003071)

UNSET MeSHAnatomy

SET MeSHAnatomy="Cholinergic Neurons"

r(HGNC:CHRNA7) reg act(a(MESH:D059329))

UNSET MeSHAnatomy
UNSET Disease

SET Evidence="Despite a
putative beneficial role for increased CBF neuron alpha7 nAChR
expression in AD (that may also be relevant to smoking behavior),
evidence suggests that increased alpha7 nAChR expression
contributes to cellular degeneration. Notably, alpha7 nAChR binds
and/or interacts with APP and Ab peptides [123–125]. Increased
NB neuronal alpha7 nAChR expression may arise as a compensatory
response that is offset by aberrant Ab-alpha7 nAChR interactions,
leading to cholinergic dysfunction."

SET Disease="Alzheimer's disease"

r(HGNC:CHRNA7) -> path(MESH:D009410)
p(HGNC:CHRNA7) -> complex(p(HGNC:CHRNA7), p(HGNC:APP))
p(HGNC:CHRNA7) -> complex(p(HGNC:CHRNA7), a(CHEBI:"amyloid-beta"))
complex(p(HGNC:CHRNA7), a(CHEBI:"amyloid-beta")) -> r(HGNC:CHRNA7)
complex(p(HGNC:CHRNA7), a(CHEBI:"amyloid-beta")) -| bp(GO:"GO:0007271")

UNSET Disease

SET Evidence="Conversely, M1 subunit gene expression in single CBF neurons
is preserved during the progression of AD (Table 1) [119,126]. The
M1 receptor is a most interesting drug target as it links several
of the major hallmarks of this disorder, including cholinergic
deficiency, cognitive dysfunctions, Ab and tau pathologies."

SET Disease="Alzheimer's disease"

path(MESH:D000544) causesNoChange r(HGNC:CHRM1)
p(HGNC:CHRM1) -- path(MESH:D060825)
#D024801 = Taupathies
p(HGNC:CHRM1) -- path(MESH:D024801)
#D000686 = Amyloidosis
p(HGNC:CHRM1) -- path(MESH:D000686)

UNSET Disease

SET Evidence="Recently, a novel group
of M1 partial agonists was developed (AF102B, AF150(S) and
AF267B-i) [129]. In a series of studies using the 3x transgenic-AD
mice, which recapitulate the major pathologies of AD [130], chronic
AF267B treatment rescued cognitive impairment and decreased
Abeta42 and tau pathologies in the cortex and hippocampus. These
changes were associated with M1 mAChR-mediated activation
of the TNFalpha-converting enzyme ADAM17/TACE, decreased
BACE1 steady state levels and inhibition of GSK3beta [130]."

SET MeSHAnatomy={"Hippocampus","Cerebral Cortex"}
SET Disease="Alzheimer's disease"
SET Species="10090"

#(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one (i.e AF267B)

a(MESHC:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| path(MESH:D060825)
a(MESHC:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| path(MESH:D024801)
a(MESHC:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| a(CHEBI:"amyloid-beta polypeptide 42")
a(MESHC:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -> act(p(MGI:Chrm1))
act(p(MGI:Chrm1)) -> act(p(MGI:Adam17))
a(MESHC:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| p(MGI:Bace1)
a(MESHC:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| act(p(MGI:Gsk3b))

UNSET Species
UNSET Disease
UNSET MeSHAnatomy

SET Evidence="Single cell gene array studies have shown that synaptic
transcripts are selectively downregulated in CBF neurons in AD,
with significant reductions in synaptophysin and synaptotagmin
but not synaptobrevin or SNAP29 mRNA [134,135]. Intriguingly,
synaptotagmin function is related to vesicle-presynaptic membrane
fusion and neurotransmitter release, suggesting that perturbations
in presynaptic vesicle trafficking comprise a common
event in vulnerable neuronal populations in AD. In contrast to
synaptic transcripts, mRNAs encoding APP and Notch were
unchanged between control and AD subjects, whereas acid
hydrolase cathepsin D mRNA was upregulated in AD [134–136]."

#D016708 = Synaptophysin
path(MESH:D000544) -| r(HGNC:SYP)
path(MESH:D000544) -| r(GFAM:"Synaptotagmins")
path(MESH:D000544) causesNoChange r(HGNC:VAMP1)
path(MESH:D000544) causesNoChange r(HGNC:SNAP29)
#GO:0031629 = synaptic vesicle fusion to presynaptic active zone membrane
p(GFAM:"Synaptotagmins") -- bp(GO:"GO:0031629")
#GO:0007269 = neurotransmitter secretion
p(GFAM:"Synaptotagmins") -- bp(GO:"GO:0007269")
path(MESH:D000544) causesNoChange r(HGNC:APP)
path(MESH:D000544) causesNoChange r(HGNC:NOTCH1)
path(MESH:D000544) -> r(HGNC:CTSD)

SET Evidence="In addition, subunits of protein phosphatase PP1 (Unigene-NCBI
annotation PPP1CA and PPP1CC) mRNAs were downregulated
in CBF neurons in AD [135]. This observation is interesting in
light of the observation that PP1 can phosphorylate tau on several
serine/threonine residues and experimental downregulation of
PP1 activity leads to increased tau hyperphosphorylation [137,138],
which may affect NFT formation in CBF neurons."

SET MeSHAnatomy="Cholinergic Neurons"
SET Disease="Alzheimer's disease"

path(MESH:D000544) -| r(HGNC:PPP1CA)
path(MESH:D000544) -| r(HGNC:PPP1CC)
p(GFAM:"Protein phosphatase catalytic subunits") -> p(HGNC:MAPT, pmod(Ph))
act(p(GFAM:"Protein phosphatase catalytic subunits")) -| bp(HBP:hyperphosphorylation)
bp(HBP:hyperphosphorylation) -- path(MESH:D016874)

UNSET Disease
UNSET MeSHAnatomy

SET Evidence="Cortical and CBF neurons display NFT formation in the MCI
brain [9,139,140], suggesting a concomitant alteration in tau gene
expression during the early stage of AD. The adult human brain
contains six tau isoforms ranging from 48 to 67 kDa, which
are expressed through alternative splicing of a single tau gene
on chromosome 17 [141,142]. Three of these tau isoforms contain
three tandem repeats in the carboxy-terminus end of the
molecule (3Rtau), while three isoforms display four tandem
repeats (4Rtau) in this region. Expression levels of the six tau
transcripts within CBF neurons do not differ significantly during
the progression of AD [6]"

SET MeSHAnatomy={"Cerebral Cortex", "Cholinergic Neurons"}
SET Disease="Alzheimer's disease"

path(MESH:D000544) -> path(MESH:D016874)
p(HGNC:MAPT) hasMembers list (p(HBP:"Tau isoform A (316 aa)"),\
p(HBP:"Tau isoform B (381 aa)"), p(HBP:"Tau isoform C (410 aa)"),\
p(HBP:"Tau isoform D (383 aa)"), p(HBP:"Tau isoform E (412 aa)"),\
p(HBP:"Tau isoform F (441 aa)"))
path(MESH:D000544) causesNoChange r(HGNC:MAPT)

UNSET Disease
UNSET MeSHAnatomy

SET Evidence=" However, a calculation of the ratio
of 3Rtau/4Rtau revealed a significant shift in the 3Rtau/4Rtau
ratio, with a decrement in 3Rtau in relation to 4Rtau levels for
each tau transcript analyzed within CBF perikarya obtained from
MCI and AD cases (Table 1) [6]. A similar shift did not occur during
normal aging. These data suggest a subtle, yet pervasive shift
in the gene dosage of 3Rtau and 4Rtau within vulnerable CBF
neurons in MCI and AD [6]. Shifts in the ratio of tau transcripts
may be a fundamental mechanism whereby normal tau function
is dysregulated, not only in CBF neurons, but may be a more
widespread process contributing to the selective vulnerability of
neurons to NFT formation (Figure 1B) [143–145]. "

SET Disease="Alzheimer's disease"

path(MESH:D000544) -| a(HBP:"3R tau")
path(MESH:D060825) -| a(HBP:"3R tau")

UNSET Disease

SET Evidence=" For example, the
neuropeptide GAL, which functions via the interaction with
three G protein-coupled receptors termed GALR1, GALR2
and GALR3, has multiple biological actions, including effects
on cognition and neuroplasticity [15,146,147]. In the late [148–150]
but not early [151] stage of AD, fibers within the basal forebrain
containing the neuropeptide GAL thicken and hyperinnervate
surviving CBF neurons. Although animal and cell-culture studies
have shown that GAL plays a crucial role in the regulation
of CBF neuron activity [152] and rescues cholinergic cells
from amyloid toxicity [153], the molecular consequences of this
unique plasticity response upon CBF neurons in AD remain
unclear. Gene expression studies of cholinergic transcripts have
shown that GAL hyperinnervated, but not nonhyperinnervated,
CBF neurons display an upregulation of ChAT expression in
AD compared to controls [126]"

act(p(HGNC:GAL)) -- path(MESH:D003071)
#D009473 = Neuronal Plasticity
act(p(HGNC:GAL)) -- bp(MESH:D009473)

SET Disease="Alzheimer's disease"
SET MeSHAnatomy={"Basal Forebrain", "Cholinergic Neurons"}
#GO:0060384 = innervation
act(p(HGNC:GAL)) -> bp(GO:"GO:0060384")
act(p(HGNC:GAL)) reg act(a(MESH:D059329))
act(p(HGNC:GAL)) -| a(CHEBI:"amyloid-beta")
act(p(HGNC:GAL)) -> r(HGNC:CHAT)

UNSET Disease
UNSET MeSHAnatomy

SET Evidence="CBF neurons
displaying the tau C3 epitope, a marker for early stage NFT formation,
were often hyperinnervated by GAL-containing fibers,
whereas CBF neurons displaying the tau epitope MN423, an
end-stage NFT marker, were not associated with GAL. Single
cell gene expression studies have demonstrated that the levels of
mRNAs encoding select subclasses of PP1 subunits (e.g., PP1alpha
and PP1gamma) are stable in GAL hyperinnervated but downregulated
in non-hyperinnervated CBF neurons in AD [149]"

SET Disease="Alzheimer's disease"
SET MeSHAnatomy="Cholinergic Neurons"

a(HBP:"Tau C3") biomarkerFor path(MESH:D059329)
act(p(HGNC:GAL)) causesNoChange a(HBP:"Tau MN423")
act(p(HGNC:GAL)) -> r(HGNC:PPP1CA)
act(p(HGNC:GAL)) -> r(HGNC:PPP1CC)

UNSET Disease
UNSET MeSHAnatomy

SET Evidence=" In this
vein, intraventricular infusion of NGF increased hippocampal
GAL mRNA expression in rats [158], suggesting that the use
of NGF for the treatment of AD [113] may indirectly increase
brain GAL providing a dual therapeutic benefit for the treatment
of CBF dysfunction in AD."

p(RGD:Ngf) -> r(RGD:Gal)
p(RGD:Ngf) -| path(MESH:D060825)

SET Evidence="Recent studies report that anticholinesterase
drugs reduce circulating Ab deposition in several
dementia types, including AD [159]. Evidence from clinical trials
[160], noninvasive functional imaging [161] and basic science
research suggest that cholinesterase inhibitors might alter APP
processing and therefore provide some degree of neuroprotection
[162,163]"

SET Disease="Alzheimer's disease"

a(MESH:D002800) -| a(CHEBI:"amyloid-beta")
#D000066829 = Neuroprotection
a(MESH:D002800) -> bp(MESH:D000066829)
#GO:0050435 = amyloid-beta metabolic process
a(MESH:D002800) -- bp(GO:"GO:0050435")

UNSET Disease

SET Evidence="In this regard, AChE has been localized to SPs in
the vicinity of cholinergic synapses, and experimental evidence
suggests that AChE promotes Ab fibrillization [164], suggesting
that a further benefit from AChE inhibitor therapy may be
to prevent continued Ab deposition in cholinergic projection
sites. "

p(HGNC:ACHE) -- path(MESH:D058225)
#GO:1990000 = amyloid fibril formation
p(HGNC:ACHE) -> bp(GO:"GO:1990000")

SET Evidence="Since the cholinergic deficit is
not an early defect in the progression of AD [18–20], the use of
these drugs in the prodromal stages of AD should be continued.
However, the limited effect of cholinesterase inhibitors for the
treatment of cognitive decline in AD coupled with unwanted
side effects such as diarrhea, nausea, insomnia, fatigue and loss
of appetite indicates the need to move beyond this conventional
drug treatment with somewhat circumscribed efficacy."

SET Disease="Alzheimer's disease"

#D007319 = Sleep Initiation and Maintenance Disorders
a(MESH:D002800) -- path(MESH:D007319)
#D003967 = Diarrhea
a(MESH:D002800) -- path(MESH:D003967)
#D009325 = nausea
a(MESH:D002800) -- path(MESH:D009325)
#D005221= Fatigue
a(MESH:D002800) -- path(MESH:D005221)
#D000855 = Anorexia
a(MESH:D002800) -- path(MESH:D000855)

UNSET Disease

SET Evidence="NGF therapy has been tested in clinical trials of diabetic
peripheral neuropathy and HIV-associated neuropathy, as well
as patients with AD [177]. Each of these clinical trials met with
disappointment owing to lack of efficacy, toxicity or both. "

#D003929 = Diabetic Neuropathies
p(HGNC:NGF) causesNoChange path(MESH:D003929)
#D016263 = AIDS-Associated Nephropathy
p(HGNC:NGF) causesNoChange path(MESH:D016263)
p(HGNC:NGF) causesNoChange path(MESH:D000544)

SET Evidence="In addition, indirect effects and unwanted side effects
appeared with systemic administration including unregulated
neurotransmitter release, hyperinnervation, sprouting of neurons,
sympathetic stimulation, induction of antibodies, cachexia
and hyperalgesia [178–181]."

p(HGNC:NGF) -- bp(GO:"GO:0060384")
#GO:0007269 = neurotransmitter secretion
p(HGNC:NGF) -- bp(GO:"GO:0007269")
#GO:0048485 = sympathetic nervous system development
p(HGNC:NGF) -- bp(GO:"GO:0048485")
#D002100 = Cachexia
p(HGNC:NGF) -- path(MESH:D002100)
#D006930 = Hyperalgesia
p(HGNC:NGF) -- path(MESH:D006930)

SET Evidence="The ex vivo Phase I trial attempted to move beyond currently
available treatments for AD [113]. The goal of this NGF trial was
to protect CBF neurons from degeneration, as well as augment
the function of remaining cholinergic neurons by delivery of
human NGF. After obtaining informed consent, skin biopsies
were attained to generate primary cultures of autologous fibroblasts
that were transfected to produce human NGF [184]. If
fibroblasts were found to be acceptable based on NGF production
rates, then grafts were stereotaxically placed into multiple locations
within the region of the CBF neurons. Following a period
of 22 months, no long-term post-surgical adverse effects were
found and the rate of cognitive decline appeared to be reduced
[113]. "

SET MeSHAnatomy="Fibroblasts"

g(HGNC:NGF) => p(HGNC:NGF)
p(HGNC:NGF) -| path(MESH:D060825)

UNSET MeSHAnatomy

SET Evidence="Recently, the
novel non-selective antihistamine dimebon (2,3,4,5-tetrahydro2,8-dimethyl-5–2[-6methyl
3-pyridnyl)ethyl]-1H-pyrido[4,3-b]
indole) was shown to inhibit BChE and AChE, block the NMDA
receptor signaling pathway, inhibit mitochondrial permeability
and provide neuroprotective effects in models of AD [194]."

SET Disease="Alzheimer's disease"
#latrepirdine (i.e dimebon)
a(MESHC:"latrepirdine") -| act(p(HGNC:BCHE))
a(MESHC:"latrepirdine") -| act(p(HGNC:ACHE))
#GO:0098989 = NMDA selective glutamate receptor signaling pathway
a(MESHC:"latrepirdine") -| bp(GO:"GO:0098989")
#GO:0046902 = regulation of mitochondrial membrane permeability
a(MESHC:"latrepirdine") -| bp(GO:"GO:0046902")
a(MESHC:"latrepirdine") -> bp(MESH:D000066829)

UNSET Disease
