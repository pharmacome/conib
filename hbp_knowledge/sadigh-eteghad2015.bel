#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "sadigh-eteghad2015"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Rana Aldisi"
SET DOCUMENT Description = "This file encodes the article Selective activation of α7 nicotinic acetylcholine receptor by PHA-543613 improves Aβ25-35-mediated cognitive deficits in mice by sadigh-eteghad et al. 2015"

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/bd0996a28201cad363557315043c6392e31abf58/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/73688d6dc24e309fca59a1340dc9ee971e9f3baa/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "25881725"}

SET Evidence = "Inter-group analysis showed that Ab increased
escape latency in MWM in comparison with normal
saline and sham groups (p<0.001). In addition, sham
operation did not have a significant effect on escape
latency in comparison with the normal saline group
(p>0.05)."
# escape latency measures learning
# The abeta used in this experiment was abeta 25-35

SET Species = "10090"

p(MGI:App, frag("25_35")) -| bp(GO:learning)

UNSET Species

SET Evidence = "Moreover in Ab-received mice, treatment
with PHA and Gal improved the acquisition performance
on comparing with the normal saline group (p<0.001)"

SET Species = "10090"
#PHA = N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide
#GAL = Galantamine
a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") -| act(p(MGI:App, frag("25_35")))
a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") -> bp(GO:learning)
a(CHEBI:galanthamine) -| act(p(MGI:App, frag("25_35")))
a(CHEBI:galanthamine) -> bp(GO:learning)

UNSET Species

SET Evidence = "Notably, PHA could completely block the Ab-impaired
task acquisition and reduce escape-latency to the
normal level. Furthermore, the effect of PHA against the
Ab-impaired task acquisition could be abolished by the
pre-treatment of the a7 nAChR antagonist MLA
(p<0.001). It was also noted that blockade of a7
nAChR by MLA in the Gal-treated group resulted in a
significant prolongation of the escape-latency on
comparing with the Gal group (p<0.001) (Fig. 2)."

SET Species = "10090"

a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") -| act(p(MGI:App, frag("25_35")))
a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") -> bp(GO:learning)
a(MESH:methyllycaconitine) -| act(a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide"))
a(MESH:methyllycaconitine) -| act(a(CHEBI:galanthamine))
a(MESH:methyllycaconitine) -| act(p(MGI:Chrna7))
act(p(MGI:Chrna7)) -> bp(GO:learning)

UNSET Species

SET Evidence = "Time spent in target quadrant in probe trials. On the
probe trial, there was a statistically significant difference
between groups as determined by a one-way ANOVA
(F(4,70)=35.21, p<0.001). Post hoc analysis revealed
a significant effect of Ab injection on the time spent in
the target quadrant, indicating that the Ab decreased
the searching time in the target quadrant on comparing
with the control (p<0.001)."

SET Species = "10090"
# Probe test is for testing working memory
p(MGI:App, frag(25_35)) -| path(MESH:"Spatial Memory")

UNSET Species

SET Evidence = "The analysis also showed
significant enhancing effects of PHA and Gal on the
time spent in the target quadrant on comparing with the
normal saline in AD mice (p<0.001). Also, in
comparison with the control group, PHA-treated AD
animals did not have a significant difference (p>0.05)
on the time spent in the target quadrant, but in the Gal
group it was significantly lower (p<0.001) (Fig. 3)."

SET Species = "10090"
SET MeSHDisease = "Alzheimer Disease"

a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") -> path(MESH:"Spatial Memory")
a(CHEBI:galanthamine) -> path(MESH:"Spatial Memory")

UNSET Species
UNSET MeSHDisease

SET Evidence = "Effect of a7 nAChRs blockage on time spent in target
quadrant in probe trials. The implications of the a7
nAChRs on PHA- or Gal-induced spatial memory
enhancement were investigated by pretreatment with
normal saline or MLA. A two-way ANOVA revealed
significant differences of treatment [F(1,56)=4.24,
p<0.05], pretreatment [F(1,56)=96.87, p<0.001] and
treatment  pretreatment interaction [F(1,32)=10.69,
p<0.01]. Inter-group analysis showed that this effect
was blocked by the selective a7 nAChR antagonist MLA
(p<0.001)."

SET Species = "10090"

a(MESH:methyllycaconitine) -| act(p(MGI:Chrna7))
act(p(MGI:Chrna7)) -> path(MESH:"Spatial Memory")

UNSET Species

SET Evidence = "MLA could completely block the PHA-induced spatial
memory improvement (p<0.001). Furthermore, the
effect of Gal against the Ab-impaired reference memory
could be abolished by the pre-treatment of the a7
nAChR antagonist MLA (p<0.001). However, a7
nAChR blockage in the Gal group has a lesser effect on
reference memory in comparison of blockage of that
receptor in the PHA group (p<0.05) (Fig. 4)."

SET Species = "10090"

a(MESH:methyllycaconitine) -| act(a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide"))
a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") -> path(MESH:"Spatial Memory")
a(MESH:methyllycaconitine) -| path(MESH:"Spatial Memory")
a(MESH:methyllycaconitine) -| act(a(CHEBI:galanthamine))

UNSET Species

SET Evidence = "In contrast, brain sections of animals
treated with 10-nmol-aged Ab25–35 peptide demonstrated
the presence of numerous and distinct extracellular
amyloid deposits, widely disseminated throughout the
brain (Fig. 5)."

SET Species = "10090"
SET MeSHAnatomy = "Brain"

p(MGI:App, frag(25_35)) -> a(HBP:"amyloid-beta oligomers", loc(GO:"extracellular region"))

UNSET MeSHAnatomy
UNSET Species

SET Evidence = "mRNA levels of a7 nAChR subunit. Relative
quantification by using a real-time RT-PCR did not
reveal any significant differences in the levels of mRNA
for the a7 nAChR subunits either in the hippocampus or
in the cortex of various groups (p>0.05). The groups
showed results in the cortex (data not shown) similar to
those observed in the hippocampus (Fig. 6)."

SET Species = "10090"
SET MeSHAnatomy = {"Hippocampus", "Cerebral Cortex"}

p(MGI:App, frag(25_35)) causesNoChange r(MGI:Chrna7)

UNSET MeSHAnatomy
UNSET Species

SET Evidence = "Also, no alterations in the expression of a7 nAChR
mRNA were observed in pretreatment with normal
saline or MLA in PHA- or Gal-treated groups (p>0.05).
In the same way, the pattern of mRNA expression in the
cortex (data not shown) was similar to the hippocampus
(Fig. 7)."

SET Species = "10090"
SET MeSHAnatomy = {"Hippocampus", "Cerebral Cortex"}

a(MESH:methyllycaconitine) causesNoChange r(MGI:Chrna7)
a(CHEBI:galanthamine) causesNoChange r(MGI:Chrna7)
a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") causesNoChange r(MGI:Chrna7)

UNSET MeSHAnatomy
UNSET Species

SET Evidence = "Post hoc analysis revealed that, levels of
the a7 nAChR protein in normal saline-treated AD mice
were significantly lower than its level in the control
animals (p<0.001)."

SET Species = "10090"

path(MESH:"Alzheimer Disease") neg p(MGI:Chrna7)

UNSET Species

SET Evidence = "The analysis also showed
significant effects of PHA and Gal on the protein levels
on comparing with normal saline in AD mice
(p<0.001). Moreover, Gal could not completely protect
the a7 nAChR protein level in AD animals. This group
has a lesser a7 nAChR protein level in comparison with
the control group (p<0.001) (Fig. 8). The groups
showed results in the cortex similar to those observed in
the hippocampus (data not shown)"

SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = {"Hippocampus", "Cerebral Cortex"}

a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") -> p(MGI:Chrna7)
a(CHEBI:galanthamine) -> p(MGI:Chrna7)

UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "Inter-group
analysis showed that the protein level decreased by
pretreatment with selective a7 nAChR antagonist MLA
(p<0.001). PHA and Gal prevent receptor destroying in
AD animals, but this effect is completely abolished by
MLA (p<0.001) (Fig. 9). The groups showed results in
the cortex similar to those observed in the hippocampus
(data not shown)."

SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = {"Hippocampus", "Cerebral Cortex"}

a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide") -> p(MGI:Chrna7)
a(CHEBI:galanthamine) -> p(MGI:Chrna7)
a(MESH:methyllycaconitine) -| p(MGI:Chrna7)
a(MESH:methyllycaconitine) -| act(a(MESH:"N-(1-azabicyclo(2.2.2)oct-3-yl)furo(2,3-c)pyridine-5-carboxamide"))
a(MESH:methyllycaconitine) -| act(a(CHEBI:galanthamine))

UNSET MeSHAnatomy
UNSET MeSHDisease
