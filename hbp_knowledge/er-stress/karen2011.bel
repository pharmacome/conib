#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Unfolded proteins and endoplasmic reticulum stress in neurodegenerative disorders"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Trusha Adeshara"
SET DOCUMENT Description = "ER stress response plays a role in neurodegenerative disorders. This review summarizes the molecular events occurring during ER stress and the unfolded protein response"

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
DEFINE NAMESPACE MGI           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
DEFINE NAMESPACE RGD           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "21722302"}

SET Evidence = "They include Alzheimer’s disease (AD), Parkinson’s disease (PD), amyotrophic lateral sclerosis (ALS) and prion diseases [5]. Emerging evidence suggests that ER stress may play a pivotal role in the development or pathology of many neurodegenerative diseases."

# they= neurodegenerative diseases
path(MESH:"Alzheimer Disease") -- path(HBP:Neurodegeneration)
path(MESH:"Parkinson Disease") -- path(HBP:Neurodegeneration)
path(MESH:"Amyotrophic Lateral Sclerosis") -- path(HBP:Neurodegeneration)
path(MESH:"Prion Diseases") -- path(HBP:Neurodegeneration)
bp(MESH:"Endoplasmic Reticulum Stress") -> path(HBP:Neurodegeneration)

SET Evidence = "Physiological or pathological processes that disturb protein folding in the ER cause ER stress. The cells initial and rapid response to ER stress is the activation of a set of pro-survival signalling pathways termed the UPR. Activation of the UPR causes a shutdown of global protein synthesis and activates mechanisms that allow the cell to deal with the accumulation of unfolded proteins. For example, it enhances the protein folding capacity by increasing the expression of ER chaperones and it up-regulates the degradation of misfolded proteins."
#UPR= unfolded protein response
p(MESH:Proteins , pmod(HBP:misfolded)) -- bp(MESH:"Endoplasmic Reticulum Stress")
bp(MESH:"Endoplasmic Reticulum Stress") -> bp(MESH:"Unfolded Protein Response")
bp(MESH:"Unfolded Protein Response") -| bp(GO:translation)
bp(MESH:"Unfolded Protein Response") -| bp(GO:"mRNA transcription")
#because protein synthesis includes transcription as well as translation
bp(MESH:"Unfolded Protein Response") -> bp(GO:"protein folding")
bp(MESH:"Unfolded Protein Response") -> p(MESH:"Molecular Chaperones")
p(MESH:"Molecular Chaperones") -> bp(GO:"protein folding")
bp(MESH:"Unfolded Protein Response") -> deg(p(MESH:Proteins , pmod(HBP:misfolded)))

SET Evidence = "In mammals, the three major ER stress sensors are IRE1 (inositol requiring 1; ERN1, endoplasmic reticulum-to-nucleus signalling 1), PERK [double-stranded RNA-activated protein kinase (PKR)-like ER kinase; PEK, pancreatic eukaryotic initiation factor 2alpha kinase; EIF2AK3] and ATF6 (activating transcription factor 6)[6]."

bp(MESH:"Endoplasmic Reticulum Stress") -> p(HGNC:ERN1)
bp(MESH:"Endoplasmic Reticulum Stress") -> p(MESH:"PERK kinase")
bp(MESH:"Endoplasmic Reticulum Stress") -> p(HGNC:ATF6)

SET Evidence = "The ER-luminal
domain of PERK, IRE1 and ATF6 interacts with the ER chaperone
glucose-regulated protein 78 (GRP78); however, upon accumulation
of unfolded proteins, GRP78 dissociates from these molecules,
leading to their activation [7]. Activation of PERK, IRE1 and
ATF6 initiates a network of intracellular signalling pathways during
the UPR (Fig. 1)"

complex(p(HGNC:ERN1),p(MESH:"molecular chaperone GRP78")) -| act(p(HGNC:ERN1))
complex(p(MESH:"PERK kinase"),p(MESH:"molecular chaperone GRP78")) -| act(p(MESH:"PERK kinase"))
complex(p(HGNC:ATF6),p(MESH:"molecular chaperone GRP78")) -| act(p(HGNC:ATF6))
p(MESH:Proteins , pmod(HBP:misfolded)) -> act(p(HGNC:ERN1))
p(MESH:Proteins , pmod(HBP:misfolded)) -> act(p(MESH:"PERK kinase"))
p(MESH:Proteins , pmod(HBP:misfolded)) -> act(p(HGNC:ATF6))

SET Evidence = "The cytoplasmic domain
of IRE1 contains a serine/threonine kinase domain and a C-terminal
endoribonuclease domain [9]. ER stress leads to dissociation
of GRP78 from IRE1, resulting in autophosphorylation of IRE1alpha
and activation of its RNAse activity [9]."

bp(MESH:"Endoplasmic Reticulum Stress") -> p(HGNC:ERN1, pmod(Ph))

SET Evidence = "In addition, activated IRE1 can bind to tumour necrosis
factor (TNF)-receptor-associated factor 2 (TRAF2), an adaptor
protein that promotes activation of JUN N-terminal kinase (JNK)
through apoptosis signal-regulating kinase 1 (ASK1) [10]. JNK
activation results in enhanced autophagy [11]. This might allow
cells to adapt to stress by initiating autophagy."

complex(p(HGNC:ERN1),p(HGNC:TRAF2)) -> p(MESH:"ASK1 protein, S cerevisiae")
p(MESH:"ASK1 protein, S cerevisiae") -> bp(GO:"JNK cascade")
bp(GO:"JNK cascade") -> bp(GO:autophagy)

SET Evidence = "The pro-apoptotic B-cell lymphoma 2
(BCL-2) family members BCL-2–associated X protein (BAX) and
BCL-2 antagonist/killer (BAK) augment both the kinase and
endoribonuclease activities of IRE1. BAX and BAK form a protein
complex with the cytosolic domain of IRE1, which requires their
conserved BH1 and BH3 domains [12]."

complex(GO:"BCL-2 complex") hasMembers list(p(HGNC:BAX),p(HGNC:BAK1))
p(HGNC:BAX) -> act(p(HGNC:ERN1), ma(kin))
p(HGNC:BAK1) -> act(p(HGNC:ERN1), ma(kin))
complex(p(HGNC:ERN1),p(HGNC:BAX),p(HGNC:BAK1))

SET Evidence = "ASK1-interacting protein 1 (AIP1)
was recently shown to specifically regulate and enhance IRE1
signalling. [12]."

complex(GO:"AIP1-IRE1 complex") -> act(p(HGNC:ERN1))

SET Evidence = "We have recently shown
that the stress-inducible form of HSP70 (HSP72) can interact with
IRE1 and increase XBP1 mRNA splicing, thus modulating the
expression of XBP1’s target genes causing attenuated apoptosis
under ER stress conditions [13]. It is likely that additional proteins
can interact with IRE1 to alter its activity."

composite(p(MESH:"HSP72 Heat-Shock Proteins"),p(HGNC:ERN1)) -> p(HGNC:XBP1)
p(HGNC:XBP1) -| bp(MESH:Apoptosis)

SET Evidence = "PERK-mediated phosphorylation
of the alpha subunit of eukaryotic translation initiation factor 2 alpha
(eIF2alpha) at Ser51 leads to translational attenuation [14]. Although
phosphorylation of eIF2alpha inhibits general translation initiation, it
paradoxically increases translation of activating transcription factor
4 (ATF4) [15] through a cap-independent process (Fig. 1)."

p(MESH:"PERK kinase") -> p(HGNC:EIF2A, pmod(Ph,S,51))
p(HGNC:EIF2A, pmod(Ph,S,51)) -| bp(GO:translation)
p(HGNC:EIF2A, pmod(Ph,S,51)) -> bp(GO:"cap-independent translational initiation")
bp(GO:"cap-independent translational initiation") -> p(HGNC:ATF4)
p(HGNC:EIF2A, pmod(Ph,S,51)) -> p(HGNC:ATF4)

SET Evidence = "NRF2 is retained in the cytoplasm
through its association with the microtubule-associated protein
KEAP1 (Kelch-like Ech-associated protein 1) [16]. Upon ER stress,
PERK-mediated phosphorylation of NRF2 promotes its dissociation
from KEAP1, leading to the nuclear accumulation of NRF2
[16]. NRF2 binds to the antioxidant response element to activate
transcription of genes encoding detoxifying enzymes such as A1
and A2 subunits of glutathione S-transferase, NAD(P)H:quinone
oxidoreductase, gamma-glutamylcysteine synthetase, heme oxygenase-
1 and UDP-glucoronosyl transferase [16]."

complex(p(MESH:"Nrf2a protein, zebrafish"),p(HGNC:KEAP1),loc(MESH:Cytoplasm))
p(MESH:"PERK kinase") -| complex(p(MESH:"Nrf2a protein, zebrafish"),p(HGNC:KEAP1))
p(MESH:"PERK kinase") -> p(MESH:"Nrf2a protein, zebrafish", pmod(Ph),loc(MESH:"Cell Nucleus"))
p(MESH:"Nrf2a protein, zebrafish", pmod(Ph)) -> p(HGNCGENEFAMILY:"Soluble glutathione S-transferases")
p(MESH:"Nrf2a protein, zebrafish", pmod(Ph)) -> p(HGNCGENEFAMILY:"NADH:ubiquinone oxidoreductase core subunits")
p(MESH:"Nrf2a protein, zebrafish", pmod(Ph)) -> p(MESH:"Heme Oxygenase-1")
##was not able to find UDP-glucoronosyl transferase and gamma-glutamylcysteine synthetase

SET Evidence = "In response to ER3
stress, GRP78 dissociation permits trafficking of ATF6 to the Golgi
complex, where ATF6 is sequentially cleaved by two proteases
[17]."

bp(MESH:"Endoplasmic Reticulum Stress") -| complex(p(MESH:"molecular chaperone GRP78"),p(HGNC:ATF6))
bp(MESH:"Endoplasmic Reticulum Stress") -> tloc(p(HGNC:ATF6), fromLoc(GO:"endoplasmic reticulum membrane"), toLoc(GO:"Golgi apparatus"))

SET Evidence = "The processed forms of ATF6-alpha and ATF6-beta translocate to the
nucleus and bind to the ATF/cAMP response element and to the ER
stress responsive element to activate target genes (Fig. 1)."

tloc(p(HGNC:ATF6B),fromLoc(GO:"Golgi apparatus"), toLoc(GO:nucleus))
tloc(p(HGNC:ATF6),fromLoc(GO:"Golgi apparatus"), toLoc(GO:nucleus))
##ATF6 alpha is denother as ATF6 only.

SET Evidence = "ATF6 is responsible for
transcriptional induction of a cohort of ER proteins which includes
chaperones, folding enzymes and ER-associated degradation
(ERAD) components [18]. A number of other bZIP transcription
factors that localize to the ER have been identified including
OASIS, CREBH, LUMAN/CREB3, CREB4 and BBF2H7 [12]."

p(HGNC:ATF6) -> a(MESH:"Molecular Chaperones")
p(HGNC:ATF6) -> bp(GO:"ERAD pathway")
p(MESH:"OASIS protein, rat",loc(GO:"Golgi apparatus"))
p(MESH:"CREBH protein, rat",loc(GO:"Golgi apparatus"))
p(MESH:"CREB3 protein, human",loc(GO:"Golgi apparatus"))
p(HGNC:CREB3L2,loc(GO:"Golgi apparatus"))
#BBF2H7= CREB3L2

SET Evidence = "OASIS has been shown to be
involved in bone formation [19] and BBF2H7 targets Sec23a,
which governs protein transport from the ER to the Golgi."

p(MESH:"OASIS protein, rat") -> bp(MESH:"Bone Development")
p(HGNC:CREB3L2) reg p(HGNC:SEC23A)
p(HGNC:SEC23A) -> tloc(a(MESH:Proteins),fromLoc(GO:"endoplasmic reticulum"),toLoc(GO:"Golgi apparatus"))

SET Evidence = "The most salient feature of the UPR is to increase the transactivation
function of an array of bZIP transcription factors such as
ATF6, ATF4, ATF3, NRF2 and XBP1. Once activated, these transcription
factors co-ordinate transcriptional induction of ER chaperones
and genes involved in ERAD, to enhance the protein folding
capacity of the cell and to decrease the unfolded protein load
of the ER, respectively [6]."

bp(MESH:"Unfolded Protein Response") -> act(p(HGNC:ATF6))
bp(MESH:"Unfolded Protein Response") -> act(p(HGNC:ATF4))
bp(MESH:"Unfolded Protein Response") -> act(p(HGNC:ATF3))
bp(MESH:"Unfolded Protein Response") -> act(p(MESH:"Nrf2a protein, zebrafish"))
bp(MESH:"Unfolded Protein Response") -> act(p(HGNC:XBP1))
p(HGNC:XBP1) -> a(MESH:"Molecular Chaperones")
p(MESH:"Nrf2a protein, zebrafish") -> a(MESH:"Molecular Chaperones")
p(HGNC:ATF3) -> a(MESH:"Molecular Chaperones")
p(HGNC:ATF4) -> a(MESH:"Molecular Chaperones")
p(HGNC:ATF6) -> a(MESH:"Molecular Chaperones")

SET Evidence = "Overall, it is thought that the apoptotic signals generated from
excessive activation of the UPR converge on the mitochondria
resulting in opening of the permeability transmembrane pore
(PTP) and loss of mitochondrial membrane potential (delta psi m) with
consequent release of pro-apoptotic factors, including
cytochrome c (Fig. 2)."

bp(MESH:"Unfolded Protein Response") -> bp(GO:"apoptotic signaling pathway")
bp(GO:"apoptotic signaling pathway") -| complex(GO:"mitochondrial permeability transition pore complex")
complex(GO:"mitochondrial permeability transition pore complex") => bp(GO:"regulation of mitochondrial membrane potential")
bp(GO:"negative regulation of mitochondrial membrane potential") -> bp(GO:"release of cytochrome c from mitochondria")

SET Evidence = "In conjunction with apoptotic protease activating
factor 1 (Apaf-1), pro-caspase-9 and cytochrome c form
the apoptosome [22, 23]. The apoptosome is a complex consisting
of adaptor proteins, which mediate the activation of initiator
caspases at the onset of apoptosis. Specifically it processes procaspase-
9 to its active form, which then activates downstream
effector caspases including caspase-3, -7 and -6 [24], leading to
apoptosis."

composite(p(HGNC:APAF1),p(HGNC:CASP9),p(HGNC:CYC1)) -> a(MESH:Apoptosomes)
p(HGNC:CASP9) -> p(HGNC:CASP3)
p(HGNC:CASP9) -> p(HGNC:CASP7)
p(HGNC:CASP9) -> p(HGNC:CASP6)
composite(p(HGNC:CASP6),p(HGNC:CASP7),p(HGNC:CASP3)) -> bp(MESH:Apoptosis)


SET Evidence = "Caspase-4
has high homology to caspase-12 and its expression and cleavage
is increased during ER stress [2]."

bp(MESH:"Endoplasmic Reticulum Stress") -> p(HGNC:CASP4)

SET Evidence = "Caspase-2 is cleaved in
response to excessive ER stress. Inhibition of caspase-2 confers
resistance to ER stress–induced apoptosis [28]."

bp(MESH:"Endoplasmic Reticulum Stress") -> p(HGNC:CASP2)
p(HGNC:CASP2) -> bp(MESH:Apoptosis)

SET Evidence = "Experimental evidence supports a role for
the BCL-2 family in ER stress–induced apoptosis. Overexpression
of BCL-2 can protect cells from ER stress–induced cell death [29].
Also, many of the BCL-2 family members associate with the ER
where they function to regulate Ca2+ homeostasis."

bp(MESH:"Endoplasmic Reticulum Stress") -- p(HGNC:BCL2)
p(HGNC:BCL2) -| bp(MESH:Apoptosis)
p(HGNC:BCL2) -- bp(GO:"calcium ion homeostasis")

SET Evidence = "Pro-apoptotic members BAX and BAK cause
mitochondrial outer membrane permeabilization (MOMP) and formation
of the PTP, in a process which ultimately leads to release
of pro-apoptotic molecules such as second mitochondria-derived
activator of caspases and cytochrome c. Pro-apoptotic family
members sequester anti-apoptotic members such as BCL-2,
thereby tipping the balance towards death"

p(HGNC:BAX) -> bp(GO:"mitochondrial outer membrane permeabilization")
p(HGNC:BAK1) -> bp(GO:"mitochondrial outer membrane permeabilization")
p(HGNC:BAX) -> complex(GO:"mitochondrial permeability transition pore complex")
p(HGNC:BAK1) -> complex(GO:"mitochondrial permeability transition pore complex")
complex(GO:"mitochondrial permeability transition pore complex") -> bp(GO:"release of cytochrome c from mitochondria")
p(HGNC:BCL2) -| bp(MESH:Apoptosis)

SET Evidence = "ER stress induces expression of the BH3-only proteins BIM,
PUMA and NOXA and can also down-regulate expression of BCL-
2 and cause cell death. Overexpression of PUMA induced apoptosis,
whereas PUMA-/- cells were resistant to ER stress–induced
apoptosis [31]."

bp(MESH:"Endoplasmic Reticulum Stress") -> complex(GO:"BIM-BCL-2 complex")
bp(MESH:"Endoplasmic Reticulum Stress") -> complex(GO:"PUMA-BCL-2 complex")
bp(MESH:"Endoplasmic Reticulum Stress") -> complex(GO:"NOXA-BCL-2 complex")
bp(MESH:"Endoplasmic Reticulum Stress") -| p(HGNC:BCL2)
complex(GO:"PUMA-BCL-2 complex") -> bp(MESH:Apoptosis)

SET Evidence = "CHOP, also known as growth arrest and DNA damage-inducible
gene 153 (GADD153), is a member of the C/EBP family that heterodimerizes
with other members of the C/EBP transcription factor
family. This 29 kD factor is expressed at low levels in
unstressed cells and is strongly induced in response to ER stress
[33]. It can be induced by all three arms of the UPR. It has been
shown that mouse embryonic fibroblasts derived from CHOP-/-
animals exhibited significantly less cell death when challenged
with ER stress–inducing agents compared to wild type [33]."

complex(GO:"CHOP-C/EBP complex") -> bp(MESH:"DNA Damage")
bp(MESH:"Endoplasmic Reticulum Stress") -> complex(GO:"CHOP-C/EBP complex")
bp(MESH:"Unfolded Protein Response") -> complex(GO:"CHOP-C/EBP complex")
complex(GO:"CHOP-C/EBP complex") -> bp(MESH:"Cell Death")

SET Evidence = "CHOP’s pro-apoptotic effects are linked to down-regulation of
BCL-2 and enhanced production of reactive oxygen species (ROS)
[34]. Caspase-11 has been reported to act downstream of
CHOP to induce cell death by activating death effector caspases-1 and -3 [35]."

complex(GO:"CHOP-C/EBP complex") -| p(HGNC:BCL2)
complex(GO:"CHOP-C/EBP complex") -> a(MESH:"Reactive Oxygen Species")
complex(GO:"CHOP-C/EBP complex") -> p(MESH:"Casp11 protein, mouse")
p(MESH:"Casp11 protein, mouse") -> p(HGNC:CASP1)
p(MESH:"Casp11 protein, mouse") -> p(HGNC:CASP3)
p(HGNC:CASP1) -> bp(MESH:"Cell Death")
p(HGNC:CASP3) -> bp(MESH:"Cell Death")

SET Evidence = "CHOP can also bind to the promoter region of proapoptotic
BIM, increasing its expression as well as transcriptionally
down-regulating BCL-2 and in this way it induces cell death."

complex(GO:"CHOP-C/EBP complex") -> complex(GO:"BIM-BCL-2 complex")
complex(GO:"CHOP-C/EBP complex") -| r(HGNC:BCL2)
complex (GO:"CHOP-C/EBP complex") -> bp(MESH:"Cell Death")

SET Evidence = "Accumulating evidence indicates that autophagy may
confer neuroprotection by enhancing clearance of soluble and
aggregated misfolded proteins and conversely, deregulation of
autophagy may lead to neurodegeneration [37]."

bp(GO:autophagy) -> bp(MESH:Neuroprotection)
bp(GO:autophagy) -| p(MESH:Proteins , pmod(HBP:misfolded))

SET Evidence = "misfolded or
unassembled proteins are retained in the ER and subsequently
degraded by ERAD. In the ERAD pathway, ER-resident chaperones
recognize the misfolded proteins and ER reductases remove disulfide
bonds in these proteins to facilitate retrograde transport to the
cytosol where they are degraded by the proteasome [38]."

SET CellStructure = "Endoplasmic Reticulum"

bp(GO:"ERAD pathway") -> deg(p(MESH:Proteins , pmod(HBP:misfolded)))
complex(p(MESH:Proteins , pmod(HBP:misfolded)),p(MESH:"Molecular Chaperones"))
p(MESH:Oxidoreductases) -> tloc(p(MESH:Proteins , pmod(HBP:misfolded)),fromLoc(GO:"endoplasmic reticulum"),toLoc(GO:cytoplasm))

UNSET CellStructure

SET CellStructure = "Cytoplasm"

p(HGNCGENEFAMILY:Proteasome) -| deg(p(MESH:Proteins,pmod(HBP:misfolded)))

UNSET CellStructure

SET Evidence = "To
remove the aggregates of misfolded proteins that cannot be
degraded by the ERAD, the UPR activates autophagy [11]. During
ER stress–induced autophagy, portions of the ER and protein
aggregates are engulfed in double-membrane structures called
autophagosomes and delivered to lysosomes for degradation
[39]. The initiation of autophagy requires activation of the
ATG1/ULK induction complex [40]. This complex is essential for
the formation of a small double membrane structure known as a
phagophore, which will eventually mature into a double-membraned
vacuole termed an autophagosome [41]."

bp(MESH:"Unfolded Protein Response") -> bp(GO:autophagy)
a(MESH:Autophagosomes) -> p(MESH:Proteins , pmod(HBP:misfolded))
composite(a(MESH:Autophagosomes),a(MESH:Lysosomes)) -> deg(a(MESH:Autophagosomes))
complex(GO:"Atg1/ULK1 kinase complex") -> bp(GO:autophagy)
complex(GO:"Atg1/ULK1 kinase complex") -> a(GO:phagophore)
a(GO:phagophore) -> a(MESH:Autophagosomes)

SET Evidence = "Mammalian target of rapamycin (mTOR) is a key kinase in the
regulation of autophagy and activated mTOR inhibits autophagy.
mTOR exists in two different complex forms, mTOR complex I
(mTORC1) and mTOR complex II [42]."

p(HGNC:MTOR) reg bp(GO:autophagy)
act(p(HGNC:MTOR)) -| bp(GO:autophagy)
p(HGNC:MTOR) hasComponents list(p(HGNCGENEFAMILY:"MTOR complex 1"),p(HGNCGENEFAMILY:"MTOR complex 2"))

SET Evidence = "AMP-activated protein
kinase (AMPK) negatively regulates mTOR via the tuberous sclerosis
complex (TSC). AMPK is activated in response to many
stresses such as hypoxia, starvation, heat shock, ischaemia and
ER stress [43]. During ER stress, Ca2+ flux from the ER lumen
to the cytosol can lead to the activation of Ca2+/calmodulindependent
protein kinase kinase-beta (CaMKK-beta) [44]. CaMKK-beta
activates AMPK, in turn inhibiting mTOR and activating the
ATG/ULK induction complex. The inhibition of mTOR during ER
stress via AMPK is an important event during ER stress for the
induction of autophagy [45]."

p(HGNC:PRKAA1) neg p(HGNC:MTOR)
p(HGNC:PRKAA1) reg p(HGNCGENEFAMILY:"TSC complex")
p(HGNCGENEFAMILY:"TSC complex") reg p(HGNC:MTOR)
path(MESH:Hypoxia) -> act(p(HGNC:PRKAA1))
path(MESH:Starvation) -> act(p(HGNC:PRKAA1))
bp(MESH:"Heat-Shock Response") -> act(p(HGNC:PRKAA1))
bp(MESH:"Endoplasmic Reticulum Stress") -> act(p(HGNC:PRKAA1))
bp(MESH:"Endoplasmic Reticulum Stress") -> tloc(a(CHEBI:"calcium cation"),fromLoc(GO:"endoplasmic reticulum"),toLoc(GO:cytoplasm))
a(CHEBI:"calcium cation",loc(GO:cytoplasm)) -> act(p(HGNC:CAMK2B))
p(HGNC:CAMK2B) -> act(p(HGNC:PRKAA1))
p(HGNC:CAMK2B) -> act(complex(GO:"Atg1/ULK1 kinase complex"))
p(HGNC:MTOR) -| bp(GO:autophagy)

SET Evidence = "Different conditions that induce ER stress lead to induction of
autophagy [39]. Both the PERK/eIF2alpha and IRE1 arms of the UPR
have been implicated in the regulation of autophagy [46, 47].
Treatment of cells with tunicamycin, thapsigargin or proteasome
inhibitors induces autophagy in an IRE1-dependent manner [47]."

bp(MESH:"Endoplasmic Reticulum Stress") -> bp(GO:autophagy)
p(MESH:"PERK kinase") reg bp(GO:autophagy)
p(HGNC:ERN1) reg bp(GO:autophagy)
a(CHEBI:tunicamycin) -> bp(GO:autophagy)
a(CHEBI:thapsigargin) -> bp(GO:autophagy)
a(MESH:"Proteasome Inhibitors") -> bp(GO:autophagy)

SET Evidence = "The pro-autophagic actions of IRE1 seem to rely on the ability of
IRE1 to activate JNK. JNK has been shown to regulate autophagy
through BCL-2 phosphorylation, which disrupts its interaction
with Beclin-1 [48]."

p(HGNC:ERN1) -> bp(GO:"JNK cascade")
bp(GO:"JNK cascade") reg bp(GO:autophagy)
bp(GO:"JNK cascade") -> p(HGNC:BCL2,pmod(Ph))
p(HGNC:BCL2,pmod(Ph)) -| composite(a(MESH:"Beclin-1"),p(HGNC:BCL2))

SET Evidence = "Intriguingly, it has been recently shown that
XBP1 ablation increases autophagy and protects cells from the
toxicity induced by aggregates of the mutant form of enzyme
superoxide dismutase 1 (mSOD1) in a model of ALS [49]."
SET Disease = "amyotrophic lateral sclerosis"

p(HGNC:XBP1) -| bp(GO:autophagy)
p(HGNC:XBP1) -> p(MESH:"Superoxide Dismutase-1",pmod(MESH:"Mutant Proteins"))
p(MESH:"Superoxide Dismutase-1",pmod(MESH:"Mutant Proteins")) -> bp(HBP:"protein aggregation")
bp(HBP:"protein aggregation") -> bp(MESH:"Cell Death")

UNSET Disease

SET Evidence = "It has been shown that PERK signalling is also required for
autophagy following expression of the Huntington’s disease-associated
expanded polyglutamine repeats, which is a result of expansion
of a CAG trinucleotide repeat and extension of a polyglutamine
tract at the N-terminus of the encoded, ubiquitously
expressed protein called huntingtin [50]. PERK-eIF2alpha–dependent
ATG12 up-regulation is required for induction of autophagy in
response to polyglutamine protein accumulation [51]."

SET Disease = "Huntington's disease"

p(MESH:"Huntingtin Protein",pmod(MESH:polyglutamine)) -> p(MESH:"PERK kinase")
p(MESH:"PERK kinase") -> bp(GO:autophagy)
p(MESH:"PERK kinase") -> p(HGNC:ATG12)
composite(p(HGNC:ATG12),p(MESH:"Huntingtin Protein",pmod(MESH:polyglutamine))) -> bp(GO:autophagy)

UNSET Disease

SET Evidence = "PERKdependent
transcription factors ATF4 and CHOP have been shown
to induce transcriptional activation of MAP1LC3B and ATG5 during
hypoxia [52]. Further, eIF2alpha-dependent up-regulation of the
transcription factors p8, ATF4, CHOP and TRB3 is required for
induction of autophagy [53]."

SET MeSHDisease = "Hypoxia"

p(MESH:"PERK kinase") -> complex(GO:"CHOP-C/EBP complex")
p(MESH:"PERK kinase") -> p(HGNC:ATF4)
p(HGNC:ATF4) -> p(HGNC:MAP1LC3B)
p(HGNC:ATF4) -> p(HGNC:ATG5)
complex(GO:"CHOP-C/EBP complex") -> p(HGNC:MAP1LC3B)
complex(GO:"CHOP-C/EBP complex") -> p(HGNC:ATG5)

p(HGNC:EIF2A) -> p(HGNC:NUPR1)
p(HGNC:EIF2A) -> p(HGNC:ATF4)
p(HGNC:EIF2A) -> complex(GO:"CHOP-C/EBP complex")
complex(GO:"CHOP-C/EBP complex") -> bp(GO:autophagy)
p(HGNC:NUPR1) -> bp(GO:autophagy)
p(HGNC:ATF4) -> bp(GO:autophagy)

UNSET MeSHDisease

SET Evidence = "Protein folding in vivo is an inefficient process and is aided by
molecular chaperones, which increase folding efficiency. In addition,
degradation systems such as ERAD, the endo-lysosomal
pathway, the proteasome and autophagy rapidly remove misfolded
proteins.[38]."

p(MESH:"Molecular Chaperones") -> bp(GO:"protein folding")
bp(GO:"ERAD pathway") -> deg(p(MESH:Proteins , pmod(HBP:misfolded)))
bp(GO:"lysosomal protein catabolic process") -> deg(p(MESH:Proteins , pmod(HBP:misfolded)))
p(HGNCGENEFAMILY:Proteasome) -> deg(p(MESH:Proteins , pmod(HBP:misfolded)))
bp(GO:autophagy) -> deg(p(MESH:Proteins,pmod(HBP:misfolded)))

SET Evidence = "Neurons are
heavily reliant on the removal of misfolded proteins to maintain
homeostasis [56] and accumulation of misfolded proteins is a
characteristic feature of many neurodegenerative diseases including
AD [57], PD [58], transmissible spongiform encephalopathy
[59] and also acute neurodegenerative disorders such as
traumatic brain injury [60] and cerebral ischaemia [61]."

p(MESH:Proteins , pmod(HBP:misfolded)) -> bp(HBP:"protein aggregation")

SET Cell = "neuron"
bp(HBP:"protein aggregation") -- path(MESH:"Neurodegenerative Diseases")
bp(HBP:"protein aggregation") -- path(MESH:"Alzheimer Disease")
bp(HBP:"protein aggregation") -- path(MESH:"Parkinson Disease")
bp(HBP:"protein aggregation") -- path(MESH:"Amyloidosis, Cerebral, with Spongiform Encephalopathy")
bp(HBP:"protein aggregation") -- path(MESH:"Brain Injuries, Traumatic")

UNSET Cell

SET Evidence = "Because
misfolded proteins trigger the UPR, this has prompted several
groups to investigate the involvement of ER stress in neurodegenerative
disorders."

p(MESH:Proteins , pmod(HBP:misfolded)) -> bp(MESH:"Unfolded Protein Response")
bp(MESH:"Endoplasmic Reticulum Stress") -- path(MESH:"Neurodegenerative Diseases")

SET Disease = "Alzheimer's disease"

SET Evidence = "AD is characterized by the presence of senile plaques with a core
of extracellular beta-amyloid protein and intracellular neurofibrillary
tangles containing hyperphosphorylated tau [62]."

path(MESH:"Alzheimer Disease") -- path(HP:"Senile plaques")
path(HP:"Senile plaques") -> a(HBP:"amyloid-beta aggregates")
path(HP:"Senile plaques") -> a(MESH:"Neurofibrillary Tangles")

a(MESH:"Neurofibrillary Tangles") -> p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))

SET Evidence = "These pathological
hallmarks are accompanied by ballooning of neurites and
neuronal loss. Beta-Amyloid is cleaved from its precursor protein,
amyloid precursor protein (APP), through the action of Gamma-secretase
(BACE) and gamma-secretase that results in the production of
beta-amyloid protein. Gamma-Secretase can produce beta-amyloid of different
lengths, most notably Abeta40 and Abeta42 [63]."

bp(GO:"neuron death") prognosticBiomarkerFor path(MESH:"Alzheimer Disease")
p(HGNC:APP) -> a(CHEBI:"amyloid-beta")
p(HGNC:BACE1) -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))
complex(FPLX:"Gamma_secretase") -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))
complex(FPLX:"Gamma_secretase") -> p(HGNC:APP, frag(672_713))
#frag(672_713) is Abeta42
complex(FPLX:"Gamma_secretase") -> p(HGNC:APP, frag(672_711))
#frag(672_711) is Abeta40

SET Evidence = "The Abeta42 form of
the protein is the most amyloidogenic and is prevalent in senile
plaques. The extracellular deposition of senile plaques may precede
the development of neurofibrillary tangles, and has been the
subject of much investigative interest [64]."

p(HGNC:APP, frag(672_713)) isA a(MESH:"Amyloidogenic Proteins")
p(HGNC:APP, frag(672_713)) -> path(HP:"Senile plaques")

SET CellStructure = "Extracellular Space"
path(HP:"Senile plaques") -> a(MESH:"Neurofibrillary Tangles")
UNSET CellStructure

SET Evidence= "Although the evolution
of senile plaques is closely linked to the development of the neurodegeneration
and onset and progression of symptoms in AD,
there is arguably not a direct causal relationship between beta-amyloid
deposition and neurodegeneration.[65]."

path(HP:"Senile plaques") -- path(MESH:"Neurodegenerative Diseases")
a(HBP:"amyloid-beta aggregates") -- path(HBP:Neurodegeneration)

SET Evidence = "Neurofibrillary
tangles contain twisted pairs of helical filaments formed by the
aggregation of hyperphosphorylated tau [69]. Hyperphosphorylation
of tau impairs its ability to interact with cytoskeletal microfilaments,
resulting in disorganization of the cytoskeleton."

a(MESH:"Neurofibrillary Tangles") -> p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -| bp(GO:"cytoskeleton organization")

SET Evidence = "The presenilin protein is a component of the Gamma-secretase complex,
which is widely expressed in the ER and Golgi apparatus
[70]. Presenilin mutations are linked with the majority of early
onset forms of AD [69] with presenilin 1 (PS1) being more highly
expressed than PS2, and mutations in the genes coding for a presenilin
protein reduce the average age of onset of AD [71]."

SET CellStructure = {"Endoplasmic Reticulum","Golgi Apparatus"}

complex(FPLX:"Gamma_secretase") hasComponent p(PFAM:Presenilin)
p(PFAM:Presenilin,var("?")) -- path(MESH:"Alzheimer Disease")
path(MESH:"Alzheimer Disease") -> p(MESH:"Presenilin-1")

UNSET CellStructure

SET Evidence = "Evidence also demonstrates that PS1 mutations render cells more
susceptible to apoptosis induced by a range of insults [72].
Presenilin is an integral membrane protein that is located primarily
in ER and has been shown to influence the activity of two of
the key ER stress sensors IRE1 and PERK (Fig. 3A). Presenilin
mutations reduced phosphorylation of PERK and eIF2alpha, resulting
in failure to attenuate protein synthesis causing protein accumulation
in the ER [73]."

p(MESH:"Presenilin-1",var("?")) -> bp(MESH:Apoptosis)
p(PFAM:Presenilin) isA complex(GO:"ER membrane protein complex")
p(PFAM:Presenilin) -- p(HGNC:ERN1)
p(PFAM:Presenilin) -- p(MESH:"PERK kinase")
p(PFAM:Presenilin) -| p(MESH:"PERK kinase",pmod(Ph))
p(PFAM:Presenilin) -| p(HGNC:EIF2A,pmod(Ph))

SET CellStructure = "Endoplasmic Reticulum"
p(MESH:"PERK kinase",pmod(Ph)) -> bp(HBP:"protein aggregation")
p(HGNC:EIF2A,pmod(Ph)) -> bp(HBP:"protein aggregation")
UNSET CellStructure

SET Evidence = "Mutant PS1 is also known to bind, and inhibit
IRE1, thereby reducing, or delaying, the transcription of ER chaperones
such as GRP78 which has consistently been found to
be down-regulated in AD [73]."

p(MESH:"Presenilin-1",var("?")) -| p(HGNC:ERN1)
p(MESH:"Presenilin-1",var("?")) -| r(MESH:"molecular chaperone GRP78")
path(MESH:"Alzheimer Disease") neg p(MESH:"molecular chaperone GRP78")

SET Evidence = "In fact, the increased sensitivity
of neurons to ER stress is attributed to the decreased levels of
GRP78 mRNA. We have recently demonstrated that modulation of
IRE1 activity and the resultant effect on XBP1 splicing can regulate
cell fate [13]. Therefore, it is possible that mutant PS1, acting
on IRE1, can reduce or delay splicing of XBP1, thus switching signalling
to a pro-death response."

bp(MESH:"Endoplasmic Reticulum Stress") -- r(MESH:"molecular chaperone GRP78")
p(HGNC:ERN1) reg p(HGNC:XBP1)
p(MESH:"Presenilin-1",var("?")) -| r(HGNC:XBP1)

SET Evidence = "XBP1 can bind to the promotors of at least
one key component of the Gamma-secretase complex, namely UBQLN1
that is a negative regulator of the Gamma-secretase complex. It has been
suggested that UBQLN1 may control APP trafficking and thus the
generation of Abeta. XBP1 can also bind to the promoter of genes
involved in APP trafficking and processing as well as genes
involved in AD pathogenesis, thereby implicating XBP1 in AD
(Fig. 3A) [75]."

p(HGNC:XBP1) reg g(FPLX:"Gamma_secretase")
p(HGNC:UBQLN1) neg complex(FPLX:"Gamma_secretase")
p(HGNC:UBQLN1) reg p(HGNC:APP)
p(HGNC:UBQLN1) reg a(CHEBI:"amyloid-beta")
p(HGNC:XBP1) reg p(HGNC:APP)
p(HGNC:XBP1) -- path(MESH:"Alzheimer Disease")

SET Evidence = "It is possible therefore that reduced expression of
XBP1 in AD influences the generation of Abeta and affects cell fate
decisions."

p(HGNC:XBP1) neg path(MESH:"Alzheimer Disease")
p(HGNC:XBP1) -- a(CHEBI:"amyloid-beta")

SET Evidence = "Ca2+ homeostasis is important for proper functioning of ER
chaperones and protein folding. Alterations in Ca2+ homeostasis
lead to reduced chaperone activity, protein misfolding and initiation
of the UPR. Abeta peptides have been shown to cause depletion
of ER Ca2+ stores by triggering release of Ca2+ into the cytoplasm."

bp(GO:"calcium ion homeostasis") -> p(MESH:"Molecular Chaperones",loc(GO:"endoplasmic reticulum"))
bp(GO:"calcium ion homeostasis") -> act(p(MESH:"Molecular Chaperones",loc(GO:"endoplasmic reticulum")))
bp(GO:"calcium ion homeostasis") -> bp(GO:"protein folding")
bp(GO:"calcium ion homeostasis") -> bp(MESH:"Unfolded Protein Response")
a(CHEBI:"amyloid-beta") -| tloc(a(CHEBI:"calcium cation"),fromLoc(GO:"endoplasmic reticulum"),toLoc(GO:cytoplasm))

SET Evidence = "In addition, PS1 mutations increase Abeta42 levels and have also been shown to impair ER Ca2+ homeostasis. Cells containing
human PS1 mutations exhibit increased Ca2+ release from intracellular
stores in response to stress in vitro [76]. Therefore, current
studies suggest that there is a perturbed UPR response in AD."

p(MESH:"Presenilin-1",var("?")) -> p(HGNC:APP, frag(672_713))

SET CellStructure = "Endoplasmic Reticulum"
p(MESH:"Presenilin-1",var("?")) -| bp(GO:"calcium ion homeostasis")
UNSET CellStructure

p(MESH:"Presenilin-1",var("?")) -> tloc(a(CHEBI:"calcium cation"),fromLoc(GO:"endoplasmic reticulum"),toLoc(GO:cytoplasm))
path(MESH:"Alzheimer Disease") -| bp(MESH:"Unfolded Protein Response")

SET Evidence = "Autophagosomes and precursor autophagosomes (autophagic
vacuoles) are abundant in swollen and dystrophic neurites from
human AD brains, suggesting that the later stages of autophagy or
the removal of autophagic vacuoles may be deregulated [77]."

path(MESH:"Alzheimer Disease") -- a(GO:autophagosome)
path(MESH:"Alzheimer Disease") neg bp(GO:autophagy)

UNSET Disease

SET Evidence = "PD is characterized by motor symptoms such as dyskinesia, muscle
rigidity, postural instability and resting tremor. In addition,
olfactory sensory loss and gastrointestinal disturbance are common
in PD sufferers. Degeneration of the dopaminergic neurones
of the nigrostriatal pathway and the presence of alpha-synuclein containing
Lewy bodies and Lewy neurites are characteristic of the
disease [78]."

path(HP:Dyskinesia) prognosticBiomarkerFor path(MESH:"Parkinson Disease")
path(MESH:"Muscle Rigidity") prognosticBiomarkerFor path(MESH:"Parkinson Disease")
path(HP:"Postural instability") prognosticBiomarkerFor path(MESH:"Parkinson Disease")
path(HP:"Resting tremor") prognosticBiomarkerFor path(MESH:"Parkinson Disease")
path(DOID:"olfactory nerve disease") prognosticBiomarkerFor path(MESH:"Parkinson Disease")
path(HBP:"Gastrointestinal dysfunctions") prognosticBiomarkerFor path(MESH:"Parkinson Disease")
path(MESH:"Parkinson Disease") -| a(MESH:"Dopaminergic Neurons")
a(GO:"Lewy body") -> p(MESH:"alpha-Synuclein")
a(GO:"Lewy neurite") biomarkerFor path(MESH:"Parkinson Disease")
a(GO:"Lewy body") biomarkerFor path(MESH:"Parkinson Disease")

SET Disease = "Parkinson's disease"

SET Evidence = "Within the Lewy plaques, diffuse deposits of misfolded
alpha-synuclein form the core in association with other proteins,
notably components of the ubiquitin–proteasome system
[3]."

a(GO:"Lewy body") -> p(MESH:"alpha-Synuclein",pmod(HBP:misfolded))

SET Evidence = "Genes associated with autosomal
dominant PD include alpha-synuclein, ubiquitin carboxyl-terminal
esterase L1 (UCHL1) and leucine-rich repeat kinase 2 (LRRK2);
autosomal recessive PD genes include Parkinson protein 2
(PARK2/Parkin); PTEN-induced kinase 1 (PARK6/PINK1); PD(autosomal recessive, early onset) 7 (PARK7/DJ-1) and PD (autosomal
recessive) 9; (PARK9/ATP13A2) [80]."

path(DOID:"autosomal dominant Parkinson's disease 1") -- g(MESH:"alpha-Synuclein")
path(DOID:"autosomal dominant Parkinson's disease 1") -- g(MESH:"UCHL1 protein, human")
path(DOID:"autosomal dominant Parkinson's disease 1") -- g(MESH:"LRRK2 protein, human")
path(DOID:"autosomal recessive Parkinson's disease 14") -- g(MESH:"parkin protein")
path(DOID:"autosomal recessive Parkinson's disease 14") -- g(HGNC:PINK1)
path(DOID:"autosomal recessive Parkinson's disease 14") -- g(HGNC:PARK7)
path(DOID:"autosomal recessive Parkinson's disease 14") -- g(HGNC:ATP13A2)

SET Evidence = "Alpha-Synuclein is expressed in synaptic vesicles and on cell membranes
in nervous tissue. Post-translational modification of
alpha-synuclein such as phosphorylation and nitrosylation can cause
misfolding and subsequent deposition of the protein [78]. There is a suggestion
that alpha-synuclein, although normally contained within
cells, may be released upon cell death [78]."

SET Anatomy = "nervous system"
SET CellStructure = {"Synaptic Vesicles","Cell Membrane"}

p(MESH:"alpha-Synuclein")
p(MESH:"alpha-Synuclein",pmod(Ph)) -> p(MESH:"alpha-Synuclein",pmod(HBP:misfolded))
p(MESH:"alpha-Synuclein",pmod(NO)) -> p(MESH:"alpha-Synuclein",pmod(HBP:misfolded))
p(MESH:"alpha-Synuclein",pmod(HBP:misfolded)) -> a(HBP:"alpha-synuclein aggregates")
bp(MESH:"Cell Death") -> tloc(p(MESH:"alpha-Synuclein"),fromLoc(GO:"intracellular"), toLoc(GO:"extracellular space"))

UNSET CellStructure
UNSET Anatomy

SET Evidence = "Missense mutations in the gene coding for
alpha-synuclein cause dominant familial PD. The A53T mutation is
associated with UPR activation as evidenced by increased expression
of CHOP and GRP78 and increased phosphorylation of eIF2alpha,
suggesting the UPR is active in these cells (Fig. 3B) [80].Inhibition of phosphorylation of eIF2alpha protected the A53T alpha-
synuclein-overexpressing cells from cell death, suggesting that the
activated UPR was shifting the balance towards apoptosis [80]."

p(MESH:"alpha-Synuclein",var("?")) -> path(DOID:"autosomal dominant Parkinson's disease 1")
p(MESH:"alpha-Synuclein",var("p.Ala53Thr")) -> bp(MESH:"Unfolded Protein Response")
bp(MESH:"Unfolded Protein Response") -> complex(GO:"CHOP-C/EBP complex")
bp(MESH:"Unfolded Protein Response") -> p(MESH:"molecular chaperone GRP78")
bp(MESH:"Unfolded Protein Response") -> p(HGNC:EIF2A, pmod(Ph))
composite(p(HGNC:EIF2A, pmod(Ph)),p(MESH:"alpha-Synuclein",var("p.Ala53Thr"))) -> bp(MESH:"Cell Death")
bp(MESH:"Unfolded Protein Response") -> bp(MESH:Apoptosis)

SET Evidence = "LRRK2 mutations also cause dominant familial PD, and may
also account for a number of previously considered sporadic
cases of PD [81,82]. Mutations in LRRK2 cause impairment of
protein degradation pathways with ageing [82]. This can lead to
accumulation of alpha-synuclein and ubiquitinated proteins,
impairment of the autophagy-lysosomal pathway, accumulation
of oxidised proteins, an inflammatory response and increased
apoptosis [82]."

p(HGNC:LRRK2,var("?")) -> path(DOID:"autosomal dominant Parkinson's disease 1")
p(HGNC:LRRK2,var("?")) -| bp(GO:"protein catabolic process")
p(HGNC:LRRK2,var("?")) -> a(HBP:"alpha-synuclein aggregates")
p(HGNC:LRRK2,var("?")) -> a(MESH:"Ubiquitinated Proteins")
p(HGNC:LRRK2,var("?")) -| bp(GO:"lysosomal protein catabolic process")
p(HGNC:LRRK2,var("?")) -| bp(GO:"regulation of protein oxidation")
p(HGNC:LRRK2,var("?")) -> bp(GO:"inflammatory response")
p(HGNC:LRRK2,var("?")) -> bp(MESH:Apoptosis)

SET Evidence = "UCH-L1 mutations are linked to autosomal-dominant PD, however
the mechanism by which it caused the disease is unclear, with
conflicting evidence reported for its in vivo functions. Recent studies
reveal a role for UCH-L1 in chaperone-mediated autophagy
(CMA) and mutant UCH-L1 was shown to inhibit CMA-mediated
removal of alpha-synuclein [83]."

p(HGNC:UCHL1,var("?")) -- path(DOID:"autosomal dominant Parkinson's disease 1")
p(HGNC:UCHL1) -- bp(GO:"chaperone-mediated autophagy")
p(HGNC:UCHL1,var("?")) -| bp(GO:"chaperone-mediated autophagy")

SET Evidence = "Several genetic mutations have been linked to the recessive
form of PD including Parkin, an E3 ligase which forms part of the
cascade reaction which targets misfolded proteins for degradation
by the proteasome [84]. Mutations in Parkin result in loss of ubiquitin-
protein ligase activity [85, 86], which can result in the accumulation
of misfolded proteins within cells and may underpin the
development of PD in people with this genetic mutation [85, 86]."

p(MESH:"parkin protein") -- path(DOID:"autosomal recessive Parkinson's disease 14")
p(MESH:"parkin protein") -> bp(GO:"proteasomal protein catabolic process")
bp(GO:"negative regulation of ubiquitin protein ligase activity") -> p(MESH:Proteins , pmod(HBP:misfolded))
p(MESH:Proteins , pmod(HBP:misfolded)) -> bp(HBP:"protein aggregation")

SET Evidence = "Parkin has been shown to be up-regulated via AFT4, following ER
stress and this event is associated with promotion of cell survival.
A reciprocal relationship was also shown between JNK and Parkin.
In addition, it was found that CHOP could down-regulate Parkin
expression [87]."

bp(MESH:"Endoplasmic Reticulum Stress") -> p(HGNC:ATF4)
#AFT4 seems to be a typo. Replaced by ATF4
p(HGNC:ATF4) -> p(MESH:"parkin protein")
p(MESH:"parkin protein") -> bp(MESH:"Cell Survival")
bp(GO:"JNK cascade") neg p(MESH:"parkin protein")
complex(GO:"CHOP-C/EBP complex") -| p(MESH:"parkin protein")

SET Evidence = "However, prolonged stress will eventually
lead to cell death with mutant Parkin potentially tipping the balance
towards cell death [87]. More recently, the role of Parkin in
mitophagy (selective degradation of mitochondria via the
autophagy pathway) has been implicated in contributing to PD."

p(MESH:"parkin protein",var("?")) -> bp(MESH:"Cell Death")
p(MESH:"parkin protein") -- bp(GO:mitophagy)
bp(GO:mitophagy) -> p(MESH:"parkin protein")
p(MESH:"parkin protein") -- path(MESH:"Parkinson Disease")

SET Evidence = "Parkin is recruited to depolarized
mitochondria via PTEN-induced kinase-1 (PINK1) leading to
mitophagy. It is proposed that Parkin then causes ubiquitination of
voltage-dependent anion channel 1 leading to mitochondrial clearance
[88]. Interestingly, disease-associated Parkin mutations disrupted
mitophagy at distinct steps highlighting the importance of
Parkin-mediated mitophagy in PD [88]."

bp(GO:"mitochondrial depolarization") -> p(HGNC:PINK1)
p(HGNC:PINK1) -> p(MESH:"parkin protein")
p(MESH:"parkin protein") -> bp(GO:mitophagy)
p(MESH:"parkin protein") -> p(HGNCGENEFAMILY:"Voltage dependent anion channels",pmod(Ub))
p(HGNCGENEFAMILY:"Voltage dependent anion channels",pmod(Ub)) -> bp(MESH:"Mitochondrial Degradation")
p(MESH:"parkin protein",var("?")) -| bp(GO:"parkin-mediated stimulation of mitophagy in response to mitochondrial depolarization")
bp(GO:"parkin-mediated stimulation of mitophagy in response to mitochondrial depolarization") -- path(MESH:"Parkinson Disease")

SET Evidence = "Mutations in PINK1 have also been implicated in the recessive
form of PD. Apart from its role in recruiting Parkin to mitochondria
for subsequent mitophagy, PINK1 has also been implicated in
protein stability and the wild-type protein may protect cells from
oxidative stress, mitochondrial dysfunction and apoptosis [89]."

p(HGNC:PINK1,var("?")) -> path(DOID:"autosomal recessive Parkinson's disease 14")
p(HGNC:PINK1) -- bp(MESH:"Protein Stability")
p(HGNC:PINK1) -| bp(MESH:"Oxidative Stress")
p(HGNC:PINK1) -| path(HBP:"mitochondrial dysfunction")
p(HGNC:PINK1) -| bp(MESH:Apoptosis)

SET Evidence = "Mutations in DJ-1 are also linked to PD. DJ-1 has been suggested
to act as an antioxidant or redox sensor protein and defective
DJ-1 may predispose to oxidative stress and activation of the ER
stress cascade [90]."

p(HGNC:PARK7,var("?")) -- path(MESH:"Parkinson Disease")
p(HGNC:PARK7) -> bp(GO:"detection of redox state")
p(HGNC:PARK7,var("?")) -> bp(MESH:"Oxidative Stress")
p(HGNC:PARK7,var("?")) -> bp(MESH:"Endoplasmic Reticulum Stress")

SET Evidence = "Paraquat, an agricultural herbicide which is
linked to sporadic PD, can induce expression of ER stress markers
such as GRP78 and CHOP. Further investigation has revealed
that paraquat activates IRE1/ASK1/JNK leading to apoptosis [91]."

a(MESH:Paraquat) isA a(CHEBI:herbicide)
a(MESH:Paraquat) -- path(MESH:"Parkinson Disease")
a(MESH:Paraquat) -> p(MESH:"molecular chaperone GRP78")
a(MESH:Paraquat) -> complex(GO:"CHOP-C/EBP complex")
complex(GO:"CHOP-C/EBP complex") biomarkerFor bp(MESH:"Endoplasmic Reticulum Stress")
p(MESH:"molecular chaperone GRP78") biomarkerFor bp(MESH:"Endoplasmic Reticulum Stress")
a(MESH:Paraquat) -> act(p(HGNC:ERN1))
a(MESH:Paraquat) -> act(p(MESH:"ASK1 protein, S cerevisiae"))
a(MESH:Paraquat) -> bp(GO:"JNK cascade")

SET Evidence = "Therefore, significant evidence indicates that protein products
of genes mutated in PD have a role in regulating protein stability
such as alpha-synuclein (proteasome), Parkin (E3 ligase), DJ-1 (redox
sensor) and PINK1 (protein stability) (Fig. 3B)."

p(HGNCGENEFAMILY:"Parkinson disease associated genes") hasComponents list(p(MESH:"alpha-Synuclein"),p(HGNC:PARK7),p(HGNC:PINK1),p(MESH:"parkin protein"))
p(MESH:"parkin protein") reg bp(MESH:"Protein Stability")
p(HGNC:PINK1) reg bp(MESH:"Protein Stability")
p(HGNC:PARK7) reg bp(MESH:"Protein Stability")
p(MESH:"alpha-Synuclein") reg bp(MESH:"Protein Stability")
p(MESH:"parkin protein",var("?")) -| bp(MESH:"Protein Stability")
p(HGNC:PINK1,var("?")) -| bp(MESH:"Protein Stability")
p(HGNC:PARK7,var("?")) -| bp(MESH:"Protein Stability")
p(MESH:"alpha-Synuclein",var("?")) -| bp(MESH:"Protein Stability")

SET Evidence = "In addition, drugs
such as 6-hydroxydopamine (6-OHDA) and 1-methyl-4-
phenylpyridinium (MPP+) which are used to develop animal models
of PD, induce ER stress [92] (Fig. 3B). These studies therefore
implicate protein quality control and the UPR as a key function that
is disrupted in familial and sporadic PD leading to neuronal cell
death. In addition, recent evidence points to the involvement of
mitophagy influenced by the UPR playing a role in the development
of PD."

a(MESH:Hydroxydopamines) -> path(MESH:"Parkinson Disease")
a(MESH:"1-Methyl-4-phenylpyridinium") -> path(MESH:"Parkinson Disease")
a(MESH:"1-Methyl-4-phenylpyridinium") => bp(MESH:"Endoplasmic Reticulum Stress")
a(MESH:Hydroxydopamines) => bp(MESH:"Endoplasmic Reticulum Stress")
path(MESH:"Parkinson Disease") -| bp(GO:"protein quality control for misfolded or incompletely synthesized proteins")
path(MESH:"Parkinson Disease") -| bp(MESH:"Unfolded Protein Response")
path(MESH:"Parkinson Disease") -> bp(GO:"neuron death")
bp(MESH:"Unfolded Protein Response") -> bp(GO:mitophagy)
bp(GO:mitophagy) -> path(MESH:"Parkinson Disease")

UNSET Disease

SET Disease = "amyotrophic lateral sclerosis"

SET Evidence = "ALS is a progressive fatal neurodegenerative disease that principally
affects motor neurones. Pathological mechanisms
such as excitotoxicity, oxidative damage, mitochondrial dysfunction
and defective axonal transport have all been implicated as
causative factors in the apoptotic death of the motor neurones
[93, 94]"

path(MESH:"Amyotrophic Lateral Sclerosis") -- path(HBP:Neurodegeneration)
path(MESH:"Amyotrophic Lateral Sclerosis") -- a(HBP:"Motor Neurons")
path(HBP:Excitotoxicity) -> bp(GO:"motor neuron apoptotic process")
bp(MESH:"Oxidative Stress") -> bp(GO:"motor neuron apoptotic process")
path(HBP:"mitochondrial dysfunction") -> bp(GO:"motor neuron apoptotic process")
bp(GO:"axonal transport") -- bp(GO:"motor neuron apoptotic process")

SET Evidence = "Abnormal protein aggregation has also been reported in
ALS. Bunina bodies, neurofilament cytoskeletal aggregation and
deposition of aggregates of proteins such as ubiquitin, mutant superoxide dismutase 1 (mSOD1) and protein disulfide isomerase
(PDI) are characteristics of the disease [95, 96]."

path(MESH:"Amyotrophic Lateral Sclerosis") -> bp(HBP:"protein aggregation")
a(GO:"Bunina body") -- path(MESH:"Amyotrophic Lateral Sclerosis")
a(GO:"neurofilament cytoskeleton") -- path(MESH:"Amyotrophic Lateral Sclerosis")
a(CHEBI:ubiquitin) -- path(MESH:"Amyotrophic Lateral Sclerosis")
p(MESH:"Superoxide Dismutase-1",var("?")) -- path(MESH:"Amyotrophic Lateral Sclerosis")
p(HGNCGENEFAMILY:"Protein disulfide isomerases") -- path(MESH:"Amyotrophic Lateral Sclerosis")

SET Evidence = "mSOD1 misfolds, aggregates and induces the UPR in transgenic
mSOD1 mice, causing apoptosis [97] and has been implicated in
the development of ALS. The protein level of the ER chaperone,
PDI, in particular was increased, and was shown to co-localize
with aggregated mSOD1 protein [97]."

a(MESH:"Superoxide Dismutase-1") -- bp(MESH:"Unfolded Protein Response")
bp(MESH:"Unfolded Protein Response") -> bp(MESH:Apoptosis)
a(MESH:"Superoxide Dismutase-1") -- path(MESH:"Amyotrophic Lateral Sclerosis")

SET Evidence = "As previously discussed, ERAD of misfolded proteins
has been implicated in a range of neurodegenerative conditions,
including ALS. Dysfunction of ERAD, causing ER stress has been
shown to occur in mSOD1 containing motor neurones [99],
through a mechanism involving Derilin-1, an ERAD-linked protein,
subsequent ER stress–induced activation of the ASK1 pathway
and ultimately apoptosis [99]. Specifically, mSOD1 was shown to
interact with Derilin-1 causing dysregulation of ERAD leading to
ER stress–induced ASK1 activation, apoptosis and disease progression
(Fig. 3D)."

bp(GO:"ERAD pathway") -- path(MESH:"Amyotrophic Lateral Sclerosis")

SET Cell = "motor neuron"

bp(GO:"ERAD pathway") -| bp(MESH:"Endoplasmic Reticulum Stress")
bp(GO:"ERAD pathway") -| p(MESH:"derlin-1 protein, mouse")
#typo- Derilin does not exist.
p(MESH:"derlin-1 protein, mouse") -> p(MESH:"ASK1 protein, S cerevisiae")
p(MESH:"ASK1 protein, S cerevisiae") -> bp(MESH:Apoptosis)

UNSET Cell

SET Evidence = "Mutation of the vesicle-associated membrane protein/synaptobrevin-
associated protein B (VAPB), which associates with
intracellular membranes, such as ER, has been implicated in the
development of late-onset ALS [100]. It has been proposed that
development of ALS may occur due to the disruption of the UPR
caused by the mutation in VAPB, resulting in accumulation of misfolded
protein in the ER [101]."

p(HGNC:VAPB,var("?")) -- path(MESH:"Amyotrophic Lateral Sclerosis")
p(HGNC:VAPB,var("?")) -| bp(MESH:"Unfolded Protein Response")

SET CellStructure = "Endoplasmic Reticulum"
p(HGNC:VAPB,var("?")) -> bp(HBP:"protein aggregation")
UNSET CellStructure

SET Evidence = "Native VAPB has been implicated in
the UPR via the IRE1/XBP1[101], and ATF6 pathways [102], a
function that is lost in mutants which contain abnormally highly
ubiquitinated and misfolded VAPBP56S [101,102]. It was found
that both VAPB and VAPBP56S directly interact with ATF6 reducing
its ability to promote transcription of XBP1 with the mutant having
more potent activity as an ATF6 inhibitor [102]."

p(HGNC:VAPB) -- bp(MESH:"Unfolded Protein Response")
p(HGNC:VAPB) -- p(HGNC:XBP1)
p(HGNC:VAPB) -- p(HGNC:ERN1)
p(HGNC:VAPB) -- p(HGNC:ATF6)
p(HGNC:VAPB,pmod(Ub),var("p.Pro56Ser")) cnc p(HGNC:XBP1)
p(HGNC:VAPB,pmod(Ub),var("p.Pro56Ser")) cnc p(HGNC:ERN1)
p(HGNC:VAPB,pmod(Ub),var("p.Pro56Ser")) cnc p(HGNC:ATF6)
complex(p(HGNC:VAPB),p(HGNC:ATF6)) -| act(p(HGNC:ATF6))
act(p(HGNC:ATF6)) -> p(HGNC:XBP1)
complex(p(HGNC:VAPB,pmod(Ub),var("p.Pro56Ser")),p(HGNC:ATF6)) -| act(p(HGNC:ATF6))

SET Evidence = "In addition, mutations in the gene coding for the TAR DNAbinding
protein (TARBP) also known as TDP-43 protein have
recently been implicated in familial and sporadic ALS [103] and in
frontotemporal lobar degeneration (FTLD) [104]. Abnormal and
ubiquitinated TDP-43 has been identified as a key pathological
hallmark of ALS and FTLD [105]."

p(MESH:"TARBP1 protein, human") -- path(MESH:"Amyotrophic Lateral Sclerosis")
p(MESH:"TARBP1 protein, human") -- path(MESH:"Frontotemporal Lobar Degeneration")
p(MESH:"TARBP1 protein, human",var("?")) biomarkerFor path(MESH:"Amyotrophic Lateral Sclerosis")
p(MESH:"TARBP1 protein, human",var("?")) biomarkerFor path(MESH:"Frontotemporal Lobar Degeneration")
p(MESH:"TARBP1 protein, human",pmod(Ub)) biomarkerFor path(MESH:"Amyotrophic Lateral Sclerosis")
p(MESH:"TARBP1 protein, human",pmod(Ub)) biomarkerFor path(MESH:"Frontotemporal Lobar Degeneration")

SET Evidence = "UPR sensors IRE1, PERK
and ATF6 show increased expression in spinal cord from sporadic
human ALS patients [96]. However, conflicting evidence suggests
that activation of the UPR may cause ER stress–induced apoptosis
[96] or may actually be a neuroprotective response triggering
increased levels of autophagy [12]."

SET Anatomy = "spinal cord"

path(MESH:"Amyotrophic Lateral Sclerosis") -- p(HGNC:ERN1)
path(MESH:"Amyotrophic Lateral Sclerosis") -- p(MESH:"PERK kinase")
path(MESH:"Amyotrophic Lateral Sclerosis") -- p(HGNC:ATF6)
bp(MESH:"Unfolded Protein Response") -> bp(MESH:Apoptosis)
bp(MESH:"Unfolded Protein Response") -> bp(GO:autophagy)

UNSET Anatomy

UNSET Disease

SET Disease = "prion disease"

SET Evidence = "Prion diseases such as new variant Creutzfeldt Jacob’s disease
(CJD) are rare, fatal neurodegenerative diseases that are both
inheritable and infectious. CJD causes a spongiform encephalopathy,
reflecting neurodegeneration and an accumulation of abnormal
protein aggregates in diffuse synaptic plaques containing
prion protein and amyloid [84]."

path(DOID:"variant Creutzfeldt-Jakob disease") isA path(DOID:"prion disease")
path(DOID:"variant Creutzfeldt-Jakob disease") isA path(DOID:"neurodegenerative disease")
path(DOID:"variant Creutzfeldt-Jakob disease") -> path(MESH:"Spongiform Encephalopathy with Neuropsychiatric Features")
path(DOID:"variant Creutzfeldt-Jakob disease") -> bp(HBP:"protein aggregation")
bp(HBP:"protein aggregation") -> a(MESH:"Prion Proteins")
bp(HBP:"protein aggregation") -> a(HBP:"amyloid-beta aggregates"))

SET Evidence = "The mature prion protein contains 209
amino acids, and the sequence of amino acids between residues
106–126 can trigger apoptosis [86]."

p(HGNC:PRNP,frag("106_126")) -> bp(MESH:Apoptosis)

SET Evidence = "The physiological function of
PrPc is not well established. It has been linked to neuronal growth
and survival [109] and cytoprotection against a number of cellular
stresses including oxidative stress [110], TNF-induced cell death
[111], BAX-induced cell death [112] and serum deprivation in a
BAX-dependent manner [113]."

p(HGNC:PRNP) -- bp(GO:"neuron development")
p(HGNC:PRNP) -- bp(GO:"neuron death")
p(HGNC:PRNP) -| bp(MESH:"Oxidative Stress")
p(HGNC:PRNP) -| p(HGNC:TNF)
p(HGNC:PRNP) -| p(HGNC:BAX)

##Aint sure of how to differentiate between Prpc and Prpsc.

UNSET Disease

SET Evidence = "There is evidence for the accumulation
of misfolded proteins and also evidence for the involvement of
the UPR in several human neurodegenerative conditions."

path(MESH:"Neurodegenerative Diseases") -- bp(HBP:"protein aggregation")
path(MESH:"Neurodegenerative Diseases") -- bp(MESH:"Unfolded Protein Response")
