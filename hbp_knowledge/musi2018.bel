##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name        = "Tau protein aggregation is associated with cellular senescence in the brain"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Description = ""
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

###############
# Definitions #
###############

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DNSNP AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY AS PATTERN "^\d+$"

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE MESH     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mesh-names.belns"
DEFINE NAMESPACE CHEBI    AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE   AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
DEFINE NAMESPACE NCBIGENE AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE GFAM     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE HGNC     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE MIRBASE  AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE HBP      AS URL "https://raw.githubusercontent.com/pharmacome/terminology/785431732a26d6809b4162d98b95687d16404e63/export/hbp.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel
# --------------------------------------------------------------------
DEFINE NAMESPACE CTO      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE MGI      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170725.belns"
DEFINE NAMESPACE SCOMP    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM     AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-20170731.belns"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "30126037"}

#tauNFT = p(MGI:Mapt, var("p.Pro301Leu"))

SET Evidence = "Tau protein accumulation is the most common pathology among degenerative brain diseases,
including Alzheimer’s disease (AD), progressive supranuclear palsy (PSP), traumatic brain injury (TBI) and over twenty others"
SET Confidence = "High"
p(HGNC:MAPT) -- path(MESH:"Alzheimer Disease")
p(HGNC:MAPT) -- path(MESH:"Supranuclear Palsy, Progressive")
p(HGNC:MAPT) -- path(MESH:"Brain Injuries, Traumatic")

SET Evidence = "Tau-containing neurofibrillary tangle (NFT) accumulation is the closest correlate with cognitive decline
and cell loss (Arriagada et al., 1992), yet mechanisms mediating tau toxicity are poorly understood"
SET Confidence = "High"
a(GO:"neurofibrillary tangle") -| bp(GO:cognition)
a(GO:"neurofibrillary tangle") -> bp(GO:"cell death")

SET Evidence = "Transcriptomic analyses of NFT-containing neurons microdissected from postmortem AD brain
revealed an expression profile consistent with cellular senescence. This complex stress response induces aberrant
cell cycle activity, adaptations to maintain survival, cellular remodeling, and metabolic dysfunction"
SET MeSHDisease = "Alzheimer Disease"
SET Confidence = "Medium"
bp(GO:"cellular senescence") -- bp(GO:"cell cycle")
bp(GO:"cellular senescence") -> bp(MESH:"Cell Survival")
bp(GO:"cellular senescence") -> bp(GO:"tissue remodeling")
bp(GO:"cellular senescence") -| bp(GO:"metabolic process")

SET Evidence = "Cdkn2a transcript level, a hallmark measure of senescence,
directly correlated with brain atrophy and NFT burden in mice"
SET Confidence = "Medium"
p(MGI:Cdkn2a) -- bp(GO:"cellular senescence")
p(MGI:Cdkn2a) positiveCorrelation path(HP:"Cerebral atrophy")
p(MGI:Cdkn2a) positiveCorrelation a(GO:"neurofibrillary tangle")

SET Evidence = "Tau transgenic mice with late stage pathology were treated with senolytics to remove senescent cells.
Despite the advanced age and disease progression, MRI brain imaging and histopathological analyses indicated a reduction
in total NFT density, neuron loss and ventricular enlargement"
SET Confidence = "High"
bp(GO:"cellular senescence") -> a(GO:"neurofibrillary tangle")
bp(GO:"cellular senescence") -> bp(GO:"neuron death")
bp(GO:"cellular senescence") -> path(MESH:"Hypertrophy, Right Ventricular")
bp(GO:"cellular senescence") -> path(MESH:"Hypertrophy, Left Ventricular")

SET Evidence = "Collectively, these findings indicate a strong association between the presence of NFTs and cellular
senescence in the brain, which contributes to neurodegeneration"
SET Confidence = "Medium"
SET MeSHAnatomy = "Brain"
a(GO:"neurofibrillary tangle") -- bp(GO:"cellular senescence")
bp(GO:"cellular senescence") -> bp(HP:Neurodegeneration)

SET Evidence = "Experimental data from various studies indicate that tau pathology may be associated with cellular senescence,
a fundamental aging mechanism shown to contribute to several chronic diseases (recent review Kirkland and Tchkonia, 2017)"
SET Confidence = "High"
path(MESH:Tauopathies) -- bp(GO:"cellular senescence")

SET Evidence = "This complex stress response induces a near permanent cell cycle arrest, adaptations to maintain survival,
cellular remodeling, metabolic dysfunction and disruption to surrounding tissue to the secretion of toxic molecules
(Childs et al., 2016)"
SET Confidence = "Medium"
bp(GO:"cellular senescence") -> bp(GO:"cell cycle arrest")
bp(GO:"cellular senescence") -> bp(MESH:"Cell Survival")
bp(GO:"cellular senescence") -> bp(GO:"tissue remodeling")
bp(GO:"cellular senescence") -| bp(GO:"metabolic process")
bp(GO:"cellular senescence") -| sec(a(MESH:Immunotoxins))

SET Evidence = "NFT containing neurons upregulated genes involved in cell survival and viability, inflammation,
cell cycle progression and molecular transport and downregulated apoptosis, necrosis and cell death pathways (Figure 1a).
NFkB, a pro-survival master transcriptional regulator of inflammation, was the highest predicted upstream regulator
of the NFT-gene expression profile. In agreement with inflammatory activation, other predicted upstream regulators 
included IFNG, TNF, TLR4, IL1B and CXCL1 (Figure 1b)"
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
SET MeSHDisease = "Alzheimer Disease"
a(GO:"neurofibrillary tangle") -> bp(MESH:"Cell Survival")
a(GO:"neurofibrillary tangle") -> bp(GO:"inflammatory response")
a(GO:"neurofibrillary tangle") -> bp(GO:"cell cycle")
a(GO:"neurofibrillary tangle") -| bp(GO:"apoptotic process")
a(GO:"neurofibrillary tangle") -| path(MESH:Necrosis)
a(GO:"neurofibrillary tangle") -| bp(GO:"cell death")
a(GO:"neurofibrillary tangle") -> p(HGNC:NFKB1)
a(GO:"neurofibrillary tangle") -> p(HGNC:IFNG)
a(GO:"neurofibrillary tangle") -> p(HGNC:TNF)
a(GO:"neurofibrillary tangle") -> p(HGNC:TLR4)
a(GO:"neurofibrillary tangle") -> p(HGNC:IL1B)
a(GO:"neurofibrillary tangle") -> p(HGNC:CXCL1)

SET Evidence = "Consistent with NFTs from human AD, mouse NFTs also caused significant
activation scores for IFNG, TNF, IL-1B, as well as enrichment in other senescence associated
JAK, STAT, CDKN2A and BCL2 predicted upstream regulators (Figure 1c) indicating translational
relevance for using tauNFT mice to explore our hypothesis"
SET Confidence = "Medium"
a(GO:"neurofibrillary tangle") -> act(p(MGI:Ifng))
a(GO:"neurofibrillary tangle") -> act(p(MGI:Tnf))
a(GO:"neurofibrillary tangle") -> act(p(MGI:Il1b))
a(GO:"neurofibrillary tangle") -> act(p(MGI:Jak1))
a(GO:"neurofibrillary tangle") -> act(p(MGI:Stat1))
a(GO:"neurofibrillary tangle") -> act(p(MGI:Cdkn2a))
a(GO:"neurofibrillary tangle") -> act(p(MGI:Bcl2))

SET Evidence = "Senescence-inducing stressors often inflict DNA-damage that drives production of the SASP (Rodier et al., 2009)"
SET Confidence = "Medium"
bp(GO:"cellular senescence") -> bp(MESH:"DNA Damage")

SET Evidence = "TauNFT mouse brains displayed significantly elevated histone γ-H2ax, a sensitive marker of both double-stranded
DNA breaks and cellular senescence (Sedelnikova et al., 2004) (P = 0.0056; Figure 1d-e)"
SET Confidence = "Medium"
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:H2afx)
p(MGI:H2afx) biomarkerFor bp(MESH:"DNA Breaks, Double-Stranded")
p(MGI:H2afx) biomarkerFor bp(GO:"cellular senescence")

SET Evidence = "The cell cycle protein p21, encoded by Cdkn1a, is upregulated in many senescent cell types and has
been associated with DNA damage during neuronal aging (Jurk et al., 2012)"
SET Confidence = "High"
bp(GO:"cellular senescence") -> p(MGI:Cdkn1a)
bp(GO:"cellular senescence") -- bp(MESH:"DNA Damage")

SET Evidence = "Similarly, elevated expression of the cyclin
dependent kinase inhibitor 2a, Cdkn2a, is one of the most robust markers of cellular
senescence, and its protein product, p16INK4A, co-localizes with NFTs in human AD (Arendt et al., 1996)"
SET Confidence = "High"
SET MeSHDisease = "Alzheimer Disease"
bp(GO:"cellular senescence") -> p(HGNC:CDKN2A)
p(HGNC:CDKN2A) -- a(GO:"neurofibrillary tangle")

SET Evidence = "TauNFT brains expressed 3-fold higher Cdkn1a than control mice
(P = 0.0178, Figure 1f), which was replicated in a separate mouse cohort (P = 0.0086, Figure S1f)"
SET Confidence = "High"
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:Cdkn1a)

SET Evidence = "Moreover, Cdkn2a was expressed at levels 2.7- and 2.6-fold higher in tauNFT than CTL
and tauWT, respectively (P = 0.0303 and P = 0.0352, respectively; Figure 1g); this effect was replicated
in an independent mouse cohort (P = 0.0016, Figure S1g)"
SET Confidence = "High"
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:Cdkn2a)

SET Evidence = "Consistent with the transcriptomic profile in human NFT-bearing neurons and mouse brain tissue
(Figure 1a-c), SASP genes were found to be upregulated in tauNFT brains, i.e., Il1b was 4- and 2-fold higher than
CTL and tauWT, respectively; and Cxcl1 was 4-fold higher than both control genotypes; Tnfa was 13- and 8-fold higher
than CTL and tauWT, respectively; Tlr4 was 3-fold higher than both control genotypes (Figure 2a-d)"
SET Confidence = "Medium"
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:Il1b)
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:Cxcl1)
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:Tnf)
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:Tlr4)

SET Evidence = "NFkB regulates the pro-survival, pro-inflammatory SASP gene
expression profile characteristic of cellular senescence (Salminen & Kaarniranta, 2011)"
SET Confidence = "Medium"
act(p(MGI:Nfkb1)) -> bp(MESH:"Cell Survival")
act(p(MGI:Nfkb1)) -> bp(GO:"inflammatory response")
bp(GO:"inflammatory response") -- bp(GO:"cellular senescence")
bp(MESH:"Cell Survival") -- bp(GO:"cellular senescence")

SET Evidence = "Consistent with NFkB pathway activation and the SASP profile,
nuclear localized NFkB p65 was significantly increased in tauNFT brains (Figure 2e-f)"
SET Confidence = "Medium"
p(MGI:Mapt, var("p.Pro301Leu")) -> bp(GO:"NIK/NF-kappaB signaling")
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:Rela)

SET Evidence = "Examination of the gene that codes for the hydrolase enzyme, galactosidase beta  1 (Glb1),
revealed that tauNFT mice expressed higher Glb1 gene expression than controls (Figure S3)"
SET Confidence = "High"
p(MGI:Mapt, var("p.Pro301Leu")) -> p(MGI:Glb1)

SET Evidence = "Further, SA beta-gal reactive cells were observed even in very young mice (1-month-old) and the number
of SA beta-gal reactive cells was positively correlated with brain mass (R2: 0.4852, P = 0.0039 Figure S3)"
SET Confidence = "High"
p(MGI:Glb1) positiveCorrelation a(HP:brain)

SET Evidence = "Mitochondrial dysfunction is obligatory for SASP production and cellular senescence (Correia-Melo et al., 2016; Hutter et al., 2004)"
SET Confidence = "High"
bp(GO:"cellular senescence") -> bp(HBP:HBP00037)

SET Evidence = "We found a significant genotype main effect for oxygen flux in both cortex and hippocampus, indicating that
global respiratory capacity was impaired in NFT containing brain regions (P < 0.0001; Figure 3),
an effect primarily driven by CI+CII respiration coupled to ATP production (cortex: P = 0.0034;
hippocampus: P = 0.0215; Figure 2g and h, respectively), and uncoupled or maximum respiratory capacity
(cortex: P = 0.0248; hippocampus: P = 0.0261; Figure 3g and h, respectively)"
SET Confidence = "High"
SET MeSHAnatomy = {"Cerebral Cortex","Hippocampus"}
a(GO:"neurofibrillary tangle") -| bp(GO:"oxygen transport")
bp(GO:"oxidative phosphorylation") -| bp(GO:"oxygen transport")

SET Evidence = "Citrate synthase activity is a surrogate marker of total mitochondrial
content/mass, and was similar across genotypes and brain regions (Figure 3i) suggesting
that the defects in cellular respiration were due to altered mitochondrial quality, not content/mass"
SET Confidence = "High"
bp(GO:"cellular respiration") -- act(a(MESH:Mitochondria))

SET Evidence = "However, genetically ablating endogenous mouse tau (microtubule associated protein tau, Mapt)
reduces NFT pathology and neurodegeneration in tauNFT mice (tauNFT-Mapt0/0) (Wegmann et al., 2015)"
SET Confidence = "Medium"
p(MGI:Mapt) -> a(GO:"neurofibrillary tangle")
p(MGI:Mapt) -> bp(HP:Neurodegeneration)

SET Evidence = "The reduced tau pathology corresponded with 60% lower Cdkn2a expression
(P = 0.0041, Figure 4a), decreased SASP (Figure S4) and decreased brain atrophy
(tauNFT-Mapt0/0: 0.4058 ± 0.009 versus age-matched tauNFT Maptwt/wt: 0.3451 ± 0.0116; 17.5% difference, P = 0.0143, Figure 4b)"
SET Confidence = "High"
p(MGI:Mapt) -> p(MGI:Cdkn2a)
p(MGI:Mapt) -> bp(GO:"cellular senescence")
p(MGI:Mapt) -> path(HP:"Brain atrophy")

SET Evidence = "TauNFT mice develop aggressive tauopathy with NFT formation in
early life, and show a senescence-associated transcriptomic profile with NFT onset (Figure 1c)"
SET Confidence = "High"
p(MGI:Mapt, var("p.Pro301Leu")) -> path(MESH:Tauopathies)
p(MGI:Mapt, var("p.Pro301Leu")) -> a(GO:"neurofibrillary tangle")
p(MGI:Mapt, var("p.Pro301Leu")) -> bp(GO:"cellular senescence")

SET Evidence = "Cdkn2a gene expression increased significantly during this age interval, and at 28 months of age
tauWT Cdkn2a expression was similar to that of 16-month-old tauNFT mice (Figure 4c)"
SET Confidence = "High"
a(GO:"neurofibrillary tangle") -> p(MGI:Cdkn2a)

SET Evidence = "In 15-month-old mice with heavy Abeta deposition and phosphorylated tau,
but lacking NFT pathology (Orr et al., 2014), Cdkn2a expression was not elevated (Figure 4e).
These data indicate that Cdkn2a expression was neither a response to general protein accumulation,
nor to pre-NFT tau pathology, but instead required the presence of NFTs"
SET Confidence = "High"
a(CHEBI:"amyloid-beta") causesNoChange p(MGI:Cdkn2a)
p(MGI:Mapt,pmod(Ph)) causesNoChange p(MGI:Cdkn2a)
a(GO:"neurofibrillary tangle") -> p(MGI:Cdkn2a)

SET Evidence = "Further, when plotted against brain weight,
Cdkn2a expression was a strong predictor of brain atrophy across mouse lines (P < 0.0001, R2 = 0.5615; Figure 4f)"
SET Confidence = "Medium"
p(MGI:Cdkn2a) positiveCorrelation path(HP:"Brain atrophy")

SET MeSHDisease = "Supranuclear Palsy, Progressive"

SET Evidence = "PSP is an age-associated tauopathy that clinically manifests as parkinsonism with additional motor
abnormalities and cognitive dysfunction (Orr et al., 2017), and is neuropathologically defined by accumulation of
four-repeat (4R) tau, NFTs, gliosis and neurodegeneration (Flament et al., 1991)"
SET Confidence = "Medium"
path(MESH:"Supranuclear Palsy, Progressive") -> a(HBP:HBP00045)
path(MESH:"Supranuclear Palsy, Progressive") -> a(GO:"neurofibrillary tangle")
path(MESH:"Supranuclear Palsy, Progressive") -> bp(HP:Neurodegeneration)
path(MESH:"Supranuclear Palsy, Progressive") -> path(MESH:Gliosis)
path(MESH:"Supranuclear Palsy, Progressive") -> path(MESH:"Motor Disorders")
path(MESH:"Supranuclear Palsy, Progressive") -- path(MESH:"Parkinsonian Disorders")
path(MESH:"Supranuclear Palsy, Progressive") -- path(MESH:Tauopathies)
path(MESH:"Supranuclear Palsy, Progressive") -> path(MESH:"Cognitive Dysfunction")

SET Evidence = "Consistent with the results from transgenic mice, CDKN2A was upregulated in PSP brains
(P = 0.0415, Figure 4g) and expression correlated with NFT deposition, specifically in the parietal lobe
(ANOVA, P = 0.0008; Kendall’s Tau rank correlation P = 0.059, Figure 4h)"
SET Confidence = "Medium"
path(MESH:"Supranuclear Palsy, Progressive") positiveCorrelation p(HGNC:CDKN2A)
SET MeSHAnatomy = "Parietal Lobe"
p(HGNC:CDKN2A) positiveCorrelation a(GO:"neurofibrillary tangle")

SET Evidence = "Collectively, these findings led us to conclude that NFTs were directly linked to
senescence-associated Cdkn2a upregulation, which in turn was a strong predictor of neurodegeneration
and cognitive decline"
SET Confidence = "Medium"
a(GO:"neurofibrillary tangle") -> p(HGNC:CDKN2A)
p(HGNC:CDKN2A) -- bp(HP:Neurodegeneration)
p(HGNC:CDKN2A) -- path(MESH:"Cognitive Dysfunction")

UNSET MeSHDisease

SET Evidence = "Consistent with senescent cell removal, intermittent DQ treatment significantly reduced the
number of NFT-containing cortical neurons (P < 0.0001, 5% reduction; Figure 5a,b)"
SET Confidence = "High"
SET MeSHAnatomy = "Neurons"
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| a(GO:"neurofibrillary tangle")

SET Evidence = "Relative to the existing neuronal population at this advanced age,
gene expression of the NFT-associated senescence gene array was reduced by DQ (P = 0.0006; Figure S6a)"
SET Confidence = "High"
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| bp(GO:"cellular senescence")

SET Evidence = "NFTs are highly correlated with the rate of ventricular enlargement,
an indicator of brain atrophy and hallmark of AD pathology (Silbert et al., 2003)"
SET Confidence = "High"
a(GO:"neurofibrillary tangle") -- path(MESH:"Hypertrophy, Right Ventricular")
a(GO:"neurofibrillary tangle") -- path(MESH:"Hypertrophy, Left Ventricular")
a(GO:"neurofibrillary tangle") -- path(HP:"Brain atrophy")

SET Evidence = "The DQ-dependent reduction in cortical NFTs corresponded with decreased ventricular volume pathology
(28% decrease, P = 0.05, Figure 5d, f) and a reduction in cortical brain atrophy
(compared to controls: P = 0.0092 and P = 0.0274, vehicle and DQ, respectively; Figure S5a)"
SET Confidence = "High"
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| a(GO:"neurofibrillary tangle")
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| path(HP:"Brain atrophy")
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| path(HP:"cardiac ventricle")

#white matter hyperintensity = WMH
SET Evidence = "WMH is driven by cerebral small vessel disease, which causes chronic ischemia and
increased risk of cognitive decline and dementia (reviewed, (Prins & Scheltens, 2015))"
SET Confidence= "High"
path(MESH:"Cerebral Small Vessel Diseases") -> path(HP:"Hyperintensity of cerebral white matter on MRI")
path(MESH:"Cerebral Small Vessel Diseases") -> path(MESH:Ischemia)
path(MESH:"Cerebral Small Vessel Diseases") -> path(MESH:"Cognitive Dysfunction")
path(MESH:"Cerebral Small Vessel Diseases") -> path(MESH:Dementia)

SET Evidence = "However, tauNFT-Mapt0/0 mice treated with DQ did not display
WMH volumes statistically different than control mice (P = 0.2458; Figure S5b, c)"
SET Confidence = "High"
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| path(HP:"Hyperintensity of cerebral white matter on MRI")

SET Evidence = "Aberrant cerebral blood flow is a functional defect that occurs in AD and tauNFT mice,
and is closely associated with cognitive impairment (Wells et al., 2015)"
SET Confidence = "High"
p(MGI:Mapt, var("p.Pro301Leu")) -- bp(MESH:"Cerebrovascular Circulation")
path(MESH:"Alzheimer Disease") -- bp(MESH:"Cerebrovascular Circulation")
bp(MESH:"Cerebrovascular Circulation") -- path(MESH:"Cognitive Dysfunction")

SET MeSHDisease = "Tauopathies"

SET Evidence = "In brain tissue with tau pathology, cerebral blood flow was elevated in
tauNFT Mapt0/0 vehicle-treated mice (21% whole brain, P = 0.045; cortex, 48.7%, P = 0.051, Figure S5d, e),
and consistent with previous reports of tauNFT mice on a Mapt+/+ background (Wells et al., 2015)"
SET Confidence = "High"
p(MGI:Mapt, var("p.Pro301Leu")) -> bp(MESH:"Cerebrovascular Circulation")

SET Evidence = "DQ improved cerebral blood flow in tauNFT Mapt0/0 mice such that
cerebral blood flow was no longer statistically different from controls (Figure S5d, e)"
SET Confidence = "High"
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| bp(MESH:"Cerebrovascular Circulation")

UNSET MeSHDisease

SET Evidence = "DQ-treated mice expressed significantly higher levels of neuronal proteins
(NeuN: 25%, synaptophysin: 40.8%; PSD95: 38.5%; P < 0.05; Figure 5f-i)"
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -> p(MGI:Syp)
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -> p(MGI:Rbfox3)
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -> p(MGI:Dlg4)

SET Evidence = "The astrocyte protein GFAP was unchanged, while microglia Iba1 expression was elevated
(Iba1: 40%, P = 0.0013; Figure S6b-d) suggesting that DQ-mediated neuroprotection and decreased
SASP was not derived from a reduction in pro-inflammatory glia (astrocytes or microglia)
but instead associated with fewer NFT-containing neurons"
SET Confidence = "High"
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -> p(MGI:Aif1)
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -> bp(MESH:Neuroprotection)
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| bp(GO:"cellular senescence")
composite(a(CHEBI:quercetin),a(CHEBI:"dasatinib (anhydrous)")) -| a(GO:"neurofibrillary tangle")
