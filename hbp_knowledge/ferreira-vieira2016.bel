#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Alzheimer's Disease: Targeting the Cholinergic System"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "26813123"}

SET Evidence = "Acetylcholine (ACh) was the first neurotransmitter to be identified"
a(CHEBI:acetylcholine) isA a(CHEBI:neurotransmitter)

SET Evidence = "Given its widespread distribution in the brain, it is not
surprising that cholinergic neurotransmission is responsible
for modulating important neural functions"
bp(GO:"synaptic transmission, cholinergic") reg act(a(MESH:Neurons))

SET Evidence = "The cholinergic system is involved in critical physiological processes, such
as attention, learning, memory, stress response, wakefulness and sleep, and sensory information"
bp(GO:"synaptic transmission, cholinergic") -- path(MESH:Attention)
bp(GO:"synaptic transmission, cholinergic") -- bp(GO:learning)
bp(GO:"synaptic transmission, cholinergic") -- bp(GO:memory)
bp(GO:"synaptic transmission, cholinergic") -- bp(GO:"response to stress")
bp(GO:"synaptic transmission, cholinergic") -- bp(GO:"regulation of circadian sleep/wake cycle, wakefulness")
bp(GO:"synaptic transmission, cholinergic") -- bp(GO:"regulation of circadian sleep/wake cycle, sleep")
bp(GO:"synaptic transmission, cholinergic") -- bp(GO:"sensory processing")

SET Evidence = "Experimental data using non-human primates and rodents have demonstrated that
injuries introduced to basal forebrain cholinergic neurons that innervate the cortex lead to attention deficit"
SET MeSHAnatomy = "Basal Forebrain"
act(a(MESH:"Cholinergic Neurons")) -> path(MESH:Attention)
UNSET MeSHAnatomy

SET Evidence = "On the other hand, the facilitation of the cholinergic
transmission by using the cholinesterase inhibitors can improve attention in humans"
bp(GO:"synaptic transmission, cholinergic") -> path(MESH:Attention)

SET Evidence = "It has been demonstrated that the cholinergic system
plays a role in the learning process"
bp(GO:"synaptic transmission, cholinergic") positiveCorrelation bp(GO:learning)

SET Evidence = "Moreover, published data indicate that ACh is involved in memory"
a(CHEBI:acetylcholine) positiveCorrelation bp(GO:memory)

SET Evidence = "Further studies have demonstrated that endogenous
acetylcholine is important for modulation of acquisition [17],
encoding [18], consolidation [19], reconsolidation [20],
extinction [21] and retrieval of memory"
a(CHEBI:acetylcholine) reg bp(GO:memory)

SET Evidence = "The importance of the cholinergic neurons from the nucleus basalis of
Meynert on memory is highlighted by the fact that the specific degeneration of these
neurons takes place in Alzheimer’s disease (AD) and contributes to the memory
loss exhibited by AD patients"
SET MeSHAnatomy = "Basal Nucleus of Meynert"
a(MESH:"Cholinergic Neurons") positiveCorrelation bp(GO:memory)
UNSET MeSHAnatomy
SET MeSHDisease = "Alzheimer Disease"
bp(HBP:"Neurodegeneration") negativeCorrelation bp(GO:memory)
UNSET MeSHDisease

SET Evidence = "Stress is another factor that can regulate ACh release in
the forebrain and its function on the hypothalamic-pituitaryadrenal
(HPA) system can modulate biological and emotional outcomes"
bp(GO:"response to stress") reg tloc(a(CHEBI:acetylcholine),fromLoc(GO:intracellular),toLoc(MESH:"Basal Forebrain"))
a(CHEBI:acetylcholine) reg a(MESH:"Hypothalamo-Hypophyseal System")

SET Evidence = "In a work that evaluated the
function of the cholinergic system in mediating the response
to stress it was found that treatment of rats with the antagonist
of the nicotinic receptor, mecamylamine, attenuates the
activation of the HPA axis in response to a stressor agent"
SET Species = "10116"
a(MESH:"Receptors, Nicotinic") -> act(a(MESH:"Hypothalamo-Hypophyseal System"))
a(CHEBI:Mecamylamine) -| act(a(MESH:"Hypothalamo-Hypophyseal System"))
UNSET Species

SET Evidence = "Similarly, nicotine can mimic the ACh effects on the
HPA axis by activating nicotinic receptors"
a(CHEBI:nicotine) -> act(a(MESH:"Receptors, Nicotinic"))
a(CHEBI:acetylcholine) -> act(a(MESH:"Receptors, Nicotinic"))

SET Evidence = "Another important function of the cholinergic system is
to regulate the sleep cycle"
bp(GO:"synaptic transmission, cholinergic") reg bp(GO:"circadian sleep/wake cycle")

SET Evidence = "It has been demonstrated that
stimulation of cholinergic neurons in precise regions of the
brainstem can promote REM (rapid eye movement) sleep, dose-dependently"
SET MeSHAnatomy = "Brain Stem"
a(MESH:"Cholinergic Neurons") -> bp(GO:"circadian sleep/wake cycle, REM sleep")
UNSET MeSHAnatomy

SET Evidence = "ACh contributes to auditory synaptic transmission by facilitating thalamocortical communication"
a(CHEBI:acetylcholine) -> bp(GO:"synaptic transmission, cholinergic")

SET Evidence = "Recent evidences also suggest the involvement
of ACh in adult neurogenesis"
a(CHEBI:acetylcholine) -> bp(GO:neurogenesis)

SET Evidence = "ACh synthesis takes place in the cytoplasm of cholinergic neurons"
SET MeSHAnatomy = "Cytoplasm"
a(MESH:"Cholinergic Neurons") -> a(CHEBI:acetylcholine)
UNSET MeSHAnatomy

SET Evidence = "The enzyme choline acetyltransferase (ChAT) can
synthesize ACh from choline and acetyl-coenzyme A (acetyl-CoA)"
p(HGNC:CHAT) -> rxn(reactants(a(CHEBI:choline),a(CHEBI:"acetyl-CoA")),products(a(CHEBI:acetylcholine)))

SET Evidence = "Following that, the neurotransmitter is
transported by the vesicular acetylcholine transporter
(VAChT) from the cytosol into synaptic vesicles"
#the neurotransmitter: ACh
p(HGNC:SLC18A3) -> tloc(a(CHEBI:acetylcholine),fromLoc(GO:cytosol),toLoc(MESH:"Synaptic Vesicles"))

SET Evidence = "Although cholinergic neurons
can synthesize choline, the de novo synthesis contributes
only with a very small fraction of the total choline that is
needed for ACh synthesis (see Fig. 1)"
a(MESH:"Cholinergic Neurons") -> a(CHEBI:choline)

SET Evidence = "ChAT activity is regulated by neuronal depolarization,
influx of Ca2+ and phosphorylation of the enzyme by a wide variety of protein kinases"
SET MeSHAnatomy = "Neurons"
bp(GO:"membrane depolarization") reg act(p(HGNC:CHAT))
UNSET MeSHAnatomy
bp(GO:"calcium ion import") reg act(p(HGNC:CHAT))
p(HGNC:CHAT,pmod(Ph)) reg act(p(HGNC:CHAT))

SET Evidence = "Each molecule of ACh transported
by VAChT is in exchange for two vesicular protons,
which leads to the fulfill of synaptic vesicles with the neurotransmitter"
p(HGNC:SLC18A3) -> tloc(a(CHEBI:acetylcholine),fromLoc(GO:cytosol),toLoc(MESH:"Synaptic Vesicles"))
p(HGNC:SLC18A3) -> tloc(a(CHEBI:proton),fromLoc(MESH:"Synaptic Vesicles"),toLoc(GO:cytosol))

SET Evidence = "The vesicular transporter activity
can be blocked by vesamicol, which is a non-competitive inhibitor"
a(CHEBI:"2-(4-phenyl-1-piperidinyl)-1-cyclohexanol") -| act(p(HGNC:SLC18A3))
a(CHEBI:"2-(4-phenyl-1-piperidinyl)-1-cyclohexanol") -|  tloc(a(CHEBI:acetylcholine),fromLoc(GO:cytosol),toLoc(MESH:"Synaptic Vesicles"))

SET Evidence = "Interestingly, PKC can
phosphorylate VAChT and regulate its vesicular localization"
p(FPLX:PKC) -> p(HGNC:SLC18A3,pmod(Ph))

SET Species = "10090"

SET Evidence = "A VAChT knockdown mouse model, expressing
about 68% less VAChT protein, shows major neuromuscular deficits"
p(MGI:Slc18a3) -| path(MESH:"Neuromuscular Diseases")

SET Evidence = "Furthermore, cognitive impairment can take place even due to a mild
decrease in VAChT protein expression (about 45%)"
p(MGI:Slc18a3) -> bp(GO:cognition)

UNSET Species

SET Evidence = "When cholinergic neurons are depolarized, ACh is
exocytosed from synaptic vesicles and released into the
synaptic cleft, where it can activate both muscarinic and nicotinic receptors"
SET Cell = "cholinergic neuron"
bp(GO:"membrane depolarization") -> tloc(a(CHEBI:acetylcholine),fromLoc(MESH:"Synaptic Vesicles"),toLoc(GO:"synaptic cleft"))
UNSET Cell
# in the synaptic cleft
a(CHEBI:acetylcholine) -> act(p(FPLX:CHRM))
a(CHEBI:acetylcholine) -> act(p(FPLX:CHRN))

SET Evidence = "ACh present at the synaptic cleft is
rapidly inactivated by the enzyme acetylcholinesterase
(AChE), releasing choline and acetate"
# in the synaptic cleft
p(HGNC:ACHE) -| act(a(CHEBI:acetylcholine))
p(HGNC:ACHE) -> rxn(reactants(a(CHEBI:acetylcholine)),products(a(CHEBI:choline),a(CHEBI:acetate)))

SET Evidence = "Cholinergic neurons secrete AChE into the synaptic cleft, where the
enzyme is normally associated with the plasma membrane (see Fig. 1)"
a(MESH:"Cholinergic Neurons") -> tloc(p(HGNC:ACHE),fromLoc(MESH:"Synaptic Vesicles"),toLoc(GO:"synaptic cleft"))

SET Evidence = "The organophosphate compounds are irreversible
AChE inhibitors that are used as insecticides and nerve gases"
a(CHEBI:"organophosphorus compound") -| act(p(HGNC:ACHE))

SET Evidence = "Thus, antidotes, including doxylamine, can be used to
speed enzyme regeneration"
# regeneration of ACHE
a(CHEBI:antidote) -> act(p(HGNC:ACHE))
a(CHEBI:doxylamine) -> act(p(HGNC:ACHE))

SET Evidence = "Moreover, AChE inhibitors
that compete for the enzyme site, as succinylcholine, are
used as anesthetic adjuvants since these drugs can promote
neuromuscular blockage"
a(CHEBI:succinylcholine) -| act(p(HGNC:ACHE))

SET Evidence = "Choline that is released by ACh hydrolysis in the
synaptic cleft is continuously reuptaken into the presynaptic
cholinergic neuron by an active transport system (see Fig. 1)"
# active transport system: SLC5A7
deg(a(CHEBI:acetylcholine)) -> a(CHEBI:choline)
p(HGNC:SLC5A7) -> tloc(a(CHEBI:choline),fromLoc(GO:"synaptic cleft"),toLoc(MESH:"Cholinergic Neurons"))

SET Evidence = "Two choline transporters have been identified in
neurons: a ubiquitous, low-affinity, sodium-independent
transporter that can only be inhibited by high concentrations
of hemicholinium-3 (HC-3) (Ki of about 50 μM), and a highaffinity,
sodium-dependent, HC-3-sensitive (Ki of 10-100
nM) choline transporter (CHT1)"
p(HGNC:SLC5A7) -> tloc(a(CHEBI:choline),fromLoc(GO:"synaptic cleft"),toLoc(MESH:"Cholinergic Neurons"))

SET Evidence = "CHT1 is mainly found in cholinergic neurons [92-94] and is
responsible for supplying choline for the synthesis of ACh"
SET Cell = "cholinergic neuron"
p(HGNC:SLC5A7) -> a(CHEBI:choline)
a(CHEBI:choline) -> a(CHEBI:acetylcholine)
UNSET Cell

SET Evidence = "Moreover, mice that display a
disruption of CHT1 gene expression exhibit symptoms
related to ACh deficit and inevitable death within an hour of birth"
SET Species = "10090"
p(MGI:Slc5a7) -| path(MESH:Death)
UNSET Species

SET Evidence = "Interestingly, it has been demonstrated that an increase in
neuronal firing can lead to an increase in choline uptake and in the synthesis of ACh"
bp(GO:"action potential initiation") -> tloc(a(CHEBI:choline),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
bp(GO:"action potential initiation") -> a(CHEBI:acetylcholine)

SET Evidence = "It is possible that the presence of CHT1 in the membrane of synaptic
vesicles and the consequent increase in CHT1 relocation to
the plasma membrane following neuronal depolarization
could explain why an increase in neuronal firing promotes
increased choline reuptake and, thus, ACh synthesis"
SET MeSHAnatomy = {"Synaptic Vesicles","Membranes"}
p(HGNC:SLC5A7) -> tloc(a(CHEBI:choline),fromLoc(GO:"synaptic cleft"),toLoc(MESH:"Cholinergic Neurons"))
p(HGNC:SLC5A7) -> a(CHEBI:acetylcholine)
bp(GO:"action potential initiation") -> tloc(a(CHEBI:choline),fromLoc(GO:"synaptic cleft"),toLoc(MESH:"Cholinergic Neurons"))
bp(GO:"action potential initiation") -> a(CHEBI:acetylcholine)
SET MeSHAnatomy = "Neurons"
bp(GO:"membrane depolarization") -> complex(p(HGNC:SLC5A7),a(GO:"plasma membrane"))
complex(p(HGNC:SLC5A7),a(GO:"plasma membrane")) -- bp(GO:"action potential initiation")
UNSET MeSHAnatomy

SET Evidence = "Importantly, the neurons that form the nucleus basalis of
Meynert undergo extensive degeneration in AD"
path(MESH:"Alzheimer Disease") -> deg(a(MESH:"Basal Nucleus of Meynert"))

SET Evidence = "Nicotinic receptors are formed by five different
subunits: α, β, δ, and γ (fetal) or ε (adult)"
p(HGNC:CHRND) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNE) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNG) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNB1) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNA1) -> a(MESH:"Receptors, Nicotinic")

SET Evidence = "Cholinergic nicotinic receptors expressed
in muscle and ganglia are comprised of two α subunits plus
each of the other three"
SET MeSHAnatomy = {"Muscles","Ganglia"}
complex(p(HGNC:CHRNA1),p(HGNC:CHRNA1),p(HGNC:CHRNB1),p(HGNC:CHRNG),p(HGNC:CHRND)) -> p(FPLX:CHRN)
UNSET MeSHAnatomy

SET Evidence = "On the other hand, neuronal nicotinic
receptors are formed by the combination of only two types of
subunits (α2-10 and β2-4)"
SET MeSHAnatomy = "Neurons"
p(HGNC:CHRNA2) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNA3) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNA4) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNA5) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNA6) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNA7) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNA9) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNA10) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNB2) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNB3) -> a(MESH:"Receptors, Nicotinic")
p(HGNC:CHRNB4) -> a(MESH:"Receptors, Nicotinic")
UNSET MeSHAnatomy

SET Evidence = "In the peripheral nervous system, activation of nicotinic
receptors leads to rapid synaptic transmission"
SET MeSHAnatomy = "Peripheral Nervous System"
act(a(MESH:"Receptors, Nicotinic")) -> bp(MESH:"Synaptic Transmission")

SET Evidence = "In the CNS, most of the nicotinic receptors are expressed at the
presynaptic neuronal membrane and their main role is to
regulate the release of neurotransmitters, whereas nicotinic
receptors expressed in the peripheral nervous system are mainly post-synaptic"
SET MeSHAnatomy = {"Neurons","Membranes","Central Nervous System"}
a(MESH:"Receptors, Nicotinic") reg tloc(a(CHEBI:neurotransmitter),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
UNSET MeSHAnatomy

SET Evidence = "Stimulation of nicotinic receptors present at the CNS presynaptic neuronal membrane
leads to an increase in presynaptic Ca2+ concentration, which
may facilitate the release of a number of neurotransmitters,
such as glutamate, GABA, dopamine, serotonin, norepinephrine, as well as ACh"
SET MeSHAnatomy = {"Neurons","Membranes","Central Nervous System"}
act(a(MESH:"Receptors, Nicotinic")) -> tloc(a(CHEBI:"calcium(2+)"),fromLoc(GO:"extracellular region"),toLoc(GO:synapse))
SET MeSHAnatomy = "Synapses"
a(CHEBI:"calcium(2+)") -> tloc(a(CHEBI:"glutamate(2-)"),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
a(CHEBI:"calcium(2+)") -> tloc(a(CHEBI:"gamma-aminobutyric acid"),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
a(CHEBI:"calcium(2+)") -> tloc(a(CHEBI:dopamine),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
a(CHEBI:"calcium(2+)") -> tloc(a(CHEBI:serotonin),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
a(CHEBI:"calcium(2+)") -> tloc(a(CHEBI:noradrenaline),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
a(CHEBI:"calcium(2+)") -> tloc(a(CHEBI:acetylcholine),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
UNSET MeSHAnatomy

SET Evidence = "Thus, ACh can influence the strength and fidelity of various synapses and
modulate overall CNS neurotransmission"
SET MeSHAnatomy = "Central Nervous System"
a(CHEBI:acetylcholine) reg bp(MESH:"Synaptic Transmission")
UNSET MeSHAnatomy

SET Evidence = "Agonists of nicotinic ACh receptors can improve, while
antagonists for the receptor impair, performance in cognitive tasks"
p(FPLX:CHRN) -> bp(GO:cognition)

SET Evidence = "It has been demonstrated that the role of
ACh in learning and memory seems to be related to the
regulation of glutamatergic neurotransmission"
a(CHEBI:acetylcholine) reg bp(GO:"synaptic transmission, glutamatergic")
a(CHEBI:acetylcholine) -- bp(GO:learning)
a(CHEBI:acetylcholine) -- bp(GO:memory)

SET Evidence = "Moreover, the interaction of the cholinergic and glutamatergic
systems seems to be important not only for cognitive
processes but also for neuroprotection, as it has been shown
that nicotinic receptors agonists are neuroprotective in a
mechanism that is Ca2+-dependent and that involves the glutamatergic system"
bp(GO:"synaptic transmission, glutamatergic") -- bp(GO:cognition)
bp(GO:"synaptic transmission, glutamatergic") -- bp(HBP:Neurodegeneration)
bp(GO:"synaptic transmission, cholinergic") -- bp(GO:cognition)
bp(GO:"synaptic transmission, cholinergic") -- bp(HBP:Neurodegeneration)
p(FPLX:CHRN) -| bp(HBP:Neurodegeneration)

SET Evidence = "It has been demonstrated that phosphorylation of nicotinic
receptors by protein kinase A, protein kinase C, and tyrosine
kinase can regulate receptor activity"
p(FPLX:PKA) -> p(FPLX:CHRN,pmod(Ph))
p(FPLX:PKC) -> p(FPLX:CHRN,pmod(Ph))
a(MESH:"Protein-Tyrosine Kinases") -> p(FPLX:CHRN,pmod(Ph))
p(FPLX:CHRN,pmod(Ph)) reg act(p(FPLX:CHRN))

SET Evidence = "Moreover,nicotinic receptors can also be regulated allosterically by Ca2+"
a(CHEBI:"calcium(2+)") reg p(FPLX:CHRN)

SET Evidence = "Not only nicotine, but also DMPP (1,1-Dimethyl-4-
phenylpiperazinium) and cystine can act as nicotinic receptor agonists"
complex(p(FPLX:CHRN),a(CHEBI:"1,1-dimethyl-4-phenylpiperazinium iodide")) -> act(p(FPLX:CHRN))
complex(p(FPLX:CHRN),a(CHEBI:cystine)) -> act(p(FPLX:CHRN))
complex(p(FPLX:CHRN),a(CHEBI:nicotine)) -> act(p(FPLX:CHRN))

SET Evidence = "Curare is the best known
antagonist for nicotinic receptors, although this drug is not
capable of blocking central nicotinic receptors"
a(CHEBI:tubocurarine) -| act(p(FPLX:CHRN))

SET Evidence = "Muscarinic receptors are G protein coupled receptors that
are capable of modulating a wide variety of ion channels"
p(FPLX:CHRM) isA a(MESH:"Receptors, G-Protein-Coupled")
p(FPLX:CHRM) reg a(MESH:"Ion Channels")

SET Evidence = "M1, M3, and M5 are coupled to Gαq, whereas M2 and M4 are coupled to Gαi."
complex(p(HGNC:CHRM1),p(INTERPRO:"G-protein alpha subunit, group Q"))
complex(p(HGNC:CHRM3),p(INTERPRO:"G-protein alpha subunit, group Q"))
complex(p(HGNC:CHRM5),p(INTERPRO:"G-protein alpha subunit, group Q"))
complex(p(HGNC:CHRM2),p(INTERPRO:"G-protein alpha subunit, group I"))
complex(p(HGNC:CHRM4),p(INTERPRO:"G-protein alpha subunit, group I"))

SET Evidence = "Stimulation of Gαq coupled muscarinic receptors leads to
activation of phospholipase C and formation of inositol
phosphates and other second messengers, which can promote
closure of K+ channels, thus facilitating cell excitability"
complex(p(HGNC:CHRM1),p(INTERPRO:"G-protein alpha subunit, group Q")) -> act(p(FPLX:PLC))
complex(p(HGNC:CHRM1),p(INTERPRO:"G-protein alpha subunit, group Q")) -> a(CHEBI:"myo-inositol")
complex(p(HGNC:CHRM1),p(INTERPRO:"G-protein alpha subunit, group Q")) -| act(a(MESH:"Potassium Channels"))
complex(p(HGNC:CHRM3),p(INTERPRO:"G-protein alpha subunit, group Q")) -> act(p(FPLX:PLC))
complex(p(HGNC:CHRM3),p(INTERPRO:"G-protein alpha subunit, group Q")) -> a(CHEBI:"myo-inositol")
complex(p(HGNC:CHRM3),p(INTERPRO:"G-protein alpha subunit, group Q")) -| act(a(MESH:"Potassium Channels"))
complex(p(HGNC:CHRM5),p(INTERPRO:"G-protein alpha subunit, group Q")) -> act(p(FPLX:PLC))
complex(p(HGNC:CHRM5),p(INTERPRO:"G-protein alpha subunit, group Q")) -> a(CHEBI:"myo-inositol")
complex(p(HGNC:CHRM5),p(INTERPRO:"G-protein alpha subunit, group Q")) -| act(a(MESH:"Potassium Channels"))

SET Evidence = "Activation of Gαi coupled muscarinic receptors
leads to inhibition of adenylyl cyclase and reduction of
cyclic adenosine monophosphate (cAMP) levels, promoting
inhibition of voltage-gated Ca2+ channels and, thus,
diminishing cell excitability"
complex(p(HGNC:CHRM2),p(INTERPRO:"G-protein alpha subunit, group I")) -| p(FPLX:ADCY)
complex(p(HGNC:CHRM2),p(INTERPRO:"G-protein alpha subunit, group I")) -| a(CHEBI:"3',5'-cyclic AMP")
complex(p(HGNC:CHRM2),p(INTERPRO:"G-protein alpha subunit, group I")) -| act(a(MESH:"Calcium Channels"))
complex(p(HGNC:CHRM4),p(INTERPRO:"G-protein alpha subunit, group I")) -| p(FPLX:ADCY)
complex(p(HGNC:CHRM4),p(INTERPRO:"G-protein alpha subunit, group I")) -| a(CHEBI:"3',5'-cyclic AMP")
complex(p(HGNC:CHRM4),p(INTERPRO:"G-protein alpha subunit, group I")) -| act(a(MESH:"Calcium Channels"))

SET Evidence = "As a consequence, stimulation
of ACh muscarinic receptors can promote the opening or
closing of Ca2+, K+, or Cl- channels, which might facilitate
either depolarization or hyperpolarization, depending on the
cell type where these receptors are expressed"
p(FPLX:CHRM) reg act(a(MESH:"Calcium Channels"))
p(FPLX:CHRM) reg act(a(MESH:"Potassium Channels"))
p(FPLX:CHRM) reg act(a(MESH:"Chloride Channels"))

SET Evidence = "Activation of muscarinic
receptors can block various types of K+ currents by
inhibiting many resting and voltage-gated K+ channels"
act(p(FPLX:CHRM)) -| act(a(MESH:"Potassium Channels"))

SET Evidence = "In addition to decreasing K+ currents, muscarinic
receptors located at the presynaptic region can also inhibit GABAergic neurotransmission"
act(p(FPLX:CHRM)) -| bp(GO:"synaptic transmission, glutamatergic")

SET Evidence = "On the other hand, stimulation of Gαq coupled muscarinic receptors
generates intracellular second messengers that can facilitate N-methyl-D-aspartate (NMDA) currents"
complex(p(HGNC:CHRM1),p(INTERPRO:"G-protein alpha subunit, group Q")) -> a(MESH:"Receptors, N-Methyl-D-Aspartate")
complex(p(HGNC:CHRM3),p(INTERPRO:"G-protein alpha subunit, group Q")) -> a(MESH:"Receptors, N-Methyl-D-Aspartate")
complex(p(HGNC:CHRM5),p(INTERPRO:"G-protein alpha subunit, group Q")) -> a(MESH:"Receptors, N-Methyl-D-Aspartate")

SET Evidence = "The first selective agonist identified for muscarinic
receptors was muscarine and, so far, there is no agonist
available with a specific selectivity for one particular
subtype of muscarinic receptor"
complex(a(CHEBI:Muscarine),p(FPLX:CHRM)) -> act(p(FPLX:CHRM))

SET Evidence = "The two best known
muscarinic receptor antagonists are atropine and quinuclidinyl
benzilate, which block all muscarinic receptors"
a(CHEBI:atropine) -| act(p(FPLX:CHRM))
a(MESH:"Quinuclidinyl Benzilate") -| act(p(FPLX:CHRM))

SET Evidence = "Because ACh has an important role
in cognitive processes, the cholinergic system is pointed as
an important factor in many forms of dementia, including AD"
bp(GO:"synaptic transmission, cholinergic") -- path(MESH:"Alzheimer Disease")
bp(GO:"synaptic transmission, cholinergic") -- path(MESH:Dementia)
a(CHEBI:acetylcholine) --  bp(GO:cognition)

SET Evidence = "Disruption of cholinergic inputs to the cortex can
impair attention and the use of instructive cues needed for
decision-making related to ongoing behavior"
bp(GO:"synaptic transmission, cholinergic") -> path(MESH:Attention)

SET Evidence = "Moreover, it has been shown that blocking CA3 cholinergic
receptors impairs the encoding of information and memory"
SET MeSHAnatomy = "CA3 Region, Hippocampal"
a(MESH:"Receptors, Cholinergic") -> bp(GO:memory)

SET Evidence = "In addition to cognitive alterations, psychiatric
symptoms are frequently observed in AD patients, including apathy and depression"
path(MESH:"Alzheimer Disease") positiveCorrelation path(MESH:Apathy)
path(MESH:"Alzheimer Disease") positiveCorrelation path(MESH:Depression)
path(MESH:"Alzheimer Disease") positiveCorrelation path(MESH:"Cognitive Dysfunction")

SET Evidence = "It has been postulated that the
loss of cholinergic neurons and the consequent impairment
in dopaminergic transmission could be the main factors
underling AD-related psychiatric symptoms"
a(MESH:"Cholinergic Neurons") -> bp(GO:"synaptic transmission, dopaminergic")
SET MeSHDisease = "Alzheimer Disease"
a(MESH:"Cholinergic Neurons") -| path(MESH:Apathy)
a(MESH:"Cholinergic Neurons") -| path(MESH:Depression)
UNSET MeSHDisease

SET Evidence = "Corroborating this hypothesis, it has been demonstrated that
dopamine efflux is increased in the nucleus accumbens of M4 knockout mice"
SET Species = "10090"
p(MGI:Chrm4) -| tloc(a(CHEBI:dopamine),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
UNSET Species

SET Evidence = "Further highlighting the importance of the cholinergic system in the
CNS, cholinergic neuronal loss, especially in the basal
forebrain, occurs not only in AD, but also in Parkinson’s
disease [190, 191], Down syndrome [192], amyotrophic
lateral sclerosis [193, 194], progressive supranuclear palsy
[195, 196], and olivopontocerebellar atrophy [197]"
SET MeSHAnatomy = {"Basal Forebrain","Central Nervous System"}
path(MESH:"Alzheimer Disease") negativeCorrelation a(MESH:"Cholinergic Neurons")
path(MESH:"Parkinson Disease") negativeCorrelation a(MESH:"Cholinergic Neurons")
path(MESH:"Down Syndrome") negativeCorrelation a(MESH:"Cholinergic Neurons")
path(MESH:"Amyotrophic Lateral Sclerosis") negativeCorrelation a(MESH:"Cholinergic Neurons")
path(MESH:"Supranuclear Palsy, Progressive") negativeCorrelation a(MESH:"Cholinergic Neurons")
path(MESH:"Olivopontocerebellar Atrophies") negativeCorrelation a(MESH:"Cholinergic Neurons")
UNSET MeSHAnatomy

SET Evidence = "Moreover, Huntington’s disease appears to be associated with decreased ChAT activity"
path(MESH:"Huntington Disease") negativeCorrelation act(p(HGNC:CHAT))

SET Evidence = "It has been demonstrated that cholinergic synapses are
particularly affected by Aβ oligomers early neurotoxicity
[218, 219] and that synaptic loss is the major correlate of
cognitive impairment"
a(HBP:"amyloid-beta oligomers") -- act(a(GO:"cholinergic synapse"))
path(MESH:"Cognitive Dysfunction") negativeCorrelation a(GO:synapse)

SET Evidence = "In fact, the memory deficit and neuropathology severity observed in patients exhibiting
AD highly correlate with changes in hippocampal synaptic transmission, especially with changes in
synaptophysin expression, a presynaptic vesicle protein"
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Hippocampus"
bp(MESH:"Synaptic Transmission") -- bp(GO:memory)
p(HGNC:SYP) -- bp(GO:memory)
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "The loss of the nucleus basalis cholinergic neurons in AD patients is severe:
from about 500,000 in the healthy adult to less than 100,000
in patients displaying advanced AD"
path(MESH:"Alzheimer Disease") negativeCorrelation a(MESH:"Basal Nucleus of Meynert")

SET Evidence = "Moreover, it has
been demonstrated that ChAT transcription is severely
diminished in the remaining cholinergic neurons, which
leads to decreased ChAT activity and progression of dementia"
act(p(HGNC:CHAT)) -| path(MESH:Dementia)
path(MESH:"Alzheimer Disease") -| act(p(HGNC:CHAT))

SET Evidence = "All these cholinergic alterations
that take place in AD closely correlates with impaired
attention and memory observed in patients"
SET MeSHDisease = "Alzheimer Disease"
a(MESH:"Cholinergic Neurons") -> path(MESH:Attention)
a(MESH:"Cholinergic Neurons") -> bp(GO:memory)
UNSET MeSHDisease

SET Evidence = "Furthermore, some evidences also suggest the involvement
of AChE in the pathogenesis of AD"
p(HGNC:ACHE) -- path(MESH:"Alzheimer Disease")

SET Evidence = "A molecular modeling study showed that AChE interacts with the Aβ
peptide and promotes amyloid fibril formation"
complex(p(HGNC:ACHE),a(MESH:"Amyloid beta-Peptides")) -> a(HBP:"amyloid-beta fibrils")

SET Evidence = "In this work, kinetic analyses revealed that a structural motif in AChE (a
hydrophobic sequence of 35 resides peptides) was able to promote amyloid formation and its
incorporation into the growing Aβ-fibrils"
p(HGNC:ACHE) -> a(CHEBI:"amyloid-beta")
a(CHEBI:"amyloid-beta") -> a(HBP:"amyloid-beta fibrils")

SET Evidence = "In addition, quantitative autoradiography assay revealed
that choline uptake was reduced in the hippocampus of these
animals and that the expression of muscarinic and nicotinic
cholinergic receptors was diminished"
# these animals: Tg2576 mice
SET Species = "10090"
SET MeSHAnatomy = "Hippocampus"
p(MGI:App,var("?")) -| tloc(a(CHEBI:choline),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
p(MGI:App,var("?")) -| p(FPLX:CHRM)
p(MGI:App,var("?")) -| p(FPLX:CHRN)
UNSET Species
UNSET MeSHAnatomy

SET MeSHDisease = "Alzheimer Disease"

SET Evidence = "Cholinesterase inhibitors can increase ACh levels in the
synaptic cleft and partially ameliorate cognitive symptoms,
enhance quality of life and diminish caregiver burden for
patients with mild to severe AD"
# in the synaptic cleft
a(MESH:"Cholinesterase Inhibitors") -> a(CHEBI:acetylcholine)
a(MESH:"Cholinesterase Inhibitors") -> bp(GO:cognition)

SET Evidence = "It has been demonstrated that treatment with donepezil
is associated with significant cognitive and functional
benefits over the course of 12 months in patients with
moderate to severe AD"
a(CHEBI:donepezil) -> bp(GO:cognition)

SET Evidence = "In a double-blind study
designed to assess patients survival with mild to moderately
severe AD, the long-term treatment (2 years) with galantamine
significantly reduced mortality and cognition decline,
besides improving daily live activities in mild to moderate AD patients"
a(CHEBI:galanthamine) -| path(MESH:Death)
a(CHEBI:galanthamine) -> bp(GO:cognition)

SET Evidence = "Rivastigmine has also been used for AD
treatment due to its ease of use (transdermal patch) and good
tolerability by patients"
a(CHEBI:rivastigmine) negativeCorrelation path(MESH:"Alzheimer Disease")

SET Evidence = "It has been demonstrated that M1 muscarinic
receptors coupling to G-proteins is impaired in the neocortex
of AD patients and that the extent of M1/G-protein
uncoupling is related to the severity of cognitive symptoms in AD"
SET MeSHAnatomy = "Neocortex"
path(MESH:"Alzheimer Disease") negativeCorrelation complex(p(HGNC:CHRM1),p(INTERPRO:"G-protein alpha subunit, group Q"))
complex(p(HGNC:CHRM1),p(INTERPRO:"G-protein alpha subunit, group Q")) -> bp(GO:cognition)
UNSET MeSHAnatomy

SET Evidence = "Interestingly, M1 receptor signaling affects several of AD major hallmarks,
including cholinergic deficit, cognitive dysfunction, and tau and Aβ pathologies"
act(p(HGNC:CHRM1)) -- a(MESH:"Cholinergic Neurons")
act(p(HGNC:CHRM1)) -- path(MESH:"Cognitive Dysfunction")
act(p(HGNC:CHRM1)) -- act(p(HGNC:MAPT))
act(p(HGNC:CHRM1)) -- act(a(CHEBI:"amyloid-beta"))

SET Evidence = "It has been shown that activation
of M1 receptors decreases tau hyperphosphorylation via
activation of PKC and inhibition of GSK-3β"
act(p(HGNC:CHRM1)) -| p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
act(p(HGNC:CHRM1)) -> act(p(FPLX:PKC))
act(p(HGNC:CHRM1)) -| act(p(HGNC:GSK3B))

SET Evidence = "Moreover, the M1 agonist AF267B can rescue the cognitive
impairment and decrease Aβ42 and tau abnormalities in the
cortex and hippocampus of a mouse model of AD"
SET MeSHAnatomy = {"Hippocampus","Neocortex"}
a(PUBCHEM:10013505) -> act(p(HGNC:CHRM1))
a(PUBCHEM:10013505) -> bp(GO:cognition)
a(PUBCHEM:10013505) -| a(CHEBI:"amyloid-beta polypeptide 42")
a(PUBCHEM:10013505) -| p(HGNC:MAPT,var("?"))
UNSET MeSHAnatomy

SET Evidence = "Moreover, AF267B treatment leads to an increase in alpha
secretase, which is an enzyme that can prevent the production of Aβ peptide"
a(PUBCHEM:10013505) -> a(MESH:"Amyloid Precursor Protein Secretases")
a(MESH:"Amyloid Precursor Protein Secretases") -| a(MESH:"Amyloid beta-Peptides")

SET Evidence = "Moreover, the use of M2 antagonists, such as SCH-57790 and SC-72788, can lead to
blockage of M2-mediated inhibition of presynaptic release of ACh, which can activate M1 and nicotinic receptors,
ameliorating cognitive impairment in AD"
a(PUBCHEM:9867750) -> tloc(a(CHEBI:acetylcholine),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
p(HGNC:CHRM2) -| tloc(a(CHEBI:acetylcholine),fromLoc(GO:intracellular),toLoc(GO:"extracellular region"))
a(PUBCHEM:9867750) -> act(p(HGNC:CHRM1))
a(PUBCHEM:9867750) -> act(a(MESH:"Receptors, Nicotinic"))
a(PUBCHEM:9867750) -> bp(GO:cognition)

SET Evidence = "In addition, activation of M2 receptors can cause an increase in Aβ production"
act(p(HGNC:CHRM2)) -> a(CHEBI:"amyloid-beta")

SET Evidence = "However, further studies have
demonstrated that nicotinic receptor activation can lead to an
increase in Aβ-mediated tau phosphorylation"
act(a(MESH:"Receptors, Nicotinic")) -> p(HGNC:MAPT,pmod(Ph))
a(CHEBI:"amyloid-beta") reg p(HGNC:MAPT,pmod(Ph))

SET Evidence = "In a large-scale placebo-controlled clinical trial, it was
demonstrated that xanomeline, a muscarinic agonist with
reasonable selectivity for M1/M4 receptors, exhibits a
positive effect in minimizing, in a dose-dependent manner,
cognitive and psychiatric symptoms in AD, including memory
deficit, mood disturbance, agitation and hallucinations"
a(CHEBI:xanomeline) -> bp(GO:cognition)
a(CHEBI:xanomeline) -> bp(GO:memory)
a(CHEBI:xanomeline) -| path(MESH:"Mood Disorders")
a(CHEBI:xanomeline) -| path(MESH:"Psychomotor Agitation")
a(CHEBI:xanomeline) -| path(MESH:Hallucinations)

SET Evidence = "For example, it has been shown that the benzyl
quinolone carboxylic acid (BQCA), which is an M1-selective
allosteric agonist, is effective in increasing spontaneous
excitatory postsynaptic currents in the medial prefrontal
cortex (mPFC) pyramidal cells and improving memory in a
transgenic mouse model of AD"
SET MeSHAnatomy = "Pyramidal Cells"
a(PUBCHEM:1476756) -> bp(GO:"excitatory postsynaptic potential")
a(PUBCHEM:1476756) -> bp(GO:memory)
UNSET MeSHAnatomy

SET Evidence = "In fact, it has been shown that APP interacts
with CHT1 proteins increasing their endocytosis from the cell surface [258] and that mice that display disruption of
CHT1 gene expression exhibit symptoms related to ACh deficit"
complex(p(HGNC:APP),p(HGNC:SLC5A7)) -> tloc(p(HGNC:APP),fromLoc(GO:"cell surface"),toLoc(GO:intracellular))

SET Evidence = "In fact, memantine is able to reduce the excitotoxicity in AD"
SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:memantine) -| a(HBP:Excitotoxicity)
UNSET MeSHDisease

SET Evidence = "Furthermore, it has also been
proposed that the nerve growth factor (NGF) has the
potential to preserve cholinergic neurons"
a(MESH:"Nerve Growth Factor") -> a(MESH:"Cholinergic Neurons")

SET Evidence = "Moreover, previous studies have shown that treatment
with NGF can counteract cholinergic atrophy and memory
deficits in aged rats"
a(MESH:"Nerve Growth Factor") -> a(MESH:"Cholinergic Neurons")
a(MESH:"Nerve Growth Factor") -> bp(GO:memory)
