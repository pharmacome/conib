#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Tau degradation: the ubiquitin-proteasome system versus the autophagy-lysosome system."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3c85897632afa791ac78fb3aa2e392963ab155b1/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET STATEMENT_GROUP = "Esther"

# Add statements below this comment

SET Citation = {"PubMed","Prog Neurobiol. 2013 Jun;105:49-59. doi: 10.1016/j.pneurobio.2013.03.001. Epub 2013 Mar 23.","23528736"}

SET Evidence = "The levels of Ub in AD brain appear to be significantly increased ( Kudo et al.,  71 1994 )."
#Ub proteins:UBB, UBC, UBA52, and UBA80
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:UBB)
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:UBC)
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:UBA52)
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:RPS27A)

SET Evidence = " A Ub with a 19-residue C-terminal extension from the UBB
 gene, or UBB+1 (Fig.2A) is selectively expressed in the brains of AD patients (van Leeuwen et al.,1998) and is
 often found to be accumulated in NFT in Alzheimer’s disease and other tauopathies"
SET MeSHAnatomy = "Brain"
p(HBP:"UBB+1") positiveCorrelation path(MESH:"Alzheimer Disease")
SET MeSHDisease = "Alzheimer Disease"
p(HBP:"UBB+1") -- a(MESH:"Neurofibrillary Tangles")
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "UBB+ 1-capped polyUb chains are resistant to
deubiquitination and inhibit proteasomal activity, which may mediate
neurodegeneration through mitochondrial stress and p53 activation in
neurites (Tan et al., 2007)."
p(HBP:"UBB+1") -| bp(GO:"protein deubiquitination")
p(HBP:"UBB+1") -| act(p(FPLX:Proteasome))
p(HBP:"UBB+1") -> bp(HP:Neurodegeneration)
SET MeSHAnatomy = {"Neurites","Mitochondria"}
p(HBP:"UBB+1") -> bp(MESH:"Stress, Physiological")
SET MeSHAnatomy = "Neurites"
p(HBP:"UBB+1") -> act(p(HGNC:TP53))
UNSET MeSHAnatomy

SET Evidence = "Interestingly, E1 and E2 enzymes have been reported
to be downregulated in AD (de Vrij et al., 2004) (Fig. 2B)."
path(MESH:"Alzheimer Disease") negativeCorrelation p(HGNCGENEFAMILY:"Ubiquitin like modifier activating enzymes")
path(MESH:"Alzheimer Disease") negativeCorrelation p(HGNCGENEFAMILY:"Ubiquitin conjugating enzymes E2")

SET Evidence = "Several E3 Ub ligases, such as CHIP, Parkin, RNF182,
may have roles in AD and other neurodegenerative diseases (Shimura et
al., 2004; Khandelwal et al., 2011; Lei et al., 2012)."
p(HGNC:STUB1) -- path(MESH:"Neurodegenerative Diseases")
p(HGNC:PRKN) -- path(MESH:"Neurodegenerative Diseases")
p(HGNC:RNF182) -- path(MESH:"Neurodegenerative Diseases")

SET Evidence = "Mutations in ubiquitin C-terminal hydrolase L1 (UCH-L1)
have been proposed to have a role in various neurodegenerative diseases
including AD, although the causality of mutations in this gene in
Parkinson’s disease is now considered doubtful (Healy et al., 2006; Hutter
et al., 2008)."
p(HGNC:UCHL1, var("?")) -- path(MESH:"Neurodegenerative Diseases")
p(HGNC:UCHL1, var("?")) -- path(MESH:"Alzheimer Disease")

SET Evidence = "Another DUB known to be mutated in familial neurodegenerative
diseases is Ataxin-3 in the polyglutamine (polyQ) spinocerebellar ataxia
type 3 (Kawaguchi et al., 1994). "
p(HGNC:ATXN3, var("?")) -- path(MESH:"Neurodegenerative Diseases")

SET Evidence = "All three proteolytic activities have been reported to
be decreased in AD brains (Keller et al., 2000)."
SET MeSHAnatomy = "Brain"
path(MESH:"Alzheimer Disease") -| bp(MESH:"Proteolysis")
UNSET MeSHAnatomy

SET Evidence = "Recent evidence has shown that dysfunction in the UPS is closely
related with tau degradation/aggregation and neurodedeneration in AD."
SET MeSHDisease = "Alzheimer Disease"
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -- deg(p(HGNC:MAPT))
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -- a(HBP:"Tau aggregates")
UNSET MeSHDisease

SET Evidence = "Related to these data, reversible and irreversible proteasome
inhibitors including lactacystin, leupeptin, and epoxomicin delay the degradation
of endogenous and/or transiently overexpressed tau (Cardozo and Michaud, 2002; David
et al., 2002; Zhang et al., 2005). "
a(CHEBI:lactacystin) -| act(p(FPLX:Proteasome))
a(CHEBI:leupeptin) -| act(p(FPLX:Proteasome))
a(CHEBI:epoxomicin) -| act(p(FPLX:Proteasome))
a(CHEBI:lactacystin) -| deg(p(HGNC:MAPT))
a(CHEBI:leupeptin) -| deg(p(HGNC:MAPT))
a(CHEBI:epoxomicin) -| deg(p(HGNC:MAPT))

SET Evidence = "Blocking proteasomes using Ab oligomers also effectively facilitates
tau accumulation in AD mice (Tseng et al., 2008)."
SET MeSHDisease = "Alzheimer Disease"
a(HBP:"amyloid-beta oligomers") -| act(p(FPLX:Proteasome))
a(HBP:"amyloid-beta oligomers") -> p(HGNC:MAPT)
UNSET MeSHDisease

SET Evidence = "Further evidence of UPS-mediated tau degradation came from
the identiﬁcation of the tau E3 ligase, which is the carboxyl terminus of
the Hsc70–interacting protein (CHIP)–Hsc70 complex, with UbcH5B as the E2
enzyme (Petrucelli et al., 2004; Shimura et al., 2004)."
complex(p(HGNC:STUB1),p(HGNC:HSPA8))
composite(p(HGNC:STUB1),p(HGNC:HSPA8),p(HGNC:UBE2D2)) -> deg(p(HGNC:MAPT))

SET Evidence = "Pathologically, the AD brains show both accumulated autophagosomes
and autolysosomes, suggesting the basal autophagy process in neurons is abnormal
in AD (Fig. 3A)."
SET MeSHAnatomy = "Brain"
path(MESH:"Alzheimer Disease") -> a(MESH:Autophagosomes)
path(MESH:"Alzheimer Disease") -> a(GO:autolysosome)
UNSET MeSHAnatomy

SET Evidence = "The accumulated autophagic vacuoles in AD mainly reﬂect
defected lysosomal clearance instead of induced autophagy."
path(MESH:"Alzheimer Disease") -| bp(GO:"lysosomal protein catabolic process")

SET Evidence = "Oxidative stress also upregulates autophagy induction,
which limits the production of reactive oxygen species from dysfunctional
mitochondria."
bp(GO:"response to oxidative stress") -> bp(GO:autophagy)

SET Evidence = "The mammalian target of rapamycin (mTOR) kinase negatively
modulates autophagy by phosphorylating Atg1, an autophagy initiating factor,
while adenosine monophosphate-activated protein kinase (AMPK), a major
sensor for the cellular energy status, activates autophagy through inhibiting
mTOR signaling as well as by direct phosphorylation of Atg1 (Egan et al., 2011;
Kim et al., 2011). Increased mTOR activity results in autophagy downregulation
and tau accumulation."
p(FPLX:mTORC1) -| bp(GO:autophagy)
kin(p(FPLX:mTORC1)) -> p(HGNC:ULK1, pmod(Ph))
p(HGNC:ULK1) -> bp(GO:autophagy)
p(FPLX:AMPK) -> bp(GO:autophagy)
p(FPLX:AMPK) -| act(p(FPLX:mTORC1))
p(FPLX:AMPK) -> p(HGNC:ULK1, pmod(Ph))
act(p(FPLX:mTORC1)) -> p(HGNC:MAPT)

SET Evidence = "For instance, c-Jun NH 2-terminal kinase 1 (JNK1) may induce
autophagy by phosphorylating Bcl-2 or Bim and abolishing their inhibitory
effects on autophagy (Luo et al., 2012; Wei et al., 2008)."
p(HGNC:MAPK8) -> bp(GO:autophagy)
p(HGNC:MAPK8) -> p(HGNC:BCL2, pmod(Ph))
p(HGNC:MAPK8) -> p(HGNC:BCL2L11, pmod(Ph))
p(HGNC:BCL2) -| bp(GO:autophagy)
p(HGNC:BCL2L11) -| bp(GO:autophagy)

SET Evidence = "PHF-tau isolated from human AD brains in the soluble state
was not degraded by the proteasome but instead functioned as an inhibitor
of its activity (Keck et al., 2003)."
SET MeSHAnatomy = "Brain"
SET MeSHDisease = "Alzheimer Disease"
a(HP:"Neurofibrillary tangles") -| act(p(FPLX:Proteasome))
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "Tau proteins in a variety of forms are reported as degraded
through the autophagy-lysosome system."
bp(GO:autophagy) -> deg(p(HGNC:MAPT))

SET Evidence = "Cathepsin D, a lysosomal protease, exhibited the capacity
to degrade tau proteins in cultured hippocampal slices (Bednarski and Lynch,
1996)."
p(HGNC:CTSD) -> deg(p(HGNC:MAPT))

SET Evidence = "Autophagy inducers, including rapamycin (Fig. 3A),
facilitate the degradation of insoluble forms of tau and also protect
against its toxicity in Drosophila (Berger et al., 2006)."
#rapamycin is sirolimus
a(CHEBI:sirolimus) -> deg(p(HGNC:MAPT))

SET Evidence = "Trehalose, an natural disaccharide and mTOR-independent
activator of autophagy (Sarkar et al., 2007), showed an effect on neuronal
survival, reducing the level of tau aggregates in the brain of human
tauopathy model mice (Kruger et al., 2011; Rodriguez-Navarro et al.,
2010; Schaeffer et al., 2012)."
a(CHEBI:"alpha,alpha-trehalose") -> bp(GO:autophagy)
a(CHEBI:"alpha,alpha-trehalose") -| bp(GO:"neuron apoptotic process")
SET MeSHAnatomy = "Brain"
a(CHEBI:"alpha,alpha-trehalose") -| a(HBP:"Tau aggregates")
UNSET MeSHAnatomy

SET Evidence = "Methylene blue, which has been known to directly
inhibit tau aggregation, is also capable to induce autophagy and
reduce total and phospho-tau levels with improved cognitive performance
in tau transgenic mice by oral administration (Congdon et al., 2012)."
a(CHEBI:"methylene blue") -| a(HBP:"Tau aggregates")
a(CHEBI:"methylene blue") -> bp(GO:autophagy)
a(CHEBI:"methylene blue") -| p(HGNC:MAPT)
a(CHEBI:"methylene blue") -| p(HGNC:MAPT, pmod(Ph))

SET Evidence = "Moreover, constitutive inactivation of p62 in mice
resulted in aggregated tau proteins with Lys63–Ub chains (Babu et
al., 2008)."
p(HGNC:DCTN4) -| a(HBP:"Tau aggregates")

SET Evidence = "Aging, a major risk factor for AD, affects both the
UPS and autophagy."
bp(MESH:Aging) -> path(MESH:"Alzheimer Disease")
bp(MESH:Aging) -| bp(GO:autophagy)
bp(MESH:Aging) -| act(p(FPLX:Proteasome))

SET Evidence = "For example, the small molecule IU1 was recently identiﬁed
from a high-throughput screen as a potent and selective inhibitor of USP14,"
a(PUBCHEM:"675434") -| act(p(HGNC:USP14))

SET Evidence = "In a cell-based assay, IU1 treatment increased proteasomal
activity to result in accelerated degradation rates of tau and oxidatively
damaged proteins."
a(PUBCHEM:"675434") -> act(p(FPLX:Proteasome))
a(PUBCHEM:"675434") -> deg(p(HGNC:MAPT))

SET Evidence = "and lithium (Shimada et al., 2012) in human tau over-expressing
Tg mice induced autophagy, which is accompanied by the reduced levels of pathological
tau and NFT formation in tauopathy model mice."
a(CHEBI:"lithium atom") -> bp(GO:autophagy)
SET MeSHDisease = "Tauopathies"
a(CHEBI:"lithium atom") -| p(HGNC:MAPT)
a(CHEBI:"lithium atom") -| a(HP:"Neurofibrillary tangles")
UNSET MeSHDisease
