#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Hypoxia Inducible Factor-1 as a Target for Neurodegenerative Diseases."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
 DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/4aa36980578c7d2cff07371abf768f15db89cd11/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
 DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
 DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
 DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
# DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
 DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"
 DEFINE NAMESPACE NCIT           AS PATTERN "^C\d+$"


###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
 DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
 DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
 DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
# DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
 DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "21861815"}

SET Evidence = "HIF-1 is a heterodimetric complex consisting of a constitutively expressed β subunit (also
known as the aryl hydrocarbon receptor nuclear translocation, ARNT) and a hypoxia
inducible subunit, α subunit [2]."
p(MESH:"Hypoxia-Inducible Factor 1") hasComponents list(p(HGNC:HIF1A),p(HGNC:ARNT))

SET Evidence = "The bHLH and PAS motifs are required for dimerization between HIF-1α and HIF-1β."
#bHLH - basic helix-loop-helix
#PAS - PER-ARNT-SIM
p(HGNCGENEFAMILY:"Basic helix-loop-helix proteins") -> p(MESH:"Hypoxia-Inducible Factor 1")
p(PFAM:PAS) -> p(MESH:"Hypoxia-Inducible Factor 1")

SET Evidence = "Under hypoxia, HIF-1α is stabilized and translocated to the nucleus where it dimerizes with HIF-1β
[3]."
bp(GO:"response to hypoxia") -> tloc(p(HGNC:HIF1A),fromLoc(GO:cytoplasm),toLoc(GO:nucleus))
p(HGNC:HIF1A,loc(GO:nucleus)) -> complex(p(HGNC:HIF1A),p(HGNC:ARNT),loc(GO:nucleus))
complex(p(HGNC:HIF1A),p(HGNC:ARNT),loc(GO:nucleus)) -> act(p(MESH:"Hypoxia-Inducible Factor 1"))

SET Evidence = "The activated HIF-1 complex subsequently binds to hypoxic response elements (HREs)
in the regulatory regions of genes and recruits co-transactivation factors such as CBP/p300
[CBP: CREB (cAMP-response element-binding protein)-binding protein/E1A-binding
protein]."
#NCIT:C13718 - HRE
complex(p(MESH:"Hypoxia-Inducible Factor 1"),p(NCIT:C13718)) -> p(HGNC:CREBBP)
complex(p(MESH:"Hypoxia-Inducible Factor 1"),p(NCIT:C13718)) -> p(HGNC:EP300)

SET Evidence = "The activity of HIF-1 is mainly controlled through regulating the protein level of HIF-1α [3]."
p(HGNC:HIF1A) reg act(p(MESH:"Hypoxia-Inducible Factor 1"))

SET Evidence = "The hydroxylation is mediated by a family of prolyl hydroxylases, namely PHD1,
PHD2, and PHD3 [12], among which PHD2 has the highest specificity for hydroxylation of HIF-1α [11]."
#prolyl residue 402,546
p(HGNC:EGLN2) -> p(HGNC:HIF1A,pmod(Hy,Pro,402))
p(HGNC:EGLN2) -> p(HGNC:HIF1A,pmod(Hy,Pro,546))
p(HGNC:EGLN1) -> p(HGNC:HIF1A,pmod(Hy,Pro,402))
p(HGNC:EGLN1) -> p(HGNC:HIF1A,pmod(Hy,Pro,546))
p(HGNC:EGLN3) -> p(HGNC:HIF1A,pmod(Hy,Pro,402))
p(HGNC:EGLN3) -> p(HGNC:HIF1A,pmod(Hy,Pro,546))

SET Evidence = "These PHD enzymes are 2-oxoglutarate-dependent dioxygenases and require oxygen as well as Fe2+ as cofactors [13–14]."
composite(a(CHEBI:"2-oxoglutarate(1-)"),a(CHEBI:dioxygen),a(CHEBI:"iron(2+)")) -> act(p(HGNC:EGLN2))
composite(a(CHEBI:"2-oxoglutarate(1-)"),a(CHEBI:dioxygen),a(CHEBI:"iron(2+)")) -> act(p(HGNC:EGLN1))
composite(a(CHEBI:"2-oxoglutarate(1-)"),a(CHEBI:dioxygen),a(CHEBI:"iron(2+)")) -> act(p(HGNC:EGLN3))

SET Evidence = "Hydroxylation promotes interaction of HIF-1α with the von Hippel-Lindau (pVHL)
ubiquitin E3 ligase complex [15–16]. HIF-1α is thereby poly-ubiquitinated and degraded by the 26S proteasome [17–18]."
#NCIT:C13970 - 26S proteasome
p(HGNC:HIF1A,pmod(Hy,Pro,402)) -> complex(p(HGNC:VHL),p(HGNC:HIF1A))
p(HGNC:HIF1A,pmod(Hy,Pro,402)) -> complex(p(HGNC:VHL),p(HGNC:HIF1A))
complex(p(HGNC:VHL),p(HGNC:HIF1A)) -> p(HGNC:HIF1A,pmod(Ub))
p(HGNC:HIF1A,pmod(Ub)) -> composite(p(HGNC:HIF1A,pmod(Ub)),p(NCIT:C13970))
composite(p(HGNC:HIF1A,pmod(Ub)),p(NCIT:C13970)) -> deg(p(HGNC:HIF1A))

SET Evidence = "Since the PHDs employ non-heme iron
in the catalytic moiety, it can be predicted that shortage of intracellular iron will
result in inhibition of activity of PHDs and HIF-1 upregulation."
a(CHEBI:"iron chelator") -| p(HGNC:EGLN2)
a(CHEBI:"iron chelator") -| p(HGNC:EGLN1)
a(CHEBI:"iron chelator") -| p(HGNC:EGLN3)
a(CHEBI:"iron chelator") -> r(HGNC:HIF1A)

SET Evidence = "At the transcriptional level, oxygen availability enables hydroxylation of the
asparagine residue 803 in the C-TAD of HIF-1α by factor inhibiting HIF (FIH-1),
which blocks the association of HIF-1α with transcriptional co-activators CBP/
p300 and thus inhibits its transcriptional activity [20]."
composite(p(HGNC:HIF1AN),a(CHEBI:dioxygen)) -> p(HGNC:HIF1A,pmod(Hy,Asn,803))
p(HGNC:HIF1A,pmod(Hy,Asn,803)) -| complex(p(HGNC:HIF1A),p(HGNC:CREBBP))
p(HGNC:HIF1A,pmod(Hy,Asn,803)) -| complex(p(HGNC:HIF1A),p(HGNC:EP300))
p(HGNC:HIF1A,pmod(Hy,Asn,803)) -| tscript(p(HGNC:CREBBP))
p(HGNC:HIF1A,pmod(Hy,Asn,803)) -| tscript(p(HGNC:EP300))

SET Evidence = "K532 acetylation by the ARrest-Defective-1 (ARD-1) enhances the interaction between HIF-1α and pVHL
and, thus, leads to increased ubiquitination and concomitant proteasomal degradation [21]."
p(HGNC:NAA10) -> p(HGNC:HIF1A,pmod(Ac,Lys,532))
p(HGNC:HIF1A,pmod(Ac,Lys,532)) -> complex(p(HGNC:VHL),p(HGNC:HIF1A))
p(HGNC:NAA10) -> complex(p(HGNC:VHL),p(HGNC:HIF1A))
complex(p(HGNC:VHL),p(HGNC:HIF1A)) -> deg(p(HGNC:HIF1A))
p(HGNC:NAA10) -> deg(p(HGNC:HIF1A))

SET Evidence = "Mutation of lysine 532 to arginine has been reported to result in increased stability of overexpressed HIF-1α [22]."
p(HGNC:HIF1A,var("p.Lys532?")) -> p(HGNC:HIF1A)

SET Evidence = "Direct phosphorylation on threonine 796 by mitogen-activated protein kinase
(MAPK) has recently been identified as a novel modification on HIF-1α and is able
to increase HIF-1 activity [23]."
p(HGNCGENEFAMILY:"Mitogen-activated protein kinases") -> p(HGNC:HIF1A,pmod(Ph,Thr,796))
p(HGNCGENEFAMILY:"Mitogen-activated protein kinases") -> act(p(MESH:"Hypoxia-Inducible Factor 1"))

SET Evidence = "One mechanism to explain the increased transcriptional activity is that HIF-1β binds preferentially to the phosphorylated form of HIF-1α [24]."
p(HGNC:HIF1A,pmod(Ph,Thr,796)) -> complex(p(HGNC:HIF1A),p(HGNC:ARNT))

SET Evidence = "Moreover, it has been reported that phosphorylation at T796
in HIF-1α increases the affinity of the interaction between HIF-1α and the
transcriptional co-activator CBP/p300 [25]."
p(HGNC:HIF1A,pmod(Ph,Thr,796)) -> complex(p(HGNC:HIF1A),p(HGNC:CREBBP))
p(HGNC:HIF1A,pmod(Ph,Thr,796)) -> complex(p(HGNC:HIF1A),p(HGNC:EP300))

SET Evidence = "Consistent with this, it is found that
phosphorylation of T796 prevents the hydroxylation of N803 by FIH [25]."
p(HGNC:HIF1A,pmod(Ph,Thr,796)) -| act(p(HGNC:HIF1AN))
p(HGNC:HIF1A,pmod(Ph,Thr,796)) -| p(HGNC:HIF1A,pmod(Hy,Asn,803))

SET Evidence = "Inhibition of these phosphorylation sites has been shown to impair both
transcriptional activity and the nuclear localization of HIF-1α [26]."
p(HGNC:HIF1A,pmod(Ph,Thr,796)) -> tscript(p(HGNC:HIF1A))
p(HGNC:HIF1A,pmod(Ph,Thr,796)) -> tloc(p(HGNC:HIF1A),fromLoc(GO:cytoplasm),toLoc(GO:nucleus))

SET Evidence = "In addition, phosphorylation of HIF-1α by glycogen synthase kinase (GSK)-3β may target
HIF-1α for proteasomal degradation [26]."
p(HGNC:GSK3B) -> p(HGNC:HIF1A,pmod(Ph))
act(p(HGNC:HIF1A,pmod(Ph)),ma(GO:"proteasome binding")) -> deg(p(HGNC:HIF1A))

SET Evidence = "Hydroxyl radical and hydrogen peroxide can destabilize
HIF-1α protein in both normoxic and hypoxic conditions [27–28]."
a(CHEBI:hydroxyl) -| p(HGNC:HIF1A)
a(CHEBI:"hydrogen peroxide") -| p(HGNC:HIF1A)

SET Evidence = "The oxidized HIF-1α protein might be recognized and degraded by the ubiquitin-independent
20S proteasomal pathway, which primarily degrades cellular oxidized proteins under oxidative stress conditions [29]."
#C13737 - 20S Proteasome
#Oxidative stress condition
p(HGNC:HIF1A,pmod(NCIT:C16944)) -> complex(p(HGNC:HIF1A),a(NCIT:C13737))
complex(p(HGNC:HIF1A),a(NCIT:C13737)) -> deg(p(HGNC:HIF1A))

SET Evidence = "Moreover, studies have revealed
that S-nitrosation stabilizes HIF-1α protein and S-nitrosation of cysteine 800 of
HIF-1α promotes its interaction with CBP/p300, thus enhancing HIF-1α activation
[30]."
p(HGNC:HIF1A,pmod(NO)) -> p(HGNC:HIF1A)
p(HGNC:HIF1A,pmod(NO,Cys,800)) -> complex(p(HGNC:HIF1A),p(HGNC:CREBBP))
p(HGNC:HIF1A,pmod(NO,Cys,800)) -> complex(p(HGNC:HIF1A),p(HGNC:EP300))
p(HGNC:HIF1A,pmod(NO,Cys,800)) -> act(p(HGNC:HIF1A))

SET Evidence = "Sumoylation of HIF-1α has been reported to regulate its protein level and
transcriptional activity."
p(HGNC:HIF1A,pmod(Sumo)) reg r(HGNC:HIF1A)
p(HGNC:HIF1A,pmod(Sumo)) reg tscript(p(HGNC:HIF1A))

SET Evidence = "Increasing the activity of RWD-containing sumoylation
enhancer (RSUME) could promote the sumoylation of HIF-1α by Ubc9 SUMO
conjugase and stabilize HIF-1α during hypoxia [32]."
bp(GO:"response to hypoxia") -> act(p(HGNC:RWDD3))
act(p(HGNC:RWDD3)) -> act(p(HGNC:UBE2I))
p(HGNC:UBE2I) -> p(HGNC:HIF1A,pmod(Sumo))
p(HGNC:HIF1A,pmod(Sumo)) -> p(HGNC:HIF1A)

SET Evidence = "However, desumoylation by
SUMO-Specific Protease 1 (SENP-1) has been shown to be essential for the
stabilization of HIF-1α [33]."
p(HGNCGENEFAMILY:"SUMO specific peptidases") -| p(HGNC:HIF1A,pmod(Sumo))
p(HGNCGENEFAMILY:"SUMO specific peptidases") reg p(HGNC:HIF1A)

SET MeSHDisease = "Amyotrophic Lateral Sclerosis"

SET Evidence = "This lack of glucose and oxygen fails to meet
the energy requirement of motor neurons, which will in turn induce neuronal death and lead
to the occurrence of ALS."
#deficits in glucose (hypoglycemia) and oxygen (hypoxia)
SET MeSHAnatomy = "Motor Neurons"
path(DOID:hypoglycemia) neg bp(GO:"energy homeostasis")
path(EFO:hypoxia) neg bp(GO:"energy homeostasis")
bp(GO:"energy homeostasis") -| bp(GO:"neuron death")
path(DOID:hypoglycemia) -> bp(GO:"neuron death")
path(EFO:hypoxia) -> bp(GO:"neuron death")
bp(GO:"neuron death") -> path(DOID:"amyotrophic lateral sclerosis")
UNSET MeSHAnatomy

SET Evidence = "VEGF can induce angiogenesis
and increase blood supply to motor neurons during hypoxia through action on endothelial
cells [39]."
SET MeSHAnatomy = "Motor Neurons"
bp(GO:"response to hypoxia") -> act(p(HGNC:VEGFA))
p(HGNC:VEGFA) -> bp(GO:"blood vessel development")
UNSET MeSHAnatomy

SET Evidence = "There is a negative
correlation between the VEGF level and the severity of hypoxemia in patients with ALS,
which suggests a deregulation of VEGF in ALS [40–41]."
path(EFO:hypoxemia) neg p(HGNC:VEGFA)
path(DOID:"amyotrophic lateral sclerosis") neg act(p(HGNC:VEGFA))

SET Evidence = "ALS-like symptoms and neuropathology can be produced in mice by a targeted deletion of
the hypoxia response element that eliminates the expression of VEGF in response to hypoxia
[42]."
SET Species = "10090"
p(NCIT:C13718) -> p(HGNC:VEGFA)
p(NCIT:C13718) -| path(DOID:neuropathy)
UNSET Species

SET Evidence = "This indicates that an impaired HIF-1-VEGF pathway may contribute to the
pathogenesis of ALS. Furthermore, overexpression of VEGF in SOD1G93A mutant mice
delays the degeneration of motor neurons and neuronal death and prolongs the survival of
these ALS mice [43]."
SET Species = "10090"
path(DOID:"amyotrophic lateral sclerosis") neg act(p(NCIT:C13718))
composite(p(NCIT:C13718),p(HGNC:SOD1,var("Gly93Ala"))) -| bp(GO:"neuron death")
composite(p(NCIT:C13718),p(HGNC:SOD1,var("Gly93Ala"))) -| path(MESH:"Motor Disorders")
UNSET Species

SET Evidence = "By this mechanism, VEGF can prevent the
death of motor neurons and postpone the occurrence of ALS."
#This mechanism - angigensis in brain and spinal cord
SET MeSHAnatomy = {"Brain","Spinal Cord"}
p(HGNC:VEGFA) -> bp(GO:"blood vessel development")
p(HGNC:VEGFA) -| path(DOID:"amyotrophic lateral sclerosis")
UNSET MeSHAnatomy

SET Evidence = "VEGF supports the survival
of primary motor neurons and a motor-like cell line in vitro and could protect these motor
neurons from hypoxia-induced cell death by binding with neuropilin-1, a receptor known to
be involved in axon guidance during development [42, 45]."
SET MeSHAnatomy = "Motor Neurons"
bp(GO:"response to hypoxia") -> bp(GO:"cell death")
complex(p(HGNC:VEGFA),p(HGNC:NRP1)) -| bp(GO:"cell death")
p(HGNC:NRP1) -- bp(GO:"axon guidance")
UNSET MeSHAnatomy

SET Evidence = "It has
been reported that EPO levels are significantly decreased in cerebrospinal fluids from
patients with differing severity and duration of ALS [46], while EPO immunoreacitivity is
significantly increased in midbrain, brain stem, and cortex of the SOD1G93A mutant mice
[47]."
SET MeSHAnatomy = "Cerebrospinal Fluid"
path(DOID:"amyotrophic lateral sclerosis") -| p(HGNC:EPO)
UNSET MeSHAnatomy
SET Species = "10090"
SET Anatomy = {"midbrain","brainstem","cortex"}
p(HGNC:SOD1,var("Gly93Ala")) -> p(HGNC:EPO)
UNSET Anatomy
UNSET Species

SET Evidence = "In this SOD1G93A mutant mouse ALS model, administration of EPO suppresses the
onset and progression of ALS by preventing motor neuron death and inflammation [48–49]."
SET Species = "10090"
composite(p(HGNC:SOD1,var("Gly93Ala")),p(HGNC:EPO)) -| path(DOID:"amyotrophic lateral sclerosis")
composite(p(HGNC:SOD1,var("Gly93Ala")),p(HGNC:EPO)) -| bp(GO:"neuron death")
composite(p(HGNC:SOD1,var("Gly93Ala")),p(HGNC:EPO)) -| bp(GO:"inflammatory response")
UNSET Species

SET Evidence = "Indeed, the iron chelator M30, which up-regulates HIF-1 expression,
protects NSC-34 motor neuron cells from oxidative damage in vitro and significantly delays
the onset of ALS in SOD1G93A mutant mice [50] (Table 2)."
#NSC-34 cell line
a(PUBCHEM:6918822) pos p(MESH:"Hypoxia-Inducible Factor 1")
a(PUBCHEM:6918822) -| bp(GO:"neuron death in response to oxidative stress")
SET Species = "10090"
composite(p(HGNC:SOD1,var("Gly93Ala")),a(PUBCHEM:6918822)) -| path(DOID:"amyotrophic lateral sclerosis")
UNSET Species

UNSET MeSHDisease

SET MeSHDisease = "Alzheimer Disease"

SET Evidence = "The HIF-1 inducer, M30, has been reported to increase the HIF-1α protein level and elevate
expression of the HIF-1 target genes, VEGF and EPO."
a(PUBCHEM:6918822) -> r(HGNC:HIF1A)
a(PUBCHEM:6918822) -> r(HGNC:EPO)
a(PUBCHEM:6918822) -> r(HGNC:VEGFA)

SET Evidence = "It has been reported that M30
simultaneously attenuates tau phosphorylation and protects cortical neurons against Aβ (25–
35) toxicity [57]."
a(PUBCHEM:6918822) -| p(HGNC:MAPT,pmod(Ph))
SET Anatomy = "cortex"
a(PUBCHEM:6918822) -| path(MESH:"Amyloid Neuropathies")
UNSET Anatomy

SET Evidence = "Overexpression of HIF-1α has been shown to protect the rat sympathetic
nerves-like cell line PC12, the central nervous system cell line B12, and primary cultured
cortical neurons from Aβ-induced neurotoxicity [58], possibly through activating glycolytic
and hexose monophosphate shunt-related enzymes."
SET CellLine = {"PC12","B12 cell"}
p(HGNC:HIF1A) -> bp(GO:"glycolytic process")
p(HGNC:HIF1A) -> bp(GO:"pentose-phosphate shunt")
UNSET CellLine

SET Evidence = "Deferoxamine
(DFO), a widely used HIF-1 inducer, has been used in clinical trial in AD patients and
slowed cognitive decline [59]."
a(PUBCHEM:2973) -> p(MESH:"Hypoxia-Inducible Factor 1")
a(PUBCHEM:2973) -| bp(GO:cognition)
a(PUBCHEM:2973) -| path(DOID:"Alzheimer's disease")

SET Evidence = "HIF-1 activation promotes cellular
response to low oxygen, reduced glucose supply, and oxidative stress."
act(p(MESH:"Hypoxia-Inducible Factor 1")) -> bp(GO:"cellular response to oxygen-glucose deprivation")
act(p(MESH:"Hypoxia-Inducible Factor 1")) -> bp(GO:"cellular response to oxidative stress")

SET Evidence = "It is known that reduced cerebral blood flow [60], impaired
glucose uptake and metabolism [61], and increased oxidative stress [62] are involved in the
pathogenesis of AD."
#NCIT:C94869 - Cerebral blood flow
#NCIT:C17741 - oxidative stress
path(DOID:"Alzheimer's disease") -| bp(NCIT:C94869)
path(DOID:"Alzheimer's disease") neg bp(GO:"glucose metabolic process")
path(DOID:"Alzheimer's disease") neg bp(GO:"glucose import")
path(DOID:"Alzheimer's disease") -> bp(NCIT:C17741)

SET Evidence = "It has been shown that individuals who have suffered severe hypoxia or ischemia
are more susceptible to developing AD [63]."
path(EFO:hypoxia) -> path(DOID:"Alzheimer's disease")
path(DOID:ischemia) -> path(DOID:"Alzheimer's disease")

SET Evidence = "In brains of transgenicAD mice, hypoxia
elevates the β-cleavage of amyloid-β precursor protein and increases the expression
of γ-secretase, which in turn increases Aβ generation and senile plaque formation
[66]."
#beta cleavge of APP by BACE1
#amyloid-beta aggregates - senile plaque
SET Species = "10090"
SET MeSHAnatomy = "Brain"
path(EFO:hypoxia) -> p(HGNC:BACE1)
path(EFO:hypoxia) -> complex(GO:"gamma-secretase complex")
complex(GO:"gamma-secretase complex") -> a(CHEBI:"amyloid-β")
complex(GO:"gamma-secretase complex") -> a(CONSO:"amyloid-beta aggregates")
path(EFO:hypoxia) -> a(CHEBI:"amyloid-beta")
path(EFO:hypoxia) -> a(CONSO:"amyloid-beta aggregates")
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "HIF-1 can induce the erythropoiesis and angiogenesis to increase oxygen
delivery and supply [3]. Thus, an increase of HIF-1 activity might reduce the risk
of AD by suppressing the Aβ generation and AD occurrence induced by hypoxia."
act(p(MESH:"Hypoxia-Inducible Factor 1")) -> bp(NCIT:C16559)
act(p(MESH:"Hypoxia-Inducible Factor 1")) -> bp(GO:angiogenesis)
act(p(MESH:"Hypoxia-Inducible Factor 1")) -| a(CHEBI:"amyloid-beta")
act(p(MESH:"Hypoxia-Inducible Factor 1")) -| path(DOID:"Alzheimer's disease")

SET Evidence = "The reduced glucose uptake is due to decreased
expressions of neuronal glucose transporters (GLUT-1 and GLUT-3) [67]."
#in case of AD
path(DOID:"Alzheimer's disease") -| r(HGNC:SLC2A1)
path(DOID:"Alzheimer's disease") -| r(HGNC:SLC2A3)
p(HGNC:SLC2A1) reg bp(GO:"glucose import")
p(HGNC:SLC2A3) reg bp(GO:"glucose import")

SET Evidence = "Since
GLUT-1 and GLUT-3 are target genes of HIF-1, induction of HIF-1α expression
may restore the glucose uptake and postpone the progression of AD."
p(HGNC:HIF1A) -> p(HGNC:SLC2A1)
p(HGNC:HIF1A) -> p(HGNC:SLC2A3)
p(HGNC:HIF1A) -| path(DOID:"Alzheimer's disease")

SET Evidence = "It has been shown that EPO is able to block the Aβ-generated
neuronal apoptosis and that this protection is completely abolished by co-treatment
with an anti-EPO neutralizing antibody [70]."
a(CONSO:"amyloid-beta aggregates") -> bp(GO:"neuron death")
p(HGNC:EPO) -| bp(GO:"neuron death")
p(HGNC:EPO) -| act(a(CONSO:"amyloid-beta aggregates"))

UNSET MeSHDisease

SET MeSHDisease = "Parkinson Disease"

SET Evidence = "It is reported that MPTP treatment
inhibits HIF-1α accumulation in dopaminergic cell lines PC12 and in mice [80], suggesting
that HIF-1 activity is impaired in PD."
#MPTP - 1-methyl-4-phenyl-1,2,3,6-tetrahydropyridine
SET Species = {"10090","10116"}
SET CellLine = "PC12"
a(CHEBI:"1-methyl-4-phenyl-1,2,3,6-tetrahydropyridine") -| p(HGNC:HIF1A)
path(DOID:"Parkinson's disease") neg act(p(MESH:"Hypoxia-Inducible Factor 1"))
UNSET CellLine
UNSET Species

SET Evidence = "Promoter activity studies suggest that the TH gene is under the
control of a hypoxia response element [82–83]."
#TH - tyrosine hydroxylase
p(NCIT:C13718) reg r(HGNC:TH)

SET Evidence = "Hypoxia and DFO, both are known to
upregulate HIF-1, can increase TH expression in rat brains [84–85]."
SET Species = "10116"
SET MeSHAnatomy = "Brain"
path(EFO:hypoxia) -> r(HGNC:TH)
a(PUBCHEM:2973) -> r(HGNC:TH)
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "Meanwhile, a
conditional knock-down of HIF-1α in mice results in a 40% decrease of TH expression in
substantia nigra [86]."
SET MeSHAnatomy = "Substantia Nigra"
p(HGNC:HIF1A) -> r(HGNC:TH)
UNSET MeSHAnatomy

SET Evidence = "Injection of the PHD inhibitor FG0041 increases HIF-1 activity and
the extracellular DA level in the striatum of the rat brain [87]."
#DA - dopamine
SET Species = "10116"
SET MeSHAnatomy = "Neostriatum"
a(PUBCHEM:137275695) -> act(p(MESH:"Hypoxia-Inducible Factor 1"))
a(PUBCHEM:137275695) -> a(CHEBI:dopamine,loc(GO:"extracellular region"))
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "Furthermore, administration
of HIF-1 activator cobalt chloride raises the DA release [87]."
a(CHEBI:"cobalt dichloride") -> sec(a(CHEBI:dopamine))
a(CHEBI:"cobalt dichloride") -> p(MESH:"Hypoxia-Inducible Factor 1")

SET Evidence = "Therefore, through increasing synthesis and secretion
of DA, HIF-1 may prevent the PD pathological process."
p(MESH:"Hypoxia-Inducible Factor 1") -> a(CHEBI:dopamine)
p(MESH:"Hypoxia-Inducible Factor 1") -> sec(a(CHEBI:dopamine))
p(MESH:"Hypoxia-Inducible Factor 1") -| path(DOID:"Parkinson's disease")

SET Evidence = "It has been found that 3,4-DHB treatment is able to stabilize HIF-1α
protein, leading to the elevation of HIF-1 target genes expression, such as ferroportin and
heme oxygenase (HO)-1, in the substantia nigra."
#DHB - 3,4-dihydroxybenzoate
SET MeSHAnatomy = "Substantia Nigra"
a(CHEBI:"3,4-dihydroxybenzoate") -> p(HGNC:HIF1A)
a(CHEBI:"3,4-dihydroxybenzoate") -> g(HGNC:SLC40A1)
a(CHEBI:"3,4-dihydroxybenzoate") -> g(HGNC:HMOX1)
UNSET MeSHAnatomy

SET Evidence = "The research
group also reported that the iron chelator clioquinol increases HIF-1α within DAergic
neurons and protects against MPTP-induced nigral cell death [88]."
a(CHEBI:"5-chloro-7-iodoquinolin-8-ol") -> p(HGNC:HIF1A)
SET MeSHAnatomy = "Substantia Nigra"
a(CHEBI:"1-methyl-4-phenyl-1,2,3,6-tetrahydropyridine") -> bp(GO:"cell death")
a(CHEBI:"5-chloro-7-iodoquinolin-8-ol") -| bp(GO:"cell death")
UNSET MeSHAnatomy

SET Evidence = "Moreover, DFO is shown
to prevent neurotoxicity in the MPTP-mouse model of PD [89]."
SET Species = "10090"
a(CHEBI:"1-methyl-4-phenyl-1,2,3,6-tetrahydropyridine") -> path(MESH:"Neurotoxicity Syndromes")
a(PUBCHEM:2973) -| path(MESH:"Neurotoxicity Syndromes")
UNSET Species

SET Evidence = "In addition, Ben-Shachar et
al. have demonstrated that intracerebroventricular-injection of DFO protects the rat brain
from DAergic neurodegeneration induced by 6-hydroxydopamine [90]."
SET Species = "10116"
SET MeSHAnatomy = "Cerebral Ventricles"
a(CHEBI:oxidopamine) -> path(DOID:"neurodegenerative disease")
a(PUBCHEM:2973) -| path(DOID:"neurodegenerative disease")
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "EPO is neuroprotective against the DAergic neurotoxin, 6-
hydroxydopamine, in both the DAergic cell line MN9D and primary DAergic neurons [91]."
SET CellLine = "MN9D"
p(HGNC:EPO) -| a(CHEBI:oxidopamine)
UNSET CellLine

SET Evidence = "In an embryonic rat explant model, DAergic neurons, particularly, exhibit increased survival
in response to VEGF application [92], suggesting that VEGF may promote the growth of
DAergic neurons."
SET Species = "10116"
SET Cell = "dopaminergic neuron"
p(HGNC:VEGFA) -> a(MESH:"Dopaminergic Neurons")
UNSET Cell
UNSET Species

SET Evidence = "In addition, a mouse model of pan-neuronal overexpression of VEGF demonstrates
that VEGF provides protection against MPTP toxicity in nigral cells [88]."
SET MeSHAnatomy = "Substantia Nigra"
p(HGNC:VEGFA) -| bp(GO:"cell death")
UNSET MeSHAnatomy

UNSET MeSHDisease

SET MeSHDisease = "Huntington Disease"

SET Evidence = "In this way,
huntingtin protein-induced mitochondrial metabolism dysfunction in the brain serves as the
primary cause of HD pathogenesis."
p(HGNC:HTT,pmod(CONSO:"CAG repeats")) neg path(MESH:"Mitochondrial Dynamics")

SET Evidence = "HIF-1 regulates glycolysis and hexose
monophosphate shunt, which increases the production of adenosine triphosphate (ATP) and
nicotinamide adenine dinucleotide phosphate (NADPH) for cell survival."
#NCIT:C16407 - Cell survival
p(MESH:"Hypoxia-Inducible Factor 1") -> a(CHEBI:ATP)
p(MESH:"Hypoxia-Inducible Factor 1") -> a(CHEBI:NADPH)
p(MESH:"Hypoxia-Inducible Factor 1") -> bp(NCIT:C16407)

SET Evidence = "Therefore, increasing HIF-1
activity may prevent neurons from mutant huntingtin protein damage."
SET MeSHAnatomy = "Neurons"
act(p(MESH:"Hypoxia-Inducible Factor 1")) -| act(p(HGNC:HTT,pmod(CONSO:"CAG repeats")))
UNSET MeSHAnatomy

SET Evidence = "Three HIF-1 inducers, CoCl2, mimosine, and DFO,
have been reported to significantly attenuate cytotoxicity induced by 3-nitropropionic acid in
glioma cells."
#NCIT:C27988 - Cytotoxicity
SET MeSHDisease = "Glioma"
a(CHEBI:"cobalt dichloride") neg path(NCIT:C27988)
a(CHEBI:"L-mimosine") neg path(NCIT:C27988)
a(PUBCHEM:2973) neg path(NCIT:C27988)
a(CHEBI:"3-nitropropanoic acid") -> path(NCIT:C27988)
UNSET MeSHDisease

SET Evidence = "Another metal chelator, clioquinol, increases HIF-1α expression and reduces
cell death and mutant protein accumulation in both a tissue culture model and a transgenic
Huntington’s animal model [101–102]."
a(CHEBI:"5-chloro-7-iodoquinolin-8-ol") -| a(CONSO:"huntingtin aggregates")

SET Evidence = "Therefore, HIF-1 induction may provide
cytoprotection by increasing energy supply and contributing to cellular redox homeostasis."
#with glycoltic pathway
p(MESH:"Hypoxia-Inducible Factor 1") -> bp(GO:"cell redox homeostasis")

UNSET MeSHDisease

SET Evidence = "PHDs can induce HIF-1α degradation while FIH inhibits HIF-1 activity. Inhibiting the
hydroxylases has been shown to provide protective effects in neurodegenerative diseases"
#FH - asparaginyl hydroxylase
p(MESH:"Prolyl Hydroxylases") -> deg(p(HGNC:HIF1A))
p(HGNC:ASPH) -| act(p(HGNC:HIF1A))
p(MESH:"Prolyl Hydroxylases") -> path(DOID:"neurodegenerative disease")
p(HGNC:ASPH) -> path(DOID:"neurodegenerative disease")

SET Evidence = "Moreover, the preconditioning of rat C6 astroglia cells with
the CoCl2 provides extensive cytoprotective effects against metabolic insults induced by
mitochondrial toxin 3-nitropropionic acid [102]."
SET Species = "10116"
SET CellLine = "C6 cell"
a(CHEBI:"cobalt dichloride") -> path(MESH:Cytoprotection)
UNSET CellLine
UNSET Species

SET Evidence = "Iron chelators have been traditionally regarded to provide neuroprotection by sequestering
redox-active iron and thereby prevent hydroxyl radical formation [106]."
composite(a(CHEBI:"iron chelator"),a(CHEBI:"iron(2+)")) -| a(CHEBI:hydroxyl)

SET Evidence = "The sequestering of ferrous ion by DFO
prevents formation of a catalytically active center in the PHDs and induces HIF-1 activation
[11]."
composite(a(PUBCHEM:2973),a(CHEBI:"iron(2+)")) -> act(p(MESH:"Hypoxia-Inducible Factor 1"))

SET Evidence = "Preconditioning of C6
astroglial cells with DFO can activate HIF-1 and thereby attenuate cytotoxicity induced by
mitochondrial inhibitor 3-nitropropionic acid [102]."
SET CellLine = "C6 cell"
a(PUBCHEM:2973) -> act(p(MESH:"Hypoxia-Inducible Factor 1"))
a(PUBCHEM:2973) -| path(NCIT:C27988)
UNSET CellLine

SET Evidence = "Clioquinol-treated transgenic HD mice
(R6/2) demonstrate improved behavioral and pathologic phenotypes, including decreased
huntingtin aggregate accumulation as well as enhanced motor functions and survival [101]."
#transgenic mice - R6/2
SET Species = "10090"
a(CHEBI:"5-chloro-7-iodoquinolin-8-ol") -| path(HP:"Behavioral abnormality")
a(CHEBI:"5-chloro-7-iodoquinolin-8-ol") -> path(EFO:"physical activity")
UNSET Species

SET Evidence = "M30 and another iron chelator
HLA20 (Fig. 1.4) are both designed from the brain-permeable iron chelator VK28 (Fig. 1.5)
and chemically attached to the neuroprotective N-propargyl moiety of the anti-Parkinson
drug rasageline."
composite(a(PUBCHEM:3052776),a(PUBCHEM:6918822)) -| path(DOID:"Parkinson's disease")
composite(a(PUBCHEM:3052776),a(PUBCHEM:6918819)) -| path(DOID:"Parkinson's disease")

SET Evidence = "Furthermore, both M30 and HLA20 increase the levels of
the endogenous HIF-1-dependent genes, including enolase-1, VEGF, and brain derived
neurotrophic factor (BDNF) in NSC-34 cells [50]."
#NSC-34 cells
a(PUBCHEM:6918822) -> p(HGNC:ENO1)
a(PUBCHEM:6918819) -> p(HGNC:ENO1)
a(PUBCHEM:6918822) -> p(HGNC:VEGFA)
a(PUBCHEM:6918819) -> p(HGNC:VEGFA)
a(PUBCHEM:6918822) -> p(HGNC:BDNF)
a(PUBCHEM:6918819) -> p(HGNC:BDNF)
p(MESH:"Hypoxia-Inducible Factor 1") -> p(HGNC:ENO1)
p(MESH:"Hypoxia-Inducible Factor 1") -> p(HGNC:VEGFA)
p(MESH:"Hypoxia-Inducible Factor 1") -> p(HGNC:BDNF)

SET Evidence = "In addition, 3,4-DHB
also elevates the expression of HIF-dependent genes such as heme oxygenase-1 and
manganese superoxide dismutase in the presence of MPTP [88]."
a(CHEBI:"3,4-dihydroxybenzoate") -> g(HGNC:SOD1)
