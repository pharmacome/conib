#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Amelioration of Experimental Autoimmune  Encephalomyelitis by Anatabine"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/bd0996a28201cad363557315043c6392e31abf58/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET STATEMENT_GROUP = "Sandra"

# Add statements below this comment

SET Citation = {"PubMed", "PLoS One. 2013;8(1):e55392. doi: 10.1371/journal.pone.0055392", "23383175"}

SET Species = "10090"

SET Evidence = "The axonal damage in EAE mice leads to well
defined clinical signs such as tail paralysis, hind-limb weakness and paralysis"
SET MeSHDisease = "Encephalomyelitis"
act(a(GO:"axon")) -| path(MESH:"Paralysis")
act(a(GO:"axon")) -| path(HP:"Limb muscle weakness")
UNSET MeSHDisease

SET Evidence = "This particular dosage was selected from a
previous study showing that this dosage is efficient at lowering
brain cytokine levels in a mouse model of Alzheimer’s disease
displaying chronic neuroinflammation"
# this dosage refers to anatabine dosage
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
a(CHEBI:Anatabine) -| a(MESH:Cytokines)
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "Clinical signs of EAE became apparent by day 6
following the immunization in the placebo group but were delayed
to day 11 in the anatabine treatment group (Fig. 1A)"
a(CHEBI:Anatabine) neg path(MESH:Encephalomyelitis)

SET Evidence = "The average clinical severity of EAE was also significantly reduced by the
anatabine treatment (Fig. 1B)"
a(CHEBI:Anatabine) -| path(MESH:Encephalomyelitis)

SET Evidence = "Importantly, approximately 70% of
the mice in the placebo group developed hind-limb weakness or
paralysis compared to only 20% in the anatabine treatment group
(Fig. 1C) showing that the mice treated with anatabine displayed
significantly milder disease symptoms than the placebo group"
SET MeSHDisease = "Encephalomyelitis"
a(CHEBI:Anatabine) -| path(MESH:"Paralysis")
a(CHEBI:Anatabine) -| path(HP:"Limb muscle weakness")
UNSET MeSHDisease

SET Evidence = "We found that IL-1b, IFN-gamma,
TNF-alpha, IL-6 and IL-17A were significantly elevated in the serum
of EAE mice compared to control non-immunized mice"
SET MeSHAnatomy = "Serum"
path(MESH:Encephalomyelitis) pos p(MGI:Il1b)
path(MESH:Encephalomyelitis) pos p(MGI:Ifng)
path(MESH:Encephalomyelitis) pos p(MGI:Tnf)
path(MESH:Encephalomyelitis) pos p(MGI:Il6)
path(MESH:Encephalomyelitis) pos p(MGI:Il17a)
UNSET MeSHAnatomy

SET Evidence = "However, anatabine significantly suppressed the production of
IL-1b, IL-6 and IL-17A in the serum of EAE mice (Fig. 2B) but
did not significantly impact IFN-gamma and TNF-alpha"
SET MeSHDisease = "Encephalomyelitis"
SET MeSHAnatomy = "Serum"
a(CHEBI:Anatabine) -| p(MGI:Il1b)
a(CHEBI:Anatabine) -| p(MGI:Il6)
a(CHEBI:Anatabine) -| p(MGI:Il17a)
a(CHEBI:Anatabine) cnc p(MGI:Ifng)
a(CHEBI:Anatabine) cnc p(MGI:Tnf)
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "A significant elevation of IL-1b, INF-gamma and TNF-alpha
was observed in the spleen of EAE mice compared to control non
immunized animals"
SET MeSHAnatomy = "Spleen"
path(MESH:Encephalomyelitis) -> p(MGI:Il1b)
path(MESH:Encephalomyelitis) -> p(MGI:Tnf)
path(MESH:Encephalomyelitis) -> p(MGI:Ifng)
UNSET MeSHAnatomy

SET Evidence = "Anatabine appears to fully suppress IL-1b,
INF-gamma and TNF-a elevation in the spleen of EAE mice (Fig. 2C)."
SET MeSHDisease = "Encephalomyelitis"
SET MeSHAnatomy = "Spleen"
a(CHEBI:Anatabine) -| p(MGI:Il1b)
a(CHEBI:Anatabine) -| p(MGI:Ifng)
a(CHEBI:Anatabine) -| p(MGI:Tnf)
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "However, a significant
reduction in IL-17 was observed in the spleen of EAE anatabine
treated mice compared to EAE placebo mice (Fig. 2C)"
SET MeSHDisease = "Encephalomyelitis"
SET MeSHAnatomy = "Spleen"
a(CHEBI:Anatabine) -| p(MGI:Il17a)
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "We have shown previously that anatabine prevents STAT3 and
p65 NFkB phosphorylation in various cell types and suggested that
anatabine impact cytokine production by this mechanism as
both STAT3 and p65 NFkB are known to regulate cytokine production."
a(CHEBI:Anatabine) -| p(MGI:Stat3,pmod(Ph))
p(MGI:Stat3) reg bp(GO:"cytokine production")
a(CHEBI:Anatabine) -| p(MGI:Rela,pmod(Ph))
p(MGI:Rela) reg bp(GO:"cytokine production")

SET Evidence = "Interestingly, a significant increase in STAT3 phosphorylation was
observed in the spleen of EAE mice compared to control non
immunized mice and a significant decrease in STAT3 phosphorylation
level was detected in EAE mice treated with anatabine (Fig. 3)"
SET MeSHAnatomy = "Spleen"
path(MESH:Encephalomyelitis) -> p(MGI:Stat3,pmod(Ph))
SET MeSHDisease = "Encephalomyelitis"
a(CHEBI:Anatabine) -| p(MGI:Stat3,pmod(Ph))
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "Western-blot
experiments using brain homogenates from EAE mice also reveal
that both STAT3 and p65 NFkB phosphorylation are significantly
elevated in the brain of EAE mice compared to control non
immunized mice (Fig. 4)"
SET MeSHAnatomy = "Brain"
path(MESH:Encephalomyelitis) -> p(MGI:Stat3,pmod(Ph))
path(MESH:Encephalomyelitis) -> p(MGI:Rela,pmod(Ph))
UNSET MeSHAnatomy

SET Evidence = "Additionally, we observed that anatabine
significantly prevented STAT3 and p65 NFkB phosphorylation in
brain homogenates of EAE mice (Fig. 4)"
SET MeSHAnatomy = "Brain"
SET MeSHDisease = "Encephalomyelitis"
a(CHEBI:Anatabine) -| p(MGI:Stat3,pmod(Ph))
a(CHEBI:Anatabine) -| p(MGI:Rela,pmod(Ph))
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "Interestingly, a
statistically significant correlation was observed between the
amount of STAT3 phosphorylation detected in brain homogenates
and the clinical severity of EAE (Pearson correlation
= 0.653, P<0.001) but not with p65 NFkB phosphorylation
(Pearson correlation = 0.371, P= 0.062) suggesting that STAT3
may play a more preponderant role than NFkB in the
development of neurological deficits of EAE"
SET MeSHAnatomy = "Brain"
p(MGI:Stat3,pmod(Ph)) pos path(MESH:Encephalomyelitis)
p(MGI:Stat3,pmod(Ph)) -- path(MESH:"Neurologic Manifestations")
UNSET MeSHAnatomy

SET Evidence = "In addition, we
observed a statistically significant correlation between the amount
of p65 NFkB phosphorylation and STAT3 phosphorylation in
brain homogenates (Pearson Correlation = 0.637, P<0.001)"
SET MeSHAnatomy = "Brain"
p(MGI:Stat3,pmod(Ph)) pos p(MGI:Rela,pmod(Ph))
UNSET MeSHAnatomy

SET Evidence = "We further assessed
STAT3 phosphorylation by immunohistochemistry using brain
sections of the animals and observed an increased number of
phosphorylated STAT3 immunopositive cells in the cortex of EAE
mice compared to control non-immunized mice (Fig. 5)"
SET MeSHAnatomy = "Cerebral Cortex"
path(MESH:Encephalomyelitis) -> p(MGI:Stat3,pmod(Ph))
UNSET MeSHAnatomy

SET Evidence = "A reduction in the number of phosphorylated STAT3 immunoreactive
cells was observed in the cerebral cortex of EAE mice
treated with anatabine further confirming the data obtained by
western-blotting using brain homogenates (Fig. 5)"
SET MeSHDisease = "Encephalomyelitis"
SET MeSHAnatomy = "Cerebral Cortex"
a(CHEBI:Anatabine) -| p(MGI:Stat3,pmod(Ph))
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "Similarly, we
observed an increased number of phosphorylated STAT3
immunopositive cells in the spinal cord of EAE mice compared
to control non-immunized mice and a decreased number of
phosphorylated STAT3 immunoreactive cells in EAE mice treated
with anatabine (Fig. 6) showing overall that anatabine reduces
STAT3 phosphorylation in the brain, spinal cord and spleen of
EAE mice."
SET MeSHAnatomy = "Spinal Cord"
path(MESH:Encephalomyelitis) -> p(MGI:Stat3,pmod(Ph))
SET MeSHDisease = "Encephalomyelitis"
a(CHEBI:Anatabine) -| p(MGI:Stat3,pmod(Ph))
UNSET MeSHAnatomy
SET MeSHAnatomy = {"Spinal Cord","Cerebral Cortex","Brain"}
a(CHEBI:Anatabine) -| p(MGI:Stat3,pmod(Ph))
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "A statistically significant correlation was observed
between the number of STAT3 phosphorylated cells in the
cerebral cortex and the clinical severity of EAE (Pearson
Correlation =0.531, P,0.002)"
SET MeSHAnatomy = "Cerebral Cortex"
p(MGI:Stat3,pmod(Ph)) pos path(MESH:Encephalomyelitis)
UNSET MeSHAnatomy

SET Evidence = "A statistically significant correlation
was also observed between the amount of STAT3 phosphorylated
cells in the spinal cord and the clinical severity of EAE
(Pearson Correlation = 0.512, P,0.004)"
SET MeSHAnatomy = "Spinal Cord"
p(MGI:Stat3,pmod(Ph)) pos path(MESH:Encephalomyelitis)
UNSET MeSHAnatomy

SET Evidence = "A significant increase in
astrogliosis revealed by a GFAP immunostaining was observed in
the cortex and medulla of EAE mice compared to control non
immunized animals (Fig. 7)"
SET MeSHAnatomy = "Cerebral Cortex"
path(MESH:Encephalomyelitis) -> bp(HBP:Astrogliosis)
UNSET MeSHAnatomy

SET Evidence = "Overall, anatabine did not significantly impact EAE induced astrogliosis for
the different areas of the brain examined (Fig. 7)"
SET MeSHAnatomy = {"Cerebral Cortex","Hippocampus"}
a(CHEBI:Anatabine) cnc bp(HBP:Astrogliosis)
UNSET MeSHAnatomy

SET Evidence = "A significant
increase in the number of microglial cells immunoreactive for Iba1
was observed in the hippocampus and medulla of EAE mice compared to control non-immunized mice (Fig. 8)"
SET MeSHAnatomy = {"Medulla Oblongata","Hippocampus"}
path(MESH:Encephalomyelitis) -> bp(HBP:Microgliosis)
UNSET MeSHAnatomy

SET Evidence = "Interestingly, a
significant reduction in the number of IBa1 immunopositive
microglial cells was found in the hippocampus and medulla of
anatabine treated EAE mice (Fig. 8)"
SET MeSHAnatomy = {"Medulla Oblongata","Hippocampus"}
SET MeSHDisease = "Encephalomyelitis"
a(CHEBI:Anatabine) -| bp(HBP:Microgliosis)
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "As shown in
Fig. 9, hematoxylin and eosin staining revealed numerous
perivascular clusters of mononuclear infiltrating cells in the spinal
cord of EAE mice and an absence of these inflammatory cell
infiltrates in the spinal cord of control non-immunized mice"
path(MESH:Encephalomyelitis) -> tloc(a(MESH:Leukocytes),fromLoc(GO:"extracellular region"),toLoc(MESH:"Spinal Cord"))

SET Evidence = "Less cellular infiltration was observed in the spinal cord of anatabine
treated mice compared to EAE placebo mice (Fig. 9)"
SET MeSHDisease = "Encephalomyelitis"
a(CHEBI:Anatabine) -| tloc(a(MESH:Leukocytes),fromLoc(GO:"extracellular region"),toLoc(MESH:"Spinal Cord"))
UNSET MeSHDisease

SET Evidence = "Globally, Iba1 immunostaining is significantly increased
in the spinal cord of EAE mice compared to control nonimmunized
mice (Fig. 10)"
path(MESH:Encephalomyelitis) -> a(MESH:Microglia)

SET Evidence = "In addition, anatabine markedly
decreased the Iba1 immunostaining in the spinal cord of EAE
mice showing that anatabine reduces the infiltration of macrophage/
microglia in the spinal cord of EAE mice (Fig. 10)"
SET MeSHDisease = "Encephalomyelitis"
SET MeSHAnatomy = "Spinal Cord"
a(CHEBI:Anatabine) -| a(MESH:Microglia)
a(CHEBI:Anatabine) -| tloc(a(MESH:Macrophages),fromLoc(GO:"extracellular region"),toLoc(MESH:"Spinal Cord"))
a(CHEBI:Anatabine) -| tloc(a(MESH:Microglia),fromLoc(GO:"extracellular region"),toLoc(MESH:"Spinal Cord"))
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "A marked increase in GFAP immunostaining was also observed in
the spinal cord of EAE mice and was significantly suppressed by
the anatabine treatment showing that anatabine prevents astrogliosis
in the spinal cord of EAE mice (Fig. 11)"
SET MeSHAnatomy = "Spinal Cord"
path(MESH:Encephalomyelitis) -> bp(HBP:Astrogliosis)
SET MeSHDisease = "Encephalomyelitis"
a(CHEBI:Anatabine) -| bp(HBP:Astrogliosis)
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "Finally, Luxol Fast Blue staining showed significant myelin loss in the white matter of
the spinal cord of EAE mice compared control non-immunized mice (Fig. 12)"
SET MeSHAnatomy = {"Spinal Cord","White Matter"}
path(MESH:Encephalomyelitis) -| a(GO:"myelin sheath")
UNSET MeSHAnatomy

SET Evidence = "Interestingly, anatabine significantly prevented demyelination
associated with EAE in the spinal cord (Fig. 12)"
SET MeSHDisease = "Encephalomyelitis"
SET MeSHAnatomy = {"Spinal Cord","White Matter"}
a(CHEBI:Anatabine) pos a(GO:"myelin sheath")
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "A statistically significant positive correlation was observed between the amount
of GFAP and Iba1 burden in the spinal cord (Pearson
correlation = 0.612, P,0.002) whereas negative correlations were
observed between the GFAP burden and the Luxol fast blue
burden (Pearson correlation =20.525, P= 0.007), and between
the IBa1 burden and the Luxol fast blue burden (Pearson
correlation =20.609, P =0.001) suggesting that astrogliosis and
microgliosis are associated with the loss of myelin in the spinal cord"
SET MeSHAnatomy = "Spinal Cord"
a(MESH:Astrocytes) pos a(MESH:Microglia)
a(MESH:Astrocytes) neg a(GO:"myelin sheath")
a(MESH:Microglia) neg  a(GO:"myelin sheath")
bp(HBP:Astrogliosis) neg a(GO:"myelin sheath")
bp(HBP:Microgliosis) neg a(GO:"myelin sheath")
UNSET MeSHAnatomy

UNSET Species

##########################################################


UNSET STATEMENT_GROUP
