#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Role of the nicotinic acetylcholine receptor in Alzheimer's disease pathology and treatment"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/56d6631d06deeead416b3b5100d3b4b8d88c29c6/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "25514383"}

SET Evidence = "Dementia is a debilitating condition frequent in ageing populations,
and Alzheimer's Disease (AD) accounts for 70% of all dementia
cases. AD is characterized by neuropathological hallmarks
consisting of an accumulation of Amyloid beta peptide (Ab) in extracellular
plaques, intracellular deposits of tau protein, neuronal loss
and, more recently, a prominent synaptic loss was identified (Braak
and Braak,1991; Masliah et al., 2001; Selkoe,1991; Spires-Jones and Hyman, 2014)"
bp(GO:aging) -- path(MESH:Dementia)
path(MESH:"Alzheimer Disease") -- path(MESH:Dementia)
path(MESH:"Alzheimer Disease") positiveCorrelation path(MESH:"Plaque, Amyloid")
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT)
path(MESH:"Alzheimer Disease") positiveCorrelation bp(GO:"neuron death")
path(MESH:"Alzheimer Disease") negativeCorrelation a(GO:synapse)

SET Evidence = "In addition, anatomical studies in AD patients
showed a massive loss of brain white matter and a specific reduction
of cholinergic neurons of the basal forebrain (Auld et al., 2002;
Bowen et al., 1976; Coyle et al., 1983; Kim et al., 2013; Whitehouse et al., 1981, 1982)"
SET MeSHAnatomy = "Brain"
path(MESH:"Alzheimer Disease") negativeCorrelation a(MESH:"White Matter")
SET MeSHAnatomy = "Basal Forebrain"
path(MESH:"Alzheimer Disease") negativeCorrelation a(HP:"cholinergic neuron")
UNSET MeSHAnatomy

SET Evidence = "Several studies demonstrated the pivotal role of these cholinergic nuclei in cognitive functions"
a(HP:"cholinergic neuron") -- bp(GO:cognition)

SET Evidence = "Woolf (1998) proposed a model in
which acetylcholine (ACh) release leads to the modulation of
cortical circuitry that finally encodes for storage of long-term memory"
tloc(a(CHEBI:acetylcholine),fromLoc(MESH:"Intracellular Space"),toLoc(MESH:"Extracellular Space")) reg bp(GO:"long-term memory")

SET Evidence = "The cholinergic system is also involved in attention processes
(Muir et al., 1993; Sarter and Bruno, 1997; Wenk, 1997)"
a(HP:"cholinergic neuron") -- path(MESH:Attention)

SET Evidence = "In a mouse model, the lack of ACh receptors in the prefrontal cortex
(PFC) was demonstrated to be responsible for attention deficit,
restored by the expression of the receptor in this area (Guillem et al., 2011)"
SET MeSHAnatomy = "Prefrontal Cortex"
a(MESH:"Receptors, Cholinergic") -> path(MESH:Attention)
UNSET MeSHAnatomy

SET Evidence = "The neurotransmitter ACh binds to two families of receptors,
nicotinic acetylcholine receptors (nAChRs) and muscarinic acetylcholine
receptors (mAChRs). Both families of receptors regulate the
cognitive processes mentioned above (Ghoneim and Mewaldt,
1977; Petersen, 1977; Sarter and Paolone, 2011), and are both affected in AD"
complex(a(CHEBI:acetylcholine),p(FPLX:CHRN))
p(FPLX:CHRN) reg bp(GO:cognition)
complex(a(CHEBI:acetylcholine),p(FPLX:CHRM))
p(FPLX:CHRM) reg bp(GO:cognition)

SET Evidence = "Binding studies performed with the use of [3H]-nicotine and
[3H]-ACh showed a significant reduction in nicotine and ACh
binding sites in cerebral cortex of patients suffering from AD, demonstrating
a decrease of both nAChR and mAChR populations (Gotti et al., 2006a; Paterson and Nordberg, 2000;
Perry et al., 1981, 1985, 1987, 1988; Shimohama et al., 1986; Whitehouse et al., 1981, 1982, 1986)"
SET MeSHAnatomy = "Cerebral Cortex"
path(MESH:"Alzheimer Disease") -| complex(a(CHEBI:acetylcholine),p(FPLX:CHRN))
path(MESH:"Alzheimer Disease") -| complex(a(CHEBI:acetylcholine),p(FPLX:CHRM))
path(MESH:"Alzheimer Disease") -| complex(a(CHEBI:nicotine),p(FPLX:CHRN))
path(MESH:"Alzheimer Disease") -| complex(a(CHEBI:nicotine),p(FPLX:CHRM))
path(MESH:"Alzheimer Disease") -| p(FPLX:CHRN)
path(MESH:"Alzheimer Disease") -| p(FPLX:CHRM)
UNSET MeSHAnatomy

SET Evidence = "In addition to nAChRs and mAChRs, the enzyme
choline acetyltransferase (ChAT), involved in ACh production, is
also affected in AD. The activity of this ChAT enzyme, and consequently
the synthesis of ACh, is decreased in AD brains"
SET MeSHAnatomy = "Brain"
act(p(HGNC:CHAT)) -> a(CHEBI:acetylcholine)
path(MESH:"Alzheimer Disease") -| act(p(HGNC:CHAT))
path(MESH:"Alzheimer Disease") -| a(CHEBI:acetylcholine)
UNSET MeSHAnatomy

SET Evidence = "In addition,
several authors observed a reduction in the activity of acetylcholinesterase
(AChE), the enzyme that metabolises ACh after its
release in the synaptic cleft (Auld et al., 2002; Bowen et al., 1976;
Coyle et al., 1983; Davies and Maloney, 1976; Perry et al., 1978)"
act(p(HGNC:ACHE)) -> bp(GO:"acetylcholine catabolic process")

SET Evidence = "The role of the cholinergic system in cognition and the modification
observed in neurodegenerative diseases, and in particular in the
case of AD, led to the formulation of the “cholinergic hypothesis” of
geriatric disorders (Bartus et al., 1982; Contestabile, 2011), according
to which the reduction in cholinergic innervation is
responsible for the cognitive decline observed in AD patients"
SET MeSHDisease = "Alzheimer Disease"
a(HP:"cholinergic neuron") -> bp(GO:cognition)
UNSET MeSHDisease

SET Evidence = "Nicotinic receptors are transmembrane pentameric proteins
that belong to the “cys-loop” superfamily of ligand-gated ion
channels together with GABAA, GABAC, glycine and 5- hydroxytryptamine (5-HT3)
ionotropic receptors (Changeux and Edelstein, 1998; Le Novère and Changeux, 1995)"
p(FPLX:CHRN) isA a(MESH:"Cysteine Loop Ligand-Gated Ion Channel Receptors")
a(MESH:"Cysteine Loop Ligand-Gated Ion Channel Receptors") hasMembers list(p(FPLX:CHRN),p(FPLX:GABR),a(MESH:"Receptors, Glycine"),a(MESH:"Receptors, Serotonin, 5-HT3"))

SET Evidence = "The five subunits that compose the receptor are assembled around a central hydrophilic
pore that mediates the flow of the cations K+, Na+ and Ca++. In the human nervous system, there are
eight alpha subunits (alpha2-alpha7, alpha9, alpha10) and
three beta subunits (beta2-beta4) that assemble in different combinations
to generate a variety of nAChR subtypes with distinct
electrophysiological properties and brain localization (Albuquerque
et al., 2009; Gotti et al., 2006b, 2007, 2009)"
p(FPLX:CHRN) hasMembers list(p(HGNC:CHRNA2),p(HGNC:CHRNA3),p(HGNC:CHRNA4),p(HGNC:CHRNA5),p(HGNC:CHRNA6),p(HGNC:CHRNA7),p(HGNC:CHRNA9),p(HGNC:CHRNA10))
p(FPLX:CHRN) hasMembers list(p(HGNC:CHRNB2),p(HGNC:CHRNB3),p(HGNC:CHRNB4))
p(FPLX:CHRN) reg bp(GO:"potassium ion transport")
p(FPLX:CHRN) reg bp(GO:"sodium ion transport")
p(FPLX:CHRN) reg bp(GO:"calcium ion transport")

SET Evidence = "In this context we will focus on these alpha7 and beta2 subunits, that were shown
to interact with Abeta (Liu et al., 2009, 2012; Søderman et al., 2008; Sudweeks and Yakel, 2000; Wang et al., 2000a, 2000b)"
complex(p(HGNC:CHRNA7),a(CHEBI:"amyloid-beta"))
complex(p(HGNC:CHRNB2),a(CHEBI:"amyloid-beta"))

SET Evidence = "The alpha7 homomeric receptor demonstrates a wide-spread
localization in the brain and is characterized by a high calcium
ion permeability and a fast desensitization rate (Dani and Bertrand, 2007; Quick and Lester, 2002)"
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> bp(GO:"calcium ion transport")

SET Evidence = "alpha7 nAChR on presynaptic terminals
mediate release of others neurotransmitters (Wonnacott et al.,
2006), while a postsynaptic or somatic localization elicits important
changes in intracellular Ca++ concentration, that can activate
second messenger pathways mediating cellular processes such as
neuronal survival and gene expression (Berg and Conroy, 2002;
Messi et al., 1997; Morley and Happe, 2000)"
SET MeSHAnatomy = "Presynaptic Terminals"
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") reg tloc(a(CHEBI:neurotransmitter),fromLoc(MESH:"Intracellular Space"),toLoc(MESH:"Extracellular Space"))
SET MeSHAnatomy = "Cell Body"
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") reg bp(GO:"second-messenger-mediated signaling")
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") reg bp(GO:"neuron cellular homeostasis")
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") reg bp(GO:"gene expression")
UNSET MeSHAnatomy

SET Evidence = "Moreover, it was
demonstrated that the activation of alpha7 nAChRs is important during
development for the maturation of glutamatergic synapses (Lozada et al., 2012)"
act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor")) -> a(GO:"glutamatergic synapse")

SET Evidence = "Historically, the first nAChR subunit identified to interact
with Abeta was alpha7 (Wang et al., 2000a, 2000b)"
complex(p(HGNC:CHRNA7),a(CHEBI:"amyloid-beta"))

SET Evidence = "Later it was shown that Abeta is able to activate
also beta2*-nAChRs (beta2 subunit-containing nAChRs)"
a(CHEBI:"amyloid-beta") -> act(a(HBP:"beta-2-containing nAChR"))

SET Evidence = "This subunit commonly forms heteropentameric
receptors in combination with the alpha4 subunit"
#this subunit: beta2
complex(p(HGNC:CHRNB2),p(HGNC:CHRNA4)) -> a(HBP:"alpha-4 beta-2 nAChR")

SET Evidence = "The subtype alpha4beta2 is characterized by lower calcium ion permeability and a slow
desensitization rate compared to the homopentameric alpha7 nAChR (Quick and Lester, 2002)"
a(HBP:"alpha-4 beta-2 nAChR") negativeCorrelation bp(GO:"calcium ion transport")
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") positiveCorrelation bp(GO:"calcium ion transport")

SET Evidence = "In addition to the
alpha4beta2 subtype, it was demonstrated that the alpha7 subunit is able to coassemble
with the beta2 subunit to form a heteropentameric receptor"
complex(p(HGNC:CHRNB2),p(HGNC:CHRNA7)) -> a(HBP:"alpha-7 beta-2 nAChR")

SET Evidence = "This class of receptors seems to be particularly
sensitive to Abeta-induced toxicity (Khiroug et al., 2002; Liu et al.,2009, 2012)"
# this class: alpha7beta2* nAChR
act(a(CHEBI:"amyloid-beta")) negativeCorrelation a(HBP:"alpha-7 beta-2 nAChR")

SET Evidence = "Under these experimental conditions, alpha7
and beta2 are able to co-assemble into a functional receptor that localizes at the cell surface"
complex(p(HGNC:CHRNB2),p(HGNC:CHRNA7)) -> surf(a(HBP:"alpha-7 beta-2 nAChR"))

SET Evidence = "The importance of beta2 in maintaining brain homeostasis during
normal ageing was highlighted in the KO mouse for this subunit"
SET MeSHAnatomy = "Brain"
p(MGI:Chrnb2) reg bp(MESH:Homeostasis)
UNSET MeSHAnatomy

SET Evidence = "Aged beta2 null mutant mice have a thinner cortex compared to agematched
wild-type controls (Zoli et al., 1999). This work should be
pursued further as it indicates a “neurotrophic” action of beta2 receptor
activation by endogenous ACh (Zanardi et al., 2007)"
p(MGI:Chrnb2) -> a(MESH:"Cerebral Cortex")
a(CHEBI:acetylcholine) -> act(a(HBP:"beta-2-containing nAChR"))

SET Evidence = "Null mutant beta2 mice were also tested to determine the role of this
subunit in cognition. Guillem et al. (2011) showed that these mice
exhibit an attention deficit which was restored by re-expression of
this subunit with a lentiviral vector in the PFC"
p(MGI:Chrnb2) -> path(MESH:Attention)
p(MGI:Chrnb2) -> bp(GO:cognition)

SET Evidence = "The interaction between the alpha7 nAChR and Abeta is widely demonstrated"
complex(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"),a(CHEBI:"amyloid-beta"))

SET Evidence = "They showed that alpha7 subunits co-localize with Abeta1-42 in senile plaques of brain slices
obtained from patients that suffered from sporadic AD"
SET MeSHDisease = "Alzheimer Disease"
complex(p(HGNC:CHRNA7),a(CHEBI:"amyloid-beta polypeptide 42")) -- path(MESH:"Plaque, Amyloid")
UNSET MeSHDisease

SET Evidence = "The same result was obtained with the reciprocal experiment,
alpha7 immunoprecipitation and Abeta1-42 detection, meaning that the two proteins strongly interact"
complex(p(HGNC:CHRNA7),a(CHEBI:"amyloid-beta polypeptide 42"))

SET Evidence = "Experiments performed with
fragments of Abeta helped identify the sequence responsible for the
interaction with alpha7, which corresponds to the amino acid residues
12-28 of the Abeta sequence (Wang et al., 2000b)"
#: TODO replace with domain
p(HGNC:APP, frag("12_28")) -> complex(p(HGNC:CHRNA7), p(HGNC:APP, frag("12_28")))

SET Evidence = "Subsequently, competition studies performed by incubating alpha7 nAChRs with Abeta
and alpha-Bungarotoxin showed that the application of alpha-Bungarotoxin is
able to decrease the amount of Abeta bound to alpha7 nAChRs, suggesting that both molecules compete
for the same ligand binding domain (Wang et al., 2000b)"
a(MESH:Bungarotoxins) -| complex(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"),a(CHEBI:"amyloid-beta"))

SET Evidence = "Furthermore, studies using two selective
nicotinic receptor ligands, namely [3H]methyllycaconitine and [3H]
cytisine, clearly showed that alpha7 nAChRs bind the less toxic peptide
Abeta1-40, but with lower affinity compared to the 1-42 form (Wang et al., 2000a)"
complex(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"),a(CHEBI:"amyloid-beta polypeptide 40"))
complex(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"),a(CHEBI:"amyloid-beta polypeptide 42"))

SET Evidence = "Direct interaction between Abeta and alpha7 was further
demonstrated in a transgenic mouse line, APPSwe/PS1DE9, where
alpha7 was shown to immunoprecipitate with Ab1e40/42 (Søderman et al., 2008)"
complex(p(MGI:Chrna7),a(CHEBI:"amyloid-beta"))
complex(p(MGI:Chrna7),a(CHEBI:"amyloid-beta polypeptide 40"))
complex(p(MGI:Chrna7),a(CHEBI:"amyloid-beta polypeptide 42"))

SET Evidence = "Modulation of nAChRs by Abeta was also found in
ex vivo studies: Pettit and colleagues (2001) used rat hippocampal
slices to show that Abeta1-42 incubation is able to reduce postsynaptic
currents and open probability of both alpha7 and non-alpha7 nAChRs
subtypes, demonstrating an interaction between Abeta and other nAChR subunits"
complex(a(CHEBI:"amyloid-beta"),p(FPLX:CHRN))
complex(p(RGD:Chrna7),a(CHEBI:"amyloid-beta"))
a(CHEBI:"amyloid-beta") reg act(p(FPLX:CHRN))
a(CHEBI:"amyloid-beta polypeptide 42") -| bp(MESH:"Synaptic Potentials")

SET Evidence = "Even though studies have mainly focused on the
interaction between alpha7 nAChRs and Abeta and its functional consequences,
it has been demonstrated that also beta2*-nAChRs bind to Abeta"
complex(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"),a(CHEBI:"amyloid-beta"))
complex(a(CHEBI:"amyloid-beta"),a(HBP:"beta-2-containing nAChR"))

SET Evidence = "In both hippocampal interneurons and oocytes they observed a block of beta2*-nAChRs,
that could be prevented by the application of a positive allosteric
modulator (PAM) specific for beta2 subunit containing receptors,
desformylflustrabromine (dFBr) (Pandya and Yakel, 2011)"
a(PUBCHEM:637026) -> act(a(HBP:"beta-2-containing nAChR"))

SET Evidence = "Important evidence for a functional interaction between
nAChRs and Abeta came from the work of Pettit et al. (2001)"
complex(a(CHEBI:"amyloid-beta"),p(FPLX:CHRN))

SET Evidence = "They demonstrated that Abeta1-42 drives a reversible inhibition of nAChR-mediated
currents in hippocampal GABAergic neurons recorded from rat slices. In these experimental conditions
the most effective Abeta1-42 concentration was 500 nM, but inhibition was found also at
the lower concentration of 100 nM"
#SET Dosage = {"100 nM","500 nM"}
SET MeSHAnatomy = "Hippocampus"
SET Cell = "GABAergic neuron"
a(CHEBI:"amyloid-beta polypeptide 42") -| act(p(FPLX:CHRN))
act(p(FPLX:CHRN)) -> bp(MESH:"Synaptic Potentials")
a(CHEBI:"amyloid-beta polypeptide 42") -| bp(MESH:"Synaptic Potentials")

UNSET MeSHAnatomy
UNSET Cell

SET Evidence = "With the use of selective antagonists
it was possible to determine that inhibition operates on
both alpha7 and non-alpha7 receptors (Pettit et al., 2001)"
# inhibition by Abeta1-42
a(CHEBI:"amyloid-beta polypeptide 42") -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))
a(CHEBI:"amyloid-beta polypeptide 42") -| act(p(FPLX:CHRN))

SET Evidence = "Experiments performed on X. laevis oocytes transiently transfected with alpha7 or
alpha4beta2 cDNA showed a concentration dependent effect of Abeta on receptor
inhibition. In this case the peptide used was Abeta1-40 and the
concentrations adopted were between 0.1 and 10 mM, with
increased Abeta concentration resulting in a bigger inhibition of the receptor (Tozaki et al., 2002)"
#SET Dosage = "0.1 and 10 mM"
SET Cell = "oocyte"
a(CHEBI:"amyloid-beta polypeptide 40") -| act(a(HBP:"alpha-4 beta-2 nAChR"))
a(CHEBI:"amyloid-beta polypeptide 40") -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))

SET Evidence = "A different set of experiments
demonstrated that Abeta enhances ACh activation of the alpha4beta2 nAChRs
expressed in oocytes, this first activation of the receptor was followed
by its inhibition (Pym et al., 2005)"
SET Cell = "oocyte"
a(CHEBI:"amyloid-beta") -> act(a(CHEBI:acetylcholine))
act(a(CHEBI:acetylcholine)) -> act(a(HBP:"alpha-4 beta-2 nAChR"))
UNSET Cell

SET Evidence = "However, alpha7 nAChR activation was observed in X. laevis oocytes when a range of Abeta
concentration spanning from 1 to 100 pM was applied (Dineley et al., 2002)"
#SET Dosage = "1 to 100 pM"
a(CHEBI:"amyloid-beta") -> act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))

SET Evidence = "The incubation of cultured
rat hippocampal neurons with Abeta1-42 resulted in inhibition of alpha7
nAChRs, more precisely of both somato-dendritic and presynaptic populations of receptors"
SET MeSHAnatomy = {"Neurons","Hippocampus"}
a(CHEBI:"amyloid-beta polypeptide 42") -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))

UNSET MeSHAnatomy

SET Evidence = "Further studies showed an
inhibitory effect of Abeta1-42 on human alpha4beta2 nAChRs transfected in
the cell line (SHEP1) (Wu et al., 2004)"
a(CHEBI:"amyloid-beta polypeptide 42") -| act(a(HBP:"alpha-4 beta-2 nAChR"))

SET Evidence = "The oligomeric form of Abeta1-40 was able to activate alpha7 nAChR expressed
in SH-SY5Y cell line (Lilja et al., 2011)"
a(HBP:"amyloid-beta 40 oligomers") -> act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))

SET Evidence = "Arora et al. (2013) investigated,
in a cellular system, the effect of prolonged Abeta exposure on
nAChR function. The rodent neuroblastoma cell line NG108-15 was
transfected with alpha4beta2 nAChRs and treated for three days with
100 nM Abeta. The following acute stimulation with Abeta and nicotine
led to receptor activation that caused a perturbation of intracellular
calcium homeostasis followed by mitochondrial dysfunction and
increased oxidative stress (Arora et al., 2013)"
composite(a(CHEBI:"amyloid-beta"),a(CHEBI:nicotine)) -> act(a(HBP:"alpha-4 beta-2 nAChR"))
act(a(HBP:"alpha-4 beta-2 nAChR")) -| bp(GO:"cellular calcium ion homeostasis")
act(a(HBP:"alpha-4 beta-2 nAChR")) -> path(HBP:"mitochondrial dysfunction")
act(a(HBP:"alpha-4 beta-2 nAChR")) -> bp(GO:"response to oxidative stress")

SET Evidence = "In a different study, rat hippocampus and cortex were investigated and the activation of
both alpha7 and non-alpha7 receptors was obtained with an enhancement
of Ca++ influx into the neuron following the application of picomolar (pM) concentrations of Abeta1-42"
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> tloc(a(CHEBI:"calcium(2+)"),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Intracellular Space"))

SET Evidence = "However, it is clear that nAChR-Aß interaction initiates intracellular
signalling implicating a set of transduction cascades"
complex(a(CHEBI:"amyloid-beta"),p(FPLX:CHRN)) -> bp(GO:"intracellular signal transduction")

SET Evidence = "Akt phosphorylation mediates the downstream activation of an antiapoptotic
pathway, which is also activated by nicotine treatment (Kihara et al., 2001)"
p(FPLX:AKT,pmod(Ph)) -| bp(GO:"apoptotic process")
a(CHEBI:nicotine) -| bp(GO:"apoptotic process")

SET Evidence = "Subsequent to incubation with pM concentrations of Abeta1-42 monomers and
oligomers, an increase of hippocampal LTP was observed"
a(HBP:"amyloid-beta 42 oligomers") -> bp(GO:"long-term synaptic potentiation")
a(CHEBI:"amyloid-beta polypeptide 42") -> bp(GO:"long-term synaptic potentiation")

SET Evidence = "This enhancement of synaptic plasticity and the activation of intracellular
pathways are mediated by the activation of alpha7 nAChRs
(Dineley et al., 2001; Parri et al., 2011; Plant et al., 2003; Puzzo et al., 2008)"
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> bp(GO:"positive regulation of synaptic plasticity")
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> bp(GO:"intracellular signal transduction")

SET Evidence = "An in vivo Abeta infusion in mice was able to enhance hippocampal
dependent memory, highlighted with memory tasks such
as the Morris water maze and contextual fear conditioning, which
are both hippocampus dependent behavioural tasks (Puzzo et al.,2008)"
SET MeSHAnatomy = "Hippocampus"
a(CHEBI:"amyloid-beta") -> bp(GO:memory)
UNSET MeSHAnatomy

SET Evidence = "On the other hand, an opposite effect was shown with Abeta-nAChR
interaction being responsible for inhibition of survival pathways"
complex(a(CHEBI:"amyloid-beta"),p(FPLX:CHRN)) -| bp(MESH:"Cell Survival")

SET Evidence = "In this system enhancement of Akt phosphorylation and
activation of ERK pathway was observed following alpha7 agonist
treatment, suggesting that Abeta inhibits the neuroprotective effect of
alpha7 nAChR activation (Zhi et al., 2014)"
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> p(FPLX:AKT,pmod(Ph))
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> bp(GO:"ERK1 and ERK2 cascade")
a(CHEBI:"amyloid-beta") -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -| bp(HP:Neurodegeneration)

SET Evidence = "It was then postulated that Abeta-nAChR
interaction has a physiological role in neuronal homeostasis that is
disrupted when Abeta concentrations increase in a pathological
context, leading to receptor inhibition and possible cellular toxicity
(Dineley et al., 2001; Parri et al., 2011)"
complex(a(CHEBI:"amyloid-beta"),p(FPLX:CHRN)) -- bp(GO:"neuron cellular homeostasis")
a(CHEBI:"amyloid-beta") -| act(p(FPLX:CHRN))
a(CHEBI:"amyloid-beta") -| bp(GO:"neuron cellular homeostasis")

SET MeSHDisease = "Alzheimer Disease"

# role of the alpha7 subunit in AD was studied
# APP-alpha7KO: human APP with the Swedish (KM670/671NL) and the Indiana (V717F) mutations crossed with the alpha7 nAChR null-mutant

SET Evidence = "This AD model displays spatial memory
deficit at 13-16 months of age, while APP-alpha7KO mice did not
exhibit any memory deficit, suggesting that the absence of the alpha7
subunit of the nicotinic receptor protects against the behavioural
deficit caused by expression of the mutated forms of APP in this AD model"
p(MGI:App, var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Val717Phe")) -| path(MESH:"Spatial Memory")
p(MGI:Chrna7) -| path(MESH:"Spatial Memory")

SET Evidence = "In the APP-alpha7KO line the lack of alpha7 was sufficient to preserve synaptic
terminals and dendrites, rescuing levels of synaptophysin and
MAP2 to reach that of aged-matched WT controls"
p(MGI:Chrna7) -| p(MGI:Syp)
p(MGI:Chrna7) -| p(MGI:Map2)

SET Evidence = "These experiments confirmed
that the level of expression of APP and the consequent synthesis of
Ab were comparable between mouse lines, demonstrating that the
difference in cognitive deficit and neuropathology were mediated
exclusively by the nicotinic recept"
p(FPLX:CHRN) reg path(MESH:"Cognition Disorders")

SET Evidence = "The authors postulated that the absence of alpha7 could
prevent Abeta intracellular accumulation ameliorating the cognitive
neuropathology and its phenotypic association (Dziewczapolski et al., 2009)"
p(MGI:Chrna7) -> a(CHEBI:"amyloid-beta")
p(MGI:Chrna7) -> bp(GO:cognition)

# APP-alpha7KO: Swedish mutation crossed with alpha7 KO mouse

SET Evidence = "The contextual fear conditioning and the novel object recognition tasks
both showed that the cognitive deficits worsen when alpha7 is absent"
p(MGI:Chrna7) -| path(MESH:"Cognition Disorders")

SET Evidence = "In the hippocampus, it was shown that APP-alpha7KO mice had high
levels of Abeta, although significantly less than APP mice, an effect
which is not due to modification of the APP expression level,equivalent in the two lines"
SET MeSHAnatomy = "Hippocampus"
p(MGI:Chrna7) -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy

SET Evidence = "As a consequence of the lower Abeta concentration, the
plaque load was clearly reduced in APP-alpha7KO mice"
p(MGI:Chrna7) -> path(MESH:"Plaque, Amyloid")

SET Evidence = "Signs of neuropathology were found in APP-alpha7KO illustrated
by loss of MAP2 immunoreactivity in the hippocampus"
SET MeSHAnatomy = "Hippocampus"
p(MGI:Chrna7) -> p(MGI:Map2)
UNSET MeSHAnatomy

SET Evidence = "The mechanism proposed is that the Aß-alpha7 nAChR interaction
could activate neuroprotective downstream pathways (Parri et al., 2011), and that
at the same time the interaction engages Abeta preventing its aggregation"
complex(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"),a(CHEBI:"amyloid-beta")) -| bp(HP:Neurodegeneration)
complex(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"),a(CHEBI:"amyloid-beta")) -| a(HBP:"amyloid-beta aggregates")

SET Evidence = "With the progression of the disease the amount of Abeta increases, it starts
to accumulate, and becomes toxic for the neurons (Hernandez et al., 2010)"
# disease: Alzheimer Disease
path(MESH:"Alzheimer Disease") positiveCorrelation a(CHEBI:"amyloid-beta")
path(MESH:"Alzheimer Disease") positiveCorrelation a(HBP:"amyloid-beta aggregates")

SET Evidence = "Cotinine is a nicotine metabolite known for its positive effects on memory
and attention and lower toxicity compared to nicotine (Hatsukami et al., 1997)"
a(CHEBI:"(-)-cotinine") -> path(MESH:Attention)
a(CHEBI:"(-)-cotinine") -> bp(GO:memory)

SET Evidence = "Methyllycaconitine (MLA), an alpha7 nAChR antagonist,
showed neuroprotective effect on mouse and rat primary cell culture (Martin et al., 2004)"
a(CHEBI:methyllycaconitine) -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))
a(CHEBI:methyllycaconitine) -| bp(HP:Neurodegeneration)
act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor")) -> bp(HP:Neurodegeneration)

SET Evidence = "Another molecule investigated was 2-[2-(4- bromophenyl)-2-oxoethyl]-1-methyl pyridinium (S 24795), a
partial alpha7 nAChR agonist. When this molecule was applied to synaptosomal preparations from rat frontal cortex and post mortem
human AD samples it was able to dissociate Abeta in a concentration dependent manner"
a(HBP:"S 24795") -| a(CHEBI:"amyloid-beta")

SET Evidence = "The incubation with S 24795 was able to normalize Ca++ influx mediated
by both alpha7 nAChR and NMDAR (Wang et al., 2009, 2010)"
act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor")) reg tloc(a(CHEBI:"calcium(2+)"),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Intracellular Space"))
a(MESH:"Receptors, N-Methyl-D-Aspartate") reg tloc(a(CHEBI:"calcium(2+)"),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Intracellular Space"))
a(HBP:"S 24795") -> tloc(a(CHEBI:"calcium(2+)"),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Intracellular Space"))

SET Evidence = "The analysis of the Abeta fraction reduced by nicotine
showed that mainly insoluble Ab1-40/42 was affected while there
was no change in soluble Abeta (Nordberg et al., 2002)"
a(CHEBI:nicotine) -| a(CHEBI:"amyloid-beta polypeptide 42")
a(CHEBI:nicotine) -| a(CHEBI:"amyloid-beta polypeptide 40")

SET Evidence = "The short-term treatment of 10 days showed a significant
reduction in cortical insoluble Abeta1-40/42"
a(CHEBI:nicotine) -| a(CHEBI:"amyloid-beta polypeptide 42")
a(CHEBI:nicotine) -| a(CHEBI:"amyloid-beta polypeptide 40")

SET Evidence = "Long-term nicotine administration elicited a reduction in Abeta deposits in blood vessel"
SET MeSHAnatomy = "Blood Vessels"
a(CHEBI:nicotine) -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy

SET Evidence = "APPSwe mice at 14.5 months have fewer alphaBungarotoxin binding sites, while in transgenic
mice treated with nicotine the number of alphaBungarotoxin
binding sites was recovered and comparable to non transgenic age-matched
control mice, suggesting that there was an increase in the
population of alpha7 nAChRs (Hellstrom-Lindahl et al., 2004)"
a(CHEBI:nicotine) -> a(MESH:"alpha7 Nicotinic Acetylcholine Receptor")

SET Evidence = "Mice aged 6 months were treated for one month with nicotine
injections, which led to an improvement in working and episodic
memory compared to non-treated transgenic mice"
# APP/PS1 double transgenic mouse
a(CHEBI:nicotine) -> path(MESH:"Memory, Episodic")

SET Evidence = "Like the young mice, they also displayed an improvement in spatial memory,
demonstrating that nicotine enhances memory in both young and old mice"
a(CHEBI:nicotine) -> path(MESH:"Spatial Memory")

SET Evidence = "The amount of Abeta was quantified, and following
nicotine injections a reduction in Abeta, particularly in the oligomeric form, was found"
a(CHEBI:nicotine) -| a(HBP:"amyloid-beta oligomers")

SET Evidence = "The explanation proposed by the authors is that alpha7 nAChR
activation through nicotine binding could promote survival pathways
and recover the synaptic damage caused by Abeta (Inestrosa et al., 2013)"
a(CHEBI:nicotine) -> act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))
act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor")) -> bp(MESH:"Cell Survival")
act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor")) -> a(GO:synapse)
a(CHEBI:"amyloid-beta") -| a(GO:synapse)

SET Evidence = "The long-term nicotine treatment caused faster tau aggregation in CA1 pyramidal neurons"
SET MeSHAnatomy = {"CA1 Region, Hippocampal","Pyramidal Cells"}
a(CHEBI:nicotine) -> a(HBP:"Tau aggregates")
UNSET MeSHAnatomy

SET Evidence = "The possible mechanism by which nicotine enhances the aggregation
of tau is through the activation of p38-MAP kinase"
a(CHEBI:nicotine) -> a(HBP:"Tau aggregates")
a(CHEBI:nicotine) -> bp(GO:"p38MAPK cascade")
bp(GO:"p38MAPK cascade") -> a(HBP:"Tau aggregates")

SET Evidence = "This kinase is sensitive to Ca++, whose levels are increased following
nAChR activation (Oddo et al., 2005)"
act(p(FPLX:CHRN)) -> a(CHEBI:"calcium(2+)")

SET Evidence = "Even though nicotine showed
a positive effect reducing plaque load (Hellstrom-Lindahl et al.,
2004; Inestrosa et al., 2013; Nordberg et al., 2002), its use in AD
treatment should be limited due to its toxic effect on tau pathology"
a(CHEBI:nicotine) -| path(MESH:"Plaque, Amyloid")

SET Evidence = "Nicotine treatment improved the memory deficit, highlighted with
the Morris water maze task. Surprisingly, this study showed a dose
dependent increase of alpha7 nAChR, a result that is in contrast with the
literature (Oddo et al., 2005)"
a(CHEBI:nicotine) -> bp(GO:memory)
a(CHEBI:nicotine) -> a(MESH:"alpha7 Nicotinic Acetylcholine Receptor")

SET Evidence = "In a mouse model of AD, cotinine treatment decreased the plaque load and was able to
activate the Akt pathway, that was shown to be neuroprotective (Echeverria et al., 2011)"
a(CHEBI:"(-)-cotinine") -| path(MESH:"Plaque, Amyloid")
a(CHEBI:"(-)-cotinine") -> act(p(FPLX:AKT))
act(p(FPLX:AKT)) -| bp(HP:Neurodegeneration)

SET Evidence = "Cotinine is an alpha7 nAChR PAM (positive allosteric modulator)"
a(CHEBI:"(-)-cotinine") -> act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))

SET Evidence = "Whilst the spatial memory deficit
was restored by 4OH-GTS-21 treatment, this molecule had no effect
on neuronal density (Ren et al., 2007)"
a(HBP:"4-OH-GTS-21") -> path(MESH:"Spatial Memory")
a(HBP:"4-OH-GTS-21") causesNoChange bp(GO:"neuron cellular homeostasis")

SET Evidence = "Wild-type mice treated with
nicotine or with SSR180711, another partial agonist of alpha7 (Biton
et al., 2006), showed increased LTP, while the transgenic AD
model APPSwe/PS1DE9 showed no effect on LTP following SSR180711 treatment"
a(CHEBI:nicotine) -> bp(GO:"long-term synaptic potentiation")
a(PUBCHEM:9928899) -> bp(GO:"long-term synaptic potentiation")

SET Evidence = "Pregnenolone sulfate (PREGS) is an endogenous steroid known
to ameliorate cognitive performance in animals. PREGS is a
modulator of synaptic plasticity, acting on the activation of glutamatergic
transmission (Smith et al., 2014)"
a(CHEBI:"pregnenolone sulfate") -> bp(GO:cognition)
a(CHEBI:"pregnenolone sulfate") -> bp(GO:"positive regulation of synaptic plasticity")
a(CHEBI:"pregnenolone sulfate") -> bp(GO:"synaptic transmission, glutamatergic")

SET Evidence = "It was proposed that the positive action of PREGS is mediated by alpha7 nAChR"
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> act(a(CHEBI:"pregnenolone sulfate"))
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> bp(GO:cognition)
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> bp(GO:"positive regulation of synaptic plasticity")
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> bp(GO:"synaptic transmission, glutamatergic")

SET Evidence = "The treatment improved spatial memory and reduced apoptosis in CA1 pyramidal cells"
# treatment with pregnenolone sulfate
a(CHEBI:"pregnenolone sulfate") -> path(MESH:"Spatial Memory")
SET MeSHAnatomy = {"CA1 Region, Hippocampal","Pyramidal Cells"}
a(CHEBI:"pregnenolone sulfate") -| bp(GO:"apoptotic process")
UNSET MeSHAnatomy

SET Evidence = "Simvastatin (SV) is a statin commonly used in the clinic to
control cholesterol levels and it was shown to improve cognitive
function in AD patients (Simons et al., 2002; Sparks et al., 2006)"
a(CHEBI:simvastatin) reg bp(GO:"cholesterol homeostasis")
a(CHEBI:simvastatin) -> bp(GO:cognition)

SET Evidence = "In a study conducted by Zhi et al. (2014), this molecule was used to
treat a mouse model obtained with injections of the peptide
Aß25-35. Administration for 11 days of SV improved memory performance
in the Morris water maze task and promoted survival of
CA1 pyramidal cells. This effect was proposed to be promoted by alpha7
nAChRs, since it is blocked by MLA administration (Zhi et al., 2014)"
# MLA: Methyllycaconitine
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> act(a(CHEBI:simvastatin))
composite(a(CHEBI:simvastatin),p(MGI:App,frag("25_35"))) -> bp(GO:memory)
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -> bp(GO:memory)
a(PUBCHEM:5311278) -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))
SET MeSHAnatomy = {"CA1 Region, Hippocampal","Pyramidal Cells"}
a(CHEBI:simvastatin) -| bp(GO:"apoptotic process")
a(MESH:"alpha7 Nicotinic Acetylcholine Receptor") -| bp(GO:"apoptotic process")
UNSET MeSHAnatomy

UNSET MeSHDisease

SET Evidence = "The intracellular signalling
initiated by the binding of Abeta to nAChR at the cell surface requires
the recruitment of Filamin A (FLNA), a scaffold protein that is
known to crosslink actin, and in addition could also have a function
in certain intracellular pathways (Stossel et al., 2001)"
complex(a(CHEBI:"amyloid-beta"),p(FPLX:CHRN),p(HGNC:FLNA)) -> bp(GO:"intracellular signal transduction")

SET Evidence = "Wang et al. showed that the association between FLNA and the alpha7 subunit is
elevated in AD samples compared to age matched controls"
complex(p(HGNC:CHRNA7),p(HGNC:FLNA)) positiveCorrelation path(MESH:"Alzheimer Disease")

SET Evidence = "A novel molecule called PTI-125 was used to interfere with the interaction
of FLNA and alpha7. The treatment with PTI-125 prevents FLNA binding
to alpha7 and as consequence reduces the affinity of Abeta for nAChRs,
attenuating the toxic effect of Abeta (Wang et al., 2012)"
a(HBP:"PTI-125") -| complex(p(HGNC:CHRNA7),p(HGNC:FLNA))
a(HBP:"PTI-125") -| complex(p(FPLX:CHRN),a(CHEBI:"amyloid-beta"))
a(HBP:"PTI-125") -> act(a(CHEBI:"amyloid-beta"))
