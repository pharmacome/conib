#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Anatabine lowers Alzheimer's Aβ production in vitro and in vivo"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
#DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
#DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
#DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
#DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
#DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "21958873"}

SET CellLine = "CHO cell"

SET Evidence = "Anatabine dose dependently inhibited Aβ1–40 and Aβ1–42 with an approximate half maximal
inhibitory concentration of 640 μg/ml for both Aβ1–40 and Aβ1–42 (Fig. 2)."
#dose concentration 640 microgrm per ml
SET Confidence = "High"
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta polypeptide 42")
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta polypeptide 40")
UNSET Confidence

SET Evidence = "No cytotoxicity was observed (as measured by the release of lactate dehydrogenase in the culture medium of 7W CHO cells) with
the dose range of anatabine tested (data not shown)."
#LDHA : Lactose dehydrogenase
SET Confidence = "High"
a(CHEBI:Anatabine) cnc sec(p(HGNC:LDHA))
UNSET Confidence

SET Evidence = "Contrary to anatabine, (−)-nicotine and other nicotinic acetylcholine
receptors agonists and antagonists do not inhibit Aβ production by 7W CHO cells (Fig. 3)."
# nicotinic acetylcholine receptors agonists (5-Iodo-A85380 dihydrochloride, (−)-nicotine and PHA543613 hydrochloride)
# antagonists (α-bungarotoxin and dihydro-β-erythroidine hydrobromide).
SET Confidence = "High"
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta")
a(PUBCHEM:71311633) cnc a(CHEBI:"amyloid-beta")
a(PUBCHEM:71311633) -> act(a(MESH:"Receptors, Nicotinic"))
a(CHEBI:nicotine) cnc a(CHEBI:"amyloid-beta")
a(CHEBI:nicotine) -> act(a(MESH:"Receptors, Nicotinic"))
a(PUBCHEM:56972222) cnc a(CHEBI:"amyloid-beta")
a(PUBCHEM:56972222) -> act(a(MESH:"Receptors, Nicotinic"))
p(HBP:"alpha-Bungarotoxin") cnc a(CHEBI:"amyloid-beta")
p(HBP:"alpha-Bungarotoxin") -| act(a(MESH:"Receptors, Nicotinic"))
a(PUBCHEM:11957537) cnc a(CHEBI:"amyloid-beta")
a(PUBCHEM:11957537) -| act(a(MESH:"Receptors, Nicotinic"))
UNSET Confidence

SET Evidence = "We then tested the impact of anatabine on sAPPα and sAPPβ production using 7W CHO cells and observed that
anatabine inhibits sAPPβ secretion without impacting sAPPα suggesting that anatabine is preventing the β-cleavage of APP (Fig. 4)."
# cleavage by beta-secretase yields the amyloidogenic form Abeta1–40 or the longer, more fibrillogenic Abeta1–42
SET Confidence = "High"
a(CHEBI:Anatabine) -| sec(p(HBP:"sAPP-beta"))
a(CHEBI:Anatabine) cnc sec(p(HBP:"sAPP-alpha"))
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta polypeptide 42")
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta polypeptide 40")
UNSET Confidence

SET Evidence = "Nicotine does not appear to affect sAPPβ and sAPPα secretion in 7W CHO contrary to anatabine (Fig. 4)"
SET Confidence = "High"
a(CHEBI:nicotine) cnc sec(p(HBP:"sAPP-beta"))
a(CHEBI:nicotine) cnc sec(p(HBP:"sAPP-alpha"))
UNSET Confidence

UNSET CellLine

SET Evidence = "We observed that anatabine dose dependently inhibited NFκB activation by TNFα in HEK293 NFκB luciferase
reporter cells (Fig. 5) whereas nicotine was ineffective"
SET Confidence = "Medium"
SET CellLine = "HEK293"
a(CHEBI:Anatabine) -| act(complex(GO:"NF-kappaB complex"))
p(HGNC:TNF) -> act(complex(GO:"NF-kappaB complex"))
a(CHEBI:nicotine) cnc act(complex(GO:"NF-kappaB complex"))
UNSET CellLine
UNSET Confidence

SET Evidence = "In addition, an inhibition of p65 NFκB phosphorylation was observed
following treatment with anatabine in 7W CHO overexpressing APP (Fig. 6A), in HEK293 (Fig. 6B) and in human neuronal like SH-SY5Y
cells (Fig. 6C) showing that anatabine can prevent NFκB activation in various cell lines."
SET Confidence = "Medium"
SET CellLine = "CHO cell"
a(CHEBI:Anatabine) -| p(HGNC:RELA,pmod(Ph))
UNSET CellLine
SET CellLine = {"SH-SY5Y","HEK293"}
SET Cell = "neuron"
a(CHEBI:Anatabine) -| act(complex(GO:"NF-kappaB complex"))
UNSET Cell
UNSET CellLine
UNSET Confidence

SET Evidence = "Since BACE-1 transcription is regulated by NFκB (Buggia-Prevot et al., 2008) and since we have shown that anatabine inhibits NFκB
activation, we investigated the effect of anatabine on BACE-1 transcription using human neuronal like SH-SY5Y cells"
SET Confidence = "Medium"
complex(GO:"NF-kappaB complex") reg r(HGNC:BACE1)
a(CHEBI:Anatabine) -| act(complex(GO:"NF-kappaB complex"))
UNSET Confidence

SET CellLine = "SH-SY5Y"

SET Evidence = "As expected, TNFα greatly stimulated BACE-1 transcription whereas anatabine
fully prevented the increase in BACE-1 mRNA levels induced by TNFα (Fig. 7A)."
SET Confidence = "Medium"
p(HGNC:TNF) -> r(HGNC:BACE1)
a(CHEBI:Anatabine) -| r(HGNC:BACE1)
UNSET Confidence

SET Evidence = "Following 24 h of treatment, a dose dependent inhibition of BACE-1 protein levels was observed with anatabine (Fig. 7B)"
# Treatment: 1 day
SET Confidence = "High"
a(CHEBI:Anatabine) -| p(HGNC:BACE1)
UNSET Confidence

UNSET CellLine

SET Evidence = "A significant reduction in the accumulation of brain soluble and insoluble Aβ1–40 and Aβ1–42 was observed
following four days of treatment with 2 mg/kg of anatabine (Fig. 9)."
# Dosage: 2 mg/kg
# Treatment: 4 days
SET Confidence = "High"
SET MeSHAnatomy = "Brain"
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta polypeptide 42")
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta polypeptide 40")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In addition, a significant decreased in plasma Aβ levels
was observed in mice treated with anatabine consistent with an inhibition of Aβ production (Fig. 10)."
SET Confidence = "Medium"
SET Species = "10090"
SET MeSHAnatomy = "Plasma"
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta")
a(CHEBI:Anatabine) -| a(GO:"amyloid-beta formation")
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "Since anatabine lowers NFκB activation in vitro, we explored the possibility that anatabine
may affect the level of C-reactive protein (CRP) whose expression is NFκB dependent (Karlsen et al., 2010; Yang et al.,2010)."
SET Confidence = "High"
a(CHEBI:Anatabine) -| act(complex(GO:"NF-kappaB complex"))
complex(GO:"NF-kappaB complex") reg p(HGNC:CRP)
UNSET Confidence

SET Evidence = "A statistically significant reduction in plasma CRP levels was observed following the 2 mg/kg treatment with anatabine
showing that at this dosage anatabine displays an anti-inflammatory activity in vivo (Fig. 11)."
# Dosage: 2 mg/kg
SET Confidence = "Medium"
SET MeSHAnatomy = "Plasma"
a(CHEBI:Anatabine) -| p(HGNC:CRP)
a(CHEBI:Anatabine) neg bp(GO:"inflammatory response")
UNSET MeSHAnatomy
UNSET Confidence
