#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Anatabine Attenuates Tau Phosphorylation and Oligomerization in P301S Tau Transgenic Mice"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
#DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
#DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
#DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
#DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
#DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"DOI", "10.4172/2168-975X.1000126"}
SET Species = "10090"

SET Evidence = "During the time course of the treatment (40 days), 25% of Tg Tau P301S placebo mice became
paralyzed (3 out of 12) whereas none (0 out of 11) in the anatabine treatment group developed paralysis
during the study duration (Figure 1)"
# mouse strain - Tg Tau P301S placebo
# treatment: 40 days
SET Confidence = "High"
a(CHEBI:Anatabine) -| path(MESH:Paralysis)
UNSET Confidence

SET Evidence = "Approximately 44 % (4 out of 9 non-paralyzed mice) of the placebo group (33 week-old mice) elicited an abnormal
hind-limb extension reflex when suspended by the tail compared to 18% (2 out of 11) of Tg Tau P301S mice treated with anatabine (Figure 1)."
# mouse strain - Tg Tau P301S
# hind-limb extension reflex: detection of defects in motor behavior
SET Confidence = "High"
a(CHEBI:Anatabine) -> bp(GO:"motor behavior")
UNSET Confidence

SET Evidence = "In addition, Tg Tau P301S mice treated with anatabine showed an increased latency to fall (T-test, P<0.05) from an accelerating rotarod
apparatus compared to Tg Tau P301S mice receiving regular drinking water (Figure 2) showing improved motor coordination."
# mouse strain - Tg Tau P301S
SET Confidence = "High"
a(CHEBI:Anatabine) -> bp(GO:"motor behavior")
UNSET Confidence

SET Evidence = "Tg Tau P301S mice treated with anatabine spent significantly more time (T-test, P<0.05) in the open arms of
the elevated plus maze than placebo Tg Tau P301S mice (Figure 3)."
# mouse strain - Tg Tau P301S
# impact of anatabine on anxiety related behavior
SET Confidence = "High"
a(CHEBI:Anatabine) -| path(MESH:Anxiety)
UNSET Confidence

#didnt show spatial learning impariment mean no change or increase in learning
SET Evidence = "Compared to age matched wild-type mice, 32 week-old Tg Tau P301S mice did not show spatial learning impairment
in the RAWM (data not shown)"
# mouse strain - Tg Tau P301S
# Spatial learning abilities which depend on hippocampal functions were assessed by the radial arm water maze (RAWM)
SET Confidence = "Medium"
a(CHEBI:Anatabine) cnc path(MESH:"Spatial Learning")
UNSET Confidence

SET Evidence = "No effect of the anatabine treatment was observed on tau expression (T-test, P>0.05) in Tg Tau P301S mice (Figure 4)."
# mouse strain - Tg Tau P301S
# effect of the anatabine in brain and spinal cord homogenates
SET Confidence = "High"
SET MeSHAnatomy = {"Spinal Cord","Brain"}
a(CHEBI:Anatabine) cnc p(HGNC:MAPT)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In both the detergent soluble and insoluble fractions of the brain
and spinal cord homogenates of Tg Tau P301S mice, we found that tau phosphorylation was significantly reduced (T-tests, P<0.05) by
the anatabine treatment for all the AD phosphorylated epitopes tested (Figure 6)"
# mouse strain - Tg Tau P301S
# phosphorylated epitopes (antibodies: PHF-1, RZ3 and CP13)
# PHF-1: Ser396/Ser404
# RZ3: Thr231
# CP13: Ser202
SET Confidence = "High"
SET MeSHAnatomy = {"Spinal Cord","Brain"}
a(CHEBI:Anatabine) -| p(HGNC:MAPT,pmod(Ph,Ser,396),pmod(Ph,Ser,404))
a(CHEBI:Anatabine) -| p(HGNC:MAPT,pmod(Ph,Ser,202))
a(CHEBI:Anatabine) -| p(HGNC:MAPT,pmod(Ph,Thr,231))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Moreover, a significant reduction in MC1-immunoreactivity (P<0.05) was observed in the detergent soluble and insoluble fractions
of brain and spinal cord homogenates from Tg Tau P301S mice treated with anatabine showing that anatabine prevents the formation of
pathological tau conformers (Figure 7)."
# mouse strain - Tg Tau P301S
# MC1 recognizes neurofibrillary tangles
SET Confidence = "Medium"
SET MeSHAnatomy = {"Spinal Cord","Brain"}
a(CHEBI:Anatabine) -| a(GO:"neurofibrillary tangle")
#prevetn formation of Tau conformers
a(CHEBI:Anatabine) -| act(p(HGNC:MAPT))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Additionally, a significant reduction (P<0.05) in tau oligomers levels (TOC1 immunopositive)
was detected both in the brain and the spinal cord of Tg Tau P301S treated with anatabine using dot-blots (Figure 8)."
# mouse strain - Tg Tau P301S
SET Confidence = "High"
SET MeSHAnatomy = {"Spinal Cord","Brain"}
a(CHEBI:Anatabine) -| a(HBP:"Tau oligomers")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "We observed that anatabine significantly prevented the formation of MC1-positive tau oligomers (Figure 9) by western-blots
in both the brain and the spinal cord of Tg Tau P301S mice (T-tests, P<0.05) further confirming the data obtained with the dot-blots"
# mouse strain - Tg Tau P301S
SET Confidence = "High"
SET MeSHAnatomy = {"Spinal Cord","Brain"}
a(CHEBI:Anatabine) -| a(HBP:"Tau oligomers")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "A significant reduction in Iba1 immunoreactivity was observed both in the brain (T-test, P<0.01) and spinal cord (T-test, P<0.01)
homogenates of Tg Tau P301S mice treated with anatabine. Figure 10."
# mouse strain - Tg Tau P301S
# Iba1: marker of microgliosis
SET Confidence = "High"
SET MeSHAnatomy = {"Spinal Cord","Brain"}
a(CHEBI:Anatabine) -| bp(HBP:Microgliosis)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Glycogen synthase 3β (GSK3β) is one of the main serine-threonine kinase responsible for tau phosphorylation and has been shown to
affect tau phosphorylation at multiple AD relevant epitopes including Ser396, Ser404, Thr231 and Ser202"
SET Confidence = "High"
p(HGNC:GSK3B) -> p(HGNC:MAPT,pmod(Ph,Ser,396))
p(HGNC:GSK3B) -> p(HGNC:MAPT,pmod(Ph,Ser,404))
p(HGNC:GSK3B) -> p(HGNC:MAPT,pmod(Ph,Thr,231))
p(HGNC:GSK3B) -> p(HGNC:MAPT,pmod(Ph,Ser,202))
UNSET Confidence

SET Evidence = "GSK3β is inactivated upon phosphorylation of Ser9 by protein kinase B (AKT) [41] whereas
AKT phosphorylation at Ser473 results in AKT activation [42]."
SET Confidence = "Medium"
p(FPLX:AKT) -> p(HGNC:GSK3B,pmod(Ph,Ser,9))
p(HGNC:GSK3B,pmod(Ph,Ser,9)) -| act(p(HGNC:GSK3B))
p(FPLX:AKT,pmod(Ph,Ser,473)) -> act(p(FPLX:AKT))
UNSET Confidence

SET Evidence = "We found a significant increase in brain AKT Ser473 phosphorylation (T-test, P<0.001) and
GSK3β Ser9 phosphorylation (T-test, P<0.001) in Tg Tau P301S mice treated with anatabine. Figure 11."
# mouse strain - Tg Tau P301S
SET Confidence = "High"
SET MeSHAnatomy = "Brain"
a(CHEBI:Anatabine) -> p(HGNC:GSK3B,pmod(Ph,Ser,9))
a(CHEBI:Anatabine) -> p(FPLX:AKT,pmod(Ph,Ser,473))
UNSET MeSHAnatomy
UNSET Confidence
