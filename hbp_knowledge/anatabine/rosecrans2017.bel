#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Discriminative Stimulus Properties of S(−)-Nicotine: “A Drug for All Seasons"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
#DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
#DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
#DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
#DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
#DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
#DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "28391535"}

SET Evidence = "For example, immediately after exposure to nicotine, there is a “stimulant-kick”
caused, in part, by its stimulation of the adrenal glands and resultant discharge of epinephrine (adrenaline)"
SET MeSHAnatomy = "Adrenal Glands"
a(CHEBI:nicotine) -> act(a(MESH:"Adrenal Glands"))
a(CHEBI:nicotine) -> sec(a(CHEBI:adrenaline))

SET Evidence = "The release of epinephrine stimulates the body and causes a sudden release of glucose as well as an increase in blood pressure,
respiration, and heart rate"
sec(a(CHEBI:adrenaline)) -> sec(a(CHEBI:glucose))
sec(a(CHEBI:adrenaline)) -> a(MESH:"Blood Pressure")
sec(a(CHEBI:adrenaline)) -> bp(GO:"cellular respiration")
sec(a(CHEBI:adrenaline)) -> a(MESH:"Heart Rate")

SET Evidence = "Nicotine also suppresses insulin output from the pancreas, which indicates that smokers are usually hyperglycemic
(higher blood sugar level)"
SET MeSHAnatomy = "Pancreas"
a(CHEBI:nicotine) -| sec(a(CHEBI:"insulin (human)"))
UNSET MeSHAnatomy
path(MESH:Smoking) -> a(MESH:"Blood Glucose")

SET Evidence = "Centrally, (-)-nicotine has affinity for all brain nAChR subtypes, but binds
preferentially and with high affinity to α4β2 nAChRs (e.g., [12, 13])"
a(CHEBI:nicotine) -> complex(a(CHEBI:nicotine),a(MESH:"nicotinic receptor alpha4beta2"))

SET Evidence = "Moreover, (-)-nicotine (indirectly) can produce a release of dopamine in brain regions that
are thought to control pleasure and motivation; dopamine is thought to underlie the
pleasurable sensations experienced by smokers (e.g., [14, 15] but see [16])."
SET MeSHAnatomy = "Brain"
a(CHEBI:nicotine) -> sec(a(CHEBI:dopamine))
a(CHEBI:dopamine) reg path(MESH:Pleasure)
a(CHEBI:dopamine) reg path(MESH:Motivation)
UNSET MeSHAnatomy

SET Evidence = "For example, in rodents, administration of low doses of nicotine produced increased
motor activity whereas high doses produced decreased motor activity (e.g.,[17, 18])"
a(CHEBI:nicotine) neg path(MESH:"Motor Activity")

SET Evidence = "In comparison, S(-)-nicotine was shown to be 7 times more toxic than R(+)-nicotine in rats injected intravenously"
# toxic: Lethality
SET Species = "10116"
a(CHEBI:"(S)-nicotine") -> path(MESH:Death)
a(CHEBI:"(R)-nicotine") -> path(MESH:Death)
UNSET Species

SET Evidence = "The tobacco leaf contains many alkaloids and in fresh Nicotina tabacum (the leaf
species most commonly used for the production of cigarette tobacco) the average
alkaloid mixture typically consists of 93% S(-)-nicotine, 3.9% S(-)-anatabine, 2.4%
S(-)-nornicotine, and 0.5% S(-)-anabasine (e.g., [4, 5, 71]; Fig. 2; but see [72])."
a(MESH:Tobacco) hasComponents list(a(CHEBI:nicotine),a(CHEBI:Anatabine),a(CHEBI:nornicotine),a(CHEBI:"(S)-anabasine"))

SET Evidence = "In the body, nicotine is extensively metabolized and is susceptible to a significant first-pass effect during which 80–90% of it
is metabolized by the liver. Also, the lung is able to metabolize nicotine, but to a much lesser degree [78, 79]. "
a(MESH:Liver) -> bp(GO:"nicotine metabolic process")
a(MESH:Lung) -> bp(GO:"nicotine metabolic process")

SET Evidence = " In humans, about 70–80% of nicotine is converted to the primary metabolite (-)-cotinine, a lactam derivative (Fig. 2)."
bp(GO:"nicotine metabolic process") -> a(CHEBI:"(-)-cotinine")

SET Evidence = "Lastly, S(-)-nornicotine is a minor metabolite of nicotine and, as
mentioned previously, is considered a minor alkaloid of tobacco (Fig. 2)"
bp(GO:"nicotine metabolic process") -> a(CHEBI:"nornicotine")

SET Evidence = ". It is now well established that nicotine binds to nicotinic
acetylcholine receptors (nAChRs) at the cellular level and is the prototype drug used to classify nAChRs"
a(CHEBI:nicotine) -> complex(a(CHEBI:nicotine),a(MESH:"Receptors, Nicotinic"))

SET Evidence = "(-)-Nicotine activates all brain nAChR
subtypes, but binds preferentially and with high affinity to α4β2 nAChRs (e.g., [12])"
a(CHEBI:nicotine) -> act(a(MESH:"Receptors, Nicotinic"))
a(CHEBI:nicotine) -> complex(a(CHEBI:nicotine),a(MESH:"nicotinic receptor alpha4beta2"))

SET Evidence = "For example, (-)-nicotine may increase dopamine
activity at some brain sites such as the nucleus accumbens, an area thought to be
important to drugs of abuse (e.g., [14, 101, 102]; but see [16, 103])"
SET MeSHAnatomy = "Nucleus Accumbens"
a(CHEBI:nicotine) -> act(a(CHEBI:dopamine))
UNSET MeSHAnatomy

SET Evidence = "This conclusion is based on the fact that the stimulus effects of
nicotine are convincingly blocked by (a) mecamylamine, a voltage dependent
noncompetitive channel blocker at nicotinic receptors (Fig. 3; Table 4) and
(b) dihydro-β-erythrodine (DHβE), a nicotinic receptor antagonist that shows high
affinity for the nAChR α4β2 subunit (Fig. 3; Table 5) but not by methyllycaconitine
(MLA), a α7 nicotinic receptor antagonist (Table 5)."
a(CHEBI:Mecamylamine) -| act(a(CHEBI:nicotine))
a(MESH:"Dihydro-beta-Erythroidine") -| act(a(CHEBI:nicotine))
a(MESH:"Dihydro-beta-Erythroidine") -> complex(a(MESH:"Dihydro-beta-Erythroidine"),a(MESH:"nicotinic receptor alpha4beta2"))
a(MESH:"Dihydro-beta-Erythroidine") -| act(a(MESH:"Receptors, Nicotinic"))
a(PUBCHEM:5288811) cnc act(a(CHEBI:nicotine))
a(PUBCHEM:5288811) -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))

SET Evidence = "Mecamylamine (Inversine®, Vecamyl®; Fig. 3) was developed over 60 years ago
and marketed as a ganglionic blocker for the treatment of hypertension (e.g., [127])"
a(CHEBI:Mecamylamine) -| path(MESH:Hypertension)

SET Evidence = " In addition, mecamylamine can produce CNS effects that include
tremor, mental confusion, seizures, mania, and depression but the mechanisms by
which these effects are produced are unclear"
a(CHEBI:Mecamylamine) -> path(MEH:Tremor)
a(CHEBI:Mecamylamine) -> path(MESH:Confusion)
a(CHEBI:Mecamylamine) -> path(MESH:Seizures)
a(CHEBI:Mecamylamine) -> path(MESH:"Bipolar Disorder")
a(CHEBI:Mecamylamine) -> path(MESH:Depression)

SET Evidence = " Also, mecamylamine is sometimes
used as an anti-addictive drug to help people stop smoking tobacco products (e.g.,[128, 129])"
a(CHEBI:Mecamylamine) -| path(MESH:Smoking)

SET Evidence = "Biochemical and pharmacological studies have characterized mecamylamine as a
nonselective, voltage dependent and noncompetitive receptor antagonist of neuronal
nAChRs and it is often referred to as a “nicotine receptor antagonist.”"
a(CHEBI:Mecamylamine) -| act(a(MESH:"Receptors, Nicotinic"))

SET Evidence = "For example, some biochemical studies suggest that mecamylamine is a
channel blocker that inhibits most neuronal nAChRs (e.g., [131–133])."
a(CHEBI:Mecamylamine) -| act(a(MESH:"Receptors, Nicotinic"))

SET Evidence = "DHβE (Fig. 3) is an alkaloid found in plant seeds of Erythrina and is a competitive
nAChR receptor antagonist with a preference for neuronal β2 subtypes"
# DHβE: dihydro-β-erythrodine
a(MESH:"Dihydro-beta-Erythroidine") -| act(a(MESH:"Receptors, Nicotinic"))
a(MESH:"Dihydro-beta-Erythroidine") -> complex(p(HGNC:CHRNB2),a(MESH:"Dihydro-beta-Erythroidine") )

SET Evidence = "For example, DHβE (at nM concentrations) blocks α4β2 and α3β2 nAChRs but is much less
potent at α3β4 and α7 nAChRs expressed in Xenopus oocytes (e.g., [134–137])."
# DHβE: dihydro-β-erythrodine
a(MESH:"Dihydro-beta-Erythroidine") -| act(a(MESH:"nicotinic receptor alpha3beta2"))
a(MESH:"Dihydro-beta-Erythroidine") -| act(a(MESH:"nicotinic receptor alpha4beta2"))
a(MESH:"Dihydro-beta-Erythroidine") -| act(a(MESH:"nicotinic receptor alpha3beta4"))
a(MESH:"Dihydro-beta-Erythroidine") -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))

SET Evidence = "Research results summarized in Table 5 indicate that DHβE
effectively blocked the stimulus effects of (-)-nicotine in rats or mice (but see
exceptions reported by [120, 121])."
# DHβE: dihydro-β-erythrodine
SET Species = {"10090","10116"}
a(MESH:"Dihydro-beta-Erythroidine") -| act(a(CHEBI:nicotine))
UNSET Species

SET Evidence = "Its biochemical pharmacology indicates that it is a relatively potent competitive receptor antagonist
that is selective for α7 nAChRs (e.g., [139–141])."
# its: Methyllycaconitine
a(PUBCHEM:5288811) -| act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))

SET Evidence = "Table 5 presents results of MLA/(-)-nicotine combination studies and shows that
MLA failed to alter the stimulus effects of (-)-nicotine in rats or mice (but see
partial antagonism reported by Quarta et al. [126])"
# MLA: Methyllycaconitine
SET Species = {"10090","10116"}
a(PUBCHEM:5288811) cnc act(a(CHEBI:nicotine))
UNSET Species

SET Evidence = "In antagonism tests, (-)-nicotine failed to block the stimulus effects of mecamylamine"
a(CHEBI:nicotine) cnc act(a(CHEBI:Mecamylamine))

SET Evidence = ". It should be noted that hexamethonium, at relatively low
doses, does not block the stimulus effects of (-)-nicotine but when administered at
high doses has occasionally been reported to attenuate nicotine-like responding;
probably the result of penetration into the CNS of a small proportion of the
administered dose of drug (e.g., [35, 38, 64, 106, 146])"
a(CHEBI:"Hexamethonium") neg act(a(CHEBI:nicotine))

SET Evidence = "(-)-Nicotine withdrawal symptoms might begin within a few hours after the last nicotine product, and
include irritability/anger/stress/anxiety, sleep disturbances, depressed mood, craving, cognitive and attention deficits, and increased appetite."
path(MESH:"Smoking Cessation") -> path(MESH:"Irritable Mood")
path(MESH:"Smoking Cessation") -> path(MESH:Anger)
path(MESH:"Smoking Cessation") -> path(MESH:"Stress, Psychological")
path(MESH:"Smoking Cessation") -> path(MESH:Anxiety)
path(MESH:"Smoking Cessation") -> path(MESH:"Sleep Wake Disorders")
path(MESH:"Smoking Cessation") -> path(MESH:Depression)
path(MESH:"Smoking Cessation") -> path(MESH:Craving)
path(MESH:"Smoking Cessation") -| bp(GO:cognition)
path(MESH:"Smoking Cessation") -| path(MESH:Attention)
path(MESH:"Smoking Cessation") -> bp(MESH:Appetite)

SET Evidence = "Lobeline binds with high affinity to α4β2 nAChRs and displays mixed receptor agonist/antagonist actions (e.g., [150–154])."
a(CHEBI:"(-)-lobeline") -> complex(a(MESH:"nicotinic receptor alpha4beta2"),a(CHEBI:"(-)-lobeline"))

SET Evidence = "Lobeline has, however, been shown to
antagonize partially the stimulus effects of (-)-nicotine and S(+)-methamphetamine ([64, 160]; but see [66])."
a(CHEBI:"(-)-lobeline") -| act(a(CHEBI:nicotine))
a(CHEBI:"(-)-lobeline") -| act(a(CHEBI:methamphetamine))

SET Evidence = "Bupropion [a.k.a. amfebutamone, (RS)-2-(tert-Butylamino)-1-(3-chlorophenyl)propan1-one, 3-Chloro tert-butylcathinone,
3-Chloro-N-tert-butyl-β-ketoamphetamine; Fig. 4] is a phenylaminoketone or cathinone derivative that is a weak central nervous system (CNS) stimulant"
a(CHEBI:bupropion) -> act(a(MESH:"Central Nervous System"))

SET Evidence = "It is prescribed as medication for the treatment of depression
(Wellbutrin®) and/or as an adjunct in smoking cessation therapy (Zyban®)."
# it: Bupropion
a(CHEBI:bupropion) -| path(MESH:Depression)

SET Evidence = "Other studies have reported that
bupropion blocked the acute effects of (-)-nicotine in a number of behavioral assays in mice (e.g., [171, 172])"
a(CHEBI:bupropion) -| act(a(CHEBI:nicotine))

SET Evidence = "Varenicline (Chantix®; Fig. 4) is prescribed as an adjunct medication in smoking
cessation therapy and is thought to exert its effects as a partial agonist at α4β2
nAChRs and as a full agonist at α7 nAChRs [211, 212]. "
a(CHEBI:varenicline) -> act(a(MESH:"nicotinic receptor alpha4beta2"))
a(CHEBI:varenicline) -> act(a(MESH:"alpha7 Nicotinic Acetylcholine Receptor"))
