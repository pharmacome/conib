#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Clearance systems in the brain-implications for Alzheimer disease."
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Lingling Xu"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DNSNP AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE HBP AS PATTERN ".*"
DEFINE NAMESPACE TAXONOMY AS PATTERN "^\d+$"

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE MESH     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mesh-names.belns"
DEFINE NAMESPACE CHEBI    AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE   AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
DEFINE NAMESPACE NCBIGENE AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE GO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE HGNC     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel
# --------------------------------------------------------------------
DEFINE NAMESPACE HP       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE MGI      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170725.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-20170731.belns"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "26195256"}

SET Evidence =" Emerging evidence suggests that Aβ clearance is impaired in both early-onset and late-onset forms of AD."
SET Confidence = "Medium"
path(MESH:"Alzheimer Disease") -| deg(a(CHEBI:"amyloid-beta"))
UNSET Confidence

SET Evidence ="Soluble Aβ can be removed from the brain by various clearance systems,
including enzymatic degradation and cellular uptake, transport across the blood–brain
barrier (BBB) and blood–cerebrospinal fluid barrier (BCSFB), interstitial fluid (ISF)
bulk flow, and cerebro- spinal fluid (CSF) absorption into the circulatory and lymphatic
systems."
#amyloid-beta derived diffusible ligands
SET Confidence = "Medium"
deg(a(HBP:"amyloid-beta derived diffusible ligands")) -| a(HBP:"amyloid-beta derived diffusible ligands")
tloc(a(HBP:"amyloid-beta derived diffusible ligands"),fromLoc(GO:"extracellular region"),toLoc(MESH:"Intracellular Space")) -| a(HBP:"amyloid-beta derived diffusible ligands")
tloc(a(HBP:"amyloid-beta derived diffusible ligands"),fromLoc(MESH:Brain),toLoc(MESH:Blood)) -| a(HBP:"amyloid-beta derived diffusible ligands")
tloc(a(HBP:"amyloid-beta derived diffusible ligands"),fromLoc(MESH:Blood),toLoc(MESH:"Cerebrospinal Fluid")) -| a(HBP:"amyloid-beta derived diffusible ligands")
tloc(a(HBP:"amyloid-beta derived diffusible ligands"),fromLoc(MESH:Brain),toLoc(MESH:"Extracellular Fluid")) -| a(HBP:"amyloid-beta derived diffusible ligands")
tloc(a(HBP:"amyloid-beta derived diffusible ligands"),fromLoc(MESH:"Cerebrospinal Fluid"),toLoc(MESH:"Cardiovascular System")) -| a(HBP:"amyloid-beta derived diffusible ligands")
tloc(a(HBP:"amyloid-beta derived diffusible ligands"),fromLoc(MESH:"Cerebrospinal Fluid"),toLoc(MESH:"Lymphatic System")) -| a(HBP:"amyloid-beta derived diffusible ligands")
UNSET Confidence

SET Evidence ="In the early 2000s, mouse studies demonstrated that the majority (75%)
of extracellular Aβ (eAβ) is cleared by the BBB, with only a minority (10%) being
cleared by ISF bulk flow."
#ISF bulk flow known as Glymphatic System
SET Confidence = "Medium"
SET Species = "10090"
a(MESH:"Blood-Brain Barrier") -| a(CHEBI:"amyloid-beta",loc(GO:"extracellular region"))
act(a(HBP:"Glymphatic System")) -| a(CHEBI:"amyloid-beta",loc(GO:"extracellular region"))
UNSET Species
UNSET Confidence

SET Evidence ="However, two-photon imaging studies from the past few years have suggested
 that ISF bulk flow—facilitated by astroglial aquaporin-4 (AQP4) channels and named the
 glymphatic (glial + lymphatic) system—contributes to a larger portion of eAβ clearance
 than previously thought."
SET Confidence = "Medium"
act(p(HGNC:AQP4)) -> act(a(HBP:"Glymphatic System"))
act(a(HBP:"Glymphatic System")) -| a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")
UNSET Confidence

SET Evidence ="When characterized by autosomal dominant inheritance, EOAD is related to
 mutations in the presenilin 1 (PSEN1), presenilin 2 (PSEN2) or amyloid precursor protein (APP) genes."
SET Confidence = "High"
path(MESH:"Alzheimer Disease") -- g(HGNC:PSEN1,var("?"))
path(MESH:"Alzheimer Disease") -- g(HGNC:PSEN2,var("?"))
path(MESH:"Alzheimer Disease") -- g(HGNC:APP,var("?"))
UNSET Confidence

SET Evidence ="Various factors have been reported to positively and negatively modulate the
risk of LOAD. Specifically, the greatest overall risk factor for LOAD is ageing;"
SET Confidence = "Medium"
path(MESH:Aging) -> path(MESH:"Alzheimer Disease")
UNSET Confidence

SET Evidence="The strongest identified genetic risk factor for LOAD is the apolipoprotein E (APOE) ε4 allele (APOE*ε4),"
SET Confidence = "Medium"
p(HBP:"APOE e4") -> path(MESH:"Alzheimer Disease")
UNSET Confidence

SET Evidence ="although genome-wide association studies have linked LOAD to several
other genetic variants, such as TREM2 (triggering receptor expressed on myeloid cells 2),
27 clusterin (CLU),28 and phosphatidylinositol-binding clathrin assembly protein (PICALM)."
SET Confidence = "Medium"
path(MESH:"Alzheimer Disease") -- p(HGNC:TREM2, var("?"))
path(MESH:"Alzheimer Disease") -- p(HGNC:CLU, var("?"))
path(MESH:"Alzheimer Disease") -- p(HGNC:PICALM, var("?"))
UNSET Confidence

SET Evidence ="Known envi- ronmental risk factors for LOAD include cardiovascular disease,
and factors conferring a risk of cardiovascu- lar disease, such as diabetes mellitus and
hypertension. Head trauma, physical and mental inactivity, and sleep impairment are
additional risk factors for LOAD"
SET Confidence = "Medium"
path(MESH:"Cardiovascular Diseases") -> path(MESH:"Alzheimer Disease")
path(MESH:"Diabetes Mellitus") -> path(MESH:"Alzheimer Disease")
path(MESH:Hypertension) -> path(MESH:"Alzheimer Disease")
path(MESH:"Craniocerebral Trauma") -> path(MESH:"Alzheimer Disease")
a(MESH:Exercise) -| path(MESH:"Alzheimer Disease")
a(MESH:Thinking) -| path(MESH:"Alzheimer Disease")
path(MESH:"Sleep Initiation and Maintenance Disorders") -> path(MESH:"Alzheimer Disease")
UNSET Confidence

SET Evidence =" The gross pathological changes consist of brain atrophy, particularly in
the hippocampal formation, temporal lobes and parietotemporal cortices, accompanied by
cortical thinning, enlarged ventricles and white matter abnormalities, as evident on MRI."
SET Confidence = "Medium"
SET MeSHAnatomy = {"Hippocampus","Temporal Lobe","Parietal Lobe"}
path(MESH:Atrophy) pos path(MESH:"Alzheimer Disease")
UNSET MeSHAnatomy
a(MESH:"Cerebral Cortex") neg path(MESH:"Alzheimer Disease")
a(MESH:"Cerebral Ventricles") pos path(MESH:"Alzheimer Disease")
a(MESH:"White Matter") -- path(MESH:"Alzheimer Disease")
UNSET Confidence

SET Evidence ="Microscopic changes include accumulation of Aβ into parenchymal senile plaques
(also known as neuritic plaques) or in the walls of cerebral capillaries and #arter- ies (known
as cerebral amyloid angiopathy, or CAA),as well as aggregation of hyperphosphorylated tau int
o intracellular neurofibrillary tangles (NFTs) and neuropil #threads"
SET Confidence = "Medium"
a(HBP:"amyloid-beta aggregates") -- a(MESH:"Plaque, Amyloid")
a(HBP:"amyloid-beta aggregates") -- path(MESH:"Cerebral Amyloid Angiopathy")
p(HGNC:MAPT, pmod(HBP:hyperphosphorylation)) -- a(MESH:"Neurofibrillary Tangles")
p(HGNC:MAPT, pmod(HBP:hyperphosphorylation)) -- a(MESH:"Neuropil Threads")
UNSET Confidence

SET Evidence ="Recent advances now enable several AD-related brain changes to be detected in
vivo: 18F-FDG-PET detects decreases in glucose metabolism,45,46 and MRI detects brain atrophy,
as well as diffusion and perfu- sion abnormalities, which are most prominent in the vul- nerable
hippocampal formation and cortical regions."
SET MeSHAnatomy = "Medium"
path(MESH:"Alzheimer Disease") -| bp(GO:"glucose metabolic process")
SET MeSHAnatomy = "Brain"
path(MESH:"Alzheimer Disease") -| path(MESH:Atrophy)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="ecifically, Aβ accumulation into extracellular plaques is marked by decreased
CSF levels of Aβ1–42, and tau accumulation into NFTs is marked by increased CSF levels of total
tau and hyperphosphory- lated tau."
SET Confidence = "Medium"
a(CHEBI:"amyloid-beta polypeptide 42",loc(MESH:"Cerebrospinal Fluid")) neg a(HBP:"amyloid-beta aggregates",loc(GO:"extracellular region"))
p(HGNC:MAPT,loc(MESH:"Neurofibrillary Tangles")) pos p(HGNC:MAPT,loc(MESH:"Cerebrospinal Fluid"))
p(HGNC:MAPT,loc(MESH:"Neurofibrillary Tangles")) pos p(HGNC:MAPT, pmod(HBP:hyperphosphorylation),loc(MESH:"Cerebrospinal Fluid"))
UNSET Confidence

SET Evidence ="Extracellular degradation of ISF proteins mainly consists of degradation by
proteases expressed and secreted by cells such as astrocytes"
SET Confidence = "Medium"
SET MeSHAnatomy = "Extracellular Fluid"
a(MESH:Astrocytes) -> sec(p(MESH:Endopeptidases))
p(MESH:Endopeptidases) -> bp(GO:"protein catabolic process")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Intracellular degradation of proteins occurs via the ubiquitin– proteasome
pathway, the autophagy–lysosome pathway, and the endosome–lysosome pathway."
SET Confidence = "Medium"
SET MeSHAnatomy = "Intracellular Space"
act(p(FPLX:Proteasome)) -> bp(GO:"protein catabolic process")
bp(GO:autophagy) -> bp(GO:"protein catabolic process")
act(complex(a(MESH:Endosomes),a(MESH:Lysosomes))) -> bp(GO:"protein catabolic process")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="APP is cleaved by α-secretase, which precludes forma- tion of Aβ, and the
resulting carboxy-terminal fragment is then cleaved by γ-secretase.103 The resulting
products do not aggregate.104"
SET Confidence = "Medium"
act(p(MESH:"Amyloid Precursor Protein Secretases")) -> reaction(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta"),a(HBP:"APP C-terminally truncated carboxyl-terminal fragments")))
p(FPLX:Gamma_secretase) -> deg(a(HBP:"APP C-terminally truncated carboxyl-terminal fragments"))
UNSET Confidence

SET Evidence ="If APP is first cleaved by β-secretase 1 (also known as BACE1) instead of α-secretase,
the subsequent γ-secretase cleavage will result in soluble monomeric Aβ."
SET Confidence = "Medium"
act(p(HGNC:BACE1)) -> a(CHEBI:"amyloid-beta")
UNSET Confidence

SET Evidence ="The most common soluble monomeric isoforms of Aβ are Aβ1-40 (<80%), Aβ1-38 (<20%)
and Aβ1-42 (10%)."
SET Confidence = "High"
a(CHEBI:"amyloid-beta polypeptide 40") isA a(CHEBI:"amyloid-beta")
a(CHEBI:"amyloid-beta polypeptide 42") isA a(CHEBI:"amyloid-beta")
a(HBP:"amyloid-beta polypeptide 38") isA a(CHEBI:"amyloid-beta")
UNSET Confidence

SET Evidence ="Intracellular Aβ (iAβ) can be degraded by proteasomes via the ubiquitin–proteasome
pathway in neurons,116 lyso- somal cathepsin enzymes,117 proteases (such as insulin- degrading enzyme,
a thiol metalloendopeptidase that degrades monomeric Aβ) and insulin."
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
act(p(FPLX:Proteasome)) -> deg(a(CHEBI:"amyloid-beta",loc(GO:intracellular)))
UNSET MeSHAnatomy
SET MeSHAnatomy = "Lysosomes"
act(p(MESH:Cathepsins)) -> deg(a(CHEBI:"amyloid-beta",loc(GO:intracellular)))
UNSET MeSHAnatomy
p(MESH:Endopeptidases) -> deg(a(CHEBI:"amyloid-beta",loc(GO:intracellular)))
p(HGNC:IDE) -> deg(a(CHEBI:"amyloid-beta",loc(GO:intracellular)))
UNSET Confidence

SET Evidence =" Extracellular Aβ can also be degraded by proteases, such as neprily- sin (a membrane-anchored
zinc metalloendopeptidase that degrades the Aβ monomers Aβ1-40 and Aβ1-42, and Aβ oligomers),119 matrix
metalloproteinases 2, 3 and 9,120 glutamate carboxypeptidase II,121 endothelin-converting enzyme,122 tissue
plasminogen activator,123 plasmin,120 angiotensin-converting enzyme,120 and insulin-degrading enzyme."
SET Confidence = "Medium"
p(HGNC:MME) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:MMP9) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:MMP2) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:MMP3) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:FOLH1) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:ECE1) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:ECE2) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:PLAT) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:PLG) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:ACE) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
p(HGNC:IDE) -> deg(a(CHEBI:"amyloid-beta",loc(GO:"extracellular region")))
UNSET Confidence

SET Evidence =" Specifically, ISF Aβ can be taken up by microglia and astrocytes, whereas perivascular
Aβ can be degraded by vascular smooth muscle cells, perivascular macrophages, and astrocytes"
SET Confidence = "Medium"
a(MESH:Microglia) -| a(CHEBI:"amyloid-beta",loc(MESH:"Extracellular Fluid"))
a(MESH:Astrocytes) -| a(CHEBI:"amyloid-beta",loc(MESH:"Extracellular Fluid"))
SET MeSHAnatomy = "Glymphatic System"
a(MESH:"Muscle, Smooth, Vascular") -> deg(a(CHEBI:"amyloid-beta"))
a(MESH:Macrophages) -> deg(a(CHEBI:"amyloid-beta"))
a(MESH:Astrocytes) -> deg(a(CHEBI:"amyloid-beta"))
UNSET Confidence

SET Evidence ="First, expression of neprilysin is decreased in AD,126 especially in regions
with high Aβ loads such as the hippocampus and temporal gyrus.127 "
SET Confidence = "Medium"
SET MeSHAnatomy ={"Hippocampus","Temporal Lobe"}
path(MESH:"Alzheimer Disease") neg p(MESH:Neprilysin)
UNSET Confidence

SET Evidence ="Although overall matrix metallo- proteinase 2 expression is increased in AD,
58 its activity is reduced in astrocytes that surround Aβ plaques."
SET Confidence = "Medium"
path(MESH:"Alzheimer Disease") pos p(HGNC:MMP2)
SET MeSHAnatomy = "Astrocytes"
a(MESH:"Plaque, Amyloid") neg act(p(HGNC:MMP2))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Second, both Aβ and insulin are ligands that compete for degradation by
insulin-degrading enzyme; thus, hyper- insulinaemia can reduce clearance of Aβ, which
might partly explain the link between type 2 diabetes mellitus and AD."
SET Confidence = "Medium"
p(HGNC:IDE) -> deg(p(HGNC:INS))
p(HGNC:IDE) -> deg(a(CHEBI:"amyloid-beta"))
a(HGNC:INS) -> a(CHEBI:"amyloid-beta")
UNSET Confidence

SET Evidence ="Third, plaques activate the immune effectors of the CNS—microglia and
astrocytes129—inducing both phagocytosis of Aβ, which facilitates clearance from the
extracellular space, and production of neurotoxic inflammatory cytokines."
SET Confidence = "Medium"
a(MESH:"Plaque, Amyloid") -> act(a(MESH:Microglia))
a(MESH:"Plaque, Amyloid") -> act(a(MESH:Astrocytes))
SET MeSHAnatomy = "Extracellular Space"
act(a(MESH:Microglia)) -| a(CHEBI:"amyloid-beta")
act(a(MESH:Astrocytes)) -| a(CHEBI:"amyloid-beta")
act(a(MESH:Microglia)) -> a(MESH:Cytokines)
act(a(MESH:Astrocytes)) -> a(MESH:Cytokines)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Some evidence suggests that LRP1 is the main transporter for Aβ efflux at the BBB,
whereas other studies have demonstrated its role to be quite minor."
SET Confidence = "Medium"
p(HGNC:LRP1) -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:Brain),toLoc(MESH:Blood))
UNSET Confidence

SET Evidence ="The main ABC transporter responsible for Aβ efflux is ABCB1 (also known as
P-glycoprotein 1 or MDR1),which directly exports Aβ into the circulation."
SET Confidence = "High"
p(HGNC:ABCB1) -> tloc(a(CHEBI:"amyloid-beta"))
UNSET Confidence

SET Evidence ="ABCA1, which is located on the abluminal side of the brain endo- thelium,140 does
not directly bind and extrude Aβ,141 but mediates Aβ clearance in an ApoE-dependent manner."
SET Confidence = "Medium"
p(HGNC:ABCB1) -> tloc(a(CHEBI:"amyloid-beta"))
p(HGNC:ABCB1) -> act(p(HGNC:APOE))
act(p(HGNC:APOE)) reg a(CHEBI:"amyloid-beta")
UNSET Confidence

SET Evidence = "Clearance of Aβ through the BBB is also medi- ated by α2-macroglobulin (α2M),14 and
LDLR-related protein 2 (LRP2, also known as megalin) when LRP2 forms a complex with clusterin
(also known as ApoJ)."
SET Confidence = "Medium"
SET MeSHAnatomy = "Blood-Brain Barrier"
p(HGNC:A2M) -| a(CHEBI:"amyloid-beta")
p(HGNC:LRP2) -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In addition, insulin-degrading enzyme has been proposed to have a role in Aβ
clearance through the BBB, which might explain why BBB clearance is sensitive to insulin.144"
SET Confidence = "Medium"
SET MeSHAnatomy = "Blood-Brain Barrier"
p(HGNC:IDE) -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Free Aβ can be transported from the circulation into the interstitium via
RAGE (advanced glycosylation end product-specific receptor)."
SET Confidence = "Medium"
p(HGNC:AGER) -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:"Cardiovascular System"),toLoc(MESH:"Extracellular Space"))
UNSET Confidence

SET Evidence ="Specifically, local soluble Aβ is transferred from the interstitium to
the brain by LDL receptor (LDLR) family members such as LRP1, and ATP-binding cassette
transporters (ABC transporters)."
SET Confidence = "Medium"
p(HGNC:LRP1) -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:Brain))
p(MESH:"ATP-Binding Cassette Transporters") -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:Brain))
UNSET Confidence

SET Evidence ="The main ABC transporter responsible for Aβ efflux is ABCB1 (also known
as P-glycoprotein 1 or MDR1), which directly exports Aβ into the circulation. "
SET Confidence = "High"
p(HGNC:ABCB1) -> tloc(a(CHEBI:"amyloid-beta"))
UNSET Confidence

SET Evidence ="In AD, these factors are impaired in a number of ways. First, expression
of the blood efflux transporters LRP1 123 and ABCB1 147 is decreased, whereas expression
of the blood influx transporter RAGE is upregulated."
SET Confidence = "Medium"
path(MESH:"Alzheimer Disease") neg p(HGNC:LRP1)
path(MESH:"Alzheimer Disease") neg p(HGNC:ABCB1)
path(MESH:"Alzheimer Disease") pos p(HGNC:AGER)
UNSET Confidence

SET Evidence ="Inflammation, a common feature of AD, can affect ligand affinity by making
the pH more acidic, which promotes hyperphosphorylation of tau and induces conforma- tional
changes in Aβ that hinder its clearance."
SET Confidence = "Medium"
path(MESH:Inflammation) -> p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
path(MESH:Inflammation) -| deg(a(CHEBI:"amyloid-beta"))
UNSET Confidence

SET Evidence ="ApoE is a cholesterol transporter that competes with Aβ for efflux by LRP1
from the interstitium into the circula- tion;"
SET Confidence = "Medium"
p(HGNC:LRP1) -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Cardiovascular System"))
p(HGNC:LRP1) -> tloc(p(HGNC:APOE),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Cardiovascular System"))
tloc(p(HGNC:APOE),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Cardiovascular System")) neg tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Cardiovascular System"))
UNSET Confidence

SET Evidence = "competition for shared receptors is the primary mechanism by
which ApoE mediates Aβ clearance"
SET Confidence = "Medium"
p(HGNC:APOE) reg deg(a(CHEBI:"amyloid-beta"))
UNSET Confidence

SET Evidence ="Third, ApoE4 is also associated with lower antioxidant activity
than other ApoE isoforms,154,155 and it mediates BBB breakdown through a
proinflammatory pathway involving cyclophilin A in pericytes."
SET Confidence = "Medium"
p(HBP:"APOE e4") reg bp(GO:"establishment of blood-brain barrier")
p(HBP:"APOE e4") -> act(p(HGNC:"PPIA"))
act(p(HGNC:"PPIA")) -| bp(GO:"establishment of blood-brain barrier")
UNSET Confidence

SET Evidence ="These findings are in line with evidence suggesting that increased oxi-
dative stress157 and loss of vascular integrity contribute to ageing158 and AD,159 as
demonstrated by accelerated breakdown of the BBB and the neurovascular unit."
SET Confidence = "Medium"
bp(MESH:"Oxidative Stress") -> path(MESH:Aging)
bp(MESH:"Oxidative Stress") -> path(MESH:"Alzheimer Disease")
bp(MESH:"Oxidative Stress") -| bp(GO:"establishment of blood-brain barrier")
UNSET Confidence

SET Evidence ="Aβ is cleared along perivascular drainage pathways.83 In both AD 44,160 and
CAA 44 (commonly associated with AD84), perivascular drainage of Aβ is impaired."
SET Confidence = "Medium"
bp(HBP:"perivascular drainage pathways") -| a(CHEBI:"amyloid-beta")
path(MESH:"Alzheimer Disease") neg bp(HBP:"perivascular drainage pathways")
path(MESH:"Cerebral Amyloid Angiopathy") neg bp(HBP:"perivascular drainage pathways")
UNSET Confidence

SET Evidence ="The pres- ence of ApoE4 is associated with reduced perivascular
drainage of Aβ,161 which in turn is linked to deposition of immune complexes."
SET Confidence = "Medium"
p(HBP:"APOE e4") neg bp(HBP:"perivascular drainage pathways")
p(HBP:"APOE e4") pos a(CHEBI:"amyloid-beta")
UNSET Confidence

SET Evidence ="Of note, a high-fat prenatal maternal diet has recently been
reported to result in a failure of Aβ clearance along cerebrovascular basement
membranes. "
SET Confidence = "Medium"
path(HBP:"high-fat prenatal maternal diet") -| deg(a(CHEBI:"amyloid-beta"))
UNSET Confidence

SET Evidence ="Recent mouse studies suggest that the AQP4-dependent glymphatic
pathway is an important clearance system for driving the removal of soluble Aβ
from the interstitium."
SET Confidence = "Medium"
SET Species = "10090"
SET MeSHAnatomy = "Extracellular Space"
p(MGI:Aqp4) -> act(a(HBP:"Glymphatic System"))
act(a(HBP:"Glymphatic System")) -| a(CHEBI:"amyloid-beta")
UNSET Species
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Thus, sleep could indirectly increase BBB clearance of Aβ through
increased glymphatic bulk flow, but it might also directly increase clearance
through the BBB via various mechanisms, such as molecular changes (for example,
upregulated LRP1), as seen with AD-protective physical and cognitive activity in mice.1"
SET Confidence = "Medium"
SET Species = "10090"
path(MESH:Sleep) -> act(a(MESH:"Blood-Brain Barrier"))
path(MESH:Sleep) -| a(CHEBI:"amyloid-beta")
path(MESH:Sleep) -> bp(HBP:"glymphatic bulk flow")
path(MESH:Sleep) -> p(HGNC:LRP1)
UNSET Confidence

SET Evidence ="These findings might partly explain why sleep impairment increases the risk of AD"
SET Confidence = "High"
path(MESH:"Sleep Initiation and Maintenance Disorders") -> path(MESH:"Alzheimer Disease")
UNSET Confidence

SET Evidence =" First, in ageing, and particularly in AD, CSF production by the choroid plexus
is reduced, as shown by decreased water secretion into the ventricles via AQP1 water channels."
SET Confidence = "Medium"
path(MESH:Aging) neg a(MESH:"Cerebrospinal Fluid")
path(MESH:"Alzheimer Disease") neg a(MESH:"Cerebrospinal Fluid")
path(MESH:Aging) neg act(p(HGNC:AQP1))
path(MESH:"Alzheimer Disease") neg act(p(HGNC:AQP1))
act(p(HGNC:AQP1)) -> a(MESH:"Cerebrospinal Fluid")
act(p(HGNC:AQP1)) -> sec(a(CHEBI:water))
UNSET Confidence

SET Evidence ="In AD, the choroid plexus undergoes many structural changes, such as
calcification, fibrosis and Aβ deposition, all of which can obstruct CSF production."
SET Confidence = "Medium"
SET MeSHAnatomy ="Choroid Plexus"
path(MESH:"Alzheimer Disease") -> path(MESH:"Calcification, Physiologic")
path(MESH:"Alzheimer Disease") -> path(MESH:"Fibrosis")
path(MESH:"Alzheimer Disease") -> a(HBP:"amyloid-beta aggregates")
path(MESH:"Calcification, Physiologic") -| a(MESH:"Cerebrospinal Fluid")
path(MESH:"Fibrosis") -| a(MESH:"Cerebrospinal Fluid")
a(HBP:"amyloid-beta aggregates") -| a(MESH:"Cerebrospinal Fluid")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Tau is mainly cleared through intracellular degrada- tion by lysosomes
via the autophagy–lysosome pathway, and by proteasomes via the ubiquitin–proteasome
pathway.202 "
SET Confidence = "Medium"
SET MeSHAnatomy = "Intracellular Space"
act(p(MESH:Lysosomes)) -> deg(p(HGNC:MAPT))
act(p(FPLX:Proteasome)) -> deg(p(HGNC:MAPT))
UNSET Confidence

SET Evidence ="Phosphorylation of tau by protein kinase A increases its resistance to
degradation by calpain"
SET Confidence = "Medium"
p(FPLX:PKA) -> p(HGNC:MAPT,pmod(Ph))
p(HGNC:MAPT,pmod(Ph)) -| deg(p(HGNC:MAPT))
act(p(MESH:Calpain)) -> deg(p(HGNC:MAPT))
UNSET Confidence

SET Evidence ="These findings support the link between TBI and tau aggre- gation, with
resulting neurodegeneration similar to that seen in AD and chronic traumatic
encephalopathy"
SET Confidence = "Medium"
path(MESH:"Brain Injuries, Traumatic") -- p(HBP:"tau aggregates")
SET MeSHDisease = "Brain Injuries, Traumatic"
p(HBP:"tau aggregates") -> path(MESH:"Neurodegenerative Diseases")
UNSET MeSHDisease
UNSET Confidence
