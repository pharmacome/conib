#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Neuropathogenic role of adenylate kinase-1 in Aβ-mediated tau phosphorylation via AMPK and GSK3β."
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Lingling Xu, Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
#DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation ={"PubMed","22419736"}

SET Evidence ="Neuronal expression of AK1 is upregulated in AD patients and is induced by Ab42"
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
path(MESH:"Alzheimer Disease") -> p(HGNC:AK1)
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:AK1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="the AK1 level was higher in the hippocampus of 9-, 12- and 15-month-old Tg2576 mice expressing Swedish mutant of amyloid precursor protein (hAPP) "
# Tg2576 mice
SET Confidence = "Medium"
SET Species = "10090"
p(MGI:App,var("c.27269939G>T"),var("c.27269938A>C")) pos p(MGI:Ak1)
UNSET Species
UNSET Confidence

SET Evidence ="AK1 also increased in the hippocampus of APP-J20, an AD model mouse expressing familial AD-mutant APP (Supple- mentary Material, Fig. S1A and B)."
# APP-J20 mice
SET Confidence = "Medium"
SET MeSHAnatomy = "Hippocampus"
SET Species = "10090"
p(MGI:App,var("?")) -> p(MGI:Ak1)
UNSET MeSHAnatomy
UNSET Confidence
UNSET Species

SET Evidence ="With the immunohistochemical analysis, we found that the expression levels of AK1 markedly elevated in the NeuN-positive hippocampal neurons of AD patients "
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
path(MESH:"Alzheimer Disease") pos p(HGNC:AK1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Interestingly, the phosphorylated form of tau at Ser 396/404 (PHF-1) was detected exclusively in AD patients, although total amounts of tau protein (TG5) were not changed "
SET Confidence = "Medium"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ph,Ser,396))
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ph,Ser,404))
path(MESH:"Alzheimer Disease") cnc p(HGNC:MAPT)
UNSET Confidence

SET Evidence ="A number of studies have found that Ab species, including oligomeric Ab42, are important factors responsible for tau pathogenesis"
SET Confidence = "Medium"
a(HBP:"amyloid-beta 42 oligomers") -- path(MESH:Tauopathies)
UNSET Confidence

SET Evidence ="From western blot analysis, we found that AK1 expression increased 􏰍2-fold in the cortical neurons after exposure to Ab42 "
SET Confidence = "Medium"
SET MeSHAnatomy = {"Neurons","Cerebral Cortex"}
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:AK1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Interestingly, tau phosphorylation, which was detected by PHF-1 (Ser 396/404), CP13 (Ser 202) and 12E8 (Ser 262) antibodies, was also increased by Ab42 "
SET Confidence = "High"
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Ser,202))
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Ser,396))
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Ser,404))
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Ser,262))
UNSET Confidence

SET Evidence ="AK1 expression also increased in neuronal cells which were exposed to oxidative stress but not to other toxic insults, such as proteostasis stressors "
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
bp(GO:"response to oxidative stress") -> p(HGNC:AK1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Among AK isoforms, AK2 was not detected in the primary neurons and HT22 cells, whereas AK3 expression was observed in the neurons but not regulated by Ab42"
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
a(CHEBI:"amyloid-beta polypeptide 42") cnc act(p(HGNC:AK3))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="When analyzed by reverse transcription-polymerase chain reaction, the level of AK1 mRNA, not AK1b mRNA, increased by Ab42 in primary neurons"
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
a(CHEBI:"amyloid-beta polypeptide 42") -> r(HGNC:AK1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Soluble oligomers of Ab42 serve as the prominent synaptotoxic form and induce tau hyperphosphorylation"
SET Confidence = "Medium"
a(HBP:"amyloid-beta 42 oligomers") -> p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
UNSET Confidence

SET Evidence ="Compared with control neurons, the effects of Ab42 on tau phosphorylation at CP13, PHF-1 and AT180 epitopes were significantly ameliorated in AK1 knockdown cortical neurons "
# CP13 epitope: pSer202
# PHF-1: pSer396, pSer404
# AT180: pSer231, pThr235
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
p(HGNC:AK1) -> act(a(CHEBI:"amyloid-beta polypeptide 42"))
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Ser,202))
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Ser,396))
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Ser,404))
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Ser,231))
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:MAPT,pmod(Ph,Thr,235))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence =" Ectopic expression of AK1 in mouse cortical neurons increased immunoreactivity against phosphorylated tau at Ser396/404 (PHF-1), Ser202 (CP13) and Ser231/Thr235 (AT180) "
SET Confidence = "Medium"
SET Species = "10090"
p(MGI:Ak1) -> p(MGI:Mapt,pmod(Ph,Ser,202))
p(MGI:Ak1) -> p(MGI:Mapt,pmod(Ph,Ser,396))
p(MGI:Ak1) -> p(MGI:Mapt,pmod(Ph,Ser,404))
p(MGI:Ak1) -> p(MGI:Mapt,pmod(Ph,Ser,231))
p(MGI:Ak1) -> p(MGI:Mapt,pmod(Ph,Thr,235))
UNSET Species
UNSET Confidence

SET Evidence ="The data suggest that the increased expression of AK1 can enhance tau aggregation as well as tau phosphorylation."
SET Confidence = "High"
p(HGNC:AK1) -> p(HBP:"Tau aggregates")
p(HGNC:AK1) -> p(HGNC:MAPT,pmod(Ph))
UNSET Confidence

SET Evidence ="Interestingly, treatment with Ab42 decreased the phosphorylation of AMPK at Thr172, whereas the total amount of AMPK was not altered in primary cortical neurons "
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
a(CHEBI:"amyloid-beta polypeptide 42") -| p(FPLX:AMPK,pmod(Ph,Thr,172))
a(CHEBI:"amyloid-beta polypeptide 42") cnc p(FPLX:AMPK)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Consistently, the phosphorylation of acetyl Co-A carboxylase, a well-defined downstream substrate of AMPK, was also reduced by Ab42 "
SET Confidence = "Medium"
a(CHEBI:"amyloid-beta polypeptide 42") -| p(MESH:"Acetyl-CoA Carboxylase",pmod(Ph))
p(MESH:"Acetyl-CoA Carboxylase") -- act(p(FPLX:AMPK))
UNSET Confidence

SET Evidence ="We found, in addition, that enzyme activity of AMPK was sup- pressed in the cortical neurons after exposure to Ab42"
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
a(CHEBI:"amyloid-beta polypeptide 42") -| act(p(FPLX:AMPK))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="These results indicate that AMPK activity is impaired by Ab42 in the neuronal cells in which tau phosphorylation increased. "
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
a(CHEBI:"amyloid-beta polypeptide 42") -| act(p(FPLX:AMPK))
p(HGNC:MAPT,pmod(Ph)) neg act(p(FPLX:AMPK))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Moreover, the phosphorylation of AMPK at Thr172 tended to be reduced in AD patients, although statis- tical significance was marginal "
SET Confidence = "Medium"
path(MESH:"Alzheimer Disease") neg p(FPLX:AMPK,pmod(Ph,Thr,172))
UNSET Confidence

SET Evidence ="AK1 overexpression
markedly decreased AMPK phosphorylation at Thr172 in the primary neurons, whereas AK1 R132A mutant failed to do so"
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
p(HGNC:AK1) neg p(FPLX:AMPK,pmod(Ph,Thr,172))
p(HGNC:AK1,var("p.Arg132Ala")) cnc p(FPLX:AMPK,pmod(Ph,Thr,172))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence =" In addition, we found that transient expression of AK1 (but not AK1 R132A mutant) markedly reduced the inhibitory
phosphorylation of GSK3b at Ser9 (Fig. 4D and Supplemen- tary Material, S6A), indicating that AK1 may regulate GSK3b activity. "
SET Confidence = "Medium"
p(HGNC:AK1) -| p(HGNC:GSK3B,pmod(Ph,Ser,9))
p(HGNC:GSK3B,pmod(Ph,Ser,9)) -| act(p(HGNC:GSK3B))
p(HGNC:AK1,var("p.Arg132Ala")) cnc p(HGNC:GSK3B,pmod(Ph,Ser,9))
p(HGNC:AK1) reg act(p(HGNC:GSK3B))
UNSET Confidence

SET Evidence ="As shown in Figure 5A and B, treatment of control cells with Ab42 reduced the levels of AMPK phosphorylation at Thr172 and the inhibitory phosphorylation of GSK3b at Ser9."
SET Confidence = "Medium"
a(CHEBI:"amyloid-beta polypeptide 42") -| p(FPLX:AMPK,pmod(Ph,Thr,172))
a(CHEBI:"amyloid-beta polypeptide 42") -| p(HGNC:GSK3B,pmod(Ph,Ser,9))
UNSET Confidence

SET Evidence = "These results suggest that AK1 plays a crucial role in the regulation of AMPK and GSK3b in the neuronal cells exposed to Ab42."
SET Confidence = "Low"
SET MeSHAnatomy = "Neurons"
p(HGNC:AK1) reg act(p(FPLX:AMPK))
p(HGNC:AK1) reg act(p(HGNC:GSK3B))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="The treatment of cortical neurons with compound C, a selective AMPK inhibitor (33), reduced the inhibitory phosphorylation of GSK3b at Ser9 and markedly increased tau phosphorylation at Ser396/404 "
SET Confidence = "Low"
SET MeSHAnatomy = "Neurons"
a(CHEBI:dorsomorphin) -| act(p(FPLX:AMPK))
a(CHEBI:dorsomorphin) -| p(HGNC:GSK3B,pmod(Ph,Ser,9))
act(p(FPLX:AMPK)) -> p(HGNC:GSK3B,pmod(Ph,Ser,9))
a(CHEBI:dorsomorphin) -> p(HGNC:MAPT,pmod(Ph,Ser,396))
act(p(FPLX:AMPK)) -| p(HGNC:MAPT,pmod(Ph,Ser,396))
a(CHEBI:dorsomorphin) -> p(HGNC:MAPT,pmod(Ph,Ser,404))
act(p(FPLX:AMPK)) -| p(HGNC:MAPT,pmod(Ph,Ser,404))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Conversely, treatment with AICAR (5-aminoimidazole-4- carboxamide ribonucleoside), an AMPK activator (34),
increased the inhibitory phosphorylation of GSK3b and reduced tau phosphorylation at Ser396/404 in SH-SY5Y cells and cortical neurons"
# AICAR: AICA ribonucleotide
# inhibitory phosphorylation of GSK3b at Ser9
SET Confidence = "Low"
SET MeSHAnatomy = "Neurons"
SET CellLine = "SH-SY5Y"
a(CHEBI:"AICA ribonucleotide") -> act(p(FPLX:AMPK))
a(CHEBI:"AICA ribonucleotide") -> p(HGNC:GSK3B,pmod(Ph,Ser,9))
act(p(FPLX:AMPK)) -> p(HGNC:GSK3B,pmod(Ph,Ser,9))
a(CHEBI:"AICA ribonucleotide") -| p(HGNC:MAPT,pmod(Ph,Ser,396))
act(p(FPLX:AMPK)) -| p(HGNC:MAPT,pmod(Ph,Ser,396))
a(CHEBI:"AICA ribonucleotide") -| p(HGNC:MAPT,pmod(Ph,Ser,404))
act(p(FPLX:AMPK)) -| p(HGNC:MAPT,pmod(Ph,Ser,404))
UNSET MeSHAnatomy
UNSET CellLine
UNSET Confidence

SET Evidence =" These observations indicate that AMPK negatively regulates GSK3b activity and tau phosphorylation"
SET Confidence = "Medium"
p(FPLX:AMPK) neg act(p(HGNC:GSK3B))
p(FPLX:AMPK) neg p(HGNC:MAPT,pmod(Ph))
UNSET Confidence

SET Evidence ="Co-expression of an AK1 transgene with tau in flies (gl-tau2.1/UAS-AK1) markedly enhanced tau-induced retinal degeneration,indicating that AK1 enhances tau toxicity in the fly retina."
SET Confidence = "Medium"
composite(p(HGNC:AK1),p(HGNC:MAPT)) -> path(MESH:"Retinal Degeneration")
p(HGNC:MAPT) -> path(MESH:"Retinal Degeneration")
p(HGNC:AK1) -> act(p(HGNC:MAPT))
UNSET Confidence

SET Evidence ="These results indicate that AK1 exacerbates rough eye phenotype and tau hyperphosphorylation in a tauopathy model organism and that there is a close correlation between the exacerbated rough eye pheno- type and tau phosphorylation."
SET Confidence = "Medium"
p(HGNC:AK1) -> path(MESH:"Retinal Degeneration")
p(HGNC:AK1) -> p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
p(HGNC:MAPT,pmod(Ph)) -- path(MESH:"Retinal Degeneration")
UNSET Confidence
