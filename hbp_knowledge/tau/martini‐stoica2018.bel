#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "TFEB enhances astroglial uptake of extracellular tau species and reduces tau spreading"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Lingling Xu, Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/501ceccdc9a27d97edbdc48a89ebe8e1dd3626e9/export/conso-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
#DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
#DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "30108137"}

SET Evidence ="TFEB activation has been linked to lysosomal stress and the accumulation of aberrant protein aggregates, indicated by TFEB’s nuclear localization in lysosomal storage disorders"
SET Confidence = "Medium"
act(p(HGNC:TFEB,loc(GO:nucleus))) -| act(a(MESH:Lysosomes))
act(p(HGNC:TFEB,loc(GO:nucleus))) -> a(CONSO:"protein aggregates")
UNSET Confidence

SET Evidence ="Transcriptional levels of TFEB and several of its well-known lysosomal targets were stratified by no dementia, mild cognitive impairment, and dementia,these transcript levels positively correlate with cognitive decline"
SET Confidence = "Medium"
r(HGNC:TFEB) pos path(MESH:"Cognitive Dysfunction")
r(HGNC:TFEB) -> path(MESH:Dementia)
r(HGNC:TFEB) -| bp(GO:cognition)
UNSET Confidence

SET Evidence =". In addition, human brains diagnosed with frontotemporal dementia (FTD) demonstrated an increase in transcriptional levels of TFEB and TFEB lysosomal target gene LAMP1"
SET Confidence = "Medium"
SET MeSHAnatomy = "Brain"
path(MESH:"Frontotemporal Dementia") -> r(HGNC:TFEB)
path(MESH:"Frontotemporal Dementia") -> r(HGNC:LAMP1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="On the protein level, TFEB lysosomal target genes LAMP1 and Cathepsin D (CTSD) are also increased in the FTD frontal cortex relative to normal subjects, and TFEB protein levels trended increased with statistical significance in the CBD group"
SET Confidence = "Medium"
SET MeSHAnatomy = "Prefrontal Cortex"
path(MESH:"Frontotemporal Dementia") -> p(HGNC:TFEB)
path(MESH:"Frontotemporal Dementia") -> p(HGNC:LAMP1)
path(MESH:"Frontotemporal Dementia") -> p(HGNC:CTSD)
UNSET MeSHAnatomy
UNSET Confidence

SET Species = "10090"

SET Evidence ="In the rTg4510 mouse model of tauopathy (Ramsden et al., 2005; SantaCruz et al., 2005), gene set enrichment analysis (GSEA) of microarray data revealed a similar enrichment of TFEB’s tran- scriptional profile when comparing 4-mo-old transgenic mice with widespread NFTs to wild-type mice, indicating TFEB’s activation with tau pathology (Fig. 1 F). "
# rTg4510 mouse model of tauopathy
SET Confidence = "Medium"
path(MESH:Tauopathies) -- act(p(MGI:Tfeb))
UNSET Confidence

SET Evidence ="Examining the protein levels in another tauopathy mouse model, hippocampal lysates from PS19 mice (Yoshiyama et al., 2007) at 10 mo of age showed increases in TFEB and lysosomal proteins LAMP1 and CTSD com- pared with wild-type mice (Fig. S1, G and H)."
# PS19 mice
SET Confidence = "Medium"
SET MeSHAnatomy = "Hippocampus"
path(MESH:Tauopathies) -> p(MGI:Tfeb)
path(MESH:Tauopathies) -> p(MGI:Lamp1)
path(MESH:Tauopathies) -> p(MGI:Ctsd)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Transcriptomic analysis of these isolated astrocytes revealed an increase in TFEB tran- scripts, as well as transcripts of several of its well-known target genes in the rTg4510 relative to wild-type controls "
# rTg4510 mice
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
path(MESH:Tauopathies) -> p(MGI:Tfeb)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "as expected, the expression of several TFEB lysosomal target genes was significantly up-regulated in TFEB-transduced astrocytes, including lysosomal marker LAMP1 and lysosomal proteases, cathepsins A and B"
#  wild-type mice (C57/BL6)
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
p(MGI:Tfeb) -> p(MGI:Lamp1)
p(MGI:Tfeb) -> p(MGI:Ctsa)
p(MGI:Tfeb) -> p(MGI:Ctsb)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="In sum, these results demonstrate that TFEB regulates the lysosomal pathway in primary astrocytes."
SET Confidence = "High"
SET Cell ="astrocyte"
p(MGI:Tfeb) reg act(a(MESH:Lysosomes))
UNSET Confidence

SET Evidence ="TFEB not only increased the proportion of cells taking up beads, but also increased the pro- portion of cells taking up multiple beads, suggesting an enhanced phagocytic capacity"
SET Confidence = "High"
p(MGI:Tfeb) -> act(a(MESH:Phagocytes))
UNSET Confidence

SET Evidence ="Our results demonstrate a statistically significant increase of 12% in the uptake of pffs in TFEB transduced astrocytes relative to control at the 1-h time point, with a 22% relative increase in up- take in the TFEB transduced cells at 4 h"
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
p(MGI:Tfeb) -> tloc(a(CONSO:"Tau fibrils"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Inhibiting mTORC1 blocks the phosphorylation of TFEB, allowing unphosphorylated TFEB to translocate to the nucleus and become transcriptionally active"
SET Confidence = "Medium"
act(p(FPLX:mTORC1)) -> p(MGI:Tfeb,pmod(Ph))
p(MGI:Tfeb) -> p(MGI:Tfeb,loc(GO:nucleus))
p(MGI:Tfeb,loc(GO:nucleus)) -> act(p(MGI:Tfeb))
UNSET Confidence

SET Evidence =" Flow cytometry revealed that Torin1 treatment of TFEB transduced astrocytes increased dye-conjugated pff uptake 63% relative to EGFP transduced controls as shown by median fluorescence in- tensity, while under basal conditions, the TFEB overexpressing astrocytes increased uptake just 18% relative to EGFP expressing controls (Fig. 2 K).
Thus, TFEB enhances phagocytic pathways in astrocytes, in particular increasing the uptake of pffs."
# torin 1: inhibitor of mTORC1
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
a(CHEBI:"torin 1") -| act(p(FPLX:mTORC1))
composite(a(CHEBI:"torin 1"),p(MGI:Tfeb)) -> tloc(a(CONSO:"Tau fibrils"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
p(MGI:Tfeb) -> bp(MESH:Phagocytosis)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="qRT-PCR demonstrated undetectable transcript levels of TFEB as well as a reduction in LAMP1 mRNA in TFEB KO astrocytes compared with littermate controls"
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
p(MGI:Tfeb) -> r(MGI:Lamp1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Recapitulating the uptake assay with dye-conjugated pffs in TFEB KO astrocytes, we observed a modest reduction pff uptake compared with primary astrocytes from littermate controls"
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
p(MGI:Tfeb) -> tloc(a(CONSO:"Tau fibrils"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Flow cytometry analysis of primary astrocytes incubated with DQ-BSA revealed an ∼60% increase in median fluorescence for TFEB-transduced astro- cytes, indicating that TFEB enhances uptake and proteolysis of DQ-BSA "
# DQ-BSA: Dye-quenched BSA
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
p(MGI:Tfeb) -> bp(MESH:Proteolysis)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence =" In sum, these data suggest that TFEB stimulates uptake and trafficking of pffs to the lysosome for degradation."
SET Confidence = "Medium"
p(MGI:Tfeb) -> tloc(a(CONSO:"Tau fibrils"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
p(MGI:Tfeb) -> a(CONSO:"Tau fibrils",loc(GO:lysosome))
a(CONSO:"Tau fibrils",loc(GO:lysosome)) -> deg(p(MGI:Tfeb))
UNSET Confidence

SET Evidence =" In addition, flow cytometry analysis demonstrated that heparin reduces dye-conjugated pff uptake in both TFEB and EGFP transduced astrocytes, suggesting that macropinocytosis is responsible for astroglial pff uptake"
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
a(CHEBI:heparin) -| tloc(a(CONSO:"Tau fibrils"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
bp(GO:macropinocytosis) -> act(a(MESH:Astrocytes))
act(a(MESH:Astrocytes)) -> tloc(a(CONSO:"Tau fibrils"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
bp(GO:macropinocytosis) -> tloc(a(CONSO:"Tau fibrils"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence =" Immunoblotting for total tau and phospho-tau antibodies, PHF1 and CP13, demonstrated no statistical difference in hippocampal brain lysate from TFEB- versus EGFP-injected rTg4510 male or female mice"
# rTg4510 male or female mice
SET Confidence = "Medium"
SET MeSHAnatomy = "Hippocampus"
p(MGI:Tfeb) cnc p(MGI:Mapt,pmod(Ph,Ser,396), pmod(Ph,Ser,404))
p(MGI:Tfeb) cnc p(MGI:Mapt,pmod(Ph,Ser,202))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Western blot a reduction in phospho-tau species recognized by AT8 (trending), AT180, and CP13 antibodies in the TFEB-injected mice "
SET Confidence = "Medium"
p(MGI:Tfeb) -| p(MGI:Mapt,pmod(Ph,Ser,202))
p(MGI:Tfeb) -| p(MGI:Mapt,pmod(Ph,Ser,202),pmod(Ph,Ser,208),pmod(Ph,Ser,199))
p(MGI:Tfeb) -| p(MGI:Mapt,pmod(Ph,Thr,231))
UNSET Confidence

SET Evidence ="Following fractionation, immunoblotting for phospho and total tau species in the soluble pool similarly showed a significant reduction in phospho-tau species recog- nized by CP13 and PHF1 antibodies, with no change in total tau"
SET Confidence = "Medium"
p(MGI:Tfeb) -| p(MGI:Mapt,pmod(Ph,Ser,202))
p(MGI:Tfeb) -| p(MGI:Mapt,pmod(Ph,Ser,396), pmod(Ph,Ser,404))
p(MGI:Tfeb) cnc p(MGI:Mapt)
UNSET Confidence

SET Evidence ="In the insoluble fraction, phospho- and total tau species were all reduced in the TFEB mice, as demonstrated by Western blot"
SET Confidence = "High"
p(MGI:Tfeb) -| p(MGI:Mapt, pmod(Ph))
p(MGI:Tfeb) -| p(MGI:Mapt)
UNSET Confidence

SET Evidence ="Overall, these results show that astroglial TFEB overexpression reduces tau pathology and gliosis in the hippocampus of PS19 tauopathy mice."
# PS19 tauopathy mice
SET Confidence = "High"
SET MeSHAnatomy ="Hippocampus"
p(MGI:Tfeb) -| path(MESH:Tauopathies)
p(MGI:Tfeb) -| path(MESH:Gliosis)
UNSET Confidence

SET Evidence ="Quantifying the area fluorescence of MC1 staining for both hippocampi from sections representing the entire volume of hippocampus showed that astroglial TFEB had no impact on MC1 staining on the ipsi- lateral side, but significantly reduced the area of MC1 staining on the contralateral side "
# MC1 antibody recognizes neurofibrillary tangles
SET Confidence = "Medium"
SET MeSHAnatomy = "Hippocampus"
p(MGI:Tfeb) -| a(GO:"neurofibrillary tangle")
UNSET MeSHAnatomy
UNSET Confidence

UNSET Species
