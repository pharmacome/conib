#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Alzheimer's disease pathological lesions activate the spleen tyrosine kinase."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/2451069ea9d4c220086529c4229a4e133e89d52a/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET STATEMENT_GROUP = "Esther"

# Add statements below this comment

SET Citation = {"PubMed","Acta Neuropathol Commun. 2017 Sep 6;5(1):69. doi: 10.1186/s40478-017-0472-2.","28877763"}

SET Evidence = "To investigate whether pathological Syk activation oc-
curs in the brain of AD mouse models, we analyzed the
brains of 116-week-old wild-type, Tg APPsw and Tg
PS1/APPsw mice using high-resolution confocal micros-
copy and immunofluorescence. All transgenic mice
(Fig. 1b-e) exhibit an increased Iba-1 and GFAP reactiv-
ity compared to wild-type littermates (Fig. 1a). Moreo-
ver,wild-type some of the activated amoeboid microglia
that are observed in transgenic mice are also strongly
positive for pSyk (Fig. 1b-d)."
SET MeSHAnatomy = "Brain"
SET MeSHDisease = "Alzheimer Disease"
path(MESH:"Alzheimer Disease") -> bp(GO:"microglial cell activation")
bp(GO:"microglial cell activation") pos act(p(HGNC:SYK))

SET Evidence = "In addition, we observed that pSyk immunoreactivity is
upregulated near A β plaques but neither colocalizes with
microglia nor astrocytes suggesting that it could be of
neuronal origin. (Fig. 1e). "
act(p(HGNC:SYK)) pos a(HBP:"amyloid-beta aggregates")
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "In conclusion, activated Syk is not only found in
microglia but also in neurons near A β deposits, par-
ticularly in dystrophic neurites of Tg APPsw and Tg
PS1/APPsw mice supporting a possible role of Syk
activation in the formation of dystrophic neurites."
SET MeSHAnatomy = "Neurons"
SET CellStructure = "Neurites"
act(p(HGNC:SYK)) pos a(HBP:"amyloid-beta aggregates")
UNSET MeSHAnatomy
UNSET CellStructure

SET Evidence = "Dystrophic neurites are characterized by an accumula-
tion of BACE-1 and sAPP β [31] and our previous
work [28] has shown that Syk regulates BACE-1
expression and sAPP β levels suggesting that Syk
upregulation in dystrophic neurites could contribute
to the accumulation of BACE-1 and sAPP β ."
SET MeSHAnatomy = "Neurons"
SET CellStructure = "Neurites"
p(HGNC:SYK) reg p(HGNC:BACE1)
p(HGNC:SYK) reg p(HBP:"sAPP-beta")
UNSET MeSHAnatomy
UNSET CellStructure

SET Evidence = "In contrast, microscopic fields
of older Tg APPsw mice containing A β deposits exhibit a
strong increase in pSyk burden (799.95 ± 130.19%) com-
pared to age-matched WT mice. "
SET MeSHDisease = "Alzheimer Disease"
bp(GO:aging) -> p(HGNC:SYK)
UNSET MeSHDisease

SET Evidence = "Hippocampal neurons of Tg Tau P301S mice
exhibit a high level of tau hyperphosphorylation (Fig. 4b)
as well as an accumulation of pathogenic tau conformers
(MC1, not shown) compared to WT littermates (Fig. 4a)."
SET Cell = "hippocampal neuron"
p(HGNC:MAPT, sub(P,301,S)) -> p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
p(HGNC:MAPT, sub(P,301,S)) -> a(HBP:"Tau aggregates")
UNSET Cell

SET Evidence = "Most importantly, pathological tau species clearly colo-
calize with pSyk (Y525/526) in hippocampal neurons
(Fig. 4b)."
SET Cell = "hippocampal neuron"
a(HBP:"Tau aggregates") -- p(HGNC:SYK)
UNSET Cell

SET Evidence = "Cortical neurons of Tg Tau P301S mice also exhibit an
increased level of tau hyperphosphorylation (Fig. 5b)
compared to wild-type littermates (Fig. 5a)."
SET Cell = "cerebral cortex neuron"
p(HGNC:MAPT, sub(P,301,S)) -> p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
UNSET Cell

SET Evidence = " We observed
a colocalization between pSyk and pTau (S202) immuno-
reactivities in cortical neurons."
SET Cell = "cerebral cortex neuron"
a(HBP:"Tau aggregates") -- p(HGNC:SYK)
UNSET Cell

SET Evidence = "Abnormal Syk activation seems to fol-
low tau hyperphosphorylation (S202) in the hippocampus
of Tg P301S mice (Fig. 6), as well as the formation of
MC1-tau pathological conformers (data not shown here
but MC1 and pSyk colocalization were quantified below)."
SET MeSHAnatomy = "Hippocampus"
p(HGNC:MAPT, pmod(HBP:hyperphosphorylation)) -- act(p(HGNC:SYK))
p(HGNC:MAPT, pmod(HBP:hyperphosphorylation)) -- a(HBP:"Tau aggregates")
UNSET MeSHAnatomy

SET Evidence = "Cortical neurons of Tg Tau P301S mice also show an in-
crease in tau hyperphosphorylation and pSyk with age
(Fig. 7). "
SET Cell = "cerebral cortex neuron"
bp(GO:aging) -> a(HBP:"Tau aggregates")
bp(GO:aging) -> p(HGNC:SYK)
UNSET Cell

SET Evidence = " In general, we found that neurons
that exhibit a high level of pSyk immunoreactivity also
demonstrate a higher level of tau pathogenic species
whereas neurons that are weakly immunopositive for
pSyk show less tau pathology (Figs. 9, 10, 11, 12 and 13)."
SET MeSHAnatomy = "Neurons"
a(HBP:"Tau aggregates") pos p(HGNC:SYK)
UNSET MeSHAnatomy

SET Evidence = " We found
that tau phosphorylation at Y18 is significantly increased
in neurons that are also immunopositive for pSyk (Fig. 10d,
p < 0.05) which is consistent with previous data showing
that in vitro Syk can phosphorylates tau at Y18."
SET MeSHAnatomy = "Neurons"
p(HGNC:SYK) pos p(HGNC:MAPT, pmod(Ph,Tyr,18))
UNSET MeSHAnatomy
kin(p(HGNC:SYK)) => p(HGNC:MAPT, pmod(Ph,Tyr,18))

SET Evidence = " We have
previously shown that Syk positively regulates GSK-3 β
activity in vitro."
p(HGNC:SYK) -> act(p(HGNC:GSK3B))

SET Evidence = "Altogether, these data suggest that only certain
pathogenic forms of tau (MC1, Y18) promote Syk activation, whereas
Syk activation appears to directly in-
duce tau phosphorylation at Y18 and to indirectly regulate
tau phosphorylation at multiple epitopes (S396/404, S202)
as well as tau misfolding (MC1, TOC1)."
p(HGNC:MAPT, pmod(Ph,Tyr,18)) -> act(p(HGNC:SYK))
act(p(HGNC:SYK)) => p(HGNC:MAPT, pmod(Ph,Tyr,18))
act(p(HGNC:SYK)) -> p(HGNC:MAPT, pmod(Ph,Ser,396), pmod(Ph,Ser,404))
act(p(HGNC:SYK)) -> p(HGNC:MAPT, pmod(Ph,Ser,202))
act(p(HGNC:SYK)) -> p(HGNC:MAPT, pmod(HBP:misfolded))

SET Evidence = " Interestingly, Syk up-
regulation in SH-SY5Y cells leads to a significant in-
crease (1.7-fold) in phosphorylated tau at Y18 (Fig. 14c,
p < 0.01) and at S396/404 (Fig. 14d, 3-fold, p < 0.0001)
compared to control cells."
p(HGNC:SYK) -> p(HGNC:MAPT, pmod(Ph,Tyr,18))
p(HGNC:SYK) -> p(HGNC:MAPT, pmod(Ph,Ser,396), pmod(Ph,Ser,404))

SET Evidence = " Total tau levels are also sig-
nificantly increased following Syk overexpression
(Fig. 14e, 4.2-fold, p < 0.0001). "
p(HGNC:SYK) -> p(HGNC:MAPT)

SET Evidence = "We analyzed the possible
impact of Syk overexpression on Tau mRNA levels by
quantitative RT-PCR and found that Syk overexpression
does not affect Tau transcription (data not shown) sug-
gesting that Syk may regulate tau degradation or tau
protein translation."
p(HGNC:SYK) cnc r(HGNC:MAPT)

SET Evidence = " We
found an increase in Syk activation in DNs surrounding
A β deposits as well as in neurons displaying an accumu-
lation of phosphorylated Tau at Y18 and elevated levels
of MC1 pathogenic tau conformers in AD brain sections
whereas only weak immunoreactivity for pSyk was ob-
served in brain sections from a non-demented control
(Figs. 15, 16 and 17)."
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
SET CellStructure = "Neurites"
act(p(HGNC:SYK)) pos a(HBP:"amyloid-beta aggregates")
act(p(HGNC:SYK)) pos p(HGNC:MAPT, pmod(Ph,Tyr,18))
act(p(HGNC:SYK)) pos p(HGNC:MAPT, pmod(HBP:misfolded))
UNSET MeSHDisease
UNSET MeSHAnatomy
UNSET CellStructure
