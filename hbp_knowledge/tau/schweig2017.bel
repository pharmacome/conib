#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Alzheimer's disease pathological lesions activate the spleen tyrosine kinase."
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
#DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
#DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed","28877763"}

SET Evidence = "To investigate whether pathological Syk activation oc-
curs in the brain of AD mouse models, we analyzed the
brains of 116-week-old wild-type, Tg APPsw and Tg
PS1/APPsw mice using high-resolution confocal micros-
copy and immunofluorescence. All transgenic mice
(Fig. 1b-e) exhibit an increased Iba-1 and GFAP reactiv-
ity compared to wild-type littermates (Fig. 1a). Moreo-
ver,wild-type some of the activated amoeboid microglia
that are observed in transgenic mice are also strongly
positive for pSyk (Fig. 1b-d)."
# Tg APPsw and Tg PS1/APPsw mice
SET Confidence = "Medium"
SET Species = "10090"
SET MeSHAnatomy = "Brain"
SET MeSHDisease = "Alzheimer Disease"
path(MESH:"Alzheimer Disease") -> bp(GO:"microglial cell activation")
bp(GO:"microglial cell activation") pos act(p(MGI:Syk))
bp(GO:"microglial cell activation") pos p(MGI:Syk,pmod(Ph))
UNSET Confidence

SET Evidence = "In addition, we observed that pSyk immunoreactivity is
upregulated near A β plaques but neither colocalizes with
microglia nor astrocytes suggesting that it could be of
neuronal origin. (Fig. 1e). "
SET Confidence = "Medium"
p(MGI:Syk,pmod(Ph)) pos a(HBP:"amyloid-beta aggregates")
UNSET Confidence
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "In conclusion, activated Syk is not only found in
microglia but also in neurons near A β deposits, par-
ticularly in dystrophic neurites of Tg APPsw and Tg
PS1/APPsw mice supporting a possible role of Syk
activation in the formation of dystrophic neurites."
# Tg APPsw and Tg PS1/APPsw mice
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
SET CellStructure = "Neurites"
act(p(MGI:Syk)) pos a(HBP:"amyloid-beta aggregates")
act(p(MGI:Syk)) pos a(MESH:Neurites)
UNSET MeSHAnatomy
UNSET CellStructure
UNSET Species
UNSET Confidence

SET Evidence = "Dystrophic neurites are characterized by an accumula-
tion of BACE-1 and sAPP β [31] and our previous
work [28] has shown that Syk regulates BACE-1
expression and sAPP β levels suggesting that Syk
upregulation in dystrophic neurites could contribute
to the accumulation of BACE-1 and sAPP β ."
SET Confidence = "High"
SET MeSHAnatomy = "Neurons"
SET CellStructure = "Neurites"
p(HGNC:SYK) reg p(HGNC:BACE1)
p(HGNC:SYK) reg p(HBP:"sAPP-beta")
UNSET MeSHAnatomy
UNSET CellStructure
UNSET Confidence

SET Evidence = "In contrast, microscopic fields
of older Tg APPsw mice containing A β deposits exhibit a
strong increase in pSyk burden (799.95 ± 130.19%) com-
pared to age-matched WT mice. "
# Tg APPsw mice
SET Species = "10090"
SET Confidence = "Medium"
SET MeSHDisease = "Alzheimer Disease"
bp(GO:aging) -> p(MGI:Syk,pmod(Ph))
UNSET MeSHDisease
UNSET Species
UNSET Confidence

SET Evidence = "Hippocampal neurons of Tg Tau P301S mice
exhibit a high level of tau hyperphosphorylation (Fig. 4b)
as well as an accumulation of pathogenic tau conformers
(MC1, not shown) compared to WT littermates (Fig. 4a)."
# Tg Tau P301S mice
SET Confidence = "Medium"
SET Species = "10090"
SET Cell = "hippocampal neuron"
p(MGI:Mapt, sub(P,301,S)) -> p(NGI:Mapt, pmod(HBP:hyperphosphorylation))
p(MGI:Mapt, sub(P,301,S)) -> a(HBP:"Tau aggregates")
UNSET Cell
UNSET Species
UNSET Confidence

SET Evidence = "Most importantly, pathological tau species clearly colo-
calize with pSyk (Y525/526) in hippocampal neurons (Fig. 4b)."
SET Confidence = "Medium"
SET Species = "10090"
SET Cell = "hippocampal neuron"
a(HBP:"Tau aggregates") -- p(MGI:Syk,pmod(Ph,Tyr,525))
a(HBP:"Tau aggregates") -- p(MGI:Syk,pmod(Ph,Tyr,526))
UNSET Cell
UNSET Species
UNSET Confidence

SET Evidence = "Cortical neurons of Tg Tau P301S mice also exhibit an
increased level of tau hyperphosphorylation (Fig. 5b)
compared to wild-type littermates (Fig. 5a)."
# Tg Tau P301S mice
SET Confidence = "Medium"
SET Species = "10090"
SET Cell = "cerebral cortex neuron"
p(MGI:Mapt, sub(P,301,S)) -> p(MGI:Mapt, pmod(HBP:hyperphosphorylation))
UNSET Cell
UNSET Species
UNSET Confidence

SET Evidence = " We observed
a colocalization between pSyk and pTau (S202) immuno-
reactivities in cortical neurons."
SET Confidence = "Medium"
SET Cell = "cerebral cortex neuron"
a(HBP:"Tau aggregates") -- p(HGNC:SYK,pmod(Ph))
p(MGI:Mapt,pmod(Ph.Ser,202))) -- p(HGNC:SYK,pmod(Ph))
UNSET Cell
UNSET Confidence

SET Evidence = "Abnormal Syk activation seems to fol-
low tau hyperphosphorylation (S202) in the hippocampus
of Tg P301S mice (Fig. 6), as well as the formation of
MC1-tau pathological conformers (data not shown here
but MC1 and pSyk colocalization were quantified below)."
# Tg P301S mice
SET Confidence = "Medium"
SET Species = "10090"
SET MeSHAnatomy = "Hippocampus"
p(MGI:Mapt, pmod(HBP:hyperphosphorylation)) -> act(p(MGI:Syk))
p(MGI:Mapt, pmod(HBP:hyperphosphorylation)) -> a(HBP:"Tau aggregates")
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "Cortical neurons of Tg Tau P301S mice also show an in-
crease in tau hyperphosphorylation and pSyk with age (Fig. 7). "
#Tg Tau P301S mice
SET Confidence = "Medium"
SET Species = "10090"
SET Cell = "cerebral cortex neuron"
bp(GO:aging) -> a(HBP:"Tau aggregates")
bp(GO:aging) -> p(MGI:Mapt, pmod(HBP:hyperphosphorylation))
bp(GO:aging) -> p(MGI:Syk,pmod(Ph))
UNSET Cell
UNSET Species
UNSET Confidence

SET Evidence = " In general, we found that neurons
that exhibit a high level of pSyk immunoreactivity also
demonstrate a higher level of tau pathogenic species
whereas neurons that are weakly immunopositive for
pSyk show less tau pathology (Figs. 9, 10, 11, 12 and 13)."
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
a(HBP:"Tau aggregates") pos p(HGNC:SYK,pmod(Ph))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = " We found
that tau phosphorylation at Y18 is significantly increased
in neurons that are also immunopositive for pSyk (Fig. 10d,
p < 0.05) which is consistent with previous data showing
that in vitro Syk can phosphorylates tau at Y18."
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
p(HGNC:SYK,pmod(Ph)) pos p(HGNC:MAPT, pmod(Ph,Tyr,18))
UNSET MeSHAnatomy
kin(p(HGNC:SYK)) => p(HGNC:MAPT, pmod(Ph,Tyr,18))
UNSET Confidence

SET Evidence = " We have
previously shown that Syk positively regulates GSK-3 β
activity in vitro."
SET Confidence = "High"
p(HGNC:SYK) -> act(p(HGNC:GSK3B))
UNSET Confidence

SET Evidence = "Altogether, these data suggest that only certain
pathogenic forms of tau (MC1, Y18) promote Syk activation, whereas
Syk activation appears to directly in-
duce tau phosphorylation at Y18 and to indirectly regulate
tau phosphorylation at multiple epitopes (S396/404, S202)
as well as tau misfolding (MC1, TOC1)."
SET Confidence = "High"
p(HGNC:MAPT, pmod(Ph,Tyr,18)) -> act(p(HGNC:SYK))
act(p(HGNC:SYK)) => p(HGNC:MAPT, pmod(Ph,Tyr,18))
act(p(HGNC:SYK)) -> p(HGNC:MAPT, pmod(Ph,Ser,396), pmod(Ph,Ser,404))
act(p(HGNC:SYK)) -> p(HGNC:MAPT, pmod(Ph,Ser,202))
act(p(HGNC:SYK)) -> p(HGNC:MAPT, pmod(HBP:misfolded))
UNSET Confidence

SET Evidence = " Interestingly, Syk up-
regulation in SH-SY5Y cells leads to a significant in-
crease (1.7-fold) in phosphorylated tau at Y18 (Fig. 14c,
p < 0.01) and at S396/404 (Fig. 14d, 3-fold, p < 0.0001)
compared to control cells."
SET Confidence = "Medium"
SET CellLine = "SH-SY5Y"
p(HGNC:SYK) -> p(HGNC:MAPT, pmod(Ph,Tyr,18))
p(HGNC:SYK) -> p(HGNC:MAPT, pmod(Ph,Ser,396), pmod(Ph,Ser,404))
UNSET CellLine
UNSET Confidence

SET Evidence = " Total tau levels are also sig-
nificantly increased following Syk overexpression
(Fig. 14e, 4.2-fold, p < 0.0001). "
SET Confidence = "High"
p(HGNC:SYK) -> p(HGNC:MAPT)
UNSET Confidence

SET Evidence = "We analyzed the possible
impact of Syk overexpression on Tau mRNA levels by
quantitative RT-PCR and found that Syk overexpression
does not affect Tau transcription (data not shown) sug-
gesting that Syk may regulate tau degradation or tau
protein translation."
SET Confidence = "Medium"
p(HGNC:SYK) cnc r(HGNC:MAPT)
p(HGNC:SYK) reg deg(p(HGNC:MAPT))
UNSET Confidence

SET Evidence = " We
found an increase in Syk activation in DNs surrounding
A β deposits as well as in neurons displaying an accumu-
lation of phosphorylated Tau at Y18 and elevated levels
of MC1 pathogenic tau conformers in AD brain sections
whereas only weak immunoreactivity for pSyk was ob-
served in brain sections from a non-demented control
(Figs. 15, 16 and 17)."
SET Confidence = "High"
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
SET CellStructure = "Neurites"
act(p(HGNC:SYK)) pos a(HBP:"amyloid-beta aggregates")
act(p(HGNC:SYK)) pos p(HGNC:MAPT, pmod(Ph,Tyr,18))
act(p(HGNC:SYK)) pos p(HGNC:MAPT, pmod(HBP:misfolded))
UNSET MeSHDisease
UNSET MeSHAnatomy
UNSET CellStructure
UNSET Confidence
