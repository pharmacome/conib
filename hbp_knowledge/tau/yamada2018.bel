#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "The sulfation code for propagation of neurodegeneration"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/fb-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/501ceccdc9a27d97edbdc48a89ebe8e1dd3626e9/export/conso-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mesh-names.belns"
#DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
#DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
#DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
#DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "29980653"}

SET Evidence = "Prion diseases are a group of fatal transmissible neurodegenerative
diseases characterized by the accumulation of an abnormal
form of prion protein (PrPSc) in the central nervous system (CNS)."
SET Confidence = "High"
path(MESH:"Prion Diseases") isA path(MESH:"Neurodegenerative Diseases")
SET MeSHAnatomy = "Central Nervous System"
p(HGNC:PRNP,var("?")) pos path(MESH:"Prion Diseases")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Similar to prion diseases, many neurodegenerative diseases
are also characterized by the accumulation of abnormal proteins
in the CNS including beta-amyloid protein (Ab) and tau in
Alzheimer’s disease (AD), tau in non-AD tauopathies, alpha-synuclein
in Lewy body diseases such as Parkinson’s disease and
dementia with Lewy bodies, and transactive response DNA–
binding protein 43 kDa (TDP-43) in frontotemporal dementia and amyotrophic lateral sclerosis (1–3)"
SET Confidence = "Medium"
SET MeSHAnatomy = "Central Nervous System"
path(MESH:"Alzheimer Disease") isA path(MESH:"Neurodegenerative Diseases")
path(MESH:Tauopathies) isA path(MESH:"Neurodegenerative Diseases")
path(MESH:"Lewy Body Disease") isA path(MESH:"Neurodegenerative Diseases")
path(MESH:"Parkinson Disease") isA path(MESH:"Neurodegenerative Diseases")
path(MESH:"Frontotemporal Dementia") isA path(MESH:"Neurodegenerative Diseases")
path(MESH:"Amyotrophic Lateral Sclerosis") isA path(MESH:"Neurodegenerative Diseases")
a(CHEBI:"amyloid-beta",var("?")) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT,var("?")) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT,var("?")) pos path(MESH:Tauopathies)
p(HGNC:SNCA,var("?")) pos path(MESH:"Parkinson Disease")
path(MESH:"Parkinson Disease") isA path(MESH:"Lewy Body Disease")
p(HGNC:TARDBP,var("?")) pos path(MESH:"Frontotemporal Dementia")
p(HGNC:TARDBP,var("?")) pos path(MESH:"Amyotrophic Lateral Sclerosis")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In human studies, neurodegeneration
seems to progress along the neuronal networks by propagation
of protein aggregates in the CNS, although evidence for
human-to-human transmission is still limited"
SET Confidence = "High"
a(CONSO:"protein aggregates",loc(MESH:"Central Nervous System")) -> path(CONSO:Neurodegeneration)
UNSET Confidence

SET Evidence = "They previously observed that cellular uptake and consequent
intracellular seeding of tau and alpha-synuclein fibrils require
HSPGs, similar to results from another study"
# HSPGs:heparan sulfate proteoglycans
SET Confidence = "High"
p(HGNCGENEFAMILY:Syndecans) -> tloc(p(HGNC:MAPT),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
tloc(p(HGNC:MAPT),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular)) -> a(CONSO:"Tau fibrils")
p(HGNCGENEFAMILY:Syndecans) -> tloc(p(HGNC:SNCA),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
tloc(p(HGNC:SNCA),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular)) -> a(CONSO:"alpha-synuclein fibrils")
UNSET Confidence

SET Evidence = "Inhibition of the interaction with HSPGs blocked transcellular aggregate
propagation, indicating that the interaction between HSPG and
protein aggregates may be an essential step for propagation of
neurodegeneration induced by protein aggregates"
SET Confidence = "High"
complex(p(HGNCGENEFAMILY:Syndecans),a(CONSO:"protein aggregates")) -> path(CONSO:Neurodegeneration)
a(CONSO:"protein aggregates") -> path(CONSO:Neurodegeneration)
UNSET Confidence

SET Evidence = "Specifically, they observed that the
binding of tau aggregates required a minimal length and precise
GAG-architecture with defined sulfate moieties in the N- and 6-O positions
leading to seeding activity inside cells (as reported using
biosensor assays) (Fig. 1), whereas binding of Abeta and alpha-synuclein
aggregates displayed a more complex behavior"
# binding of the aggregates to heparins (GAG motif)
# GAG: glycosaminoglycan
SET Confidence = "High"
p(HGNCGENEFAMILY:Syndecans) -- a(MESH:Glycosaminoglycans)
complex(a(CONSO:"Tau aggregates"),a(MESH:Glycosaminoglycans)) -> a(CONSO:"Tau fibrils")
complex(a(CONSO:"Tau aggregates"),a(CONSO:"6-O-sulfated heparin")) -> a(CONSO:"Tau fibrils")
complex(a(CONSO:"Tau aggregates"),p(HGNCGENEFAMILY:Syndecans,pmod(Sulf))) -> a(CONSO:"Tau fibrils")
UNSET Confidence

SET Evidence = "Individually knocking out the major
genes of the HSPG pathway in HEK293T cells corroborated the
microarray and pharmacological data that the uptake of tau is
mediated by N- and 6-O-sulfation of HSPGs."
SET Confidence = "High"
SET CellLine = "HEK293T"
a(CONSO:"6-O-sulfated heparin") -> tloc(p(HGNC:MAPT),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
p(HGNCGENEFAMILY:Syndecans,pmod(Sulf)) -> tloc(p(HGNC:MAPT),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
UNSET CellLine
UNSET Confidence

SET Evidence = "It was reported for alpha-synuclein
that internalization of amyloid fibrils depends on heparin
sulfate, whereas that of smaller non-amyloid oligomers does not"
SET Confidence = "High"
p(HGNCGENEFAMILY:Syndecans) -> tloc(a(CONSO:"alpha-synuclein fibrils"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))
UNSET Confidence
