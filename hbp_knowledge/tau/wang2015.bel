#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Tau in physiology and pathology"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/0c9853baf883545b922ff1b138a9f297a70b7951/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/73688d6dc24e309fca59a1340dc9ee971e9f3baa/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/a5b84013a08880975ca84f40999e4404b14a97e2/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############


# Add statements below this comment

SET Citation = {"PubMed", "26631930"}

SET Evidence = "Tauopathies can be classified into three groups on the basis of the tau isoforms found in the aggregates: 4R
tauopathies (including PSP, CBD and AGD), 3R tauopathies (for example, PiD) and 3R+4R tauopathies (for example, AD)"
path(MESH:"Supranuclear Palsy, Progressive") pos p(HBP:"4R tau")
path(HBP:"Corticobasal Degeneration") pos p(HBP:"4R tau")
path(MESH:"Niemann-Pick Diseases") pos p(HBP:"3R tau")
path(MESH:"Alzheimer Disease") pos p(HBP:"4R tau")
path(MESH:"Alzheimer Disease") pos p(HBP:"3R tau")

SET Evidence = "Indeed, knockdown of FUS increases the expression of the 2N and 4R tau isoforms, providing
a possible link between frontotemporal lobar degeneration-FUS (FTLDFUS; a subtype of FTD characterized by FUS inclusions
in neurons and glia) and FTLD-tau (a tauopathy)."
p(HGNC:FUS) -| p(HBP:"4R tau")
p(HGNC:FUS) -| p(HBP:"Tau isoform F (441 aa)")
p(HGNC:FUS) -| p(HBP:"Tau isoform C (410 aa)")
path(MESH:"Frontotemporal Lobar Degeneration") -- p(HGNC:FUS)

SET Evidence = "For instance, miR‑132, which is downregulated in PSP, reduces 4R tau expression in
mouse neuroblastoma cells."
path(MESH:"Supranuclear Palsy, Progressive") neg g(HGNC:MIR132)
g(HGNC:MIR132) -| p(HBP:"4R tau")

SET Evidence = "It is worth noting that another non-coding miRNA, miR‑219, can bind directly
to the 3ʹ untranslated region of tau mRNA and thereby repress tau synthesis at the post-transcriptional level,
although it does not affect splicing of tau."
g(HGNC:MIR219A1) -> complex(g(HGNC:MIR219A1),p(HGNC:MAPT))
complex(g(HGNC:MIR219A1),p(HGNC:MAPT)) -| p(HGNC:MAPT)

SET Evidence = "Third, some evidence suggests that expression of tau mRNA-binding proteins
(such as RAS GTPase-activating protein-binding protein 1 and minor histocompatibility antigen
H13) promotes the formation of ribonucleoprotein granules, resulting in a shift towards the expression of
larger tau isoforms (such as high-molecular-weight tau and E3‑containing tau isoforms)."
p(HGNC:G3BP1) -> complex(GO:"ribonucleoprotein granule")
p(HGNC:HM13) -> complex(GO:"ribonucleoprotein granule")
complex(GO:"ribonucleoprotein granule") -> p(HGNC:MAPT)

SET Evidence = "Finally, inhibition of mitochondrial complex I (for example, using annonacin
or MPP+ (1‑methyl‑4‑phenylpyridinium)) upregulates expression of the splicing factor SRSF2, thus promoting
expression of 4R tau in human neurons."
a(PUBCHEM:354398) -| act(complex(GO:"mitochondrial respiratory chain complex I"))
complex(GO:"mitochondrial respiratory chain complex I") -| p(HGNC:SRSF2)
p(HGNC:SRSF2) -> p(HBP:"4R tau")

SET Evidence = "Intriguingly, SRSF2 levels are increased in the brains of individuals with PSP."
SET MeSHAnatomy = "Brain"
path(MESH:"Supranuclear Palsy, Progressive") pos p(HGNC:SRSF2)
UNSET MeSHAnatomy

SET Evidence = "Nevertheless, the interaction of tau with RNA may induce tau aggregation and
thus contribute to neurodegeneration"
complex(p(HGNC:MAPT),a(MESH:RNA)) -> a(HBP:"Tau aggregates")
complex(p(HGNC:MAPT),a(MESH:RNA)) -> path(HBP:Neurodegeneration)

SET Evidence = "Owing to the additional repeat domain R2, 4R tau shows higher affinity for microtubules than does 3R
tau, and is therefore more efficient at promoting microtubule assembly"
p(HBP:"4R tau") => complex(p(HBP:"4R tau"),a(GO:microtubule))
complex(p(HBP:"4R tau"),a(GO:microtubule)) -> bp(GO:"microtubule polymerization")
p(HBP:"3R tau") => complex(p(HBP:"3R tau"),a(GO:microtubule))
complex(p(HBP:"3R tau"),a(GO:microtubule)) -> bp(GO:"microtubule polymerization")

SET Evidence = "The interaction of tau with FYN may regulate the postsynaptic targeting of FYN, and thereby mediate
Aβ‑induced excitotoxicity"
p(HGNC:MAPT) => complex(p(HGNC:FYN),p(HGNC:MAPT))
a(CHEBI:"amyloid-beta") -> path(HBP:Excitotoxicity)
complex(p(HGNC:FYN),p(HGNC:MAPT)) reg path(HBP:Excitotoxicity)

SET Evidence = "Truncation of tau prevents the formation of this structure and might thereby promote tau aggregation"
p(HGNC:MAPT,frag("?")) -> a(HBP:"Tau aggregates")

SET Evidence = "In AD, the phosphorylation of tau is increased further to approximately eight phosphates per molecule."
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ph))

SET Evidence = "This view is supported by a recent study showing that tau in a normal
mouse brain is phosphorylated at many sites that were previously found to be phosphorylated in tau from
the brains of patients with AD."
SET MeSHAnatomy = "Brain"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ph))
UNSET MeSHAnatomy

SET Evidence = "Other phosphorylation sites in or near the repeat domain are phosphorylated by
microtubule affinity-regulating kinases (MARKs; also known as PAR1 kinases), cyclic AMP-dependent protein
kinase (PKA) and Ca2+- or calmodulin-dependent protein kinase II (CaMKII), among others"
p(FPLX:PKA) => p(HGNC:MAPT,pmod(Ph))
p(HGNC:CAMK2A) => p(HGNC:MAPT,pmod(Ph))
p(HGNC:MARK1) => p(HGNC:MAPT,pmod(Ph))

SET Evidence = "In addition, tau can be phosphorylated by tyrosine kinases such as the SRC family members LCK, SYK and
FYN at Tyr18, and the ABL family members ARG and ABL1 at Tyr394 (REF. 45)."
p(HGNC:LCK) => p(HGNC:MAPT,pmod(Ph,Tyr,18))
p(HGNC:SYK) => p(HGNC:MAPT,pmod(Ph,Tyr,18))
p(HGNC:FYN) => p(HGNC:MAPT,pmod(Ph,Tyr,18))
p(HGNC:ABL1) => p(HGNC:MAPT,pmod(Ph,Tyr,394))
p(HGNC:ABL2) => p(HGNC:MAPT,pmod(Ph,Tyr,394))

SET Evidence = "The phosphorylation of tau at Tyr394 and Tyr18 is present in PHFs in the brains
of individuals with AD."
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ph,Tyr,18))
p(HGNC:MAPT,pmod(Ph,Tyr,18)) -- a(HBP:"paired helical filaments")
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ph,Tyr,394))
p(HGNC:MAPT,pmod(Ph,Tyr,394)) -- a(HBP:"paired helical filaments")

SET Evidence = "In a line of the JNPL3 tauopathy mouse model, which expresses human 0N4R tau bearing the missense P301L
mutation, the overall increase in Tyr phosphorylation of tau correlated with the formation of tau aggregates,
suggesting that overall Tyr phosphorylation might contribute to tau aggregation"
p(HGNC:MAPT,pmod(Ph,Tyr)) -> a(HBP:"Tau aggregates")

SET Evidence = "Protein phosphatase 1 (PP1), PP2A, PP2B, PP2C and PP5 have all been
implicated in the dephosphorylation of tau"
p(HGNC:PPP2CA) =| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP3CB) =| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP1R8) =| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPM1A) =| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP5C) =| p(HGNC:MAPT,pmod(Ph))

SET Evidence = "Among them, PP2A is the main phosphatase:it accounts for ~70% of the human brain tau
phosphatase activity, and its activity is reduced in the AD brain (by ~20% and ~40% in the grey and white
matter, respectively)"
p(HGNC:PPP2CA) =| p(HGNC:MAPT,pmod(Ph))
path(MESH:"Alzheimer Disease") neg act(p(HGNC:PPP2CA))

SET Evidence = "For example,the phosphorylation of KXGS motifs (particularly
Ser262) in the repeat domain of tau by MARK, PKA or CaMKII can reduce the affinity of tau to microtubules"
p(FPLX:PKA) => p(HGNC:MAPT,pmod(Ph,Ser,262))
p(HGNC:MARK1) => p(HGNC:MAPT,pmod(Ph,Ser,262))
p(HGNC:CAMK2A) => p(HGNC:MAPT,pmod(Ph,Ser,262))
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| complex(p(HGNC:MAPT),a(GO:microtubule))

SET Evidence = "In addition, phosphorylation of Ser214 and Thr231 in the flanking region of tau can trigger the detachment
of tau from microtubules, whereas phosphorylation at other Thr-Pro or Ser-Pro motifs in the flanking region
has only a weak influence on tau–microtubule binding"
p(HGNC:MAPT,pmod(Ph,Ser,214),pmod(Ph,Thr,231)) -| complex(p(HGNC:MAPT),a(GO:microtubule))

SET Evidence = "First, hyperphosphorylation of tau might induce tau missorting from
axons to the somatodendritic compartment, which can cause synaptic dysfunction"
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -> tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(GO:"somatodendritic compartment"))
tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(GO:"somatodendritic compartment")) -| act(a(GO:synapse))

SET Evidence = "For instance, the phosphorylation of tau at Ser422 inhibits the
cleavage of tau by caspase 3 at Asp421"
p(HGNC:MAPT,pmod(Ph,Ser,422)) -| p(HGNC:MAPT,frag("?"))
p(HGNC:CASP3) -> p(HGNC:MAPT,frag("?"))

SET Evidence = "Tau phosphorylated at Ser262 or Ser356 cannot be recognized by the C terminus of
HSP70‑interacting protein–heat shock protein 90 (CHIP–HSP90) complex and is thus spared from proteasomal
degradation"
p(HGNC:MAPT,pmod(Ph,Ser,262),pmod(Ph,Ser,356)) -| complex(p(HGNC:MAPT),p(HGNC:STUB1))
p(HGNC:MAPT,pmod(Ph,Ser,262),pmod(Ph,Ser,356)) -| deg(p(HGNC:MAPT,pmod(Ph,Ser,262),pmod(Ph,Ser,356)))
bp(GO:"proteasomal protein catabolic process") -> deg(p(HGNC:MAPT))
bp(GO:"proteasomal protein catabolic process") -| deg(p(HGNC:MAPT,pmod(Ph,Ser,262),pmod(Ph,Ser,356)))

 SET Evidence = "Third, the phosphorylation of tau is often considered to enhance tau aggregation,
as hyperphosphorylation and aggregation are both increased in AD"
p(HGNC:MAPT,pmod(Ph)) -> a(HBP:"Tau aggregates")
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
path(MESH:"Alzheimer Disease") pos a(HBP:"Tau aggregates")

SET Evidence = "For example, hyperphosphorylated tau but not unphosphorylated
tau can interact with the kinesin-associated protein JUN N‑terminal kinase-interacting protein 1 (JIP1)
and thus impair the formation of the kinesin complex,which mediates axonal transport"
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -> complex(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)),p(HGNC:MAPK8IP1))
complex(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)),p(HGNC:MAPK8IP1)) -| complex(GO:"kinesin complex")
complex(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)),p(HGNC:MAPK8IP1)) -| bp(GO:"axonal transport")
complex(GO:"kinesin complex") reg bp(GO:"axonal transport")

SET Evidence = "Tau can be acetylated by the P300 acetyltransferase or by CREB-binding protein at several Lys residues
in the flanking region or the repeat domain, and deacetylated at these sites by sirtuin 1 (SIRT1) and histone
deacetylase 6 (HDAC6), respectively"
p(HGNC:EP300) -> p(HGNC:MAPT,pmod(Ac,Lys))
p(HGNC:CREBBP) -> p(HGNC:MAPT,pmod(Ac,Lys))
p(HGNC:SIRT1) -| p(HGNC:MAPT,pmod(Ac,Lys))
p(HGNC:HDAC6) -| p(HGNC:MAPT,pmod(Ac,Lys))

SET Evidence = "Notably, tau has intrinsic acetyltransferase activity and so can catalyse auto-acetylation at
certain Lys sites, including Lys280"
p(HGNC:MAPT) => p(HGNC:MAPT,pmod(Ac,Lys,280))

SET Evidence = "Depending on the sites, the acetylation of tau could inhibit its degradation (for example,
when at Lys163, Lys280, Lys281 or Lys369) or, by contrast, facilitate its degradation and suppress its
phosphorylation and aggregation (for example, when at Lys259, Lys290, Lys321 or Lys353)"
p(HGNC:MAPT,pmod(Ac,Lys,163)) -| deg(p(HGNC:MAPT,pmod(Ac,Lys,163)))
p(HGNC:MAPT,pmod(Ac,Lys,280)) -| deg(p(HGNC:MAPT,pmod(Ac,Lys,280)))
p(HGNC:MAPT,pmod(Ac,Lys,281)) -| deg(p(HGNC:MAPT,pmod(Ac,Lys,281)))
p(HGNC:MAPT,pmod(Ac,Lys,369)) -| deg(p(HGNC:MAPT,pmod(Ac,Lys,369)))
p(HGNC:MAPT,pmod(Ac,Lys,259)) -> deg(p(HGNC:MAPT,pmod(Ac,Lys,259)))
p(HGNC:MAPT,pmod(Ac,Lys,259)) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:MAPT,pmod(Ac,Lys,259)) -| a(HBP:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac,Lys,290)) -> deg(p(HGNC:MAPT,pmod(Ac,Lys,290)))
p(HGNC:MAPT,pmod(Ac,Lys,290)) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:MAPT,pmod(Ac,Lys,290)) -| a(HBP:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac,Lys,321)) -> deg(p(HGNC:MAPT,pmod(Ac,Lys,321)))
p(HGNC:MAPT,pmod(Ac,Lys,321)) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:MAPT,pmod(Ac,Lys,321)) -| a(HBP:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac,Lys,353)) -> deg(p(HGNC:MAPT,pmod(Ac,Lys,353)))
p(HGNC:MAPT,pmod(Ac,Lys,353)) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:MAPT,pmod(Ac,Lys,353)) -| a(HBP:"Tau aggregates")

SET Evidence = "Acetylation at Lys259, Lys290, Lys321 or Lys353 within the KXGS
motifs occurs in normal tau, and is reduced in brains of individuals with AD and of rTg4510 transgenic mice"
path(MESH:"Alzheimer Disease") -| p(HGNC:MAPT,pmod(Ac,Lys,259))
path(MESH:"Alzheimer Disease") -| p(HGNC:MAPT,pmod(Ac,Lys,290))
path(MESH:"Alzheimer Disease") -| p(HGNC:MAPT,pmod(Ac,Lys,321))
path(MESH:"Alzheimer Disease") -| p(HGNC:MAPT,pmod(Ac,Lys,353))

SET Evidence = "By contrast, acetylation of tau at Lys280 has been detected in AD and other tauopathies, including
AGD, tangle-predominant senile dementia (TPSD), PiD, FTDP‑17 and PSP, and is pathological"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ac,Lys,280))
path(MESH:"Niemann-Pick Diseases") pos p(HGNC:MAPT,pmod(Ac,Lys,280))
path(MESH:"Frontotemporal Dementia") pos p(HGNC:MAPT,pmod(Ac,Lys,280))
path(MESH:"Supranuclear Palsy, Progressive") pos p(HGNC:MAPT,pmod(Ac,Lys,280))

SET Evidence = "Recently, tau acetylation at Lys174 was identified in human AD brains as well."
SET MeSHAnatomy = "Brain"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ac,Lys,174))
UNSET MeSHAnatomy

SET Evidence = "In human AD brains, but not in normal brains, tau is modified by N‑glycosylation,
which is proposed to help to maintain and stabilize PHF structure"
SET MeSHAnatomy = "Brain"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(NGlyco))
p(HGNC:MAPT,pmod(NGlyco)) reg a(HBP:"paired helical filaments")
UNSET MeSHAnatomy

SET Evidence = "Furthermore, N‑glycosylation may facilitate tau hyperphosphorylation, as it suppresses the dephosphorylation
and accelerates the phosphorylation of tau, probably because it changes the conformation of tau"
p(HGNC:MAPT,pmod(NGlyco)) -> p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
p(HGNC:MAPT,pmod(NGlyco)) -> p(HGNC:MAPT,pmod(Ph))

SET Evidence = "In contrast to N‑glycosylation, O‑GlcNAcylation (a type of
O‑glycosylation) of tau may protect it against phosphorylation, as it occupies the Ser or Thr residue of Ser‑Pro or Thr-Pro motifs"
p(HGNC:MAPT,pmod(OGlyco)) -| p(HGNC:MAPT,pmod(Ph))

SET Evidence = "In addition, O‑GlcNAcylation of tau can suppress tau aggregation"
p(HGNC:MAPT,pmod(OGlyco)) -| a(HBP:"Tau aggregates")

SET Evidence = "In AD, the O‑GlcNAcylation of tau is reduced — an effect that
might contribute to the hyperphosphorylation and aggregation of tau"
path(MESH:"Alzheimer Disease") -| p(HGNC:MAPT,pmod(OGlyco))
p(HGNC:MAPT,pmod(OGlyco)) -| a(HBP:"Tau aggregates")
p(HGNC:MAPT,pmod(OGlyco)) -| p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))

SET Evidence = "Non-enzymatic post-translational modifications, including glycation, deamidation and isomerization,
are detected in PHF-tau but not in normal tau. All of these modifications may facilitate tau aggregation"
p(HGNC:MAPT, pmod(HBP:glycation)) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT, pmod(HBP:deamidation)) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT, pmod(GO:"protein peptidyl-prolyl isomerization")) -> a(HBP:"Tau aggregates")

SET Evidence = "In addition, glycation of tau may reduce the binding of tau to microtubules"
p(HGNC:MAPT,pmod(HBP:glycation)) -| complex(p(HGNC:MAPT),a(GO:microtubule))

SET Evidence = "The nitration of tau Tyr197 is found in the normal human brain and may
have important physiological functions, whereas the nitration of Tyr18, Tyr29 and Tyr394 is detected only
in AD or other tauopathies"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(HBP:nitration,Tyr,18))
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(HBP:nitration,Tyr,29))
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(HBP:nitration,Tyr,394))
path(MESH:Tauopathies) pos p(HGNC:MAPT,pmod(HBP:nitration,Tyr,18))
path(MESH:Tauopathies) pos p(HGNC:MAPT,pmod(HBP:nitration,Tyr,29))
path(MESH:Tauopathies) pos p(HGNC:MAPT,pmod(HBP:nitration,Tyr,394))

SET Evidence = "The nitration of these Tyr residues alters the conformation of tau, reduces its binding
to microtubules and, depending on the nitration sites, can promote or inhibit aggregation"
# these Tyr residues: Tyr18, Tyr29 and Tyr394
p(HGNC:MAPT,pmod(HBP:nitration,Tyr,18)) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,pmod(HBP:nitration,Tyr,29)) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,pmod(HBP:nitration,Tyr,394)) -| complex(p(HGNC:MAPT),a(GO:microtubule))

SET Evidence = "Lysine methylation is another endogenous post-translational modification
of tau in the human brain, and in vitro studies have shown that methylation of tau suppresses its aggregation"
p(HGNC:MAPT,pmod(Me,Lys)) -| a(HBP:"Tau aggregates")

SET Evidence = "In human AD brains, PHF-tau is methylated at fewer sites than is tau from normal human brains"
path(MESH:"Alzheimer Disease") neg p(HGNC:MAPT,pmod(Me))

SET Evidence = "Tau is known to be ubiquitylated through Lys48 linkages
by CHIP for proteasomal degradation"
p(HGNC:STUB1) -> p(HGNC:MAPT,pmod(UbK48))
bp(GO:"proteasomal protein catabolic process") -> deg(p(HGNC:MAPT,pmod(Ub)))

SET Evidence = "Notably, one study demonstrated that tau can also be ubiquitylated
through Lys63 linkages by TNF receptor-associated factor 6 (TRAF6) — again, for proteasomal degradation"
p(HGNC:TRAF6) -> p(HGNC:MAPT,pmod(UbK63))
bp(GO:"proteasomal protein catabolic process") -> deg(p(HGNC:MAPT,pmod(UbK63)))

SET Evidence = "The truncation of tau occurs in AD and in other tauopathies"
path(MESH:Tauopathies) -> p(HGNC:MAPT,frag("?"))
path(MESH:"Alzheimer Disease") -> p(HGNC:MAPT,frag("?"))

SET Evidence = "Truncation of tau could generate tau fragments with a higher tendency for aggregation
(see below), probably owing to the disruption of the paperclip structure of tau, as described above."
p(HGNC:MAPT,frag("?")) -> a(HBP:"Tau aggregates")

SET Evidence = "In addition, the truncation of tau may result in tau fragments
that induce neurodegeneration independently of tau aggregation."
p(HGNC:MAPT,frag("?")) -> path(HBP:Neurodegeneration)

SET Evidence = "For instance, a 20–22-kDa N‑terminal tau fragment (amino acids 26–230) was detected in
an AD mouse model expressing a transgenic nerve growth factor (NGF)-specific antibody (AD11) and in
the cerebrospinal fluid (CSF) of individuals with AD is neurotoxic in primary neurons"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,frag("26_230"))

SET Evidence = "This fragment was proposed to be generated by caspases (although not caspase 3)."
# this fragment: N‑terminal tau fragment (amino acids 26–230)
p(FPLX:Caspase) -> p(HGNC:MAPT,frag("26_230"))

SET Evidence = "In the human brain, tau can be cleaved behind Thr123, generating an N‑terminally truncated,
tau124–441 fragment. This fragment exhibits stronger affinity for microtubules than does full-length tau, presumably
because the removal of the negatively charged N‑terminal domain enhances its binding to the negative surface of microtubules"
p(HGNC:MAPT,frag("124_441")) => complex(p(HGNC:MAPT,frag("124_441")),a(GO:microtubule))

SET Evidence = "In human AD brains, the missorting of tau into
dendrites represents one of the early signs of neurodegeneration"
SET MeSHDisease = "Alzheimer Disease"
path(MESH:"Alzheimer Disease") -> tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines"))
tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines")) -- path(HBP:Neurodegeneration)
UNSET MeSHDisease

SET Evidence = "In adult neurons, tau mainly distributes into axons, where it interacts with microtubules
through the repeat-domain and flanking regions."
SET MeSHAnatomy = "Axons"
p(HGNC:MAPT) => complex(p(HGNC:MAPT),a(GO:microtubule))
UNSET MeSHAnatomy

SET Evidence = "Tau thereby stabilizes microtubules, promotes microtubule assembly and, in particular, regulates the
dynamic instability of microtubules that allows reorganization of the cytoskeleton."
complex(p(HGNC:MAPT),a(GO:microtubule)) -> bp(GO:"microtubule bundle formation")
complex(p(HGNC:MAPT),a(GO:microtubule)) -> bp(GO:"actin cytoskeleton reorganization")

SET Evidence = "This suggests that the stabilization of the microtubule-bound conformation of tau may delay tau aggregation"
complex(p(HGNC:MAPT),a(GO:microtubule)) neg a(HBP:"Tau aggregates")

SET Evidence = "It can do this by influencing the function of the motor proteins dynein and kinesin, which transport cargoes
towards the minus ends (towards the cell body) and plus ends of microtubules (towards the axonal terminus), respectively (FIG. 3)."
#it can do this: tau may regulate axonal transport
p(HGNC:MAPT) -- act(a(MESH:Dyneins))
p(HGNC:MAPT) -- act(a(MESH:Kinesin))
p(HGNC:MAPT) reg bp(GO:"axonal transport")

SET Evidence = "First, tau competes with kinesin or dynein motors for binding to microtubules, reducing
the binding frequency, motile fraction and run length of kinesin and dynein (without changing the motor
velocity of kinesin and dynein), and thereby slowing down both anterograde and retrograde transport"
p(HGNC:MAPT) -| complex(a(MESH:Dyneins),a(GO:microtubule))
p(HGNC:MAPT) -| complex(a(MESH:Kinesin),a(GO:microtubule))
p(HGNC:MAPT) -| bp(GO:"anterograde axonal transport")
p(HGNC:MAPT) -| bp(GO:"retrograde axonal transport")

SET Evidence = "Fourth, tau can regulate the release of cargo vesicles from kinesin chains by activating PP1 and glycogen
synthase kinase 3β (GSK3β) via the 18 residues at the N terminus of tau"
p(HGNC:MAPT) -> act(p(HGNC:GSK3B))
p(HGNC:MAPT) -> act(p(HGNC:PPP1R8))

SET Evidence = "Fifth, tau may bind to the p150 subunit of dynactin and thereby facilitate the association
of dynactin with microtubules, which stabilizes the interaction of dynein with microtubules and thus supports transport by dynein"
p(HGNC:MAPT) => complex(p(HGNC:MAPT),p(HGNC:DCTN1))
complex(p(HGNC:MAPT),p(HGNC:DCTN1)) -> complex(a(GO:microtubule),p(HGNC:DCTN1))
complex(a(GO:microtubule),p(HGNC:DCTN1)) -> complex(a(MESH:Dyneins),a(GO:microtubule))

SET Evidence = "In addition, tau seems to be essential for axonal elongation
and maturation, as knockdown of tau in cultured rat neurons inhibits neurite formation, whereas
overexpression of tau promotes the formation of neurites even in non-neuronal cells1"
SET MeSHAnatomy = "Neurons"
p(HGNC:MAPT) -> a(MESH:Neurites)
UNSET MeSHAnatomy

SET Evidence = "To date, the physiological function of dendritic tau has not been well characterized. It may be involved in
the regulation of synaptic plasticity, as pharmacological synaptic activation induces translocation of endogenous
tau from the dendritic shaft to excitatory postsynaptic compartments in cultured mouse neurons and in acute
hippocampal slices"
SET MeSHAnatomy = "Dendritic Spines"
p(HGNC:MAPT) -- bp(GO:"regulation of synaptic plasticity")
act(a(GO:synapse)) -> tloc(p(HGNC:MAPT),fromLoc(GO:"dendritic shaft"),toLoc(GO:"excitatory synapse"))
UNSET MeSHAnatomy

SET Evidence = "Genetic deficiency of tau protects against excitotoxicity caused by Aβ or other excitotoxins in mice
that overexpress human amyloid precursor protein (APP), in mice that express human APP and human
presenilin 1 (PS1), and in mice that express mutant Scn1a (the gene encoding the voltage-gated sodium
channel subunit Nav1.1), as well as in mice lacking the voltage-gated potassium channel Kv1.1 subunit"
p(HGNC:MAPT) ->  path(HBP:Excitotoxicity)
a(CHEBI:"amyloid-beta") -> path(HBP:Excitotoxicity)

SET Evidence = "These studies indicate that the endogenous tau plays a part in regulating neuronal activity"
p(HGNC:MAPT) reg act(a(MESH:Neurons))

SET Evidence = "It is not clear whether the discrepancy between these results is due
to the differences between the knockout mouse lines; nevertheless,
both papers point to some involvement of tau in neurogenesis"
p(HGNC:MAPT) -- bp(GO:neurogenesis)

SET Evidence = "Intraneuronal iron accumulation, neuronal loss in the substantia nigra and a severe decline in locomotor
functions were observed in 12‑month-old tau-knockoutmice"
p(HGNC:MAPT) -| a(CHEBI:"iron(0)")
p(HGNC:MAPT) -> bp(GO:locomotion)
SET MeSHAnatomy = "Substantia Nigra"
p(HGNC:MAPT) -> a(MESH:Neurons)
UNSET MeSHAnatomy

SET Evidence = "This study revealed that tau deficiency can cause iron accumulation inside neurons by
preventing the trafficking of APP to the cell surface, where APP usually interacts with ferroportin (FPN)
to facilitate the export of iron"
SET MeSHAnatomy = "Neurons"
p(HGNC:MAPT) -| a(CHEBI:"iron(0)")
p(HGNC:MAPT) -> tloc(p(HGNC:APP),fromLoc(GO:cytoplasm),toLoc(GO:"cell surface"))
surf(p(HGNC:APP)) -> complex(p(HGNC:APP),p(HGNC:SLC40A1))
complex(p(HGNC:APP),p(HGNC:SLC40A1)) -> sec(a(CHEBI:"iron(0)"))
UNSET MeSHAnatomy

SET Evidence = "Notably, this accumulation of iron was observed in the brain regions with
reduced soluble tau levels, such as the cortex in AD,
the substantia nigra in PD and various brain regions in several other tauopathies"
SET MeSHAnatomy = "Cerebral Cortex"
path(MESH:"Alzheimer Disease") -> a(CHEBI:"iron(0)")
SET MeSHAnatomy = "Substantia Nigra"
path(MESH:"Parkinson Disease") -> a(CHEBI:"iron(0)")
UNSET MeSHAnatomy

SET Evidence = "A selective deficit in LTD but not in long-term potentiation (LTP) was observed in the CA1 region of
the hippocampus in tau-knockout mice in vivo and ex vivo"
SET MeSHAnatomy = "CA1 Region, Hippocampal"
p(HGNC:MAPT) -> bp(GO:"long-term synaptic depression")
UNSET MeSHAnatomy

SET Evidence = "In addition, tau may interact with actin to induce aligned bundles of actin filaments, thus modifying the
organization of the cytoskeleton network"
p(HGNC:MAPT) => complex(p(HGNC:MAPT),a(MESH:Actins))
complex(p(HGNC:MAPT),a(MESH:Actins)) -> a(GO:"actin filament bundle")
complex(p(HGNC:MAPT),a(MESH:Actins)) reg a(GO:"actin cytoskeleton")

SET Evidence = "The phosphorylation of tau appears to alter its association with
actin, as tau phosphorylated at the KXGS motifs tends to colocalize with actin filaments in growth cones during
development and in rod-like inclusions of cofilin and actin"
p(HGNC:MAPT,pmod(Ph)) -| complex(p(HGNC:MAPT),a(MESH:Actins))

SET Evidence = "In familial tauopathies, the mutation of MAPT seems to be the
cause of tau aggregation, but in sporadic tauopathies (such as AD), the trigger
of tau pathology is unclear."
p(HGNC:MAPT,var("?")) -> a(HBP:"Tau aggregates")

SET Evidence = "Tau protein with mutations in or near the microtubule-binding domain
(for instance, G272V, N279K, ΔK280, P301L, V337M or R406W) tend to have a reduced affinity for microtubules
and an increased tendency for aggregation"
p(HGNC:MAPT,var("p.Gly272Val")) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,var("p.Gly272Val")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,var("p.Asn279Lys")) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,var("p.Asn279Lys")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys280del")) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,var("p.Lys280del")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,var("p.Pro301Leu")) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,var("p.Pro301Leu")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,var("p.Val337Met")) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,var("p.Val337Met")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,var("p.Arg406Trp")) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,var("p.Arg406Trp")) -> a(HBP:"Tau aggregates")

SET Evidence = "Interestingly, the PSP-associated tau mutation A152T is localized far away from the repeat domain but still
decreases the binding of tau to microtubules and therefore promotes microtubule assembly less efficiently"
path(MESH:"Supranuclear Palsy, Progressive") pos p(HGNC:MAPT,var("p.Ala152Thr"))
p(HGNC:MAPT,var("p.Ala152Thr")) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,var("p.Ala152Thr")) -| bp(GO:"microtubule polymerization")

SET Evidence = "In addition, this A152T tau enhances the formation of oligomers but not fibres"
p(HGNC:MAPT,var("p.Ala152Thr")) -> a(HBP:"Tau oligomers")

SET Evidence = "Another mutation (R5H or R5L) outside the microtubule-binding domain disrupts
the binding of tau to the p150 subunit of the dynactin complex — an essential cofactor for the microtubule
motor dynein —thereby possibly interfering with general axonal transport"
p(HGNC:MAPT,var("p.Arg5His")) -| complex(p(HGNC:MAPT),p(HGNC:DCTN1))
p(HGNC:MAPT,var("p.Arg5His")) -| bp(GO:"axonal transport")
p(HGNC:MAPT,var("p.Arg5Leu")) -| complex(p(HGNC:MAPT),p(HGNC:DCTN1))
p(HGNC:MAPT,var("p.Arg5Leu")) -| bp(GO:"axonal transport")

SET Evidence = "However, there are also other mutations (such as ΔK280, L266V and G272V) that inhibit the inclusion of
E10 and thus reduce the 4R-to-3R ratio"
p(HGNC:MAPT,var("p.Lys280del")) -| p(HBP:"4R tau")
p(HGNC:MAPT,var("p.Lys280del")) -> p(HBP:"3R tau")
p(HGNC:MAPT,var("p.Leu266Val")) -| p(HBP:"4R tau")
p(HGNC:MAPT,var("p.Leu266Val")) -> p(HBP:"3R tau")
p(HGNC:MAPT,var("p.Gly272Val")) -| p(HBP:"4R tau")
p(HGNC:MAPT,var("p.Gly272Val")) -> p(HBP:"3R tau")

SET Evidence = "However, tau aggregates are also observed in the brains of some aged
individuals with a condition known as primary age-related tauopathy (PART)"
SET MeSHAnatomy = "Brain"
bp(GO:aging) pos a(HBP:"Tau aggregates")
UNSET MeSHAnatomy

SET Evidence = "The two short hexapeptide motifs VQIINK and VQIVYK at
the beginning of R2 and R3, respectively, show propensity for forming β‑sheet structures and are essential for
tau aggregation, even though they comprise only a tiny fraction of the sequence"
p(HBP:"VQIINK motif") -> a(HBP:"Tau aggregates")
p(HBP:"VQIVYK motif") -> a(HBP:"Tau aggregates")

SET Evidence = "The motif VQIVYK has been shown to be sufficient to form fibrils composed of steric
‘zippers’ formed by two tightly interdigitated β‑sheets."
p(HBP:"VQIVYK motif") -> a(HBP:"Tau fibrils")

SET Evidence = "Disruption of these motifs (for example, by Pro mutations) abrogates the tendency for tau
to aggregate; by contrast, strengthening the β‑structure with certain mutations (for instance, ΔK280 or P301L)
accelerates tau aggregation both in vitro and in vivo"
# these motifs: VQIINK and VQIVYK motif
p(HBP:"VQIINK motif") -> a(HBP:"Tau aggregates")
p(HBP:"VQIVYK motif") -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys280del")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,var("p.Pro301Leu")) -> a(HBP:"Tau aggregates")

SET Evidence = "Tau aggregation could be accelerated by cofactors such as polyanions that compensate for the repulsive
positive charges of tau"
a(CHEBI:"polyanionic polymer") -> a(HBP:"Tau aggregates")

SET Evidence = "Notably, two proteins — 14‑3‑3ζ and immunophilin (also known as FKBP52 or FKBP4) — have also been shown to induce
aggregation of recombinant tau in vitro, presumably by stabilizing an aggregation-prone conformation of tau."
p(HGNC:YWHAZ) -> a(HBP:"Tau aggregates")
p(HGNC:FKBP4) -> a(HBP:"Tau aggregates")

SET Evidence = "Notably, as aggregated tau in patients with a tauopathy or in transgenic mice invariably
show hyperphosphorylation, and tau hyperphosphorylation precedes aggregation, phosphorylation
has been assumed to drive tau aggregation"
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -> a(HBP:"Tau aggregates")

SET Evidence = "Conversely, tau aggregation can be induced in vitro efficiently
by polyanionic cofactors, regardless of phosphorylation"
a(CHEBI:"polyanionic polymer") -> a(HBP:"Tau aggregates")

SET Evidence = "Thus, it is possible that unknown cofactors trigger tau aggregation in the AD
brain, whereas phosphorylation may accelerate aggregation indirectly: for example, by detaching tau from microtubules"
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
p(HGNC:MAPT,pmod(Ph)) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,pmod(Ph)) -| complex(p(HGNC:MAPT),a(GO:microtubule))
complex(p(HGNC:MAPT),a(GO:microtubule)) -| a(HBP:"Tau aggregates")
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "Truncated tau fragments that contain the repeat domain have a higher tendency for aggregation, probably
owing to the disruption of the usual paperclip structure"
p(HGNC:MAPT,frag("?")) -> a(HBP:"Tau aggregates")

SET Evidence = "One of these fragments was identified as tau151–391, which is prone to
aggregation, as rats transgenic for this fragment develop neurofibrillary pathology;
the identity of the other fragment remains unclear"
p(HGNC:MAPT,frag("151_391")) -> a(HBP:"Tau aggregates")

SET Evidence = "In human AD brains and in the Tg4510 tauopathy mouse model, full-length tau is
cleaved by caspase 3 behind Asp421 to generate tau1–421, which is prone to aggregation and subsequent formation of NFTs"
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
p(HGNC:CASP3) -> p(HGNC:MAPT,frag("1_421"))
p(HGNC:MAPT,frag("1_421")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,frag("1_421")) -> a(GO:"neurofibrillary tangle")
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "The truncation of tau at Asn368 has been observed in human AD brains and in a P301S
mouse model of tauopathy, in which it leads to the generation of a tau1–368 fragment that is prone to aggregation
and shows compromised microtubule-assembly activity, possibly contributing to tau aggregation and neurodegeneration."
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
p(HGNC:MAPT,frag("1_368")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,frag("1_368")) -> path(HBP:Neurodegeneration)
p(HGNC:MAPT,frag("1_368")) -| bp(GO:"microtubule polymerization")
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "In a cellular model of tauopathy, cells expressing the ΔK280 repeat-domain-mutant tau show
tau aggregation that depends on the stepwise proteolysis of the N‑terminal domain by a thrombin-like protease
and of the C‑terminal domain by cathepsin L, generating a fragment (F3) that leads to robust aggregation in
cell and animal models"
# F3 fragment contains residues 258–360
SET MeSHDisease = "Tauopathies"
p(HGNC:F2) -> p(HGNC:MAPT,frag("258_360"))
p(HGNC:CTSL) -> p(HGNC:MAPT,frag("258_360"))
p(HGNC:MAPT,frag("258_360")) -> a(HBP:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys280del")) -> a(HBP:"Tau aggregates")
UNSET MeSHDisease

SET Evidence = "Although tau function can be partly compensated by other, redundant
microtubule-associated proteins (for example, MAP1A), the behavioural impairments observed in
aged (~12‑month-old) tau-knockout mice indicate that tau is necessary for normal neuronal and brain function."
p(HGNC:MAPT) -> act(a(MESH:Neurons))
p(HGNC:MAPT) -> act(a(MESH:Brain))

SET Evidence = "The reduction of tau levels in the brain causes dementia lacking distinctive histopathology
(DLDH), the most common pathological variant of sporadic FTD"
SET MeSHAnatomy = "Brain"
path(MESH:Dementia) neg p(HGNC:MAPT)
UNSET MeSHAnatomy

SET Evidence = "Hyperphosphorylation of tau at the repeat domain reduces
its microtubule binding, which may cause microtubule disassembly, leading to axonal transport deficits."
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -| complex(p(HGNC:MAPT),a(GO:microtubule))
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -| bp(GO:"axonal transport")
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -| bp(GO:"microtubule polymerization")

SET Evidence = "Tau aggregation decreases levels of soluble functional tau,
which may result in microtubule disassembly as well"
a(HBP:"Tau aggregates") -| bp(GO:"microtubule polymerization")

SET Evidence = "In addition, as tau is involved in multiple novel functions,
including iron transport, neurogenesis, LTD and neuronal DNA protection (as discussed above),
the loss of function of tau may also lead to neurodegeneration via impairment of these processes."
p(HGNC:MAPT) -> bp(GO:"iron ion transport")
p(HGNC:MAPT) -> bp(GO:neurogenesis)
p(HGNC:MAPT) -> bp(GO:"long-term synaptic depression")
act(p(HGNC:MAPT)) -| path(HBP:Neurodegeneration)
SET MeSHAnatomy = "Neurons"
p(HGNC:MAPT) -> bp(GO:"DNA protection")
UNSET MeSHAnatomy

SET Evidence = "Some tau mutations (such as A152T and R5H) may cause loss of tau function as well (as discussed above)"
p(HGNC:MAPT,var("p.Ala152Thr")) -| act(p(HGNC:MAPT))
p(HGNC:MAPT,var("p.Arg5His")) -| act(p(HGNC:MAPT))

SET Evidence = "In brains of individuals with AD, neuron loss in the superior temporal sulcus region exceeds the number of
NFTs more than sevenfold, implying that the majority of neurons probably die without having developed NFTs"
path(MESH:"Alzheimer Disease") pos bp(GO:"neuron death")

SET Evidence = "Remarkably, in two regulatable transgenic mouse models expressing human
tau with the P301L mutation (rTg4510) or expressing the repeat domain of tau with the ΔK280 mutation, switching
off tau expression improved memory impairment even though NFTs remained, clearly showing that tau
aggregates are not sufficient for neurodegeneration and the cognitive effects that are typically observed in these models"
p(HGNC:MAPT,var("p.Pro301Leu")) -> bp(GO:memory)
p(HGNC:MAPT,var("p.Lys280del")) -> bp(GO:memory)

SET Evidence = "In the longer term, however, these tau aggregates may sequester other cell components,
finally compromising neuronal functions"
a(HBP:"Tau aggregates") -| act(a(MESH:Neurons))

SET Evidence = "In addition, the aggregates may occupy space in the cell and thus directly interfere with
axonal transport, leading to neurodegeneration"
a(HBP:"Tau aggregates") -| bp(GO:"axonal transport")
a(HBP:"Tau aggregates") -> path(HBP:Neurodegeneration)
bp(GO:"axonal transport") -| path(HBP:Neurodegeneration)

SET Evidence = "In an analogy to the evolution of concepts in the ‘amyloid cascade hypothesis’, which proposes that
soluble oligomers — rather than insoluble aggregates of amyloid peptides — are the causative agents of neurodegeneration
in AD, recent studies have suggested tau oligomers to be the toxic species, and indeed levels
of SDS-stable tau oligomers are increased in AD and PSP brains."
SET MeSHAnatomy = "Brain"
a(HBP:"Tau oligomers") -> path(HBP:Neurodegeneration)
path(MESH:"Alzheimer Disease") pos a(HBP:"Tau oligomers")
path(MESH:"Supranuclear Palsy, Progressive") pos a(HBP:"Tau oligomers")
UNSET MeSHAnatomy

SET Evidence = "Some studies showed in cell toxicity assays that tau oligomers made
from pro-aggregant recombinant tau were toxic to cultured cells; other studies in cultured neurons found
that oligomers induced only local neurotoxicity that led to loss of spines"
a(HBP:"Tau oligomers") -| a(MESH:"Dendritic Spines")

SET Evidence = "The pro-aggregant mouse lines developed AD‑like features (including missorting of tau into the somatodendritic
compartment, tau conformational changes, tau hyperphosphorylation, NFTs and cognitive deficits),
whereas the anti-aggregant lines show almost no pathology"
# pro-aggregant: tau ΔK280
p(HGNC:MAPT,var("p.Lys280del")) -> tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(GO:"somatodendritic compartment"))
p(HGNC:MAPT,var("p.Lys280del")) -> p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
p(HGNC:MAPT,var("p.Lys280del")) -> a(GO:"neurofibrillary tangle")
p(HGNC:MAPT,var("p.Lys280del")) -| bp(GO:cognition)

SET Evidence = "For instance, hyperphosphorylated tau can interact with JIP1 and thus impair
the formation of kinesin complex"
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -> complex(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)),p(HGNC:MAPK8IP1))
complex(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)),p(HGNC:MAPK8IP1)) -| complex(GO:"kinesin complex")

SET Evidence = "In a Drosophila melanogaster model of tauopathy, the hyperphosphorylation
of tau led to the abnormal alignment and accumulation of F‑actin filaments, and thereby induced neurodegeneration"
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -> complex(GO:"filamentous actin")
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -> path(HBP:Neurodegeneration)

SET Evidence = "In AD and other tauopathies, the increase in dendritic
tau levels is one of the first and most overt pathological abnormalities"
SET MeSHAnatomy = "Dendritic Spines"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT)
UNSET MeSHAnatomy

SET Evidence = "Hyperphosphorylation, mutations and overexpression of tau can drive the mislocalization
of tau into postsynaptic spines, resulting in synaptic dysfunction"
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -> tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines"))
p(HGNC:MAPT,var("?")) -> tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines"))
p(HGNC:MAPT) pos tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines"))
tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines")) -| act(a(GO:synapse))
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -| act(a(GO:synapse))
p(HGNC:MAPT,var("?")) -| act(a(GO:synapse))
p(HGNC:MAPT) neg act(a(GO:synapse))

SET Evidence = "In cultured neurons, missorted dendritic tau may mediate toxicity that is
induced by Aβ or other stressors by promoting the translocation of tubulin tyrosine ligase-like enzyme 6
(TTLL6) into dendrites, and the severing of microtubules by spastin"
tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines")) reg act(a(CHEBI:"amyloid-beta"))
tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines")) -> tloc(p(HGNC:TTLL6),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines"))
tloc(p(HGNC:TTLL6),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines")) -> act(a(CHEBI:"amyloid-beta"))
tloc(p(HGNC:MAPT),fromLoc(GO:axon),toLoc(MESH:"Dendritic Spines")) neg a(GO:microtubule)
p(HGNC:SPAST) -> bp(GO:"microtubule severing")

SET Evidence = "In addition, dendritic tau could serve as a protein scaffold to deliver the kinase FYN
to postsynaptic sites, where FYN phosphorylates subunit 2 of the NMDA receptor (NR2B; also known as
GluN2B), resulting in the stabilization of the interaction of this receptor interaction with postsynaptic density
protein 95 (PSD95; also known as DLG4), potentiating glutamatergic signalling and thereby enhancing Aβ toxicity."
SET MeSHAnatomy = "Dendritic Spines"
p(HGNC:MAPT) -> complex(p(HGNC:FYN),p(HGNC:DLG4))
p(HGNC:FYN) -> p(HGNC:GRIN2B,pmod(Ph))
p(HGNC:GRIN2B,pmod(Ph)) -> complex(p(HGNC:FYN),p(HGNC:DLG4))
complex(p(HGNC:FYN),p(HGNC:DLG4)) -> act(a(GO:"glutamatergic synapse"))
complex(p(HGNC:FYN),p(HGNC:DLG4)) -> act(a(CHEBI:"amyloid-beta"))
p(HGNC:MAPT) -> act(a(CHEBI:"amyloid-beta"))
UNSET MeSHAnatomy
