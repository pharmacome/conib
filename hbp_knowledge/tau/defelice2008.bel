#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Alzheimer's disease-type neuronal tau hyperphosphorylation induced by A beta oligomers"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Rana Aldisi"
SET DOCUMENT Description = "This document contains the bel code for the Article Alzheimer’s disease-type neuronal tau hyperphosphorylation induced by Abeta oligomers by De Felice et al"

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
#DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
#DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
#DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "17403556"}

SET TextLocation = "Results"

#ADDL = amyloid-beta derived diffusible ligands

SET Evidence = "At longer
incubation times (48 and 96 h) ADDLs caused a progressive
decrease in MTT reduction (Fig. 1B), which can be due to
altered trafficking (Liu et al., 1998) as well as cell death."
#MTT assay => measures cell viability
SET Confidence = "High"
a(HBP:"amyloid-beta derived diffusible ligands") -| bp(MESH:"Cell Survival")
UNSET Confidence

SET Evidence = "While vehicle treated
cells (Fig. 2A and B) exhibited low phosphotau
immunofluorescence, cells treated with 1 microM biotinylated
ADDLs (bADDLs) for 6 h (Fig. 2C and D) showed a significant
increase in P-tau immunofluorescence (Fig. 2D)."
SET Confidence = "High"
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph))
UNSET Confidence

SET Evidence = "We then investigated whether bADDLs also induced tau
phosphorylation in rat hippocampal neurons. To this end, we
initially used frozen dissociated rat hippocampal cell preparations
(Cambrex). Cells were maintained in the presence of
1M bADDLs (Fig. 2I and J) or vehicle (not shown) for
6 h at 37 ◦C. We observed a marked P-tau immunostaining
in a subpopulation of neurons that also had ADDLs bound
(Fig. 2I, arrowheads), while cells that did not bind bADDLs
had no AT8 staining (Fig. 2J)."
SET Confidence = "High"
SET Species = "10116"
SET MeSHAnatomy = "Hippocampus"

a(HBP:"amyloid-beta derived diffusible ligands") -> p(RGD:Mapt, pmod(Ph))

UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "Following exposure
to ADDLs, double-label immunofluorescence microscopy
showed high levels of tau phosphorylated at Thr231, which
discriminates among AD and non-AD subjects and patients
with other forms of dementia (Hampel et al., 2004, 2003), in
neurons with prominent dendritic ADDL binding (detected
with NU1, Fig. 2K–M). ADDL binding to synaptic hot-spots
in hippocampal neurons is evident in images at highermagnification
(60×objective, PanelsLand M)."
SET Confidence = "High"
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph, Thr, 231))
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT, pmod(Ph, Thr, 231))

SET MeSHAnatomy = "Hippocampus"

a(HBP:"amyloid-beta derived diffusible ligands") -> complex(a(HBP:"amyloid-beta derived diffusible ligands"), a(GO:synapse))

UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Hippocampal
neurons exposed to Abeta fibrils rather than oligomers also
showed elevated P-tau immunofluorescence (Fig. 2N–P)
However, although fibrils could be seen attached to neurons,
they did not bind in the synaptic pattern observed for ADDLs
(Fig. 2O and P)."
SET Confidence = "Medium"
SET MeSHAnatomy = {"Hippocampus","Neurons"}

a(HBP:"amyloid-beta fibrils") -> p(HGNC:MAPT, pmod(Ph))

UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Vehicle-treated neurons
(Fig. 3A and E) exhibited very low phosphotau immunofluorescence,
but neurons treated for 4 h with 500nM ADDLs
showed significantly higher levels in immunofluorescence of
P-Ser404 and P-Thr231 tau (Fig. 3B and F, respectively).
Neurons treated for 4 h with 10M Abeta fibrils also showed an
increase in immunofluorescence of P-Ser404 and P-Thr231
tau (Fig. 3C and G, respectively)."
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"

a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph, Thr, 231))
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph, Ser, 404))
a(HBP:"amyloid-beta fibrils") -> p(HGNC:MAPT, pmod(Ph, Thr, 231))
a(HBP:"amyloid-beta fibrils") -> p(HGNC:MAPT, pmod(Ph, Ser, 404))

UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "NU1
completely blocked the increase in P-Ser404 and P-Thr231
phosphotau levels induced by ADDLs (Fig. 3D–H)."
SET Confidence = "Medium"
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph, Thr, 231))
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph, Ser, 404))
a(HBP:"amyloid-beta antibody, NU1") -| p(HGNC:MAPT, pmod(Ph, Ser, 404))
a(HBP:"amyloid-beta antibody, NU1") -| p(HGNC:MAPT, pmod(Ph, Thr, 231))
UNSET Confidence

SET Evidence = "These results confirm that the tau
hyperphosphorylation stimulated by soluble ADDL preparations
is indeed oligomer-induced. Tau hyperphosphorylation
induced by 10M Abeta fibrils (Fig. 3N) was partially blocked
(Fig. 3O), consistent with shared epitopes between oligomers
and fibrils."
SET Confidence = "Medium"
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
a(HBP:"amyloid-beta oligomers") -> p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
UNSET Confidence

SET Evidence = "We note that these observed increases in tau phosphorylation
occurred well before neuronal death (Fig. 1B), consistent
with this pathology representing an early stage in neurodegeneration."
SET Confidence = "High"
p(HGNC:MAPT, pmod(Ph)) pos path(HBP:Neurodegeneration)
UNSET Confidence

SET Evidence = "Verification of the findings from immunofluorescence
microscopy was provided by Western blot analysis of
hippocampal neuronal lysates with P404, P231 and P181 antiphosphotau
antibodies. A 4 h exposure to 500nM ADDLs
resulted in a significant increase in tau phosphorylated at the
three epitopes, to levels similar to those observed after exposure
to 10 M Abeta fibrils (Fig. 4A–D)."
SET Confidence = "High"
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph, Thr, 231))
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph, Ser, 404))
a(HBP:"amyloid-beta derived diffusible ligands") -> p(HGNC:MAPT, pmod(Ph, Thr, 181))
a(HBP:"amyloid-beta fibrils") -> p(HGNC:MAPT, pmod(Ph, Thr, 231))
a(HBP:"amyloid-beta fibrils") -> p(HGNC:MAPT, pmod(Ph, Ser, 404))
a(HBP:"amyloid-beta fibrils") -> p(HGNC:MAPT, pmod(Ph, Thr, 181))
UNSET Confidence

SET Evidence = "Interestingly, we found that
tau hyperphosphorylation at Thr231 was completely blocked
by the Src family tyrosine kinase inhibitor, 4-amino-5-(4-
chlorophenyl)-7(t-butyl)pyrazol(3,4-d)pyramide (PP1), and
by the phosphatidylinositol 3-kinase inhibitor, LY294002
(Fig. 5)."
SET Confidence = "High"
a(MESH:"4-amino-5-(4-methylphenyl)-7-(tert-butyl)pyrazolo(3,4-d)pyrimidine") -| p(HGNC:MAPT, pmod(Ph, Thr, 231))
a(CHEBI:LY294002) -| p(HGNC:MAPT, pmod(Ph, Thr, 231))
UNSET Confidence

SET Evidence = "Inhibition occurred even though ADDLs were still
bound to cell surfaces, indicating that those kinases are
involved in signal transduction coupling between ADDL
binding and tau hyperphosphorylation."
SET Confidence = "High"
a(CHEBI:LY294002) -| bp(GO:"signal transduction")
a(MESH:"4-amino-5-(4-methylphenyl)-7-(tert-butyl)pyrazolo(3,4-d)pyrimidine") -| bp(GO:"signal transduction")
bp(GO:"signal transduction") -> complex(a(HBP:"amyloid-beta derived diffusible ligands"), p(HGNC:MAPT))
complex(a(HBP:"amyloid-beta derived diffusible ligands"), p(HGNC:MAPT)) -> p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
UNSET Confidence

SET Evidence = "Moreover, the presence of large extracellular aggregates
in NU1-treated cultures (Fig. 5N) suggests that the
antibody effectively sequesters ADDLs and prevents their
interactions with neurons (Fig. 5O). No inhibition of ADDL
binding was associated with PP1 and LY294002 (Fig. 5H, I,
K and L, respectively), but both kinase inhibitors effectively
blocked ADDL-induced tau hyperphosphorylation (Fig. 5G
and J)."
SET Confidence = "High"
a(HBP:"amyloid-beta antibody, NU1") -| complex(a(HBP:"amyloid-beta derived diffusible ligands"), a(MESH:Neurons))
a(MESH:"4-amino-5-(4-methylphenyl)-7-(tert-butyl)pyrazolo(3,4-d)pyrimidine") -| p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
a(CHEBI:LY294002) -| p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
UNSET Confidence

SET Evidence = "AD brains could
induce AD-type tau hyperphosphorylation. Consistent with
the results obtained with synthetic ADDLs, we found that
treatment of mature hippocampal neuronal cultures with a
soluble AD brain extract led to a significant increase in
P231 tau phosphorylation (Fig. 6D) compared to cultures
treated with a non-AD brain extract (Fig. 6A)."
SET Confidence = "High"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
path(MESH:"Alzheimer Disease") -> p(HGNC:MAPT, pmod(Ph, Thr, 231))
UNSET Confidence

SET Evidence = "Importantly,
pre-incubation of AD brain extracts with NU1 significantly
blocked the increase in Thr231 phosphotau immunofluorescence
(Fig. 6G), establishing the tau hyperphosphorylation
was induced by Abeta oligomers in the AD brain extracts.
NU1 also prevented the binding of brain-derived ADDLs
to synaptic hot-spots (Fig. 6H and I). In NU1-treated cultures,
the presence of large extracellular aggregates indicates
that the antibody sequesters ADDLs and prevents their interactions
with neurons (Fig. 6I)."
SET Confidence = "High"
SET MeSHDisease = "Alzheimer Disease"

a(HBP:"amyloid-beta antibody, NU1") -| p(HGNC:MAPT, pmod(Ph, Thr, 231))
a(HBP:"amyloid-beta oligomers") -> p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
a(HBP:"amyloid-beta antibody, NU1") -| complex(a(HBP:"amyloid-beta derived diffusible ligands"), a(MESH:Neurons))
a(HBP:"amyloid-beta antibody, NU1") -| complex(a(HBP:"amyloid-beta derived diffusible ligands"), a(GO:synapse))

UNSET MeSHDisease
UNSET Confidence

UNSET TextLocation
