#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright Â© 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Tau Antibody Targeting Pathological Species Blocks Neuronal Uptake and Interneuron Propagation of Tau in Vitro"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Rana Aldisi, Sandra Spalek"
SET DOCUMENT Description = "This file encodes the article Tau Antibody Targeting Pathological Species Blocks Neuronal Uptake and Interneuron Propagation of Tau in Vitro by Nobuhara et. al. 2017"

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
#DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
#DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
#DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

DEFINE ANNOTATION HasQuantitativeInformation AS LIST {"True", "False"}

##############
# Statements #
##############
SET Citation ={"PubMed", "28408124"}

SET TextLocation="Results"

SET MeSHAnatomy="Brain"
SET Species="10090"
SET MeSHDisease="Tauopathies"

SET Evidence="Among the seven antibodies, Tau13 and 6C5 most efficiently
removed tau (>85% reduction) from rTg4510 brain
extracts on immunodepletion (Figure 2A). HT7 showed an
intermediate effect (72% reduction), whereas the other four
antibodies (40E8, 4E4, p396, and Tau46) removed only a
small fraction of tau (5.6%, 16.6%, 8.4%, and 18%
reductions, respectively) (Figure 2A)."
SET Confidence = "Medium"
SET HasQuantitativeInformation = "True"
a(HBP:"Tau antibody, Tau-13") -| p(MGI:Mapt, loc(GO:cell))
a(HBP:"Tau antibody, 6C5") -| p(MGI:Mapt, loc(GO:cell))
a(HBP:"Tau antibody, HT7") -| p(MGI:Mapt, loc(GO:cell))
a(HBP:"Tau antibody, 40E8") -| p(MGI:Mapt, loc(GO:cell))
a(HBP:"Tau antibody, pS396") -| p(MGI:Mapt, loc(GO:cell))
a(HBP:"Tau antibody, 4E4") -| p(MGI:Mapt, loc(GO:cell))
a(HBP:"Tau antibody, Tau46") -| p(MGI:Mapt, loc(GO:cell))
complex(a(HBP:"Tau antibody, Tau-13"),p(MGI:Mapt, pmod(Ph,Ser,262)))
complex(a(HBP:"Tau antibody, pS396"),p(MGI:Mapt, pmod(Ph,Ser,396)))
# DISCUSS_ representation of a domain

#complex(a(HBP:"Tau antibody, 6C5"),p(MGI:Mapt, domain(125_131)))
#complex(a(HBP:"Tau antibody, HT7"),p(MGI:Mapt, domain(12_150)))
#complex(a(HBP:"Tau antibody, 40E8"),p(MGI:Mapt, domain(195_212),pmod(Ph,Ser,202),pmod(Ph,Thr,205)))
#complex(a(HBP:"Tau antibody, 4E4"),p(MGI:Mapt, domain(385_395)))
#complex(a(HBP:"Tau antibody, Tau46"),p(MGI:Mapt, domain(404_441)))
UNSET HasQuantitativeInformation
UNSET Confidence


SET Evidence="Confocal FRET image
analysis showed robust tau aggregation in primary neurons
treated with control IgG-immunodepleted rTg4510 brain
extracts (Figure 2, B and C).The 6C5 antibody most successfully
reduced tau uptake by immunodepletion (>90%
reduction), and Tau13 and HT7 showed intermediate effects
(approximately 60% reductions) (Figure 2, B and C)"
SET Confidence = "Medium"
SET MeSHAnatomy = {"Brain","Neurons"}
tloc(p(MGI:Mapt), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular)) -> a(HBP:"Tau aggregates")

SET HasQuantitativeInformation = "True"
a(HBP:"Tau antibody, 6C5") -| tloc(p(MGI:Mapt), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, HT7") -| tloc(p(MGI:Mapt), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, Tau-13") -| tloc(p(MGI:Mapt), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
UNSET HasQuantitativeInformation
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence="The
40E8, 4E4, and p396 antibodies also reduced neuronal tau
uptake by 40% to 80%, despite their low-immunodepletion
efficiency (Figure 2). This finding suggests that they
interacted with tau species that are prone to cellular uptake,
which account for only a small fraction of all soluble tau
species in the brain extract. Notably, Tau46 had little effect
on tau uptake (Figure 2, B and C)."
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
SET HasQuantitativeInformation = "True"
a(HBP:"Tau antibody, 40E8") -| tloc(p(MGI:Mapt), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, pS396") -| tloc(p(MGI:Mapt), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, 4E4") -| tloc(p(MGI:Mapt), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, Tau46") cnc tloc(p(MGI:Mapt), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
UNSET HasQuantitativeInformation
UNSET MeSHAnatomy
UNSET Confidence

UNSET Species
UNSET MeSHDisease

SET MeSHDisease="Alzheimer Disease"

SET Evidence="Tau13, 6C5, and HT7 efficiently depleted tau
from the AD HMW fraction (97%, 82%, and 72%,
respectively), whereas the other four antibodies (40E8, 4E4,
p396, and Tau46) removed only a small fraction of tau
(33%, 4.7%, 22%, and 21% reductions, respectively)
(Figure 4A)."
SET Confidence = "Medium"
SET HasQuantitativeInformation = "True"
a(HBP:"Tau antibody, Tau-13") -| p(HGNC:MAPT, loc(GO:cell))
a(HBP:"Tau antibody, 6C5") -| p(HGNC:MAPT, loc(GO:cell))
a(HBP:"Tau antibody, HT7") -| p(HGNC:MAPT, loc(GO:cell))
a(HBP:"Tau antibody, 40E8") -| p(HGNC:MAPT, loc(GO:cell))
a(HBP:"Tau antibody, pS396") -| p(HGNC:MAPT, loc(GO:cell))
a(HBP:"Tau antibody, 4E4") -| p(HGNC:MAPT, loc(GO:cell))
a(HBP:"Tau antibody, Tau46") -| p(HGNC:MAPT, loc(GO:cell))
UNSET HasQuantitativeInformation
UNSET Confidence

SET Evidence="In the tau uptake assay, 6C5 most effectively
reduced tau uptake by immunodepletion (75% reduction)
(Figure 4, B and C). Tau13 and HT7 showed intermediate
effects (55% and 47% reductions, respectively) (Figure 4, B
and C). The 40E8, p396, and 4E4 antibodies also reduced
neuronal tau uptake (65%, 53%, and 47% reductions,
respectively), despite their low immunodepletion efficiency
(Figure 4)."
SET Confidence = "High"
SET MeSHAnatomy="Neurons"

SET HasQuantitativeInformation = "True"
a(HBP:"Tau antibody, 6C5") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, HT7") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, Tau-13") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, 40E8") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, pS396") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, 4E4") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
UNSET HasQuantitativeInformation
UNSET Confidence

UNSET MeSHAnatomy

SET Evidence="Tau46 was the only
antibody that did not show a statistically significant reduction
in neuronal tau uptake (Figure 4, B and C). Tau46
bound with high apparent affinity to both recombinant and
paired helical filament tau by ELISA (Table 2), demonstrating
the binding of the antibody to full-length tau."
SET Confidence = "Medium"
SET MeSHAnatomy="Neurons"
a(HBP:"Tau antibody, Tau46") cnc tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
complex(a(HBP:"Tau antibody, Tau46"),p(HGNC:MAPT))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence="for example, the two phosphorylation dependent
tau antibodies (40E8 and p396) were the most
efficient in the human AD case with the highest level of
phosphorylated tau (1266). Both 6C5 and 40E8, shown to be
most effective at reducing uptake from HMW human AD
brainederived tau species (Figure 3, B and C), immunostained
NFTs and neuritic plaques in postmortem human AD
frontal cortex sections (Figure 6); 40E8 was somewhat more
reactive to neuropil threads under the conditions used."
SET Confidence = "Medium"
SET MeSHAnatomy="Brain"
a(HBP:"Tau antibody, 40E8") -| p(HGNC:MAPT, pmod(Ph))
a(HBP:"Tau antibody, pS396") -| p(HGNC:MAPT, pmod(Ph))
a(HBP:"Tau antibody, 40E8") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, 40E8") -| a(GO:"neurofibrillary tangle")
a(HBP:"Tau antibody, 40E8") -| path(MESH:"Plaque, Amyloid")
a(HBP:"Tau antibody, 40E8") -| a(MESH:"Neuropil Threads")
a(HBP:"Tau antibody, 6C5") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
a(HBP:"Tau antibody, 6C5") -| a(GO:"neurofibrillary tangle")
a(HBP:"Tau antibody, 6C5") -| path(MESH:"Plaque, Amyloid")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence="This result is
consistent with the idea that the 6C5 antibody can slow tau
uptake even after neurons have been exposed to the pathological
tau and the uptake process initiated."
SET Confidence = "Medium"
SET MeSHAnatomy = "Neurons"
a(HBP:"Tau antibody, 6C5") -| tloc(p(HGNC:MAPT), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
UNSET MeSHAnatomy
UNSET Confidence

UNSET MeSHDisease
UNSET TextLocation
