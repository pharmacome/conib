SET DOCUMENT Name        = "Tau Biochemistry"
SET DOCUMENT Version     = "1.2.5"
SET DOCUMENT Description = "Tau Biochemistry Section of TauBase"
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Authors     = "Kristian Kolpeja"
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
# DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/501ceccdc9a27d97edbdc48a89ebe8e1dd3626e9/export/conso-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/1646a2ec453bf0a8d75572a4b1672596181a87e9/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
# DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
# DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
# DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"
##############
# Statements #
##############
DEFINE ANNOTATION Method AS LIST {"NMR Spectroscopy", "Western Blot", "Immunohistochemistry", "In-vivo two photon calcium imaging", "RT-PCR", "Terminal Continuation RNA Amplification", "Laser Capture Microdissection", "Microarray"}
DEFINE ANNOTATION TauIsoformGradient AS LIST {"3R-Very High", "3R-High", "3R-Medium", "3R-Low", "4R-Very High", "4R-High", "4R-Medium", "4R-Low"}
DEFINE ANNOTATION Braak_Stage AS LIST {"Stage I", "Stage II", "Stage III", "Stage IV", "Stage V", "Stage VI"}
DEFINE ANNOTATION MouseStrain AS LIST {"rTg4510"}
DEFINE ANNOTATION HBP_Disease AS LIST {"Corticobasal Degeneration", "Lewy Body Disease, Diffuse", "Frontotemporal Lobar Degeneration"}

#########################################################################################################################################################################################
########################################################################### TAU BIOCHEMISTRY ############################################################################################


########################################################################### TAU Structure ##############################################################################################

SET Citation = {"PubMed", "17493042"}
SET Evidence = "There are six major isoforms in the human brain containing 352–441 amino acid residues,
which arise from alternative mRNA splicing of exons 2, 3, and 10 of the Tau gene (46). "
SET Confidence = "High"
SET Species = "9606"
SET Anatomy = "brain"
p(CONSO:"Tau isoform F (441 aa)") partOf a(MESH:Brain)
p(CONSO:"Tau isoform E (412 aa)") partOf a(MESH:Brain)
p(CONSO:"Tau isoform D (383 aa)") partOf a(MESH:Brain)
p(CONSO:"Tau isoform C (410 aa)") partOf a(MESH:Brain)
p(CONSO:"Tau isoform B (381 aa)") partOf a(MESH:Brain)
p(CONSO:"Tau isoform Fetal-tau (352 aa)") partOf a(MESH:Brain)
UNSET Confidence

SET Evidence = "The projection domain represents roughly the N-terminal half of the molecule and the assembly domain the C terminal half (114).
The C-terminal half of tau contains three or four semi-conserved repeats, a feature shared with other MAPs.
Depending on developmentally controlled alternative mRNA splicing, the repeat domain of tau consists of three or four sequences of 31 or 32 residues each: R1 = Q244 − K274,
R2 = V275 − S305, R3 = V306 − Q336,R4 = V337 − N368"

#For the generic repeat entry, check INTERPRO:"Microtubule associated protein,  tubulin-binding repeat" or PFAM:"Tau and MAP protein, tubulin-binding repeat" (PF00418)
SET Confidence = "High"
p(CONSO:"tubulin-binding repeat 1") partOf p(CONSO:"microtubule-binding region")
p(CONSO:"tubulin-binding repeat 2") partOf p(CONSO:"microtubule-binding region")
p(CONSO:"tubulin-binding repeat 3") partOf p(CONSO:"microtubule-binding region")
p(CONSO:"tubulin-binding repeat 4") partOf p(CONSO:"microtubule-binding region")

p(CONSO:"tubulin-binding repeat 1") isA p(INTERPRO:"Microtubule associated protein,  tubulin-binding repeat")
p(CONSO:"tubulin-binding repeat 2") isA p(INTERPRO:"Microtubule associated protein,  tubulin-binding repeat")
p(CONSO:"tubulin-binding repeat 3") isA p(INTERPRO:"Microtubule associated protein,  tubulin-binding repeat")
p(CONSO:"tubulin-binding repeat 4") isA p(INTERPRO:"Microtubule associated protein,  tubulin-binding repeat")

p(CONSO:"microtubule-binding region") partOf p(HGNC:MAPT)
p(CONSO:"projection domain")  partOf p(HGNC:MAPT)
p(CONSO:"projection domain") eq p(INTERPRO:"MAP2/Tau projection")

p(CONSO:"Tau isoform F (441 aa)", frag(244_274)) eq p(CONSO:"tubulin-binding repeat 1")
p(CONSO:"Tau isoform F (441 aa)", frag(275_305)) eq p(CONSO:"tubulin-binding repeat 2")
p(CONSO:"Tau isoform F (441 aa)", frag(306_336)) eq p(CONSO:"tubulin-binding repeat 3")
p(CONSO:"Tau isoform F (441 aa)", frag(337_368)) eq p(CONSO:"tubulin-binding repeat 4")

p(CONSO:"microtubule-binding region") eq p(HGNC:MAPT, frag(199_370))
p(CONSO:"projection domain") eq p(HGNC:MAPT, frag(1_198))
UNSET Confidence

SET Evidence = "Tau binds to microtubule through the C-terminal repeat domain and 4R bind more tightly than 3R, while phosphorylation, especially of the repeat domain
tends to decrease binding."
SET Confidence = "High"
p(CONSO:"microtubule-binding region") -> act(p(HGNC:MAPT), ma(GO:"microtubule binding"))
p(CONSO:"tubulin-binding repeat 4") => act(p(HGNC:MAPT), ma(GO:"microtubule binding"))
p(INTERPRO:"Microtubule associated protein,  tubulin-binding repeat", pmod(Ph)) -| act(p(HGNC:MAPT), ma(GO:"microtubule binding"))
UNSET Confidence

SET Evidence = "Two hexapeptide motifs at the beginning of R2 and R3 promote paired helical filament (PHF) aggregation by inducing β-structure."
SET Confidence = "Medium"
p(CONSO:"VQIINK motif") partOf p(CONSO:"tubulin-binding repeat 2")
p(CONSO:"VQIVYK motif") partOf p(CONSO:"tubulin-binding repeat 3")
p(CONSO:"VQIINK motif") -> p(CONSO:"paired helical filaments")
p(CONSO:"VQIVYK motif") -> p(CONSO:"paired helical filaments")
p(CONSO:"paired helical filaments") -> bp(GO:"neurofibrillary tangle assembly")
p(CONSO:"VQIINK motif") -> bp(GO:"neurofibrillary tangle assembly")
p(CONSO:"VQIVYK motif") -> bp(GO:"neurofibrillary tangle assembly")
UNSET Confidence

SET Evidence = "Tyrosine kinases target Y18 [fyn, (79)] and Y394 [abl, (31)]."
SET Confidence = "High"
p(HGNC:FYN) -> p(HGNC:MAPT, pmod(Ph, Tyr, 18))
p(HGNC:ABL1) -> p(HGNC:MAPT, pmod(Ph, Tyr, 394))
UNSET Confidence

SET Evidence = "The lysine-isoleucine-glycineserine motif (KIGS) or lysine-cysteineglycine-serine motif (KCGS) motifs in
the repeat domain (S262, S293, S324, S356) can be phosphorylated by MARK, PKA, SAD kinases, CaMKII and p70S6K,
which strongly reduces the tau microtubule interactions (36, 74, 96), [note that phosphorylation at these sites also inhibits tau aggregation,
illustrating an analogous role for the repeat domain in the physiological and pathological functions of tau (106)]."
SET Confidence = "Medium"
p(HGNC:MARK1) -> p(HGNC:MAPT, pmod(Ph, Ser))
p(HGNC:PRKACA) -> p(HGNC:MAPT, pmod(Ph, Ser))
p(HGNC:CAMK2A) -> p(HGNC:MAPT, pmod(Ph, Ser))
p(HGNC:RPS6KB1) -> p(HGNC:MAPT, pmod(Ph, Ser))

p(HGNC:MAPT, pmod(Ph, Ser, 262)) -| complex(p(HGNC:MAPT), p(HGNCGENEFAMILY:Tubulins))
p(HGNC:MAPT, pmod(Ph, Ser, 293)) -| complex(p(HGNC:MAPT), p(HGNCGENEFAMILY:Tubulins))
p(HGNC:MAPT, pmod(Ph, Ser, 324)) -| complex(p(HGNC:MAPT), p(HGNCGENEFAMILY:Tubulins))
p(HGNC:MAPT, pmod(Ph, Ser, 356)) -| complex(p(HGNC:MAPT), p(HGNCGENEFAMILY:Tubulins))

p(HGNC:MAPT, pmod(Ph, Ser, 262)) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT, pmod(Ph, Ser, 293)) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT, pmod(Ph, Ser, 324)) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT, pmod(Ph, Ser, 356)) -| p(CONSO:"Tau aggregates")

p(HGNC:MAPT, pmod(Ph, Ser, 262)) -| bp(GO:"neurofibrillary tangle assembly")
p(HGNC:MAPT, pmod(Ph, Ser, 293)) -| bp(GO:"neurofibrillary tangle assembly")
p(HGNC:MAPT, pmod(Ph, Ser, 324)) -| bp(GO:"neurofibrillary tangle assembly")
p(HGNC:MAPT, pmod(Ph, Ser, 356)) -| bp(GO:"neurofibrillary tangle assembly")
UNSET Confidence

SET Evidence = "A further potent detaching site is phosphoS214, which can be phosphorylated by PKA and other kinases of the AGC group (PKA/PKG/PKC group of protein kinases),
and is up-regulated during mitosis (16, 63). Tau contains one or two cysteines in the repeat domain (C291 in R2, present in 4R isoforms, and C322 in R3),
which can be engaged in intra- or intermolecular cross-linking affecting conformation, dimerization and aggregation (108)."
SET Confidence = "High"
p(FPLX:PKA) -> p(HGNC:MAPT, pmod(Ph, S, 214))
p(FPLX:PKC) -> p(HGNC:MAPT, pmod(Ph, S, 214))
p(FPLX:PRKG) -> p(HGNC:MAPT, pmod(Ph, S, 214))
bp(MESH:Mitosis) -> p(HGNC:MAPT, pmod(Ph, S, 214))
UNSET Confidence


SET Citation = {"PubMed", "21757739"}
SET Evidence = "In the projection domain, an N-terminal extension and the first part of a proline-rich domain (PR1) are distinguished.
The assembly domain consists of the second part of the proline-rich domain (PR2), three or four MTBRs, and
a C-terminal extension with an upstream pseudo repeat R. The MTBRs are divided in repeats (R1 to R4) and inter-repeats (ir12, ir23, and ir34)."

###Defining protein fragments corresponding to the main substructural and functional features of tau
SET Confidence = "High"
SET Method = "NMR Spectroscopy"
p(CONSO:"assembly domain") eq p(HGNC:MAPT, frag(199_441))
p(CONSO:"microtubule-binding region") partOf p(CONSO:"assembly domain")
p(CONSO:"microtubule-binding region") eq p(HGNC:MAPT, frag(244_369))
#p(HGNC:MAPT, d(CONSO:"C-terminal domain")) eq p(HGNC:MAPT, frag(370_441))
p(HGNC:MAPT, frag(370_441)) partOf p(CONSO:"assembly domain")
p(CONSO:"proline-rich region 2") eq p(HGNC:MAPT, frag(199_244))
p(CONSO:"proline-rich region 2") partOf p(CONSO:"assembly domain")
p(CONSO:"proline-rich region 2") partOf p(CONSO:"proline-rich domain")
p(CONSO:"proline-rich domain") eq p(INTERPRO:"PSP, proline-rich")
p(CONSO:"proline-rich region 1") eq p(HGNC:MAPT, frag(152_198))
p(CONSO:"proline-rich region 1") partOf p(CONSO:"proline-rich domain")
p(CONSO:"proline-rich region 1") partOf p(CONSO:"projection domain")
#p(HGNC:MAPT, d(CONSO:"N-terminal domain")) eq p(HGNC:MAPT, frag(1_151))
p(HGNC:MAPT, frag(1_151)) partOf p(CONSO:"projection domain")
UNSET Method
UNSET Confidence


########################################################################### TAU Biochemical Studies ##############################################################################################


SET Citation = {"PubMed", "27260836"}
SET Evidence = "Phosphatidylinositol binding clathrin assembly protein, PICALM (aka CALM) assembles adaptor protein-2 (AP-2) to clathrin, thus participating in
clathrin-mediated endocytosis. We have previously reported that the level of full-length PICALM is decreased in AD brains; PICALM was co-localised with phosphorylated tau in NFTs
and in granulovacuolar degenerations (GVDs) in the brains of AD patients and of individuals with Down syndrome but was not observed in amyloid plaques (Ando et al., 2013)."
SET Confidence = "Medium"
SET Method = {"Western Blot", "Immunohistochemistry"}

p(HGNC:PICALM) -> complex(p(CONSO:"AP-2 complex"), p(HGNCGENEFAMILY:"Clathrin subunits"))
complex(p(CONSO:"AP-2 complex"), p(HGNCGENEFAMILY:"Clathrin subunits")) pos bp(GO:"clathrin-dependent endocytosis")

##### Extra information for FPLX #############

p(HGNC:AP2M1) partOf p(CONSO:"AP-2 complex")
p(HGNC:AP2B1) partOf p(CONSO:"AP-2 complex")
p(HGNC:AP2A1) partOf p(CONSO:"AP-2 complex")
p(HGNC:AP2A2) partOf p(CONSO:"AP-2 complex")
p(HGNC:AP2S1) partOf p(CONSO:"AP-2 complex")

##############################################


SET Disease = {"Alzheimer's disease", "Down syndrome"}
SET Anatomy = "brain"
p(HGNC:PICALM) neg path(MESH:"Alzheimer Disease")
complex(p(HGNC:PICALM),p(HGNC:MAPT, pmod(Ph))) partOf complex(GO:"neurofibrillary tangle")
p(HGNC:PICALM) -- p(HGNC:MAPT, pmod(Ph))
p(HGNC:PICALM) partOf path(CONSO:"granulovacuolar degeneration")
UNSET Anatomy
UNSET Disease
UNSET Confidence

SET Evidence = "Double immunostaining for PICALM and anti-phosphotau antibodies (AT8 and PHF1) showed a co-localisation
of PICALM and phosphotau in Pick bodies of Pick disease"
SET Confidence = "Medium"
SET Disease = "Pick's disease"
complex(p(HGNC:PICALM),p(HGNC:MAPT, pmod(Ph))) partOf complex(GO:"Pick body")
complex(GO:"Pick body") pos path(MESH:"Pick Disease of the Brain")
UNSET Disease
UNSET Confidence

SET Evidence = "In PSP cases, both coiled bodies (Fig. 2 D-F) and NFTs (Fig. 2G-I) in the striatum showed a complete
co-localisation of PICALM and phosphotau immunoreactivies."
SET Confidence = "Medium"
SET Disease = "progressive supranuclear palsy"
SET Anatomy = "striatum"
complex(p(HGNC:PICALM),p(HGNC:MAPT, pmod(Ph))) partOf complex(GO:"neurofibrillary tangle")
complex(p(HGNC:PICALM),p(HGNC:MAPT, pmod(Ph))) partOf complex(GO:"Cajal body")
UNSET Anatomy
UNSET Disease
UNSET Confidence

SET Evidence = "A highly significant correlation was found between decreased levels of PICALM and increased levels of LC3-II (p=0.0032) or
decreased levels of Beclin-1 (p=0.0295) in the total brain lysates from these diseases (Fig 6D,E)."
SET Confidence = "High"
SET Disease = {"Alzheimer's disease", "Down syndrome", "Pick's disease", "progressive supranuclear palsy"}
SET HBP_Disease = {"Corticobasal Degeneration","Lewy Body Disease, Diffuse", "Frontotemporal Lobar Degeneration"}
p(HGNC:PICALM) neg p(HGNC:MAP1LC3B)
p(HGNC:PICALM) pos p(HGNC:BECN1)
UNSET HBP_Disease
UNSET Disease
UNSET Confidence

UNSET Method

SET Citation = {"PubMed", "24368848"}
SET Evidence = "We found that in the visual cortex, neurons containing conspicuous quantities of mislocalized
and aggregated tau nonetheless appear to have a normal capacity to integrate dendritic inputs and respond robustly to visual stimuli
and also maintain normal somatic baseline calcium levels. In particular, we show that individual NFT-bearing neurons can respond robustly after integrating sensory inputs
and are functionally indistinguishable from neighboring non–NFT-bearing neurons. These results demonstrate that NFT-bearing neurons remain functionally integrated in cortical circuits."
SET Confidence = "High"
SET Species = "10090"
SET MouseStrain = "rTg4510"

## Mouse strain rTg4510 is a model of frontotemporal dementia, containing the P301L tau mutation

SET MeSHDisease = "Frontotemporal Dementia"
p(CONSO:"Tau isoform F (441 aa)", var("p.Pro301Leu")) -> path(MESH:"Frontotemporal Dementia")
SET Anatomy = "visual cortex"
SET TauIsoform = "Isoform F (441 aa)"
SET Method = "In-vivo two photon calcium imaging"
complex(GO:"neurofibrillary tangle") cnc act(a(MESH:Neurons))
UNSET Method
UNSET TauIsoform
UNSET Anatomy
UNSET MeSHDisease
UNSET MouseStrain
UNSET Species
UNSET Confidence


SET Citation = {"PubMed", "23407988"}
SET Evidence = "Because tracing with anti-tau antibodies does not distinguish between its isoforms, i.e.,
three-repeat (3R) and four-repeat (4R) tau, it remains to be clarified how these isoforms are related to the evolution of each lesion and its spread."
SET Confidence = "High"
SET Species = "9606"
SET Disease = "Alzheimer's disease"
SET Braak_Stage = {"Stage I", "Stage II", "Stage III", "Stage IV", "Stage V", "Stage VI"}
SET Method = {"Immunohistochemistry"}
p(CONSO:"Tau isoform F (441 aa)") isA p(CONSO:"4R tau")
p(CONSO:"Tau isoform E (412 aa)") isA p(CONSO:"4R tau")
p(CONSO:"Tau isoform D (383 aa)") isA p(CONSO:"4R tau")
p(CONSO:"Tau isoform C (410 aa)") isA p(CONSO:"3R tau")
p(CONSO:"Tau isoform B (381 aa)") isA p(CONSO:"3R tau")

p(CONSO:"tubulin-binding repeat 1") partOf p(CONSO:"4R tau")
p(CONSO:"tubulin-binding repeat 2") partOf p(CONSO:"4R tau")
p(CONSO:"tubulin-binding repeat 3") partOf p(CONSO:"4R tau")
p(CONSO:"tubulin-binding repeat 4") partOf p(CONSO:"4R tau")

p(CONSO:"tubulin-binding repeat 1") partOf p(CONSO:"3R tau")
p(CONSO:"tubulin-binding repeat 2") partOf p(CONSO:"3R tau")
p(CONSO:"tubulin-binding repeat 3") partOf p(CONSO:"3R tau")

p(CONSO:"4R tau") isA p(HGNC:MAPT)
p(CONSO:"3R tau") isA p(HGNC:MAPT)
UNSET Confidence

SET Evidence = "In summary, the cytological evolution from pretangle neurons to NFTs and then to ghost tangles was accompanied
by a profile shift from RD3-/4+ (pretangles) to RD3+/4+ (NFTs) and then to RD3+/4+ (ghost tangles)."
SET Confidence = "High"
p(CONSO:"4R tau") partOf a(CONSO:pretangles)
p(CONSO:"3R tau") partOf complex(GO:"neurofibrillary tangle")
p(CONSO:"4R tau") partOf complex(GO:"neurofibrillary tangle")
p(CONSO:"3R tau") partOf a(CONSO:"ghost tangles")
UNSET Confidence

SET Evidence = "The RD3+/4- (red) fraction was consistently higher in EC and CA1, which was serially followed by CA2, CA3, and CA4.
This trend was reversed with the RD3-/4+ (green) fraction, which was consistently highest in CA4, which was serially followed by
CA3, CA2, CA1, and EC."
SET Confidence = "High"
SET TauIsoformGradient = "3R-Very High"
p(CONSO:"3R tau") partOf a(CONSO:"Brodmann (1909) area 36")
p(CONSO:"3R tau") partOf a(CONSO:"CA1 field of hippocampus")
SET TauIsoformGradient = "3R-High"
p(CONSO:"3R tau") partOf a(CONSO:"CA2 field of hippocampus")
SET TauIsoformGradient = "3R-Medium"
p(CONSO:"3R tau") partOf a(CONSO:"CA3 field of hippocampus")
SET TauIsoformGradient = "3R-Low"
p(CONSO:"3R tau") partOf a(CONSO:"CA4 field of hippocampus")
SET TauIsoformGradient = "4R-Very High"
p(CONSO:"4R tau") pos a(CONSO:"CA4 field of hippocampus")
SET TauIsoformGradient = "4R-High"
p(CONSO:"4R tau") pos a(CONSO:"CA3 field of hippocampus")
SET TauIsoformGradient = "4R-Medium"
p(CONSO:"4R tau") pos a(CONSO:"CA2 field of hippocampus")
SET TauIsoformGradient = "4R-Low"
p(CONSO:"4R tau") pos a(CONSO:"CA1 field of hippocampus")
p(CONSO:"4R tau") pos a(CONSO:"Brodmann (1909) area 36")
UNSET TauIsoformGradient
UNSET Confidence

SET Evidence = "In addition to the mainly cytoplasmic IR of RD4, RD3 IR extended into more distal dendrites  and neuropil threads. This extended RD3 IR was
not explained by its higher sensitivity, because RD4 IR was more intense in the cytoplasm than in the dendrites, while this gradient was reversed with RD3 IR (red)."
SET Confidence = "High"
SET TauIsoformGradient = "3R-High"
p(CONSO:"3R tau") partOf complex(GO:dendrite)
p(CONSO:"3R tau") partOf a(MESH:Neuropil)
SET TauIsoformGradient = "4R-High"
p(CONSO:"4R tau") partOf complex(GO:"neuronal cell body")
SET TauIsoformGradient = "3R-Low"
p(CONSO:"3R tau") partOf complex(GO:"neuronal cell body")
SET TauIsoformGradient = "4R-Low"
p(CONSO:"4R tau") partOf complex(GO:dendrite)
p(CONSO:"4R tau") partOf a(MESH:Neuropil)
UNSET TauIsoformGradient
UNSET Confidence

SET Evidence = "Dendritic arborization was abundant in RD3-/4+ pretangles, attenuated in RD3+/4+ neurons, and further attenuated in RD3+/4- ghost tangles."
SET Confidence = "High"
a(CONSO:pretangles) -> complex(GO:"dendritic tree")
complex(GO:"neurofibrillary tangle") -| complex(GO:"dendritic tree")
a(CONSO:"ghost tangles") -| complex(GO:"dendritic tree")
complex(GO:"dendritic tree") pos act(complex(GO:dendrite))
UNSET Confidence
UNSET Method
UNSET Disease
UNSET Braak_Stage
UNSET Species


SET Citation = {"PubMed", "22142809"}
SET Evidence = "CBF tissues harvested from people who died with a clinical diagnosis
of no cognitive impairment (NCI), mild cognitive impairment, or AD were immunolabeled for both p75NTR and Rac1b. Rac1b appeared as cytoplasmic
diffuse granules, loosely aggregated filaments, or compact spheres in p75NTR-positive NB neurons. Although Rac1b colocalized with tau cytoskeletal markers,
the percentage of p75NTR-immunoreactive neurons expressing Rac1b was significantly increased only in AD compared with both mild cognitive impairment and NCI."
SET Confidence = "High"
SET Species = "9606"
SET Disease = "Alzheimer's disease"
SET Braak_Stage = "Stage VI"
SET Anatomy = "basal nucleus of telencephalon"
SET Cell = "cholinergic neuron"
SET Method = {"RT-PCR", "Western Blot", "Immunohistochemistry", "Terminal Continuation RNA Amplification", "Laser Capture Microdissection", "Microarray"}
p(CONSO:RAC1b) -- p(HGNC:NGFR)
p(CONSO:RAC1b) isA p(HGNC:RAC1)
UNSET Confidence

##NP_061485.1 refers to Rac1b splicing isoform of RAC1

SET Evidence = "Furthermore, single-cell gene expression profiling with custom-designed microarrays showed down-regulation of caveolin 2,
GNB4, and lipase A in AD Rac1b-positive/p75NTR-labeled NB neurons compared with Rac1b-negative/p75NTRpositive perikarya in NCI. These proteins are involved
in Rac1 pathway/cell cycle progression and lipid metabolism."
SET Confidence = "Medium"
p(CONSO:RAC1b) neg p(HGNC:CAV2)
p(CONSO:RAC1b) neg p(HGNC:GNB4)
p(CONSO:RAC1b) neg p(HGNC:LIPA)
p(HGNC:CAV2) -- bp(GO:"regulation of cell cycle")
p(HGNC:GNB4) -- bp(GO:"regulation of cell cycle")
p(HGNC:LIPA) -- bp(GO:"regulation of cell cycle")
p(HGNC:CAV2) -- bp(GO:"lipid metabolic process")
p(HGNC:CAV2) -- bp(GO:"lipid metabolic process")
p(HGNC:CAV2) -- bp(GO:"lipid metabolic process")
UNSET Confidence
UNSET Cell

SET Evidence = "Because we also found Rac1b-positive neurons in the AD cortex, it will be interesting to determine
whether these perikarya display a similar gene profile to that found in the NB cholinergic neurons."
SET Confidence = "High"
SET MeSHDisease = "Alzheimer Disease"
SET Anatomy = "cerebral cortex"
p(CONSO:RAC1b) pos a(CONSO:"cerebral cortex")
UNSET Anatomy
UNSET MeSHDisease
UNSET Confidence
