#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Carboxy terminus heat shock protein 70 interacting protein reduces tau-associated degenerative changes"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/cfd2ee89d1a71126da8b5f452544b7fe43be67e7/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/73688d6dc24e309fca59a1340dc9ee971e9f3baa/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/a5b84013a08880975ca84f40999e4404b14a97e2/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

# Add statements below this comment

SET Citation = {"PubMed","25374103"}

SET Evidence = "The tau-protein is a natively soluble protein that plays a key role in the dynamics of microtubules"
p(HGNC:MAPT) reg act(a(GO:microtubule))

SET Evidence = "In the pathological case of Alzheimer’s disease (AD) tau becomes hyperphosphorylated, detaches from the
microtubules, misfolds, and mislocalizes to the somatodendritic compartment where it aggregates into neurofibrillary tangles."
SET MeSHDisease = "Alzheimer Disease"
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(HBP:hyperphosphorylation))
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -| complex(p(HGNC:MAPT),a(GO:microtubule))
complex(p(HGNC:MAPT),a(GO:microtubule)) -| p(HGNC:MAPT,pmod(HBP:misfolded))
p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)) -> tloc(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)),fromLoc(GO:axon),toLoc(GO:"somatodendritic compartment"))
tloc(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)),fromLoc(GO:axon),toLoc(GO:"somatodendritic compartment")) -> a(HBP:"Tau aggregates")
a(HBP:"Tau aggregates") -> a(GO:"neurofibrillary tangle")
UNSET MeSHDisease

SET Evidence = "The overexpression of full-length tau in Chinese hamster ovary (CHO) cells [3], N2A cells
[4], cultured retinal ganglion cells [4], NB2a/d1 cells [5] H4-cells [6], and primary cortical neuron
cultures [6] led to an impairment of anterograde transport of a variety of kinesin cargos, including mitochondria"
p(HGNC:MAPT) neg bp(GO:"anterograde axonal transport")

SET Evidence = "This leads to accumulation of mitochondria in the cell body where they cluster near the microtubule center [6]."
#this: impairment of anterograde transport
SET MeSHAnatomy = "Cell Body"
bp(GO:"anterograde axonal transport") -| a(GO:microtubule)
UNSET MeSHAnatomy

SET Evidence = "Thies and Mandelkow [7] have shown previously that the co-expression of
microtubule-associated protein/microtubule affinityregulating kinase 2 (MARK2) with tau can rescue these
effects caused by tau overexpression in vitro."
#these effects: impairment of anterograde transport
composite(p(HGNC:MAPT),p(HGNC:MARK2)) pos bp(GO:"anterograde axonal transport")

SET Evidence = "The two major pathways for protein degradation in cells are through the ubiquitin-proteasome system
and the autophagy-lysosome system [10, 11], both of which have been implicated in tau degradation in AD [12]."
SET MeSHDisease = "Alzheimer Disease"
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -> deg(p(HGNC:MAPT))
bp(GO:autophagy) -> deg(p(HGNC:MAPT))
UNSET MeSHDisease

SET Evidence = "Misfolded tau proteins, like other natively unfolded molecules, can be detected and cleared by
chaperone assisted mechanisms [13]."
bp(GO:"chaperone-mediated autophagy") -> deg(p(HGNC:MAPT,pmod(HBP:misfolded)))

SET Evidence = "Recent data suggest that a critical mediator of refolding or clearance
of hyperphosphorylated tau is via the HSP70/HSP90 heat shock pathways in which a specific E3 ubiquitin
ligase, CHIP (carboxy terminus Hsp70 interacting protein), can recognize and target for degradation
abnormal but not normal tau molecules [14–16]."
p(FPLX:HSP90) reg deg(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)))
p(HGNC:STUB1) -> deg(p(HGNC:MAPT,pmod(HBP:hyperphosphorylation)))

SET Evidence = "CHIP is also implicated in regulation of Caspase 3 activity"
p(HGNC:STUB1) reg act(p(HGNC:CASP3))

SET Evidence = "In CHIP knockout mice, Caspase 3 activation is increased and caspase-cleaved tau levels are increased [17]."
SET Species = "10090"
p(MGI:Stub1) -| act(p(MGI:Casp3))
p(MGI:Stub1) -| p(HBP:"Tau C3")
UNSET Species

SET Evidence = "CHIP interacts more strongly with tauΔC than
full-length tau [18], suggesting it is involved in caspase cleaved tau degradation"
# tauΔC: tau cleaved by Caspase 3 in the C terminus at Asp421 (tauΔC); Tau C3
p(HGNC:STUB1) -> complex(p(HGNC:STUB1),p(HBP:"Tau C3"))
p(HGNC:STUB1) -> deg(p(HBP:"Tau C3"))

SET Evidence = "Densitometry analysis of total tau levels of H4- cell lysates on SDS-PAGE showed a 2.0
to 2.5 fold lower levels of tau (4R-tau, tauC3, and K18ΔK280) with co-expression with CHIP, respectively (Fig. 1)."
# 4R-tau: Tau isoform F (441 aa)
composite(p(HGNC:STUB1),p(HBP:"Tau isoform F (441 aa)")) -| p(HBP:"Tau isoform F (441 aa)")
composite(p(HBP:"Tau C3"),p(HGNC:STUB1)) -| p(HBP:"Tau C3")
composite(p(HGNC:STUB1),p(HBP:"4R tau",var("p.Lys280del"))) -| p(HBP:"4R tau",var("p.Lys280del"))

SET Evidence = "Toxicity assays revealed that neither CHIP nor any of the tau constructs caused cell death
compared to the control GFP vector (Supplementary Fig. 1)."
p(HGNC:STUB1) cnc bp(GO:"cell death")
p(HBP:"Tau isoform F (441 aa)") cnc bp(GO:"cell death")
p(HBP:"Tau C3") cnc bp(GO:"cell death")
p(HBP:"4R tau",var("p.Lys280del")) cnc bp(GO:"cell death")

SET Evidence = "Probing for phospho-tau with the 12E8 antibody in cell lysates from
K18ΔK280 transfected cells reveals that tau phosphorylation is also decreased by
CHIP overexpression (Fig. 1)."
composite(p(HGNC:STUB1),p(HBP:"4R tau",var("p.Lys280del"))) neg p(HGNC:MAPT,pmod(Ph,Ser,262))

SET Evidence = "Several publications suggest that molecular chaperones, e.g., heat shock protein 70 and 90
(Hsp70, Hsp90) play a fundamental role in the clearance of misfolded proteins including tau [12]."
a(MESH:"HSP70 Heat-Shock Proteins") -> deg(p(HGNC:MAPT,pmod(HBP:misfolded)))
p(FPLX:HSP90) -> deg(p(HGNC:MAPT,pmod(HBP:misfolded)))

SET Evidence = "The co-chaperone CHIP interacts with Hsp70/Hsp90 to bind unfolded and misfolded proteins"
p(HGNC:STUB1) -> complex(p(HGNC:STUB1),p(FPLX:HSP90))
complex(p(HGNC:STUB1),p(FPLX:HSP90)) -> complex(p(HGNC:STUB1),p(FPLX:HSP90),p(MESH:Proteins,pmod(HBP:misfolded)))
p(HGNC:STUB1) -> complex(p(HGNC:STUB1),a(MESH:"HSP70 Heat-Shock Proteins"))
complex(p(HGNC:STUB1),a(MESH:"HSP70 Heat-Shock Proteins")) -> complex(p(HGNC:STUB1),a(MESH:"HSP70 Heat-Shock Proteins"),p(MESH:Proteins,pmod(HBP:misfolded)))

SET Evidence = "Co-transfection of CHIP with 4R-tau leads to a significant increase in Hsp70 levels,
however this is not the case with wither tauC3 or K18DK280 constructs (Supplementary Fig. 2) suggesting
that increased HSP70 protein levels are not necessary for the observed reduction in tau levels."
composite(p(HBP:"Tau isoform F (441 aa)"),p(HGNC:STUB1)) -> a(MESH:"HSP70 Heat-Shock Proteins")
composite(p(HBP:"Tau C3"),p(HGNC:STUB1)) cnc a(MESH:"HSP70 Heat-Shock Proteins")
composite(p(HBP:"4R tau",var("p.Lys280del")),p(HGNC:STUB1)) cnc a(MESH:"HSP70 Heat-Shock Proteins")

SET Evidence = "CHIP has two important domains for his chaperone activity: the amino terminus
tetratricopeptide repeat (TPR) domain that links CHIP to Hsp70 and Hsp90 [24] and the carboxyl terminus
containing the U-box that is important for the E3 ubiquitin ligase function of CHIP."
p(PFAM:"CHIP_TPR_N") -> complex(p(HGNC:STUB1),p(FPLX:HSP90),a(MESH:"HSP70 Heat-Shock Proteins"))

SET Evidence = "Either of these domains could be critical for degradation of tau since ubiquitination
targets proteins for degradation and CHIP is known to interact with full-length tau in a complex
with Hsc70/Hsp70 and poly-ubiquitinated tau [17]."
p(HGNC:STUB1) -> complex(p(HGNC:MAPT,pmod(Ub)),p(HGNC:HSPA8),p(HGNC:STUB1))

SET Evidence = "Interestingly, a significant decrease in tau levels was seen
with the co-transfection of CHIPΔU (U-box deleted) when transfected with either K18ΔK280 or 4R-Tau."
complex(p(HGNC:STUB1),p(INTERPRO:"U box domain")) -> p(HBP:"Tau isoform F (441 aa)")
complex(p(HGNC:STUB1),p(INTERPRO:"U box domain")) -> p(HBP:"4R tau",var("p.Lys280del"))

SET Evidence = "After cyclohexamide treatment, we could still detect a significant
decrease in tau levels in cells that were co-transfected with CHIP indicating that the reduction in tau levels
due to CHIP is due to degradation and not to transcriptional down-regulation (Supplementary Fig. 3)."
# tau (K18ΔK280)
composite(p(HGNC:STUB1),p(HBP:"4R tau",var("p.Lys280del"))) -| p(HBP:"4R tau",var("p.Lys280del"))
p(HGNC:STUB1) -> deg(p(HBP:"4R tau",var("p.Lys280del")))

SET Evidence = "Recent data suggest that caspases are involved in the accumulation of tau pathology [10, 25, 26], and
reductions in CHIP have been shown to cause caspase activation and increased caspase-cleaved tau levels [19]."
p(FPLX:Caspase) pos path(MESH:Tauopathies)
p(HGNC:STUB1) neg act(p(FPLX:Caspase))
p(HGNC:STUB1) neg p(HBP:"Tau C3")

SET Evidence = "Tau overexpression increased active caspase 3 levels, and
co-expression of CHIP reduced cleaved caspase 3 levels compared to tau expression alone (Fig. 3)."
# tau: p(HBP:"Tau isoform F (441 aa)") ; p(HBP:"Tau C3")
p(HBP:"Tau isoform F (441 aa)") pos act(p(HGNC:CASP3))
composite(p(HGNC:STUB1),p(HBP:"Tau isoform F (441 aa)")) -| p(HGNC:CASP3,frag("?"))
p(HBP:"Tau C3") pos act(p(HGNC:CASP3))
composite(p(HGNC:STUB1),p(HBP:"Tau C3")) -| p(HGNC:CASP3,frag("?"))

SET Evidence = "Dolan et al. [20] showed that CHIP interacts more strongly with tauC3 than full length tau."
p(HGNC:STUB1) -> complex(p(HGNC:STUB1),p(HBP:"Tau isoform F (441 aa)"))
p(HGNC:STUB1) -> complex(p(HGNC:STUB1),p(HBP:"Tau C3"))

SET Evidence = "Indeed, the co-expression of CHIP caused a significant decrease tauC3 in vitro (Fig. 4)."
# co-expression of CHIP and 4R-tau
composite(p(HGNC:STUB1),p(HBP:"Tau isoform F (441 aa)")) -| p(HBP:"Tau C3")

SET Evidence = "These results indicate that CHIP is involved in degradation
of caspases and caspase-cleaved tau."
p(HGNC:STUB1) -> deg(p(FPLX:Caspase))
p(HGNC:STUB1) -> deg(p(HBP:"Tau C3"))

SET Evidence = "As expected, we observed a significant impairment of mitochondrial distribution with overexpression
of all three tau constructs (p < 0.0001, Wilcoxon test for GFP control versus 4R-tau, GFP
versus tauC3, and GFP versus K18ΔK280) (Fig. 5)."
# mitochondrial distribution: mitochondrion transport along microtubule
p(HBP:"Tau isoform F (441 aa)") neg bp(GO:"mitochondrion transport along microtubule")
p(HBP:"Tau C3") neg bp(GO:"mitochondrion transport along microtubule")
p(HBP:"4R tau",var("p.Lys280del")) neg bp(GO:"mitochondrion transport along microtubule")

SET Evidence = "CHIP co-expression led to a significant recovery in the
area of mitochondria around the nucleus (19–26%)."
# cotransfection CHIP with 4R-tau/tauC3/K18ΔK280
# mitochondrial distribution: mitochondrion transport along microtubule
composite(p(HGNC:STUB1),p(HBP:"Tau isoform F (441 aa)")) pos bp(GO:"mitochondrion transport along microtubule")
composite(p(HGNC:STUB1),p(HBP:"Tau C3")) pos bp(GO:"mitochondrion transport along microtubule")
composite(p(HGNC:STUB1),p(HBP:"4R tau",var("p.Lys280del"))) pos bp(GO:"mitochondrion transport along microtubule")

SET Evidence = "Taken together, these data indicate that tau-overexpression leads to abnormal mitochondrial
trafficking that can be rescued by CHIP-co-expression"
# tau: 4R-tau/tauC3/K18ΔK280
p(HBP:"Tau isoform F (441 aa)") neg bp(GO:"mitochondrion transport along microtubule")
p(HBP:"Tau C3") neg bp(GO:"mitochondrion transport along microtubule")
p(HBP:"4R tau",var("p.Lys280del")) neg bp(GO:"mitochondrion transport along microtubule")
composite(p(HGNC:STUB1),p(HBP:"Tau isoform F (441 aa)")) pos bp(GO:"mitochondrion transport along microtubule")
composite(p(HGNC:STUB1),p(HBP:"Tau C3")) pos bp(GO:"mitochondrion transport along microtubule")
composite(p(HGNC:STUB1),p(HBP:"4R tau",var("p.Lys280del"))) pos bp(GO:"mitochondrion transport along microtubule")
