#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Dynamin-related protein 1: A critical protein in the pathogenesis of neural system dysfunctions and neurodegenerative diseases."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
 DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/4aa36980578c7d2cff07371abf768f15db89cd11/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
 DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
 DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
 DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
# DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
 DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
 DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
 DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"
 DEFINE NAMESPACE OMIT           AS PATTERN "^\d{7}$"
 DEFINE NAMESPACE NCIT           AS PATTERN "^C\d+$"
###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
 DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
 DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
 DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
 DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
 DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "30515821"}

SET Evidence = "Dynamin‐related protein 1 (Drp1) is essential for fragmentation of
mitochondria"
SET Confidence = "High"
p(HGNC:DNM1L) reg bp(GO:"mitochondrial fission")
UNSET Confidence

SET Evidence = "It is
noteworthy that mitochondrial dynamics is essential for many cellular
and physiological activities, which include ATP production, calcium (Ca2+)
and iron homeostasis, lipid and amino acid metabolism, body temperature
control, reactive oxygen species (ROS) production, ageing, apoptosis, and
cell proliferation and differentiation (Figure 1)."
SET Confidence = "High"
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"ATP biosynthetic process")
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"calcium ion homeostasis")
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"iron ion homeostasis")
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"lipid metabolic process")
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"cellular amino acid metabolic process")
path(MESH:"Mitochondrial Dynamics") reg bp(MESH:"Body Temperature Regulation")
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"reactive oxygen species biosynthetic process")
path(MESH:"Mitochondrial Dynamics") reg bp(GO:aging)
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"programmed cell death")
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"cell population proliferation")
path(MESH:"Mitochondrial Dynamics") reg bp(GO:"cell differentiation")
UNSET Confidence

SET Evidence = "In addition, defects in
mitochondria dynamics involve in cancers and some neural system
dysfunctions and neurodegenerative diseases, such as breast cancer,
nasopharyngeal carcinoma, neural cell axonal transportation dysfunction,
aberrant release, and recycle of synapse transmitters, AD, PD, and so on
(Baloh, 2008; Grandemange, Herzig, & Martinou, 2009)."
SET Confidence = "High"
path(DOID:"neurodegenerative disease") neg path(MESH:"Mitochondrial Dynamics")
path(DOID:"breast cancer") neg path(MESH:"Mitochondrial Dynamics")
path(DOID:"nasopharynx carcinoma") neg path(MESH:"Mitochondrial Dynamics")
SET Cell = "neuron"
bp(GO:"axonal transport") pos path(MESH:"Mitochondrial Dynamics")
UNSET Cell
SET MeSHAnatomy = "Synapses"
bp(GO:"neurotransmitter secretion") pos path(MESH:"Mitochondrial Dynamics")
bp(GO:"neurotransmitter reuptake") pos path(MESH:"Mitochondrial Dynamics")
UNSET MeSHAnatomy
path(DOID:"Alzheimer's disease") neg path(MESH:"Mitochondrial Dynamics")
path(DOID:"Parkinson's disease") neg path(MESH:"Mitochondrial Dynamics")
UNSET Confidence

SET Evidence = "The fusion of mitochondria is controlled by three critical proteins; the
mitochondrial outer membrane (MOM) localized protein‐mitofusin 1
(Mfn1), Mfn2, and the mitochondrial inner membrane (MIM) localized
protein‐optic atrophy 1 (OPA1)."
SET Confidence = "High"
p(HGNC:MFN1,loc(GO:"mitochondrial outer membrane")) reg bp(GO:"mitochondrial fusion")
p(HGNC:MFN2,loc(GO:"mitochondrial outer membrane")) reg bp(GO:"mitochondrial fusion")
p(HGNC:OPA1,loc(GO:"mitochondrial inner membrane")) reg bp(GO:"mitochondrial fusion")
UNSET Confidence

SET Evidence = "First, the fusion of MOM is determined
by Mfn1 and Mfn2, whose mutation would lead to very slim and short
mitochondria and the maldevelopment of embryos."
SET Confidence = "High"
composite(p(HGNC:MFN1),p(HGNC:MFN2)) reg bp(GO:"mitochondrial outer membrane fusion")
composite(p(HGNC:MFN1, var("?")),p(HGNC:MFN2,var("?"))) neg path(MESH:"Mitochondrial Dynamics")
composite(p(HGNC:MFN1, var("?")),p(HGNC:MFN2,var("?"))) neg bp(GO:"embryo development")
UNSET Confidence

SET Evidence = "Second, the OPA1
takes control of the fusion of the MIM, the maintenance of the
mitochondrial DNA (mtDNA) and cristae morphology, and mutation
would cause autosomal dominant optic atrophy (ADOA)."
SET Confidence = "High"
p(HGNC:OPA1) reg bp(GO:"mitochondrial inner membrane fusion")
p(HGNC:OPA1) reg bp(GO:"mitochondrial genome maintenance")
p(HGNC:OPA1) reg bp(GO:"cristae formation")
p(HGNC:OPA1, var("?")) -> path(DOID:"optic atrophy")
UNSET Confidence

SET Evidence = "It is
well documented that the division of mitochondria requires Drp1 from
the cytosol to the MOM, which is mediated by four receptors located on
the MOM—mitochondrial dynamics protein of 49 kDa (MID49), mitochondrial
dynamics protein of 51 kDa (MID51), mitochondrial fission
factor (MFF), and mitochondrial fission 1 (FIS1)."
SET Confidence = "High"
p(HGNC:MIEF2,loc(GO:"mitochondrial outer membrane")) -> tloc(p(HGNC:DNM1L),fromLoc(GO:cytosol),toLoc(GO:"mitochondrial outer membrane"))
p(HGNC:MIEF1,loc(GO:"mitochondrial outer membrane")) -> tloc(p(HGNC:DNM1L),fromLoc(GO:cytosol),toLoc(GO:"mitochondrial outer membrane"))
p(HGNC:MFF,loc(GO:"mitochondrial outer membrane")) -> tloc(p(HGNC:DNM1L),fromLoc(GO:cytosol),toLoc(GO:"mitochondrial outer membrane"))
p(HGNC:FIS1,loc(GO:"mitochondrial outer membrane")) -> tloc(p(HGNC:DNM1L),fromLoc(GO:cytosol),toLoc(GO:"mitochondrial outer membrane"))
#fig 1
complex(p(HGNC:MIEF2),p(HGNC:DNM1L), loc(GO:"mitochondrial outer membrane")) -> bp(GO:"mitochondrial fission")
complex(p(HGNC:MIEF1),p(HGNC:DNM1L), loc(GO:"mitochondrial outer membrane")) -> bp(GO:"mitochondrial fission")
complex(p(HGNC:FIS1),p(HGNC:DNM1L), loc(GO:"mitochondrial outer membrane")) -> bp(GO:"mitochondrial fission")
UNSET Confidence

SET Evidence = "Overproduction
of ROS in abnormal mitochondria is a major cause for the
dysfunction of neurons."
SET Confidence = "High"
SET CellStructure = "Mitochondria"
a(CHEBI:"reactive oxygen species") -> path(EFO:"neurotic disorder")
UNSET CellStructure
UNSET Confidence

SET Evidence = "Numerous small and slim mitochondria and
numerous long and thin mitochondria caused by the abnormal Drp1 or
Mfn1, Mfn2, and OPA1 will lead to the insufficient supply of energy and
overproduction of ROS in neurons, causing neural system dysfunctions
and neurodegenerative diseases."
SET Confidence = "High"
act(p(HGNC:MIEF2)) -> bp(GO:"energy homeostasis")
act(p(HGNC:MIEF1)) -> bp(GO:"energy homeostasis")
act(p(HGNC:MFF)) -> bp(GO:"energy homeostasis")
act(p(HGNC:FIS1)) -> bp(GO:"energy homeostasis")
act(p(HGNC:DNM1L)) -> bp(GO:"energy homeostasis")
act(p(HGNC:MIEF2)) -| a(CHEBI:"reactive oxygen species")
act(p(HGNC:MIEF1)) -| a(CHEBI:"reactive oxygen species")
act(p(HGNC:MFF)) -| a(CHEBI:"reactive oxygen species")
act(p(HGNC:FIS1)) -| a(CHEBI:"reactive oxygen species")
act(p(HGNC:DNM1L)) -| a(CHEBI:"reactive oxygen species")
act(p(HGNC:MIEF2)) -| path(DOID:"neurodegenerative disease")
act(p(HGNC:MIEF1)) -| path(DOID:"neurodegenerative disease")
act(p(HGNC:MFF)) -| path(DOID:"neurodegenerative disease")
act(p(HGNC:FIS1)) -| path(DOID:"neurodegenerative disease")
act(p(HGNC:DNM1L)) -| path(DOID:"neurodegenerative disease")
a(CHEBI:"reactive oxygen species") -> path(DOID:"neurodegenerative disease")
bp(GO:"energy homeostasis") -| path(DOID:"neurodegenerative disease")
UNSET Confidence

SET Evidence = "Studies suggest that Drp1 can interact
with some key pathogenic proteins‐Aβ, mutant PINK/Parkin, mutant
Huntingtin (Htt), and mutant superoxide dismutase 1 (SOD1) important
in the pathogenesis of AD, PD, HD, and ALS, causing the dysfunction of
mitochondrial dynamics in these diseases."
SET Confidence = "High"
SET MeSHDisease = "Alzheimer Disease"
complex(p(HGNC:DNM1L),a(CHEBI:"amyloid-beta")) neg path(MESH:"Mitochondrial Dynamics")
UNSET MeSHDisease
SET MeSHDisease = "Parkinson Disease"
complex(p(HGNC:DNM1L),p(HGNC:PRKN,var("?"))) neg path(MESH:"Mitochondrial Dynamics")
UNSET MeSHDisease
SET MeSHDisease = "Huntington Disease"
complex(p(HGNC:DNM1L),p(HGNC:HTT,var("?"))) neg path(MESH:"Mitochondrial Dynamics")
UNSET MeSHDisease
SET MeSHDisease = "Amyotrophic Lateral Sclerosis"
complex(p(HGNC:DNM1L),p(HGNC:SOD1,var("?"))) neg path(MESH:"Mitochondrial Dynamics")
UNSET MeSHDisease
UNSET Confidence

SET Species = "10090"

SET Evidence = "Phosphorylation at Ser‐40 and Ser‐44 mediated
by glycogen synthase kinase 3β (GSK3β) can promote GTPase activity
of Drp1 leading to an increase in mitochondrial division causing
neuronal cell apoptosis induced by increased vulnerability to Aβ toxicity
in the AD mice model"
SET Confidence = "High"
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:GSK3B) -> p(HGNC:DNM1L,pmod(Ph,Ser,40),pmod(Ph,Ser,44))
p(HGNC:DNM1L,pmod(Ph,Ser,40),pmod(Ph,Ser,44)) -> act(p(HGNC:DNM1L),ma(GO:"GTPase activity"))
p(HGNC:GSK3B) -> act(p(HGNC:DNM1L),ma(GO:"GTPase activity"))
act(p(HGNC:DNM1L),ma(GO:"GTPase activity")) -> bp(GO:"mitochondrial fission")
SET MeSHAnatomy = "Neurons"
bp(GO:"mitochondrial fission") -> bp(GO:"programmed cell death")
bp(GO:"response to amyloid-beta") -> path(MESH:"Neurotoxicity Syndromes")
path(MESH:"Neurotoxicity Syndromes") -> bp(GO:"programmed cell death")
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "TAT‐Drp1‐SpS (a GSK3β inhibitor) notably
restored mitochondrial length, lowered the activity of caspase‐3,
reduced the Aβ level, and rescued memory deficits when administrated
in the hippocampus Cornu Ammonis 1 (CA1) region of AD transgenic
mice (Yan et al., 2015)."
SET Confidence = "High"
#artificial polypeptide TAT-Drp1-SpS
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:GSK3B) reg bp(GO:"mitochondrial fission")
p(HGNC:GSK3B) -> act(p(HGNC:CASP3))
p(HGNC:GSK3B) -> a(CHEBI:"amyloid-beta")
SET MeSHAnatomy = "CA1 Region, Hippocampal"
p(HGNC:GSK3B) -> path(EFO:"memory impairment")
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "Phosphorylation at Ser‐579 can increase
the mitochondrial fission activity accelerating the cleavage of
caspase- 3 leading to mouse cortical neurons apoptosis."
SET Confidence = "High"
p(HGNC:DNM1L,pmod(Ph,Ser,579)) -> bp(GO:"mitochondrial fission")
bp(GO:"mitochondrial fission") -> p(HGNC:CASP3, frag("?"))
p(HGNC:DNM1L,pmod(Ph,Ser,579)) -> p(HGNC:CASP3, frag("?"))
SET MeSHAnatomy = {"Neocortex","Neurons"}
p(HGNC:CASP3, frag("?")) -> bp(GO:"programmed cell death")
p(HGNC:DNM1L,pmod(Ph,Ser,579)) -> bp(GO:"programmed cell death")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "The mechanism
of phosphorylation at Ser-579 is not similar to Ser-40 and Ser-44, the
former is mediated by the hyperactivated cyclindependent kinase
5 (CDK5) due to the increased Aβ, and administration of Roscovitine (a
CDK5 inhibitor) or Drp1 KO can reduce the cleavage of caspase-3 in
AD neurons (M. Y. Guo et al., 2018)"
SET Confidence = "High"
#KO - knockout
a(CHEBI:"amyloid-beta") -> act(p(HGNC:CDK5))
act(p(HGNC:CDK5)) -> p(HGNC:DNM1L,pmod(Ph,Ser,579))
a(CHEBI:"amyloid-beta") -> p(HGNC:DNM1L,pmod(Ph,Ser,579))
a(CHEBI:seliciclib) -| p(HGNC:CDK5)
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Neurons"
a(CHEBI:seliciclib) -| p(HGNC:CASP3, frag("?"))
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "Phosphorylation at Ser‐585 also
induced by CDK5 leads to hyperactivity of Drp1 then impairing the
homeostasis of ATP production and increasing production of ROS,
causing N‐methyl‐D‐aspartate (NMDA)‐induced neuronal injury."
SET Confidence = "High"
p(HGNC:CDK5) -> p(HGNC:DNM1L,pmod(Ph,Ser,585))
p(HGNC:DNM1L,pmod(Ph,Ser,585)) -> act(p(HGNC:DNM1L))
act(p(HGNC:DNM1L)) neg bp(GO:"ATP biosynthetic process")
act(p(HGNC:DNM1L)) -> a(CHEBI:"reactive oxygen species")
a(CHEBI:"reactive oxygen species") pos bp(GO:"regulation of NMDA receptor activity")
SET MeSHAnatomy = "Neurons"
bp(GO:"regulation of NMDA receptor activity") -> path(MESH:"Wounds and Injuries")
a(CHEBI:"reactive oxygen species") -> path(MESH:"Wounds and Injuries")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Nestin
(a marker of neural system progenitor cells [NSPCs]) plays a pivotal role
in the central nervous system (CNS) development and NSPCs stemness
maintenance, and Wang et al. found that Nestin could sequester CDK5
in the cytosol then phosphorylate Drp1 at Ser‐592 leading to increased
mitochondrial fission and glycolysis causing NSPCs differentiation and
maintenance of its stemness (Jahani‐Asl et al., 2015; J. Wang et al.,
2018)."
SET Confidence = "High"
p(HGNC:NES) -> bp(GO:"central nervous system development")
p(HGNC:NES) reg bp(GO:neurogenesis)
p(HGNC:NES) -| act(p(HGNC:CDK5,loc(GO:cytosol)))
p(HGNC:NES) -> p(HGNC:DNM1L,pmod(Ph,Ser,592))
p(HGNC:DNM1L,pmod(Ph,Ser,592)) -> bp(GO:"mitochondrial fission")
p(HGNC:DNM1L,pmod(Ph,Ser,592)) -> bp(MESH:Glycolysis)
bp(GO:"mitochondrial fission") -> bp(GO:"neuron differentiation")
bp(GO:"mitochondrial fission") reg bp(GO:neurogenesis)
bp(MESH:Glycolysis) -> bp(GO:"neuron differentiation")
bp(MESH:Glycolysis) reg bp(GO:neurogenesis)
p(HGNC:DNM1L,pmod(Ph,Ser,592)) -> bp(GO:"neuron differentiation")
p(HGNC:DNM1L,pmod(Ph,Ser,592)) reg bp(GO:neurogenesis)
UNSET Confidence

SET Evidence = "For the phosphorylation at Ser‐616,
Hyun et al. found that its activation by CDK5 is associated with seizure induced
neuronal injury, because administration of Roscovitine can
ameliorate seizure‐induced astroglial apoptosis, suggesting that CDK5
may involve in astroglial apoptosis in the dentate gyrus and inhibiting its
activation may provide a protective effect toward seizure‐induced
hippocampal injury."
SET Confidence = "High"
p(HGNC:CDK5) -> p(HGNC:DNM1L,pmod(Ph,Ser,616))
path(MESH:"Wounds and Injuries") -- act(p(HGNC:CDK5))
path(MESH:"Wounds and Injuries") -- p(HGNC:DNM1L,pmod(Ph,Ser,616))
path(MESH:Seizures) -> path(MESH:"Wounds and Injuries")
SET MeSHAnatomy = {"Neuroglia","Dentate Gyrus"}
a(CHEBI:seliciclib) -| bp(GO:"programmed cell death")
act(p(HGNC:CDK5)) -> bp(GO:"programmed cell death")
UNSET MeSHAnatomy
SET MeSHAnatomy = "Hippocampus"
a(CHEBI:seliciclib) -| path(MESH:"Wounds and Injuries")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In a diabetic mouse model, GSK3β‐induced
Drp1 phosphorylation at Ser‐616 involve in its synaptic deficits, and
blockade of GSK3β by TDZD8 (an inhibitor of GSK3β) or inhibiting
the fragmentation of mitochondria both tremendously improves
mitochondrial and synaptic function."
SET Confidence = "High"
SET MeSHDisease = "Diabetes Mellitus"
p(HGNC:GSK3B) -> p(HGNC:DNM1L,pmod(Ph,Ser,616))
p(HGNC:DNM1L,pmod(Ph,Ser,616)) -> path(MESH:"Neuronal Plasticity")
a(PUBCHEM:4124851) -| p(HGNC:GSK3B)
a(PUBCHEM:4124851) -| path(MESH:"Neuronal Plasticity")
a(PUBCHEM:4124851) reg path(MESH:"Mitochondrial Dynamics")
bp(GO:"mitochondrial fission") -> path(MESH:"Neuronal Plasticity")
bp(GO:"mitochondrial fission") neg path(MESH:"Mitochondrial Dynamics")
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "Mitogen‐activated protein kinase
1 (MAPK1) can cause over‐activation of Drp1 at Ser‐616 in HD knock-in
mouse‐derived striatal cells which were found by Roe et al. causing
mitochondria dysfunction finally leading to the occurrence of HD, and
using U0126 (an inhibitor of MEK1/2) or Drp1 S616A (phosphorylation deficient
mutant 1) canceled all of it (Chen et al., 2016; Chuang et al.,
2016; Hyun, Min, & Kim, 2017; Jin et al., 2018; Kashatus et al., 2015;
Xie et al., 2015)."
SET Confidence = "High"
SET MeSHDisease = "Huntington Disease"
SET Cell = "striatum neuron"
p(HGNC:MAPK1) -> p(HGNC:DNM1L,pmod(Ph,Ser,616))
p(HGNC:DNM1L,pmod(Ph,Ser,616)) neg path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1L,pmod(Ph,Ser,616)) -> path(DOID:"Huntington's disease")
a(CHEBI:U0126) -| p(HGNC:MAP2K1)
a(CHEBI:U0126) -| p(HGNC:MAP2K2)
p(HGNC:DNM1L,var("p.Ser616Ala")) -| path(DOID:"Huntington's disease")
a(CHEBI:U0126) -| path(DOID:"Huntington's disease")
p(HGNC:DNM1L,var("p.Ser616Ala")) reg path(MESH:"Mitochondrial Dynamics")
a(CHEBI:U0126) reg path(MESH:"Mitochondrial Dynamics")
UNSET Cell
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "Mitochondrial A‐kinase anchoring protein 1 (AKAP1) is a mitochondrial
scaffolding protein found increased in the transient middle‐domain
cerebral artery occlusion mouse model of focal ischemia, and knockout
AKAP1 can cause increased occurrence of smaller mitochondria,
production of ROS, and impaired calcium homeostasis in the neuron."
SET Confidence = "High"
SET Disease = "cerebral artery occlusion"
path(DOID:"brain ischemia") -> p(HGNC:AKAP1,loc(GO:mitochondrion))
SET MeSHAnatomy = "Neurons"
p(HGNC:AKAP1,loc(GO:mitochondrion)) -| bp(GO:"mitochondrial fission")
p(HGNC:AKAP1,loc(GO:mitochondrion)) -| a(CHEBI:"reactive oxygen species")
p(HGNC:AKAP1,loc(GO:mitochondrion)) reg bp(GO:"calcium ion homeostasis")
UNSET MeSHAnatomy
UNSET Disease
UNSET Confidence

SET Evidence = "Mice lacking AKAP1 exhibit increased Drp1 localization to mitochondria
and decreased phosphorylation of Drp1 at Ser‐637, suggesting that
AKAP1 plays a critical protective role in the mouse neural system by
phosphorylation Drp1 at Ser‐637."
SET Confidence = "High"
p(HGNC:AKAP1) -| p(HGNC:DNM1L,loc(GO:mitochondrion))
p(HGNC:AKAP1) -> p(HGNC:DNM1L,pmod(Ph,Ser,637))
p(HGNC:DNM1L,pmod(Ph,Ser,637)) -> path(MESH:Neuroprotection)
p(HGNC:AKAP1) -> path(MESH:Neuroprotection)
UNSET Confidence

SET Evidence = "According to Kim et al.’s report, Siah2
(a ubiquitin ligase) can inhibit the expression of AKAP1 by decreasing
the expression of protein kinase A (PKA) and FIS1–Drp1 interaction
causing increased mitochondrial division finally leading to neural cell
apoptosis."
SET Confidence = "High"
p(HGNC:SIAH2) -| r(FPLX:PKA)
p(HGNC:SIAH2) -| complex(p(HGNC:FIS1),p(HGNC:DNM1L))
r(FPLX:PKA) -> r(HGNC:AKAP1)
complex(p(HGNC:FIS1),p(HGNC:DNM1L)) -> r(HGNC:AKAP1)
p(HGNC:SIAH2) -| r(HGNC:AKAP1)
complex(p(HGNC:FIS1),p(HGNC:DNM1L)) -> bp(GO:"mitochondrial fission")
bp(GO:"mitochondrial fission") -> bp(GO:"programmed cell death")
UNSET Confidence

SET Evidence = "Other reports suggest that dephosphorylation of Drp1 at
Ser‐637 can trigger increased mitochondrial division by calcium/calmodulin
kinase (CaMKⅡα) after the hypoxic precondition and
glutamate treatment leading to necroptosis in CA1 neurons, suggesting
that dephosphorylation of Drp1 at Ser‐637 reverses the protective
effect toward mitochondrial fission, which was mediated by phosphorylation
at Ser‐637, indicating the same function in mitochondrial
dynamics with phosphorylation at Ser‐40, 44, 579, 585, 592, and 616."
SET Confidence = "High"
SET MeSHAnatomy = "CA1 Region, Hippocampal"
a(CHEBI:"L-glutamate(1-)") -| p(HGNC:DNM1L,pmod(Ph,Ser,637))
p(HGNC:DNM1L,pmod(Ph,Ser,637)) -| complex(GO:"calcium- and calmodulin-dependent protein kinase complex")
p(HGNC:DNM1L,pmod(Ph,Ser,637)) -| bp(GO:"necroptotic process")
complex(GO:"calcium- and calmodulin-dependent protein kinase complex") -> bp(GO:"mitochondrial fission")
p(HGNC:DNM1L,pmod(Ph,Ser,40)) reg path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1L,pmod(Ph,Ser,44)) reg path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1L,pmod(Ph,Ser,579)) reg path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1L,pmod(Ph,Ser,585)) reg path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1L,pmod(Ph,Ser,592)) reg path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1L,pmod(Ph,Ser,616)) reg path(MESH:"Mitochondrial Dynamics")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Slupe et al. found that
the PKA‐mediated phosphorylation of Drp1 at Ser‐656 can decrease
the Drp1 activity which denotes decreased mitochondrial fission events
of mitochondria causing a protective effect of the neuronal system,
whereas dephosphorylation of Drp1 at Ser‐656 mediated by protein
phosphatase 2A (PP2A)/calcineurin (CaN) increases the activity of Drp1
causing neuronal death after oxygen‐glucose deprivation (OGD)
treatment."
SET Confidence = "High"
p(FPLX:PKA) -> p(HGNC:DNM1L,pmod(Ph,Ser,656))
p(HGNC:DNM1L,pmod(Ph,Ser,656)) -| act(p(HGNC:DNM1L))
p(HGNC:DNM1L,pmod(Ph,Ser,656)) -| bp(GO:"mitochondrial fission")
p(HGNC:DNM1L,pmod(Ph,Ser,656)) -> path(MESH:Neuroprotection)
complex(p(HGNC:PPP2CA),complex(GO:"calcineurin complex")) -| p(HGNC:DNM1L,pmod(Ph,Ser,656))
complex(p(HGNC:PPP2CA),complex(GO:"calcineurin complex")) -> act(p(HGNC:DNM1L))
SET Cell = "neuron"
bp(GO:"response to oxygen-glucose deprivation") -> bp(GO:"cell death")
UNSET Cell
UNSET Confidence

SET Evidence = "Audrey S. Dickey and Stefan Strack found that phosphorylation
of Drp1 at Ser-656 by the PKA/AKAP1 pathway can increase the
length of mitochondria and the outgrowth of dendrites, but decrease
the number and the density of synapses."
SET Confidence = "High"
complex(p(FPLX:PKA),p(HGNC:AKAP1)) -> p(HGNC:DNM1L,pmod(Ph,Ser,656))
complex(p(FPLX:PKA),p(HGNC:AKAP1)) reg path(MESH:"Mitochondrial Dynamics")
complex(p(FPLX:PKA),p(HGNC:AKAP1)) -> bp(GO:"dendritic cell dendrite assembly")
complex(p(FPLX:PKA),p(HGNC:AKAP1)) -| a(GO:synapse)
UNSET Confidence
UNSET Species

SET Evidence = "They also found the PP2A/Bβ2
(a product of PPP2R2B gene which is associated with the human
spinocerebellar ataxia type-12) pathway plays an opposite effect toward
the PKA/AKAP1, it can dephosphorylate Drp1 at Ser-656."
SET Confidence = "High"
SET Species = "9606"
SET Disease = "spinocerebellar ataxia type 12"
complex(p(HGNC:PPP2CA),p(HGNC:PPP2R2B)) -| p(HGNC:DNM1L,pmod(Ph,Ser,656))
UNSET Disease
UNSET Species
UNSET Confidence

SET Evidence = "In Guo et al.’s
report, they found that SUMO2/3‐specific protease‐SENP3 can be
degraded under OGD treatment and SUMOylation of Drp1 can inhibit
cytochrome c release from the mitochondria and caspase‐induced cell
apoptosis, whereas deSUMOylation of Drp1 increases the localization of
Drp1 at the mitochondrial MOM, mitochondrial fragmentation, and the
release of cytochrome c in rat cortical neurons."
SET Confidence = "High"
#OGD - Oxygen-glucose deprivation
SET Species = "10116"
SET MeSHAnatomy = {"Neocortex","Neurons"}
p(HGNC:SENP3) -> deg(p(HGNC:SUMO2))
p(HGNC:SENP3) -> deg(p(HGNC:SUMO3))
bp(GO:"response to oxygen-glucose deprivation") -> deg(p(HGNC:SENP3))
p(HGNC:DNM1,pmod(Sumo)) -| bp(GO:"release of cytochrome c from mitochondria")
p(HGNC:DNM1,pmod(Sumo)) -| bp(GO:"programmed cell death")
p(HGNCGENEFAMILY:Caspases) -> bp(GO:"programmed cell death")
p(HGNC:DNM1,pmod(Sumo)) -| tloc(p(HGNC:DNM1L),fromLoc(GO:cytosol),toLoc(GO:"mitochondrial outer membrane"))
p(HGNC:DNM1,pmod(Sumo)) -| bp(GO:"mitochondrial fission")
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "This suggests that the
SUMOylation of Drp1 may have a protective effect in the neural system
just like phosphorylation at S637, S656, and S693."
SET Confidence = "High"
p(HGNC:DNM1,pmod(Sumo)) -> path(MESH:Neuroprotection)
UNSET Confidence

SET Evidence = "Dysfunction of Parkin‐an E3 ubiquitin ligase is a major
cause of PD, and in Wang et al.’s report they found that Parkin can
interact with Drp1 in vivo and in vitro and ubiquitinate Drp1 promoting
its proteasome‐dependent degradation, whereas pathogenic mutation or
knock down of Parkin decrease the ubiquitination and degradation of
Drp1 causing increased activity of Drp1 and increased mitochondrial
fission events finally leading to the occurrence of PD."
SET Confidence = "High"
p(HGNC:PRKN) neg path(DOID:"Parkinson's disease")
complex(p(HGNC:PRKN),p(HGNC:DNM1)) -> p(HGNC:DNM1,pmod(Ub))
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -> deg(p(HGNC:DNM1,pmod(Ub)))
complex(p(HGNC:PRKN),p(HGNC:DNM1)) -| act(p(HGNC:DNM1))
act(p(HGNC:DNM1)) -> path(DOID:"Parkinson's disease")
UNSET Confidence

SET Evidence = "In Tang et al.’s
report, they found that the treatment of oxygen glucose deprivation/
reperfusion (OGDR) in neuroblastoma Neuro 2a (N2a) mouse model can
increase the number and activity of Drp1 and decrease the number and
activity of Parkin confirming that the Drp1 knockdown or Parkin
overexpression both play a protective role in neural system by decreasing
the cell apoptosis and mitochondrial dysfunction."
SET Confidence = "High"
SET Species = "10090"
SET CellLine = "Neuro-2a cell"
bp(GO:"response to oxygen-glucose deprivation") -> p(HGNC:DNM1)
bp(GO:"response to oxygen-glucose deprivation") -> act(p(HGNC:DNM1))
bp(GO:"response to oxygen-glucose deprivation") -> p(HGNC:PRKN)
bp(GO:"response to oxygen-glucose deprivation") -> act(p(HGNC:PRKN))
path(OMIT:0016183) -> p(HGNC:DNM1)
path(OMIT:0016183) -> act(p(HGNC:DNM1))
path(OMIT:0016183) -> p(HGNC:PRKN)
path(OMIT:0016183) -> act(p(HGNC:PRKN))
p(HGNC:PRKN) -> path(MESH:Neuroprotection)
p(HGNC:PRKN) -| bp(GO:"programmed cell death")
p(HGNC:PRKN) pos path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1) -| path(MESH:Neuroprotection)
p(HGNC:DNM1) -> bp(GO:"programmed cell death")
p(HGNC:DNM1) neg path(MESH:"Mitochondrial Dynamics")
UNSET CellLine
UNSET Species
UNSET Confidence

SET Evidence = "Drp1 and Parkin
synergistically maintain the integrity of mitochondrial dynamics and
function in the mouse heart and brain by Parkin‐mediated mitophagy,
and it is noteworthy that the single deficiency (heterozygote knockout) of
Drp1 causes a mild effect on the degradation of mitochondria induced by
Parkin‐mediated mitophagy and mitochondrial connectivity compared
with the double deficiency (homozygote knockout) of Drp1 and Parkin."
SET Confidence = "High"
SET Species = "10090"
SET MeSHAnatomy = {"Brain","Heart"}
composite(p(HGNC:DNM1),p(HGNC:PRKN)) reg path(MESH:"Mitochondrial Dynamics")
composite(p(HGNC:DNM1),p(HGNC:PRKN)) -> act(p(HGNC:PRKN))
act(p(HGNC:PRKN)) -> bp(GO:mitophagy)
bp(GO:mitophagy) reg path(MESH:"Mitochondrial Dynamics")
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "S-nitrosylation
of Drp1 can increase the activity of its GTPase, causing increased
level of mitochondria."
SET Confidence = "High"
p(HGNC:DNM1,pmod(NO,Cys,644)) -> act(p(HGNC:DNM1),ma(GO:"GTPase activity"))
act(p(HGNC:DNM1),ma(GO:"GTPase activity")) -> a(MESH:Mitochondria)
UNSET Confidence

SET Evidence = "Increased levels of S‐nitrosylated Drp1 was found
by Haun et al. in the HD model of the mouse striatum and human
postmortem brain from HD patients because of the abundant
production of NO mediated by mutant Htt, causing the loss of dendritic
spines and synaptic injuries, which can be reversed by inhibiting the
region of S-nitrosylation on Drp1 through a non-nitrosylatable mutant
Drp1 (C644A) and the inhibition of NO production by using nitric oxide
inhibitors, suggesting that the S-nitrosylated Drp1 induced abnormal
mitochondrial dynamics plays a crital role in the pathogenesis of HD."
SET Confidence = "High"
SET MeSHDisease = "Huntington Disease"
SET Species = "10090"
p(NCBIGENE:15194,var("?")) -> a(CHEBI:"nitric oxide")
a(CHEBI:"nitric oxide") -> p(NCBIGENE:74006,pmod(NO,Cys,644))
p(NCBIGENE:74006,pmod(NO,Cys,644)) -| a(MESH:"Dendritic Spines")
SET MeSHAnatomy = "Synapses"
p(NCBIGENE:74006,pmod(NO,Cys,644)) -| path(MESH:"Wounds and Injuries")
UNSET MeSHAnatomy
UNSET Species
SET Species = "9606"
p(HGNC:HTT,var("?")) -> a(CHEBI:"nitric oxide")
a(CHEBI:"nitric oxide") -> p(HGNC:DNM1,pmod(NO,Cys,644))
p(HGNC:DNM1,pmod(NO,Cys,644)) -| a(MESH:"Dendritic Spines")
SET MeSHAnatomy = "Synapses"
p(HGNC:DNM1,pmod(NO,Cys,644)) -| path(MESH:"Wounds and Injuries")
UNSET MeSHAnatomy
UNSET Species
p(HGNC:DNM1,var("p.Cys644Ala")) -| p(HGNC:DNM1,pmod(NO,Cys,644))
p(HGNC:DNM1,pmod(NO,Cys,644)) neg path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1,pmod(NO,Cys,644)) -> path(DOID:"Huntington's disease")
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "In Shields et al.’s report, they found that mice lacking Drp1 in CA1 and
other forebrain neurons had impaired synaptic transmission and memory
leading to neuronal cell death followed by swelling mitochondria but no
change in the number of mitochondria in individual synaptic terminals."
SET Confidence = "High"
SET Species = "10090"
SET MeSHAnatomy = {"CA1 Region, Hippocampal","Basal Forebrain"}
p(HGNC:DNM1) pos path(OMIT:0010462)
p(HGNC:DNM1) pos bp(GO:memory)
p(HGNC:DNM1) -| bp(GO:"neuron death")
p(HGNC:DNM1) cnc a(MESH:Mitochondria, loc(GO:"synaptic vesicle"))
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "In
addition, they found the normal level of mitochondria‐derived ATP
cannot be maintained as well as impaired synaptic vesicle cycling of
specific Drp1 KO in the CA1 region of the mouse model by using
fluorescence resonance energy transfer (FRET) in spite of the increased
distance between axonal mitochondria (Shields et al., 2015)."
SET Confidence = "High"
SET Species = "10090"
SET MeSHAnatomy = "CA1 Region, Hippocampal"
p(HGNC:DNM1) -> tloc(a(CHEBI:ATP),fromLoc(GO:mitochondrion),toLoc(GO:"axon cytoplasm"))
p(HGNC:DNM1) pos bp(GO:"synaptic vesicle cycle")
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "The mitochondrial
correlated ataxia is caused by the unbalanced mitochondrial dynamics,
because of several reasons such as Purkinje neurons being one of the
largest neurons in the CNS, they need a very high level of ATP through
mitochondrial oxidative phosphorylation following high production of
ROS which harms the mitochondria itself, and the loss or dysregulation of
mitophagy which is a critical pathway for the maintenance of healthy
mitochondria through the clearance of damaged mitochondria by
autophagy."
SET Confidence = "High"
SET Cell = "Purkinje cell"
bp(GO:"oxidative phosphorylation") -> a(CHEBI:ATP)
a(CHEBI:ATP) -> a(CHEBI:"reactive oxygen species")
a(CHEBI:"reactive oxygen species") neg path(MESH:"Mitochondrial Dynamics")
bp(GO:"negative regulation of mitophagy") neg path(MESH:"Mitochondrial Dynamics")
SET MeSHAnatomy = "Mitochondria"
a(CHEBI:"reactive oxygen species") -> path(OMIT:0002643)
bp(GO:"negative regulation of mitophagy") -> path(OMIT:0002643)
UNSET MeSHAnatomy
UNSET Cell
UNSET Confidence

SET Evidence = "In Yamada et al.’s (2018) report, they use Cre recombinase
creating an postmitotic Purkinje neuron‐specific Drp1 KO mouse model
(L7‐Drp1KO) and found the single‐knockout of Drp1 can cause the death
of Purkinje neuronal cells, whereas double‐knockout of Drp1 and p62
extends the lifespan of Purkinje neuronal cells, suggesting autophagy
involve in the Drp1 KO‐induced Purkinje neuronal cells death because
p62 is an important autophagic cargo receptor."
SET Confidence = "High"
#L7-Drp1KO cell line
SET Species = "10090"
SET Cell = "Purkinje cell"
p(HGNC:DNM1) -> a(OMIT:0012627)
composite(p(HGNC:DNM1),p(HGNC:SQSTM1)) -| a(OMIT:0012627)
act(p(HGNC:SQSTM1),ma(GO:"cargo receptor activity"))
UNSET Cell
UNSET Species
UNSET Confidence

SET Evidence = "In their following studies,
they found that the Drp1 KO in Purkinje neuronal cells can lead to the
progressive Purkinje neuronal cells degeneration and ataxia with
decreased motor co‐ordination, increased mitochondrial size due to the
increased fusion activity of mitochondria through Drp1 KO, decreased
mitophagy and defective mitochondrial oxidative phosphorylation."
SET Confidence = "High"
SET Cell = "Purkinje cell"
p(HGNC:DNM1) -| path(OMIT:0002643)
p(HGNC:DNM1) pos bp(GO:"motor behavior")
p(HGNC:DNM1) -| bp(GO:"mitochondrial fusion")
p(HGNC:DNM1) -> bp(GO:mitophagy)
p(HGNC:DNM1) pos bp(GO:"oxidative phosphorylation")
UNSET Cell
UNSET Confidence

SET Evidence = "Kageyama et al. and Zhang et al. both focus on the Drp1 KO in the
cerebellum, they both using N‐acetylcysteine and coenzyme (an
antioxidant) to treat the Drp1 KO Purkinje neuronal cells finding
decreased degeneration of Purkinje cells and increased elongation of
mitochondria suggesting that the   at the
downstream of the Drp1 induced neural system dysfunctions."
SET Confidence = "High"
SET Cell = "Purkinje cell"
SET MeSHAnatomy = "Cerebellum"
bp(GO:"response to oxidative stress") -> act(p(HGNC:DNM1))
act(p(HGNC:DNM1)) -> path(DOID:"neurodegenerative disease")
UNSET MeSHAnatomy
UNSET Cell
UNSET Confidence

SET MeSHDisease = "Alzheimer Disease"

SET Evidence = "Drp1 has
been implicated in various cellular changes in the pathogenesis of
AD, such as the aberrations in the structure and function of
mitochondria, the decreased number of synapses, and the increased
inflammatory response."
SET Confidence = "High"
p(HGNC:DNM1) neg path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1) -| a(GO:synapse)
p(HGNC:DNM1) -> bp(GO:"inflammatory response")
UNSET Confidence

SET Evidence = "More and more reports (Baek et al., 2017; Feng et al., 2018; Han
et al., 2017; Joshi, Saw, Shamloo et al., 2018; Kandimalla, Manczak,
Yin, Wang, & Reddy, 2018; Placido et al., 2017; Reddy et al., 2018;
Reddy, Manczak, & Yin, 2017; Rui & Zheng, 2016) have shown that
the Drp1 can interact with Aβ and the phosphorylated Tau, which are
the two most reported pathogenic factors of AD causing excessive
mitochondrial division, mitochondrial dynamics dysfuction, and
synaptic degeneration leading to the dysregulation of the shape,
size, distribution of mitochondria, and mitochondrial dynamics mediated
axonal transport in AD neuronal cells."
SET Confidence = "High"
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> bp(GO:"mitochondrial fission")
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) neg path(MESH:"Mitochondrial Dynamics")
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> deg(a(GO:synapse))
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) neg bp(GO:"axonal transport")
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> bp(GO:"mitochondrial fission")
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) neg path(MESH:"Mitochondrial Dynamics")
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> deg(a(GO:synapse))
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) neg bp(GO:"axonal transport")
UNSET Confidence

SET Evidence = "The dysfunction of
mitochondrial dynamics is a major cause of the impaired synaptic
function and the neuronal cell injury, which eventually caused the
decline of memory and cognitive in AD patients."
SET Confidence = "High"
act(a(GO:synapse)) pos path(MESH:"Mitochondrial Dynamics")
SET Cell = "neuron"
path(MESH:"Wounds and Injuries") neg path(MESH:"Mitochondrial Dynamics")
UNSET Cell
path(MESH:"Mitochondrial Dynamics") -> bp(GO:memory)
path(MESH:"Mitochondrial Dynamics") -> bp(GO:cognition)
UNSET Confidence

SET Evidence = "Recent studies have demonstrated that the total reduction
(homozygous knockout) of Drp1 in mice can cause some developmental
abnormalities, such as decreased number of neurites and
defective synapse formation due to aggregated mitochondria
not distributing appropriately within cell processes, leading to
abnormal development of the forebrain, indicating the critical role
of mitochondrial division within the highly polarized neuronal cells."
SET Confidence = "High"
SET Species = "10090"
p(HGNC:DNM1) -> a(OMIT:0016987)
p(HGNC:DNM1) pos bp(GO:"immunological synapse formation")
SET CellStructure = "Mitochondria"
path(MESH:"Cell Aggregation") neg bp(GO:"immunological synapse formation")
path(MESH:"Cell Aggregation") -> path(HP:"Abnormality of forebrain morphology")
UNSET CellStructure
UNSET Species
UNSET Confidence

SET Evidence = "Drp1 partial KO mice are viable,
fertile, of normal body size, and phenotypically have no obvious
changes in mitochondrial proteins of synapses and dendrites, but
remarkable reduction in the production of H2O2 and the level of lipid
peroxidation compared with the WT mice, suggesting that Drp1
heterozygous KO can reduce excessive mitochondria division,
maintain mitochondrial and synaptic function in neurons, and may
become a novel research target for the treatment of AD or other
neural system disorders."
SET Confidence = "High"
SET Species = "10090"
p(HGNC:DNM1) -> a(CHEBI:"hydrogen peroxide")
p(HGNC:DNM1) -> bp(OMIT:0016017)
UNSET Species
UNSET Confidence

SET Evidence = "In their
work, they found that Drp1 interact with Aβ and hyperphosphorylated
Tau which increased as AD progressed, decreased level
of proteins important for the division of mitochondria (Drp1 and
FIS1), increased level of proteins important for the fusion of
mitochondria (Mfn1, Mfn2, and Opa1), the biogenesis of mitochondria
(Nrf1, Nrf2, PGC1α, and TFAM), and the synaptic gene
(synaptophysin and PSD95, etc.) by using immunoprecipitation and
immunostaining analysis and these findings above can be ameliorated
by the partial reduction of Drp1 leading to a decrease of Aβ and
phosphorylated‐induced mitochondrial and synaptic toxicity finally
maintaining the mitochondrial dynamics and neuronal function in the
APPXDrp1 and TauXDrp1 heterozygote KO mouse."
SET Confidence = "High"
#in 6‐months old, Drp1 heterozygote KO, Tau, APP, APPXDrp1 heterozygote KO, TauXDrp1 heterozygote
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> path(DOID:"Alzheimer's disease")
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> path(DOID:"Alzheimer's disease")
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -| p(HGNC:DNM1)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -| p(HGNC:DNM1)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -| p(HGNC:FIS1)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -| p(HGNC:FIS1)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> p(HGNC:MFN1)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> p(HGNC:MFN1)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> p(HGNC:MFN2)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> p(HGNC:MFN2)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> p(HGNC:OPA1)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> p(HGNC:OPA1)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> p(HGNC:NRF1)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> p(HGNC:NRF1)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> p(HGNC:NFE2L2)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> p(HGNC:NFE2L2)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> p(HGNC:PPARGC1A)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> p(HGNC:PPARGC1A)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> p(HGNC:TFAM)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> p(HGNC:TFAM)
p(HGNC:NRF1) -> a(MESH:Mitochondria)
p(HGNC:NFE2L2) -> a(MESH:Mitochondria)
p(HGNC:PPARGC1A) -> a(MESH:Mitochondria)
p(HGNC:TFAM) -> a(MESH:Mitochondria)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> g(HGNC:SYP)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> g(HGNC:SYP)
composite(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -> g(HGNC:DLG4)
composite(p(HGNC:DNM1),p(HGNC:MAPT,pmod(Ph))) -> g(HGNC:DLG4)
p(HGNC:DNM1) -> a(CHEBI:"amyloid-beta")
p(HGNC:DNM1) -> p(HGNC:MAPT,pmod(Ph))
SET MeSHAnatomy = {"Synapses","Mitochondria"}
p(HGNC:MAPT,pmod(Ph)) -> path(NCIT:C27990)
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Confidence

SET MeSHDisease = "Parkinson Disease"

SET Evidence = "LRRK2, a pathogenic gene of PD, has many functions including
regulating the transportation of vesicles, the autophagy, oxidative
stress, neurotoxicity, and mitochondrial dynamics."
SET Confidence = "High"
p(HGNC:LRRK2) reg a(MESH:"Transport Vesicles")
p(HGNC:LRRK2) reg bp(GO:autophagy)
p(HGNC:LRRK2) reg path(NCIT:C17741)
p(HGNC:LRRK2) reg path(MESH:"Neurotoxicity Syndromes")
p(HGNC:LRRK2) reg path(MESH:"Mitochondrial Dynamics")
UNSET Confidence

SET Evidence = "In Ho et al.’s (2018)
report, they found that increased kinase activity of LRRK2 due to
the presence of the LRRK2 variant‐G2019S, which binds with and
phosphorylates the Drp1 at T595 or s616 increasing its activity
promoting the fission of mitochondria causing mitochondria‐derived
apoptosis and production of ROS in the G2019S transgenic mouse
brain, suggesting that the G2019S can lead to PD by affecting
mitochondrial dynamics, increasing the fission of mitochondria."
SET Confidence = "High"
SET Species = "10090"
SET MeSHAnatomy = "Brain"
p(HGNC:LRRK2,var("p.Gly2019Ser")) -> act(p(HGNC:LRRK2),ma(GO:"kinase activity"))
complex(p(HGNC:LRRK2,var("p.Gly2019Ser")),p(HGNC:DNM1)) -> p(HGNC:DNM1,pmod(Ph,Thr,595))
complex(p(HGNC:LRRK2,var("p.Gly2019Ser")),p(HGNC:DNM1)) -> p(HGNC:DNM1,pmod(Ph,Ser,616))
p(HGNC:DNM1,pmod(Ph,Thr,595)) -> act(p(HGNC:DNM1))
p(HGNC:DNM1,pmod(Ph,Ser,616)) -> act(p(HGNC:DNM1))
act(p(HGNC:DNM1)) -> bp(GO:"mitochondrial fission")
bp(GO:"mitochondrial fission") -> bp(GO:"programmed cell death")
bp(GO:"programmed cell death") -> a(CHEBI:"reactive oxygen species")
p(HGNC:LRRK2,var("p.Gly2019Ser")) -> path(DOID:"Parkinson's disease")
p(HGNC:LRRK2,var("p.Gly2019Ser")) -> bp(GO:"mitochondrial fission")
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "They also found that treatment of LPS can cause the
activation of microglia, following the increased level of LRRK2 and
the increased division of mitochondria, leading to the occurrence of
the neuroinflammation, which can be ameliorated by the downregulation
or inhibition of the LRRK2."
SET Confidence = "High"
a(CHEBI:lipopolysaccharide) -> act(a(NCIT:C12616))
act(a(NCIT:C12616)) -> p(HGNC:LRRK2)
p(HGNC:LRRK2) -> bp(GO:"mitochondrial fission")
SET Cell = "neuron"
bp(GO:"mitochondrial fission") -> path(MESH:Inflammation)
UNSET Cell
UNSET Confidence

SET Evidence = "Interestingly, the E193K does not
affect the kinase activity of LRRK2 compared with the G2019S,
suggesting that E193K does not affect the activity of Drp1 but
affects LRRK2 phosphorylation at Ser‐935 and increases the
mitochondrial response to MPP+ (a mitochondrial toxin can induce
PD) in the patients’ fibroblast."
SET Confidence = "High"
#MPP+ - N-methyl-4-phenylpyridinium
SET MeSHAnatomy = "Fibroblasts"
p(HGNC:LRRK2,var("p.Glu192Lys")) cnc act(p(HGNC:LRRK2),ma(GO:"kinase activity"))
p(HGNC:LRRK2,var("p.Glu192Lys")) cnc act(p(HGNC:DNM1))
p(HGNC:LRRK2,var("p.Glu192Lys")) -- p(HGNC:LRRK2,pmod(Ph,Ser,935))
p(HGNC:LRRK2,var("p.Glu192Lys")) -> a(CHEBI:"N-methyl-4-phenylpyridinium")
a(CHEBI:"N-methyl-4-phenylpyridinium") -- a(MESH:Mitochondria)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "E193K can also interact with Drp1
identified by the immunoprecipitation assay, what is important is that
E193K can decrease the affinity of LRRK2 toward Drp1, decreasing
the division of mitochondria, although E193K does not affect Drp1
directly."
SET Confidence = "High"
p(HGNC:LRRK2,var("p.Glu192Lys")) -| complex(p(HGNC:LRRK2),p(HGNC:DNM1))
p(HGNC:LRRK2,var("p.Glu192Lys")) -| bp(GO:"mitochondrial fission")
UNSET Confidence

SET Evidence = "In the previous work of Zhao et al.,
they found that Parkin/PINK1 mainly produces the effect by
promoting mitochondrial fusion because knockdown of endogenous
PINK1 or overexpression of the mutant PINK1 will cause severe
mitochondrial division in N27 cells."
SET Confidence = "High"
#N27 Cells
p(HGNC:PINK1) -| bp(GO:"mitochondrial fission")
p(HGNC:PRKN,var("?")) => bp(GO:"mitochondrial fission")
UNSET Confidence

SET Evidence = "NO can upregulate the level of Drp1 in the
MPP+ induced PD mouse model due to the S‐nitrosylated Parkin
caused by the excessive NO and SNO‐Parkin cannot execute the
function of promoting the Drp1 for proteasome degradation leading
to the upregulation of Drp1 and mitochondrial fission, which increase
the production of ROS and the apoptosis of the neuronal cells,
eventually causing PD (Buhlman et al., 2014; Kageyama et al., 2014;
Roy, Itoh, Iijima, & Sesaki, 2016; Tang, Hu, Tan, Yang, & Zeng, 2016;
H. Wang et al., 2011; Zhao et al., 2013)."
SET Confidence = "High"
SET Species = "10090"
composite(a(CHEBI:"nitric oxide"),a(CHEBI:"N-methyl-4-phenylpyridinium")) -> p(HGNC:DNM1)
composite(a(CHEBI:"nitric oxide"),a(CHEBI:"N-methyl-4-phenylpyridinium")) -> p(HGNC:PRKN,pmod(NO))
p(HGNC:PRKN,pmod(NO)) -| deg(p(HGNC:DNM1))
p(HGNC:DNM1) -> path(DOID:"Parkinson's disease")
UNSET Species
UNSET Confidence

SET Evidence = "It is reported by Zhao et al. (2013) that mutant PINK1 could
cause collapsed mitochondrial dynamics, neuronal cell death, and the
dysfunction of energy production, such as decreased ATP generation
and impaired mitochondrial membrane potential through W437X and
K219M (mutants of PINK1), or inhibiting PINK1 in the OGD neuronal
model."
SET Confidence = "High"
#W437X - nonsense mutation
p(HGNC:PINK1) pos path(MESH:"Mitochondrial Dynamics")
p(HGNC:PINK1) -| bp(GO:"neuron death")
p(HGNC:PINK1) -> a(CHEBI:ATP)
p(HGNC:PINK1) pos bp(OMIT:0025241)
p(HGNC:PINK1,var("p.Try437?")) neg path(MESH:"Mitochondrial Dynamics")
p(HGNC:PINK1,var("p.Try437?")) -> bp(GO:"neuron death")
p(HGNC:PINK1,var("p.Try437?")) -| a(CHEBI:ATP)
p(HGNC:PINK1,var("p.Try437?")) neg bp(OMIT:0025241)
p(HGNC:PINK1,var("p.Lys219Met")) neg path(MESH:"Mitochondrial Dynamics")
p(HGNC:PINK1,var("p.Lys219Met")) -> bp(GO:"neuron death")
p(HGNC:PINK1,var("p.Lys219Met")) -| a(CHEBI:ATP)
p(HGNC:PINK1,var("p.Lys219Met")) neg bp(OMIT:0025241)
bp(GO:"response to oxygen-glucose deprivation") -| p(HGNC:PINK1)
UNSET Confidence

SET Evidence = "In addition, neuronal cell death can be reversed by the
overexpression of PINK1, which can suppress the transportation of
Drp1 to the mitochondria."
SET Confidence = "High"
p(HGNC:PINK1) -| tloc(p(HGNC:DNM1L),fromLoc(GO:cytosol),toLoc(GO:"mitochondrial outer membrane"))
UNSET Confidence

SET Evidence = "Taken together,
PINK1 can restore mitochondrial function and decrease the
mitochondrial division induced by ischemia protecting the neuronal
cells from the ischemic damage through inhibiting the transportation
of Drp1 from cytosol to the mitochondria."
SET Confidence = "High"
SET Cell = "neuron"
p(HGNC:PINK1) -| bp(GO:"mitochondrial fission")
p(HGNC:PINK1) -| path(DOID:ischemia)
UNSET Cell
UNSET Confidence
UNSET MeSHDisease

SET MeSHDisease = "Huntington Disease"

SET Evidence = "It is well documented that the mutant Htt can
interact with the Drp1 causing dysfunction of the mitochondrial
dynamics and aberration of the axonal transportation finally leading
to the emergence of HD."
SET Confidence = "High"
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L)) neg path(MESH:"Mitochondrial Dynamics")
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L)) neg bp(GO:"axonal transport")
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L)) -> path(DOID:"Huntington's disease")
UNSET Confidence

SET Evidence = "In Song et al.’s report, they
found that the abnormal interaction between Drp1 and mutant Htt
impairs the mitochondrial fission–fusion balance and thereby causes
neuronal injury in rat HD model neurons and fibroblasts of HD
patients suggesting that the mutant Htt could trigger mitochondrial
division by the abnormal interaction with Drp1 and stimulating its
enzymatic activity."
SET Confidence = "High"
SET Species = {"10116","9606"}
SET MeSHAnatomy = {"Neurons","Fibroblasts"}
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L)) -> act(p(HGNC:DNM1L))
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L)) -> bp(GO:"mitochondrial fission")
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "They also found that the abnormal interaction induced
mitochondrial fission can also decrease the anterograde and
retrograde axonal transport of mitochondria in synapses and cause
neuronal cell death, which depends on the polyQ length, suggesting
that the polyQ length determines disease onset and severity, thereby
a longer expansion causes earlier onset of HD."
SET Confidence = "High"
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L)) -> bp(GO:"neuron death")
UNSET Confidence

SET Evidence = "All these findings
above can be reversed by the dominant‐negative Drp1 K38A mutant."
SET Confidence = "High"
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L,var("p.Lys38Ala"))) pos path(MESH:"Mitochondrial Dynamics")
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L,var("p.Lys38Ala"))) pos bp(GO:"axonal transport")
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L,var("p.Lys38Ala"))) -| bp(GO:"neuron death")
UNSET Confidence

SET Evidence = "The experiment by Shirendeb et al. verified the findings of Chen
et al., they also found that the mutant Htt can interact with Drp1
enhancing the GTPase activity of Drp1 causing impaired axonal
transportation, loss of synapses, and neuronal cell death in the
postmortem HD patient brain specimen and in mutant Htt transgenic
BACHD (a type of Huntinton's disease mouse model) mice developed
by Gary et al, which can express the full‐length DNA (about 170 kb)
of Htt."
SET Confidence = "High"
#BACHD
SET Species = {"10090","9606"}
complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L)) -> act(p(HGNC:DNM1L),ma(GO:"GTPase activity"))
act(p(HGNC:DNM1L),ma(GO:"GTPase activity")) neg bp(GO:"axonal transport")
act(p(HGNC:DNM1L),ma(GO:"GTPase activity")) -| a(NCIT:C13281)
act(p(HGNC:DNM1L),ma(GO:"GTPase activity")) -> bp(GO:"neuron death")
UNSET Species
UNSET Confidence

SET Evidence = "In their report, they found that mutant Htt can cause
excessive production of NO which mediates the S‐nitrosylation of
Drp1 increasing the division of mitochondrial and the loss synapses,
deteriorating the neuronal damage in BACHD mice model cortical
neurons as well as in postmortem HD patients’ brain specimens."
SET Confidence = "High"
SET Species = "10090"
SET MeSHAnatomy = "Neocortex"
p(NCBIGENE:15194,pmod(CONSO:"CAG repeats")) -> a(CHEBI:"nitric oxide")
a(CHEBI:"nitric oxide") -> p(NCBIGENE:74006,pmod(NO))
p(NCBIGENE:15194,pmod(CONSO:"CAG repeats")) -> p(NCBIGENE:74006,pmod(NO))
p(NCBIGENE:74006,pmod(NO)) -> bp(GO:"mitochondrial fission")
p(NCBIGENE:74006,pmod(NO)) -> bp(GO:"neuron death")
UNSET MeSHAnatomy
UNSET Species
SET Species = "9606"
SET MeSHAnatomy = "Brain"
p(HGNC:HTT,pmod(CONSO:"CAG repeats")) -> a(CHEBI:"nitric oxide")
a(CHEBI:"nitric oxide") -> p(HGNC:DNM1L,pmod(NO))
p(HGNC:DNM1L,pmod(NO)) -> bp(GO:"mitochondrial fission")
p(HGNC:DNM1L,pmod(NO)) -> bp(GO:"neuron death")
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = "In
addition, they found that the S‐nitrosylation of Drp1 could promote
the Drp1 interacting with the mutant Htt suggesting that the
S‐nitrosylation function has an important role in promoting the Drp1
interacting with the mutant Htt and it is possible that the mutant
Htt‐induced mitochondrial fragmentation and neuronal injury is
mediated by SNO‐Drp1."
SET Confidence = "High"
p(HGNC:DNM1L,pmod(NO)) -> complex(p(HGNC:HTT,pmod(CONSO:"CAG repeats")),p(HGNC:DNM1L))
p(HGNC:HTT,pmod(CONSO:"CAG repeats")) -> bp(GO:"mitochondrial fission")
SET MeSHAnatomy = "Neurons"
p(HGNC:HTT,pmod(CONSO:"CAG repeats")) -> path(MESH:"Wounds and Injuries")
p(HGNC:DNM1L,pmod(NO)) -> path(MESH:"Wounds and Injuries")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Importantly, these bad effect toward
neurons caused by mutant Htt‐induced aberrant mitochondrial
dynamics all can be reversed by the non-nitrosylatable mutant
Drp1 (C644A)."
SET Confidence = "High"
p(HGNC:DNM1L,var("p.Cys644Ala")) -|  bp(GO:"mitochondrial fission")
SET MeSHAnatomy = "Neurons"
p(HGNC:DNM1L,var("p.Cys644Ala")) -| path(MESH:"Wounds and Injuries")
UNSET MeSHAnatomy
p(HGNC:DNM1L,var("p.Cys644Ala")) pos path(MESH:"Mitochondrial Dynamics")
UNSET Confidence

SET Evidence = "Here, in Roe and Qi’s
(2018) paper, they show that the MAPK1 plays a critical role in the
dysfunction of mitochondria and the neurodegeneration in HD by
interaction with Drp1 and phosphorylates Drp1 at Ser‐616 causing
the overactivation of the Drp1 finally leading to the aberrant
mitochondrial dynamics in the HD knockin mouse‐derived striatal
cells."
SET Confidence = "High"
SET Species  = "10090"
SET Cell = "striatum neuron"
complex(p(HGNC:MAPK1),p(HGNC:DNM1L)) -> p(HGNC:DNM1L,pmod(Ph,Ser,616))
p(HGNC:MAPK1) -- path(MESH:"Mitochondrial Dynamics")
p(HGNC:MAPK1) -- path(DOID:"neurodegenerative disease")
p(HGNC:DNM1L,pmod(Ph,Ser,616)) => act(p(HGNC:DNM1L))
p(HGNC:DNM1L,pmod(Ph,Ser,616)) neg path(MESH:"Mitochondrial Dynamics")
UNSET Cell
UNSET Species
UNSET Confidence

SET Evidence = "U0126, an inhibitor of MEK1/2, and Drp1 S616A, a Drp1
phosphorylation‐deficient mutant, both decrease the abnormal
mitochondrial division seen in the HD, but U0126 cannot restore
the impaired mitochondrial morphology, just reduce the level of
depolarization in mitochondria and the production of reactive oxygen
species in the striatal cells of HD mouse, which express the
Drp1S616A compared with the wild‐type cells, suggesting the
phosphorylated Drp1 at the downstream of MAPK1 pathway."
SET Confidence = "High"
a(CHEBI:U0126) pos path(MESH:"Mitochondrial Dynamics")
p(HGNC:DNM1L,var("p.Ser616Ala")) pos path(MESH:"Mitochondrial Dynamics")
SET Species = "10090"
SET Cell = "striatum neuron"
p(HGNC:DNM1L,var("p.Ser616Ala")) -> a(CHEBI:"reactive oxygen species")
a(CHEBI:U0126) -| a(CHEBI:"reactive oxygen species")
a(CHEBI:U0126) reg bp(GO:"mitochondrial depolarization")
UNSET Cell
UNSET Species
UNSET Confidence

UNSET MeSHDisease

SET MeSHDisease = "Amyotrophic Lateral Sclerosis"

SET Evidence = "Increasing studies suggest that aberrant mitochondrial dynamics
is one of the critical contributors of ALS, which is characterized by
increased mitochondrial division and ROS production mediated by
the hyperactivation of Drp1 causing bioenergetic failure and oxidative
stress in the ALS finally leading to the degeneration of motor
neurons."
SET Confidence = "High"
path(DOID:"amyotrophic lateral sclerosis") neg path(MESH:"Mitochondrial Dynamics")
path(DOID:"amyotrophic lateral sclerosis") -> bp(GO:"mitochondrial fission")
path(DOID:"amyotrophic lateral sclerosis") -> a(CHEBI:"reactive oxygen species")
path(DOID:"amyotrophic lateral sclerosis") ->  act(p(HGNC:DNM1L))
act(p(HGNC:DNM1L)) neg bp(MESH:"Energy Metabolism")
act(p(HGNC:DNM1L)) -> bp(NCIT:C17741)
bp(MESH:"Energy Metabolism") neg path(DOID:"motor neuron disease")
bp(NCIT:C17741) -> path(DOID:"motor neuron disease")
UNSET Confidence

SET Evidence = "P110, a selective Drp1–FIS1 interaction
inhibitor without affecting the basal mitochondrial fission, was found
to improve the structure and function and significantly reduce the
production of ROS causing increased neuronal cell viability and
decreased muscle atrophy in SOD G93A mice suggesting that
inhibition of the abnormal mitochondrial fission by the Drp1/FIS1
inhibitor ameliorate the disease progression of ALS."
SET Confidence = "High"
# P110, a heptapeptide conjugated to TAT 47–57
p(HGNC:TAT,frag("47_57")) -| complex(p(HGNC:FIS1),p(HGNC:DNM1L))
p(HGNC:TAT,frag("47_57")) cnc bp(GO:"mitochondrial fission")
p(HGNC:TAT,frag("47_57")) -| a(CHEBI:"reactive oxygen species")
p(HGNC:TAT,frag("47_57")) -| bp(GO:"neuron death")
SET Species = "10090"
p(HGNC:SOD1,var("p.Gly93Ala")) -> path(DOID:"muscular atrophy")
p(HGNC:TAT,frag("47_57")) -| path(DOID:"muscular atrophy")
p(HGNC:TAT,frag("47_57")) -| path(DOID:"amyotrophic lateral sclerosis")
UNSET Species
UNSET Confidence
UNSET MeSHDisease
