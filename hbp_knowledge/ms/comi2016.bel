#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Evolving concepts in the treatment of relapsing multiple sclerosis"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Trusha Adeshara"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
 DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
 DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
 DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
 #DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
 DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
 DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
 #DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 #DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"
DEFINE NAMESPACE NCIT           AS PATTERN "^C\d+$"
###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
 DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
 #DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
 DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
 DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
# DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
 DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
 DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "30988501"}

SET Disease = "relapsing-remitting multiple sclerosis"

SET Evidence = "Interferon beta was the ﬁrst drug to be approved for relapsing-remitting multiple sclerosis.3–5 It is a naturally occurring cytokine with anti-inﬂammatory properties that induces transcription of many genes by binding to a speciﬁc cell-surface receptor; the overall eﬀect is an anti-inﬂ ammatory response. 17"

p(HGNC:IFNB1) -| path(MESH:"Multiple Sclerosis, Relapsing-Remitting")
p(HGNC:IFNB1) isA a(CHEBI:"anti-inflammatory agent")

SET Evidence = "Interferon beta is also associated with ﬂu-like symptoms, but serious adverse events occur rarely.3–5"

#NCIT:C78302 = flu like symptoms
p(HGNC:IFNB1) -> path(NCIT:C78302)

SET Evidence = "Glatiramer acetate can cause a self-limiting acute systemic post-injection reaction with palpitations, chest tightness, and anxiety."

a(MESH:"Glatiramer Acetate") -> path(MESH:"Injection Site Reaction")
a(MESH:"Glatiramer Acetate") -> path(HP:Palpitations)
a(MESH:"Glatiramer Acetate") -> path(HP:"Chest tightness")
a(MESH:"Glatiramer Acetate") -> path(HP:Anxiety)

SET Evidence = "Teriﬂunomide is the active metabolite of leﬂunomide, a dihydroorotate dehydrogenase inhibitor that inhibits the proliferation of autoreactive B and T cells."

a(DRUGBANK:Leflunomide) -> a(DRUGBANK:Teriflunomide)
a(DRUGBANK:Leflunomide) isA a(CHEBI:"EC 1.3.5.2 [dihydroorotate dehydrogenase (quinone)] inhibitor")
SET MeSHAnatomy = {"T-Lymphocytes","B-Lymphocytes"}
a(DRUGBANK:Leflunomide) -| bp(MESH:"Cell Proliferation")
UNSET MeSHAnatomy

SET Evidence = "Additionally, teriﬂunomide has immunomodulatory properties by inducing a shift to an anti-inﬂammatory cytokine proﬁle and a switch in the class of immunoglobulins.24"

a(DRUGBANK:Teriflunomide) -> bp(MESH:Immunomodulation)
a(DRUGBANK:Teriflunomide) -> bp(MESH:"Immunoglobulin Class Switching")

SET Evidence = "The most frequent adverse eﬀects observed with teriﬂunomide are headache, increased liver enzymes, diarrhoea, hair thinning, and nausea.11,12,25"

a(DRUGBANK:Teriflunomide) -> path(HP:Headache)
a(DRUGBANK:Teriflunomide) -> p(MESH:Enzymes,loc(MESH:Liver))
a(DRUGBANK:Teriflunomide) -> path(MESH:Diarrhea)
a(DRUGBANK:Teriflunomide) -> path(MESH:Alopecia)
a(DRUGBANK:Teriflunomide) -> path(MESH:Nausea)

SET Evidence = "Dimethyl fumarate has a dual eﬀect. By activating the transcription factor nuclear-factor-E2-related factor 2 (Nrf2), it has an antioxidant eﬀect, and through inhibition of the transcription factor nuclear-factor κB it reduces the release of inﬂammatory cytokines.28 "

a(CHEBI:"dimethyl fumarate") -> act(p(HGNC:NFE2L2),ma(GO:"antioxidant activity"))
a(CHEBI:"dimethyl fumarate") isA a(CHEBI:antioxidant)
a(CHEBI:"dimethyl fumarate") -| p(HGNC:NFKB1)
p(HGNC:NFKB1) -> a(MESH:Cytokines)
a(CHEBI:"dimethyl fumarate") -| a(MESH:Cytokines)
a(CHEBI:"dimethyl fumarate") -| p(HGNC:NFKB1)
a(CHEBI:"dimethyl fumarate") -| path(MESH:Inflammation)

SET Evidence = "Flushing and gastrointestinal symptoms are the most frequent side-eﬀects of dimethyl fumarate;13,14 they have been reported in 40% and 30% of patients, respectively, but tend to decrease after the ﬁrst 2–3 months and lead to an interruption of the treatment in few patients."

a(CHEBI:"dimethyl fumarate") -> path(MESH:Flushing)
a(CHEBI:"dimethyl fumarate") -> path(MESH:"Gastrointestinal Diseases")

SET Evidence = "After the approval of dimethyl fumarate, a few cases of progressive multifocal leukoencephalopathy were reported during treatment."

a(CHEBI:"dimethyl fumarate") -> path(DOID:"progressive multifocal leukoencephalopathy")

SET Evidence = "Fingolimod is a sphingosine 1-phosphate analogue that acts as a functional antagonist of sphingosine 1-phosphate receptors."

complex(p(HGNCGENEFAMILY:"Sphingosine 1-phosphate receptors"),a(DRUGBANK:Fingolimod)) -| p(HGNCGENEFAMILY:"Sphingosine 1-phosphate receptors")

SET Evidence = "After oral administration, ﬁngolimod is immediately phosphorylated and bound to the sphingosine 1-phosphate receptor, which is stimulated and subsequently internalised and degraded."

#a(DRUGBANK:Fingolimod) -> a(DRUGBANK:Fingolimod,pmod(Ph))
#a(DRUGBANK:Fingolimod,pmod(Ph)) -> complex(p(HGNCGENEFAMILY:"Sphingosine 1-phosphate receptors"),a(DRUGBANK:Fingolimod))
#a(DRUGBANK:Fingolimod,pmod(Ph)) is giving general parser failure
complex(p(HGNCGENEFAMILY:"Sphingosine 1-phosphate receptors"),a(DRUGBANK:Fingolimod)) -> deg(p(HGNCGENEFAMILY:"Sphingosine 1-phosphate receptors"))

SET Evidence = "In a head-to head comparison with intramuscular interferon beta-1a 30 μg weekly, oral ﬁngolimod reduced the annualised relapse rate by 52%.32"

a(DRUGBANK:Fingolimod) -| path(MESH:Recurrence)

SET Evidence = "After the ﬁrst dose is administered, some patients develop symptomatic bradycardia or atrioventricular conduction block (fewer than 2%).33"

#of Fingolimod
a(DRUGBANK:Fingolimod) -> path(HP:Bradycardia)
a(DRUGBANK:Fingolimod) -> path(MESH:"Atrioventricular Block")

SET Evidence = "Other adverse events include macula oedema, elevated liver function tests, and increased risk of infections.34"

#of Fingolimod
a(DRUGBANK:Fingolimod) -> path(MESH:"Macular Edema")
a(DRUGBANK:Fingolimod) -> path(MESH:"Liver Function Tests")
a(DRUGBANK:Fingolimod) -> path(MESH:Infection)

SET Evidence = "Speciﬁcally, rare fatal herpes simplex encephalitis and disseminated varicella zoster infections have occurred; thus a varicella zoster virus antibody test is mandatory before starting ﬁngolimod and, if necessary, vaccination is required."

#of Fingolimod
a(DRUGBANK:Fingolimod) -> path(HP:"Herpes simplex encephalitis")
a(DRUGBANK:Fingolimod) -> path(HP:"Severe varicella zoster infection")

SET Evidence = "Rare cases of opportunistic infections, cryptococcical meningitis, and progressive multifocal leukoencephalopathy, even without previous exposure to natalizumab, have been reported.35"

a(DRUGBANK:Natalizumab) -> path(MESH:"Opportunistic Infections")
a(DRUGBANK:Natalizumab) -> path(DOID:"progressive multifocal leukoencephalopathy")
a(DRUGBANK:Natalizumab) -> path(MESH:"Meningitis, Cryptococcal")

SET Evidence = "Natalizumab is a humanised therapeutic monoclonal antibody that acts by blocking α-4 integrin, a component of the very late antigen (VLA)-4 present on lymphocytes, and by inhibiting the interaction between VLA4 and the ligand VCAM, preventing lymphocytes from entering the CNS across the blood–brain barrier.36"

a(DRUGBANK:Natalizumab) isA p(MESH:"Antibodies, Monoclonal")
a(DRUGBANK:Natalizumab) -| p(HGNC:ITGA4)
a(DRUGBANK:Natalizumab) -| complex(p(MESH:"Receptors, Very Late Antigen",loc(MESH:Lymphocytes)),p(HGNC:VCAM1))
a(DRUGBANK:Natalizumab) -| bp(MESH:"Transendothelial and Transepithelial Migration")

SET Evidence = "Alemtuzumab is a humanised monoclonal antibody directed against CD52, which is present on the surface of lymphocytes and monocytes. After administering alemtuzumab, 12 mg daily for 5 consecutive days, a long-lasting depletion of B and T cells occurs."

a(DRUGBANK:Alemtuzumab) isA p(MESH:"Antibodies, Monoclonal")
a(DRUGBANK:Alemtuzumab) -> complex(a(DRUGBANK:Alemtuzumab),p(HGNC:CD52,loc(MESH:Lymphocytes)))
a(DRUGBANK:Alemtuzumab) -> complex(a(DRUGBANK:Alemtuzumab),p(HGNC:CD52,loc(MESH:Monocytes)))
a(DRUGBANK:Alemtuzumab) -> deg(a(MESH:"B-Lymphocytes"))
a(DRUGBANK:Alemtuzumab) -> deg(a(MESH:"T-Lymphocytes"))

SET Evidence = "In a comparative trial with interferon beta-1a, alemtuzumab signiﬁcantly reduced the annualised relapse rate and reduced a conﬁrmed worsening of multiple sclerosis.15,16"

a(DRUGBANK:Alemtuzumab) -| path(MESH:Recurrence)
a(DRUGBANK:Alemtuzumab) -| path(DOID:"multiple sclerosis")

SET Evidence = "The most frequent adverse event with alemtuzumab therapy is infusion reaction, which occurs in most patients but can be substantially reduced by pretreatment with corticosteroids and antihistamines."

a(DRUGBANK:Alemtuzumab) -> path(MESH:"Injection Site Reaction")
a(CHEBI:corticosteroid) -| path(MESH:"Injection Site Reaction")
a(MESH:"Histamine Antagonists") -| path(MESH:"Injection Site Reaction")

SET Evidence = "Herpetic and fungal infections can also develop; a prophylactic treatment with acyclovir is required for 1 month after each infusion.49"

#due to Alemtuzumab
a(DRUGBANK:Alemtuzumab) -> path(MESH:"Stomatitis, Herpetic")
a(DRUGBANK:Alemtuzumab) -> path(MESH:Mycoses)
#after infusion of alemtuzumab
a(DRUGBANK:Acyclovir) -| path(MESH:"Stomatitis, Herpetic")
a(DRUGBANK:Acyclovir) -| path(MESH:Mycoses)

SET Evidence = "More than 30% of patients develop thyroid disorders, and immune thrombocytopenia occurs in 1–3% of patients.49 Glomerulonephritis has been observed in 0·3% of patients.15,16 Autoimmune disorders usually occur 2–3 years after the ﬁrst dose of alemtuzumab.15,16"

#due to Alemtuzumab
a(DRUGBANK:Alemtuzumab) -> path(MESH:"Thyroid Diseases")
a(DRUGBANK:Alemtuzumab) -> path(DOID:thrombocytopenia)
a(DRUGBANK:Alemtuzumab) -> path(DOID:glomerulonephritis)
a(DRUGBANK:Alemtuzumab) -> path(MESH:"Autoimmune Diseases")

SET Evidence = "Daclizumab, already approved in the USA, is a monoclonal antibody acting as an interleukin 2 inhibitor.51 In the phase 3 trial, monthly subcutaneous administration of daclizumab signiﬁcantly decreased the annualised relapse rate by 45% compared with 30 μg interferon beta-1a.51"

a(DRUGBANK:Daclizumab) isA p(MESH:"Antibodies, Monoclonal")
a(DRUGBANK:Daclizumab) -| p(HGNC:IL2)
a(DRUGBANK:Daclizumab) -| path(MESH:Recurrence)

SET Evidence = "Ocrelizumab is a humanised monoclonal antibody directed against CD20."

a(DRUGBANK:Ocrelizumab) isA p(MESH:"Antibodies, Monoclonal")
a(DRUGBANK:Ocrelizumab) -> complex(a(DRUGBANK:Ocrelizumab),p(HGNC:MS4A1))

SET Evidence = "Ocrelizumab signiﬁcantly reduced the annualised relapse rate by 46% and 47% respectively."

a(DRUGBANK:Ocrelizumab) -| path(MESH:Recurrence)

SET Evidence = "The eﬃcacy of ocrelizumab is supported also by clinical experience with rituximab, a chimeric anti-CD20 monoclonal antibody, in multiple sclerosis.53"

a(DRUGBANK:Rituximab) isA p(MESH:"Antibodies, Monoclonal")
a(DRUGBANK:Rituximab) -> complex(a(DRUGBANK:Ocrelizumab),p(HGNC:MS4A1))

SET Evidence = "A natural history study conﬁrmed that multiple sclerosis is a serious disease; approximately 80% of patients develop severe disability 54 and life expectancy is reduced by 10 years.55"

#NCIT:C21007 = disability
path(DOID:"multiple sclerosis") -> path(NCIT:C21007)
path(DOID:"multiple sclerosis") -| path(MESH:"Life Expectancy")

SET Evidence = "Both MRI 57,58 and optical coherence tomography studies 59,60 have provided clear evidence that early axonal losses already occur in patients with a ﬁrst episode of multiple sclerosis (clinically isolated syndrome), and even earlier in radiologically isolated syndromes (patients without any clinical symptoms).61"

path(MESH:"Demyelinating Diseases") -> path(HP:"Axonal loss")
path(DOID:"multiple sclerosis") -> path(HP:"Axonal loss")

SET Evidence = "Inﬂammatory activity—the target of all the approved disease-modifying therapies in relapsing multiple sclerosis—from an impairment of adaptive peripheral immunity 65,66 predominates in the initial phase of the disease and tends to decline with the evolution of the disease, when other pathogenic mechanisms mostly related to innate immunity take a major role inside the CNS."

path(MESH:"Multiple Sclerosis, Relapsing-Remitting") -| bp(MESH:"Adaptive Immunity")
path(MESH:"Multiple Sclerosis, Relapsing-Remitting") -> path(MESH:Inflammation)

SET Evidence = "An Italian observational study in patients with relapsing-remitting multiple sclerosis showed that interferon beta treatment decreased the rate of conversion to the secondary progressive phase and the proportion of patients reaching irreversible disability (deﬁned as reaching the milestone of EDSS score of 4 or 6) compared with untreated patients.75"

p(HGNC:IFNB1) -| path(DOID:"secondary progressive multiple sclerosis")

SET Evidence = "In a long-term follow-up study, alemtuzumab had a remarkably durable treatment eﬀect, and approximately half of patients did not require retreatment in a median 7 years of follow up.92"

a(DRUGBANK:Alemtuzumab) -| path(MESH:"Multiple Sclerosis, Relapsing-Remitting")1

UNSET Disease
