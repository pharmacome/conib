#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Progressive multiple sclerosis: prospects for disease therapy, repair, and restoration of function"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Trusha Adeshara"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
 DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
 DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
 DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
 DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
 DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
 DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
 DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"
DEFINE NAMESPACE NCIT           AS PATTERN "^C\d+$"
###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
 DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
 DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
 DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
 DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
# DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
 DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
 DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "27889190"}

SET Evidence = "Patients with primary and secondary progressive multiple sclerosis exhibit gradually worsening disability, most often motor impairment with a pattern suggesting a myelopathy, but also progressive hemiparesis, ataxia, visual dysfunction, or cognitive impairment."
#NCIT:C21007 = disability
path(DOID:"primary progressive multiple sclerosis") -> path(NCIT:C21007)
path(DOID:"secondary progressive multiple sclerosis") -> path(NCIT:C21007)
path(DOID:"primary progressive multiple sclerosis") -> path(MESH:"Motor Disorders")
path(DOID:"secondary progressive multiple sclerosis") -> path(MESH:"Motor Disorders")
path(DOID:"primary progressive multiple sclerosis") -> path(HP:Myelopathy)
path(DOID:"secondary progressive multiple sclerosis") -> path(HP:Myelopathy)
path(DOID:"primary progressive multiple sclerosis") -> path(HP:Hemiparesis)
path(DOID:"secondary progressive multiple sclerosis") -> path(HP:Hemiparesis)
path(DOID:"primary progressive multiple sclerosis") -> path(HP:Ataxia)
path(DOID:"secondary progressive multiple sclerosis") -> path(HP:Ataxia)
path(DOID:"primary progressive multiple sclerosis") -> path(MESH:"Vision Disorders")
path(DOID:"secondary progressive multiple sclerosis") -> path(MESH:"Vision Disorders")
path(DOID:"primary progressive multiple sclerosis") -| path(GO:cognition)
path(DOID:"secondary progressive multiple sclerosis") -| path(GO:cognition)

SET Evidence = "In the early stages, pathology is dominated by focal inflammatory lesions with perivenular accumulation of T and B lymphocytes, blood–brain barrier disruption, demyelination, and acute axonal transection.5"

#NCIT:C3824 = lesion
path(DOID:"multiple sclerosis") -> path(NCIT:C3824)
path(DOID:"multiple sclerosis") -> deg(a(MESH:"Blood-Brain Barrier"))
path(DOID:"multiple sclerosis") -> path(HP:"CNS demyelination")
path(DOID:"multiple sclerosis") -- bp(MESH:"Axonal Transport")
SET MeSHAnatomy = {"T-Lymphocytes","B-Lymphocytes"}
#NCIT:C120860 = accumulation
path(DOID:"multiple sclerosis") -> path(NCIT:C120860)
UNSET MeSHAnatomy

SET Evidence = "The clinical importance of grey matter pathology involving cortex, deep structures, cerebellum, and spinal cord is increasingly recognised in progressive multiple sclerosis.7"

SET Anatomy = {"cortex","cerebellum","spinal cord"}
path(DOID:"progressive relapsing multiple sclerosis") -- a(MESH:"Gray Matter")
UNSET Anatomy

SET Evidence = "Cortical histopathology includes microglial activation, demyelination, neuritic transection, neuronal death, and reduced presynaptic terminals, but tends not to include perivascular lymphocytic cuffs typical of white matter lesions.8"

#in  progressive MS
path(DOID:"progressive relapsing multiple sclerosis") -> bp(GO:"microglial cell activation")
path(DOID:"progressive relapsing multiple sclerosis") -> path(HP:"CNS demyelination")
path(DOID:"progressive relapsing multiple sclerosis") -- a(MESH:Neurites)
SET Cell = "neuron"
path(DOID:"progressive relapsing multiple sclerosis") -> bp(MESH:"Cell Death")
UNSET Cell

SET Evidence = "The severity of cortical demyelination correlates with the extent of meningeal inflammation in progressive multiple sclerosis, suggesting a pathogenic role.10"

SET Disease = "progressive relapsing multiple sclerosis"
path(HP:"CNS demyelination") pos path(MESH:Meningitis)
UNSET Disease

SET Evidence = "Demyelinated axons are abundant in longstanding multiple sclerosis and are hypothesised to be susceptible to chronic injury.6"

SET CellStructure = "Axons"
#NCIT:C117277 = demyelination
path(DOID:"progressive relapsing multiple sclerosis") -> path(NCIT:C117277)

SET Evidence = "In some demyelinated axons, saltatory nerve-impulse conduction is replaced by continuous conduction, which restores function but increases energy demand and sodium accumulation in the axonal cytoplasm. Resultant reverse operation of sodium–calcium exchanger to restore ionic gradients could lead to cytoplasmic calcium accumulation, activation of calpains, and proteolysis of cytoskeleton."

path(NCIT:C117277) -- bp(MESH:"Neural Conduction")
path(NCIT:C117277) -> bp(MESH:"Energy Metabolism")
path(NCIT:C117277) -> a(MESH:Sodium,loc(MESH:Cytoplasm))
a(MESH:Sodium,loc(MESH:Cytoplasm)) -> act(p(MESH:"Sodium-Calcium Exchanger"))
composite(p(MESH:"Sodium-Calcium Exchanger"),a(MESH:Sodium,loc(MESH:Cytoplasm))) -> a(CHEBI:"calcium cation",loc(MESH:Cytoplasm))
a(CHEBI:"calcium cation",loc(MESH:Cytoplasm)) -> act(p(HGNCGENEFAMILY:Calpains))
act(p(HGNCGENEFAMILY:Calpains)) -| a(GO:cytoskeleton)
a(CHEBI:"calcium cation",loc(MESH:Cytoplasm)) -| a(GO:cytoskeleton)

SET Evidence = "Mitochondrial dysfunction in axons seems to result from impaired mitochondrial transport, susceptibility to oxidative injury, and mutations in mitochondrial DNA, all of which lead to impaired energy production and generation of reactive oxygen species."

bp(GO:"mitochondrial transport") neg path(CONSO:"mitochondrial dysfunction")
path(CONSO:"mitochondrial dysfunction") -> path(MESH:"Oxidative Stress")
UNSET CellStructure
SET CellStructure = {"Mitochondria","Axons"}
path(CONSO:"mitochondrial dysfunction") -> path(MESH:"DNA Damage")
path(MESH:"DNA Damage") -| bp(MESH:"Energy Metabolism")
path(MESH:"DNA Damage") -> a(MESH:"Reactive Oxygen Species")
UNSET CellStructure
SET CellStructure = "Mitochondria"
path(CONSO:"mitochondrial dysfunction") -| bp(MESH:"Energy Metabolism")
path(CONSO:"mitochondrial dysfunction") -> a(MESH:"Reactive Oxygen Species")
path(MESH:"Oxidative Stress") -| bp(MESH:"Energy Metabolism")
path(MESH:"Oxidative Stress") -> a(MESH:"Reactive Oxygen Species")

UNSET CellStructure

SET Evidence = "Whole-brain atrophy correlates with physical 20 and cognitive21 impairment."

SET Anatomy = "brain"
path(MESH:Atrophy) -| bp(GO:cognition)
path(MESH:Atrophy) -> path(MESH:"Motor Disorders")
UNSET Anatomy

SET Evidence = "Cortical atrophy is prominent in progressive multiple sclerosis26 and correlates with physical27 and cognitive28 impairment."

SET Anatomy = {"brain","cortex"}
path(MESH:Atrophy) -| bp(GO:cognition)
path(MESH:Atrophy) -> path(MESH:"Motor Disorders")
UNSET Anatomy

SET Evidence = "Atrophy of deep grey structures (thalamus, caudate, and hippocampus) also occurs in progressive multiple sclerosis and can be focal (presumably due to lesions) or more diffuse (presumably due to damage to afferent or efferent connections).29,30"

SET MeSHAnatomy = {"Thalamus","Caudate Nucleus","Hippocampus"}
path(DOID:"progressive relapsing multiple sclerosis") -> path(MESH:Atrophy)
UNSET MeSHAnatomy

SET Evidence = "Spinal cord atrophy correlates with clinical measures of disability.35"

SET MeSHAnatomy = "Spinal Cord"
path(MESH:Atrophy) -> path(NCIT:C21007)
UNSET MeSHAnatomy

SET MeSHDisease = "Multiple Sclerosis, Relapsing-Remitting"

SET Evidence = "Fingolimod reduced relapses, MRI lesion activity, and brain volume loss in three phase 3 trials in relapsingremitting multiple sclerosis.56–58"

a(DRUGBANK:Fingolimod) -| path(MESH:Recurrence)
a(DRUGBANK:Fingolimod) -| path(NCIT:C3824)

SET Evidence = "Fingolimod readily enters the brain and has direct effects on several CNS cell types mediated by sphingosine 1-phosphate receptors, suggesting it might be beneficial in progressive multiple sclerosis.59"

complex(a(DRUGBANK:Fingolimod,loc(MESH:Brain)),p(HGNCGENEFAMILY:"Sphingosine 1-phosphate receptors")) -| path(DOID:"relapsing-remitting multiple sclerosis")

SET Evidence = "Another trial investigated a repurposed sodium-channel blocker, phenytoin, to assess whether it protects axons from acute inflammatory injury in acute optic neuritis.63"

a(CHEBI:phenytoin) isA a(CHEBI:"sodium channel blocker")
SET MeSHDisease = "Optic Neuritis"
SET CellStructure = "Axons"
a(CHEBI:phenytoin) -| path(MESH:Inflammation)
UNSET CellStructure
UNSET MeSHDisease

SET Evidence = "Cellular energy metabolism seems to be abnormal in progressive multiple sclerosis11 and is another potential therapeutic target."

path(DOID:"progressive relapsing multiple sclerosis") neg bp(MESH:"Energy Metabolism")

SET Evidence = "The vitamin biotin is a coenzyme for many essential carboxylases and, in high doses, is hypothesised to enhance cellular energy production with resultant improved axonal function, decreased neurodegeneration, and enhanced remyelination.65"

a(CHEBI:biotin) -> bp(MESH:"Energy Metabolism")
a(CHEBI:biotin) -> act(a(MESH:Axons))
a(CHEBI:biotin) -| path(CONSO:Neurodegeneration)
a(CHEBI:biotin) -> bp(MESH:Remyelination)

SET Evidence = "LINGO-1 is a protein expressed by oligodendrocytes and neurons that inhibits remyelination.66"

a(MESH:Oligodendroglia) -> p(HGNC:LINGO1)
a(MESH:Neurons) -> p(HGNC:LINGO1)
a(MESH:Oligodendroglia) -> bp(MESH:Remyelination)
a(MESH:Neurons) -> bp(MESH:Remyelination)

SET Evidence = "A pilot study of clemastine showed improvement on visual evoked potentials in participants with multiple sclerosis-related chronic optic neuropathy.73"

a(DRUGBANK:Clemastine) -> bp(MESH:"Vision, Ocular")
a(DRUGBANK:Clemastine) -> bp(HP:"Optic neuropathy")
