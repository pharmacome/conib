#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Molecular Chaperone Functions in Protein Folding and Proteostasis"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/6ebac4dddc69402cb8fdd90c746ff6ee572c95af/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET STATEMENT_GROUP = "Esther"

# Add statements below this comment

SET Citation = {"PubMed","Annu Rev Biochem. 2013;82:323-55. doi: 10.1146/annurev-biochem-060208-092442.","23746257"}

SET Evidence = " A key
role of molecular chaperones is preventing pro-
tein aggregation, especially under conditions of
cellular stress."
a(MESH:"Molecular Chaperones") -| a(MESH:"Protein Aggregates")

SET Evidence = "Indeed, studies using model organisms
demonstrate that a gradual decline in cellular
proteostasis capacity occurs with aging (10)."
bp(GO:aging) -| bp(HBP:Proteostasis)

SET Evidence = " Many are stress proteins
or heat shock proteins (Hsps), as their synthesis
is induced under conditions of stress (e.g., heat
shock or oxidative stress), which structurally
destabilize a subset of cellular proteins."
bp(MESH:"Stress, Physiological") -> a(MESH:"Heat-Shock Proteins")
bp(GO:"response to oxidative stress") -> a(MESH:"Heat-Shock Proteins")
bp(MESH:"Heat-Shock Response") -> a(MESH:"Heat-Shock Proteins")

SET Evidence = "Chaperones that function broadly in de novo
folding and refolding (i.e., the chaperonins,
Hsp70s, and Hsp90s) are ATP regulated and
recognize segments of exposed hydropho-
bic amino acid residues, which are later
buried in the interior of the natively folded
protein."
act(p(HGNCGENEFAMILY:Chaperonins)) -- bp(GO:"'de novo' protein folding")
act(p(HGNCGENEFAMILY:Chaperonins)) -- bp(GO:"protein refolding")
act(p(FPLX:HSPA)) -- bp(GO:"'de novo' protein folding")
act(p(FPLX:HSPA)) -- bp(GO:"protein refolding")
act(p(FPLX:HSP90)) -- bp(GO:"'de novo' protein folding")
act(p(FPLX:HSP90)) -- bp(GO:"protein refolding")
a(CHEBI:ATP) reg act(p(HGNCGENEFAMILY:Chaperonins))
a(CHEBI:ATP) reg act(p(FPLX:HSPA))
a(CHEBI:ATP) reg act(p(FPLX:HSP90))

SET Evidence = "ATP-independent chaperones,
such as the small Hsps, may function as
additional holdases that buffer aggregation."
a(CHEBI:ATP) cnc act(p(FPLX:HSPB))
act(p(FPLX:HSPB)) -| a(MESH:"Protein Aggregates")

SET Evidence = "TRiC binds to
nascent chains and cooperates with Hsp70
in the cotranslational folding of multidomain
proteins (78). Investigators have demonstrated
a direct interaction between Hsp70 and TRiC
(79)."
complex(p(FPLX:"CCT_complex"),p(FPLX:HSPA)) -> bp(GO:"'de novo' protein folding")

SET Evidence = "Accordingly, Hsp90 affects many key cellular
processes, including cell cycle progression,
steroid signaling, calcium signaling, protein
trafﬁcking, protein secretion, the immune re-
sponse, and the heat shock response (HSR) (45,
48, 82)."
p(FPLX:HSP90) -- bp(GO:"cell cycle")
p(FPLX:HSP90) -- bp(GO:"steroid hormone mediated signaling pathway")
p(FPLX:HSP90) -- bp(GO:"calcium-mediated signaling")
p(FPLX:HSP90) -- bp(GO:"protein transport")
p(FPLX:HSP90) -- bp(GO:"protein secretion")
p(FPLX:HSP90) -- bp(GO:"immune response")
p(FPLX:HSP90) -- bp(MESH:"Heat-Shock Response")

SET Evidence = "Hsp70 chaperones are a ubiquitous class of
proteins. They are involved in a wide range
of protein quality control functions, including
de novo protein folding, refolding of stress-
denatured proteins, protein transport, mem-
brane translocation, and protein degradation."
act(p(FPLX:HSPA)) -- bp(GO:"'de novo' protein folding")
act(p(FPLX:HSPA)) -- bp(GO:"protein refolding")
act(p(FPLX:HSPA)) -- bp(GO:"intracellular protein transmembrane transport")
act(p(FPLX:HSPA)) -- bp(GO:"protein catabolic process")

SET Evidence = " Hsp40 ( J protein) and NEF
cochaperones regulate the Hsp70 reaction cy-
cle (38, 100)."
p(HGNCGENEFAMILY:"DNAJ (HSP40) heat shock proteins") reg act(p(FPLX:HSPA))
p(FPLX:RAPGEF) reg act(p(FPLX:HSPA))

SET Evidence = " Hsp110 may function as holdases for
nonnative proteins and cooperate with Hsp70
and Hsp40 in protein disaggregation (104, 124,
125)."
composite(p(HGNC:HSPH1), p(HGNCGENEFAMILY:"DNAJ (HSP40) heat shock proteins"), p(FPLX:HSPA)) -| a(MESH:"Protein Aggregates")

SET Evidence = "Binding to
GroEL prevents aggregation of these ﬂexible
folding intermediates, and subsequent folding
depends critically on the global encapsulation
of the substrate in the chaperonin cavity by
the cochaperone GroES (7, 132–135)."
p(HGNC:HSPD1) -| a(MESH:"Protein Aggregates")
complex(p(HGNC:HSPD1),p(HGNC:HSPE1)) -> bp(GO:"protein refolding")

SET Evidence = "GroEL and GroES undergo a complex
binding-and-release cycle that is allosterically
regulated by ATP binding and hydrolysis in the
GroEL subunits (Figure 6b) (4, 77, 100, 136)."
a(CHEBI:ATP) reg complex(p(HGNC:HSPD1),p(HGNC:HSPE1))

SET Evidence = "Cooperative binding of ATP to GroEL initiates
a series of conformational changes that trigger
the association of GroES, followed by substrate
release from hydrophobic binding sites into a
GroES-capped, hydrophilic folding chamber
(137)."
complex(a(CHEBI:ATP),p(HGNC:HSPD1)) -> complex(p(HGNC:HSPD1),p(HGNC:HSPE1))

SET Evidence = "TRiC also interacts with N-terminal fragments
of mutant huntingtin that contain an expanded
polyglutamine repeat sequence (165–168).
Binding to TRiC modulates the aggregation
properties of this protein and reduces its
cytotoxicity."
p(FPLX:"CCT_complex") -| a(HBP:"huntingtin aggregates")

SET Evidence = "Inhibition of ATP
binding and hydrolysis either by mutagenesis
of the N domain or by speciﬁc inhibitors, such
as geldanamycin and radicicol, demonstrated
the functional requirement of the Hsp90
ATPase (81, 169)."
complex(a(CHEBI:ATP),p(FPLX:HSP90)) reg act(p(FPLX:HSP90))

SET Evidence = "After hydrolysis
the Hsp90 N termini separate, releasing the
client protein in an active state (Figure 7b)."
bp(GO:"ATP metabolic process") -> act(p(FPLX:HSP90))
act(p(FPLX:HSP90)) -> bp(GO:"protein folding")

SET Evidence = "Cochaperones HOP (Sti1) and
Cdc37 (p50) stabilize the open conformation
of the Hsp90 dimer (81, 174, 175, 188), inhibit
ATP hydrolysis, and facilitate substrate protein
binding."
p(HGNC:STIP1) -| act(p(FPLX:HSP90), ma(GO:"ATPase activity"))
p(HGNC:CDC37) -| act(p(FPLX:HSP90), ma(GO:"ATPase activity"))

SET Evidence = " The activator
of Hsp90 ATPase (Aha1) binds asymmetrically
to the Hsp90 middle domain, stimulating ATP
hydrolysis and inducing transition to the closed
state (180, 181, 185, 190)."
p(HGNC:AHSA1) -> act(p(FPLX:HSP90), ma(GO:"ATPase activity"))

SET Evidence = "The PN is regulated by interconnected
pathways that respond to speciﬁc forms of
cellular stress, including the cytosolic heat
shock response (HSR) (191), the unfolded
protein response (UPR) in the endoplas-
mic reticulum (192), and the mitochondrial
UPR (193)."
SET MeSHAnatomy = "Cytosol"
bp(MESH:"Heat-Shock Response") reg bp(HBP:Proteostasis)
SET MeSHAnatomy = {"Endoplasmic Reticulum","Mitochondria"}
bp(GO:"response to unfolded protein") reg bp(HBP:Proteostasis)
UNSET MeSHAnatomy

SET Evidence = "Additionally, PN regulation is
integrated with pathways involved in inﬂam-
mation, response to oxidative stress, caloric
restriction/starvation, and longevity."
bp(HBP:Proteostasis) -- bp(GO:"inflammatory response")
bp(HBP:Proteostasis) -- bp(GO:"response to oxidative stress")
bp(HBP:Proteostasis) -- bp(GO:"behavioral response to starvation")
bp(HBP:Proteostasis) neg bp(GO:"cell death")

SET Evidence = "The PN
branch of degradation includes the ubiquitin-
proteasome system (UPS) and machinery of
autophagy (23, 196–200)."
p(FPLX:Proteasome) -> bp(GO:"protein catabolic process")
bp(GO:"autophagy") -> bp(GO:"protein catabolic process")

SET Evidence = "Degradation via the UPS depends on pro-
tein unfolding by the 26S proteasome (201) and
generally requires that chaperones maintain
target proteins in a nonaggregated state."
p(FPLX:Proteasome) -> bp(GO:"protein unfolding")

SET Evidence = " For
example, Hsp70 and Hsp90 cooperate with the
U-box-dependent ubiquitin ligase CHIP (C
terminus of Hsc70-interacting protein) and
a variety of other cofactors (such as BAG1
and BAG3) to ubiquitinate client proteins (23,
86, 198)."
composite(p(FPLX:HSPA), p(FPLX:HSP90), p(HGNC:STUB1), p(HGNC:BAG1), p(HGNC:BAG3)) -> bp(GO:"protein ubiquitination")

SET Evidence = " BAG1 targets proteins
for degradation by the UPS, whereas BAG3
mediates degradation by macroautophagy."
p(HGNC:BAG1) -> bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process")
p(HGNC:BAG3) -> bp(GO:"macroautophagy")

SET Evidence = "Aggregated proteins that cannot be un-
folded for proteasomal degradation may be
removed by autophagy and lysosomal/vacuolar
degradation."
bp(GO:"autophagy") -| a(MESH:"Protein Aggregates")

SET Evidence = "As shown in Caenorhabditis elegans, Drosophila,
and the mouse, the ability of cells and tissues
to maintain proteostasis declines during aging,
concurrent with the capacity to respond to
conformational stresses (214–220)."
bp(GO:aging) -| bp(HBP:Proteostasis)

SET Evidence = "The antihypertensive
drug guanabenz demonstrates this; besides be-
ing an α2 receptor antagonist, it also stabilizes
the translation initiation factor 2 (eIF2) in its
inactive, phosphorylated state (226)."
a(CHEBI:Guanabenz) -| act(p(FPLX:ADRA2))
a(CHEBI:Guanabenz) -| act(p(FPLX:EIF2B))
a(CHEBI:Guanabenz) -> p(FPLX:EIF2B, pmod(Ph))
p(FPLX:EIF2B, pmod(Ph)) -| act(p(FPLX:EIF2B))

SET Evidence = " For exam-
ple, small molecules (e.g., geldanamycin) that
activate heat shock factor 1, the main transcrip-
tional regulator of the cytosolic stress response,
increase the effective concentration of cytosolic
chaperones and suppress the aggregation of
various disease proteins (8, 38, 228–230)."
a(CHEBI:geldanamycin) -> act(p(HGNC:HSF1))
p(HGNC:HSF1) reg bp(GO:"cellular response to stress")
p(HGNC:HSF1) -> a(MESH:"Molecular Chaperones")
p(HGNC:HSF1) -| a(MESH:"Protein Aggregates")

SET Evidence = "This approach is based on multiple lines of
evidence demonstrating that overexpression of
chaperones such as Hsp70 and Hsp40 prevents
the aggregation and toxicity of huntingtin
and α-synuclein (38, 231–234)."
p(FPLX:HSPA) -| a(HBP:"huntingtin aggregates")
p(FPLX:HSPA) -| a(HBP:"alpha-synuclein aggregates")
p(HGNCGENEFAMILY:"DNAJ (HSP40) heat shock proteins") -| a(HBP:"huntingtin aggregates")
p(HGNCGENEFAMILY:"DNAJ (HSP40) heat shock proteins") -| a(HBP:"alpha-synuclein aggregates")

SET Evidence = "The Hsp70
system acts synergistically with the cytosolic
chaperonin TRiC to prevent aggregation of
proteins with expanded polyglutamine tracts
(165–168)."
composite(p(FPLX:HSPA),p(FPLX:"CCT_complex")) -| a(MESH:"Protein Aggregates")
composite(p(FPLX:HSPA),p(FPLX:"CCT_complex")) -| a(HBP:"huntingtin aggregates")
