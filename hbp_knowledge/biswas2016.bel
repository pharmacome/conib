#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY-SA 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "MMP-9 and MMP-2 Contribute to Neuronal Cell Death in iPSC Models of Frontotemporal Dementia with MAPT Mutations"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

###############
# Definitions #
###############

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY AS PATTERN "^\d+$"

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE MESH     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/1b20f0637c395f8aa89c2e2e342d7b704062c242/external/mesh-names.belns"
DEFINE NAMESPACE CHEBI    AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE   AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
DEFINE NAMESPACE NCBIGENE AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE GFAM     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE HGNC     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE MIRBASE  AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE HBP      AS URL "https://raw.githubusercontent.com/pharmacome/terminology/785431732a26d6809b4162d98b95687d16404e63/export/hbp.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel
# --------------------------------------------------------------------
DEFINE NAMESPACE CTO      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE MGI      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170725.belns"
DEFINE NAMESPACE SCOMP    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM     AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-20170731.belns"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############


SET STATEMENT_GROUP = "Sandra"

# Add statements below this comment

SET Citation = {"PubMed", "Stem Cell Reports. 2016 Sep 13;7(3):316-324. doi: 10.1016/j.stemcr.2016.08.006", "27594586"}

SET MeSHDisease = "Frontotemporal Dementia"
SET MeSHAnatomy = "Neurons"

# rs63751011 = MAPT IVS10+16

SET Evidence = "Four weeks after terminal differentiation, 80% of cells were positive for the neuron-specific marker
MAP2, and there was no significant difference between control and patient neurons (Figure S2A)"
SET Confidence = "High"
g(DBSNP:rs63751011) -> p(HGNC:MAP2)
p(HGNC:MAPT,var("p.Ala152Thr")) -> p(HGNC:MAP2)

SET Evidence = "The percentages of VGLUT1-positive excitatory and GABA-positive inhibitory neurons were similar
in neuronal cultures differentiated from control and patient iPSCs (Figures S2B and S2C)"
SET Confidence = "High"
g(DBSNP:rs63751011) causesNoChange a(GO:"symmetric, GABA-ergic, inhibitory synapse")
g(DBSNP:rs63751011) causesNoChange p(HGNC:SLC17A7)
p(HGNC:MAPT,var("p.Ala152Thr")) causesNoChange a(GO:"symmetric, GABA-ergic, inhibitory synapse")
p(HGNC:MAPT,var("p.Ala152Thr")) causesNoChange p(HGNC:SLC17A7)

SET Evidence = "Moreover, the total expression levels of tau and PSD-95 were the same in 1-month-old
neurons derived from control and patient iPSCs (Figure 1B)"
SET Confidence = "High"
g(DBSNP:rs63751011) causesNoChange p(HGNC:MAPT)
g(DBSNP:rs63751011) causesNoChange p(HGNC:DLGAP4)
p(HGNC:MAPT,var("p.Ala152Thr")) causesNoChange p(HGNC:MAPT)
p(HGNC:MAPT,var("p.Ala152Thr")) causesNoChange p(HGNC:DLGAP4)

SET Evidence = "The resting membrane potential did not differ among neurons differentiated
from all iPSC lines (Figure S2D)"
SET Confidence = "High"
g(DBSNP:rs63751011) causesNoChange bp(MESH:"Membrane Potentials")
p(HGNC:MAPT,var("p.Ala152Thr")) causesNoChange bp(MESH:"Membrane Potentials")

SET Evidence = "Thus, both molecular and electrophysiological analyses suggest MAPT mutations do not affect early
neuronal differentiation"
SET Confidence = "High"
g(DBSNP:rs63751011) causesNoChange bp(GO:"neuron differentiation")
p(HGNC:MAPT,var("p.Ala152Thr")) causesNoChange bp(GO:"neuron differentiation")

UNSET MeSHAnatomy

SET Evidence = "MMP-9 directly cleaves Huntingtin protein in Huntington’s disease, a progressive
neurodegenerative disorder (Miller et al., 2010)"
SET Confidence = "Medium"
SET MeSHDisease = "Huntington Disease"
p(HGNC:MMP9) -> p(HGNC:HTT,frag("?"))

UNSET MeSHDisease

SET Evidence = "Moreover, dysregulation of MMP-9 in the CNS is a cause of neuronal developmental
disorders (Reinhard et al., 2015)"
SET Confidence = "Medium"
SET MeSHAnatomy = "Central Nervous System"
act(p(HGNC:MMP9)) -- path(MESH:"Neurodevelopmental Disorders")
UNSET MeSHAnatomy

SET MeSHDisease = "Frontotemporal Dementia"

SET Evidence = "Tau-A152T neurons had >10-fold higher level and activity
of secreted MMP-9 than control neurons (Figures 2A and 2B), as did
MAPT IVS10+16 neurons to a lesser extent (Figures 2C and 2D)"
SET MeSHAnatomy = "Neurons"
SET Confidence = "High"
g(DBSNP:rs63751011) -> act(p(HGNC:MMP9))
g(DBSNP:rs63751011) -> sec(p(HGNC:MMP9))
p(HGNC:MAPT,var("p.Ala152Thr")) -> act(p(HGNC:MMP9))
p(HGNC:MAPT,var("p.Ala152Thr")) -> sec(p(HGNC:MMP9))

SET Evidence = "The level of secreted MMP-9 and
MMP-2 was also increased in cortical neurons derived from
a published iPSC line with the tau-A152T mutation (Fong
et al., 2013) (Figures 2E and 2F)"
SET Confidence = "High"
SET MeSHAnatomy = {"Cerebral Cortex","Neurons"}
p(HGNC:MAPT,var("p.Ala152Thr")) -> sec(p(HGNC:MMP9))
p(HGNC:MAPT,var("p.Ala152Thr")) -> sec(p(HGNC:MMP2))

SET Evidence = "This increase is likely in part due to transcriptional regulation,
since the mRNA levels of MMP-9 and MMP-2 in tau-A152T and MAPT IVS10+16 neurons were
higher than in control neurons (Figure S2J)"
SET MeSHAnatomy = "Neurons"
SET Confidence = "High"
g(DBSNP:rs63751011) -> r(HGNC:MMP9)
g(DBSNP:rs63751011) -> r(HGNC:MMP2)
p(HGNC:MAPT,var("p.Ala152Thr")) -> r(HGNC:MMP9)
p(HGNC:MAPT,var("p.Ala152Thr")) -> r(HGNC:MMP2)

SET CellStructure = "Extracellular Space"
SET MeSHAnatomy = {"Cerebral Cortex","Neurons"}

# disease phenotype = neuronal cell death

SET Evidence = "We found that, indeed, TUJ1+ neurons showed reduced survival as measured by TUNEL analysis (Figure 3A)"
SET Confidence = "High"
g(DBSNP:rs63751011) -> bp(GO:"neuron death")
p(HGNC:MAPT,var("p.Ala152Thr")) -> bp(GO:"neuron death")

SET Evidence = "We treated these neurons with staurosporine, a broad-spectrum kinase inhibitor that induces
apoptosis (Budd et al., 2000); rotenone, a complex inhibitor that induces mitochondrial dysfunction (Sherer
et al., 2003); and rapamycin, an inhibitor of the mammalian target of the rapamycin (mTOR) signaling pathway
with many downstream effects (Li et al., 2014)"
SET Confidence = "High"
a(CHEBI:staurosporine) -> bp(GO:"apoptotic process")
a(CHEBI:rotenone) -> bp(HBP:HBP00037)
a(CHEBI:sirolimus) -| bp(GO:"TOR signaling")

SET Evidence = "When measuring cell viability using the MTT assay, we found
that patient-specific neurons are more sensitive than control neurons to cell death
induced by rapamycin (Figure 3B) but not by staurosporine or rotenone (Figures S3B and
S3C), suggesting compromised function in the mTOR pathway or related molecular pathways"
SET Confidence = "Medium"
composite(a(CHEBI:sirolimus),p(HGNC:MAPT,var("p.Ala152Thr"))) -> bp(GO:"neuron death")
composite(a(CHEBI:rotenone),p(HGNC:MAPT,var("p.Ala152Thr"))) causesNoChange bp(GO:"neuron death")
composite(a(CHEBI:staurosporine),p(HGNC:MAPT,var("p.Ala152Thr"))) causesNoChange bp(GO:"neuron death")

SET Evidence = "MMP-9/MMP-2 mRNA levels in tau-A152T and MAPT IVS10+16 neurons were
significantly higher than that in control neurons (Figure S2J), and rapamycin
increased these levels (p < 0.01) (Figure 3C)"
SET Confidence = "High"
a(CHEBI:sirolimus) -> r(HGNC:MMP9)
a(CHEBI:sirolimus) -> r(HGNC:MMP2)
g(DBSNP:rs63751011) -> r(HGNC:MMP9)
g(DBSNP:rs63751011) -> r(HGNC:MMP2)
p(HGNC:MAPT,var("p.Ala152Thr")) -> r(HGNC:MMP9)
p(HGNC:MAPT,var("p.Ala152Thr")) -> r(HGNC:MMP2)

# #444278 = MMP-9 Inhibitor
# #444244 = MMP-2 Inhibitor = N-Hydroxyoleamide
# inhibitors from Calbiochem

SET Evidence = "To examine the contributions of increased MMP-9 and MMP-2 to
neuronal cell death, we used two compounds that are
known to inhibit MMP-9 and MMP-2 activity (catalog #444278 and #444244, Calbiochem).
We found that this treatment partially rescued the increased sensitivity to rapamycin
and improved survival (Figure 3B)"
SET Confidence = "High"
act(p(HGNC:MMP9)) -> bp(GO:"neuron death")
act(p(HGNC:MMP2)) -> bp(GO:"neuron death")

SET Evidence = "MMP-2 and MMP-9 seem to induce cell death directly, since this
treatment also increased the TUNEL signal in iPSC-derived neurons (Figures S3D and S3E)"
SET Confidence = "High"
p(HGNC:MMP9) -> bp(GO:"neuron death")
p(HGNC:MMP2) -> bp(GO:"neuron death")

UNSET CellStructure

SET Evidence = "It is known that MMP-9 expression is regulated by ERK (Wang et al., 2006)"
SET Confidence = "Medium"
p(FPLX:ERK) reg p(HGNC:MMP9)

SET Evidence = "The level of phosphorylated ERK (p-ERK) was significantly increased in
tau-A152T neurons and to a lesser extent in MAPT IVS10+16 patient neurons (Figure 4A)"
SET Confidence = "Medium"
p(HGNC:MAPT,var("p.Ala152Thr")) -> p(FPLX:ERK,pmod(Ph))

SET Evidence = "It is worth noting that the level and activity of secreted MMP-9 in tau-
A152T neurons is also substantially higher than that in MAPT IVS10+16 neurons (Figure 2)"
SET Confidence = "Medium"
p(HGNC:MAPT,var("p.Ala152Thr")) -> act(p(HGNC:MMP2))
p(HGNC:MAPT,var("p.Ala152Thr")) -> sec(p(HGNC:MMP2))
p(HGNC:MAPT,var("p.Ala152Thr")) -> act(p(HGNC:MMP9))
p(HGNC:MAPT,var("p.Ala152Thr")) -> sec(p(HGNC:MMP9))

SET Evidence = "ERK phosphorylation in MAPT mutant neurons was blocked by treatment with
PD98059, a specific inhibitor of MEK-mediated activation of ERK (Li et al., 2001) (Figures 4B and 4C)"
SET Confidence = "Medium"
a(CHEBI:"2-(2-amino-3-methoxyphenyl)chromen-4-one") -| p(FPLX:ERK,pmod(Ph))

SET Evidence = "Correspondingly, the level and activity of secreted MMP-9 was greatly
suppressed in both tau-A152T and MAPT IVS10+16 patient neurons (Figures 4B and 4C), suggesting that MMP-9
expression is regulated by the ERK signaling pathway in cortical neurons"
SET Confidence = "Medium"
a(CHEBI:"2-(2-amino-3-methoxyphenyl)chromen-4-one") -| sec(p(HGNC:MMP9))
a(CHEBI:"2-(2-amino-3-methoxyphenyl)chromen-4-one") -| act(p(HGNC:MMP9))
bp(GO:"ERK1 and ERK2 cascade") reg p(HGNC:MMP9)

SET Evidence = "We found the level and activity of secreted MMP-9 to correlate well
with the extent of ERK phosphorylation (Figure S4A)"
SET Confidence = "Medium"
p(FPLX:ERK,pmod(Ph)) positiveCorrelation act(p(HGNC:MMP9))
p(FPLX:ERK,pmod(Ph)) positiveCorrelation sec(p(HGNC:MMP9))

SET Evidence = "Interestingly, the level and activity of MMP-2 was not affected after the
PD98059 treatment in tau-A152T neurons (Figure 4B) and MAPT IVS10+16 neurons (Figure 4C)"
SET Confidence = "High"
a(CHEBI:"2-(2-amino-3-methoxyphenyl)chromen-4-one") causesNoChange act(p(HGNC:MMP2))
a(CHEBI:"2-(2-amino-3-methoxyphenyl)chromen-4-one") causesNoChange p(HGNC:MMP2)

SET Evidence = "As expected, the 4R/3R tau ratio is greatly increased in MAPT IVS10+16
neurons (Figure S3F) but not in GRN mutant neurons (Figure S3G)"
SET Confidence = "Medium"
g(DBSNP:rs63751011) -> a(HBP:HBP00045)
g(DBSNP:rs63751011) -| a(HBP:HBP00044)

SET Evidence = "Interestingly, this ratio is also increased, but to a
lesser extent, in tau-A152T neurons (Figure S3F)"
SET Confidence = "Medium"
p(HGNC:MAPT,var("p.Ala152Thr")) -> a(HBP:HBP00045)
p(HGNC:MAPT,var("p.Ala152Thr")) -| a(HBP:HBP00044)

UNSET MeSHAnatomy

SET CellLine = "HEK293"

# WT Tau = 4R tau
# mutant = 4R tau-A152T

SET Evidence = "However, the secreted MMP-9 level and activity in conditioned medium
was significantly higher in cultures of cells expressing tau-A152T than WT tau (Figure 4E)"
SET Confidence = "High"
p(HBP:HBP00045,var("p.Ala152Thr")) -> sec(p(HGNC:MMP9))
p(HBP:HBP00045,var("p.Ala152Thr")) -> act(p(HGNC:MMP9))

SET Evidence = "The MMP-9 mRNA level was also higher in cells expressing tau-A152T (Figure 4F)"
SET Confidence = "High"
p(HBP:HBP00045,var("p.Ala152Thr")) -> r(HGNC:MMP9)

SET Evidence = "Correspondingly, the level of p-ERK was also elevated in
HEK293 cells expressing tau-A152T (Figure 4E)"
SET Confidence = "Medium"
p(HBP:HBP00045,var("p.Ala152Thr")) -> p(FPLX:ERK,pmod(Ph))

# WT Tau = 3R tau
# mutant = 3R tau-A152T

SET Evidence = "Interestingly, the level and activity of secreted MMP-9 in HEK293
cells expressing WT 3R tau and 3R tau-A152T are similar (Figures S4B and S4C)"
SET Confidence = "High"
p(HBP:HBP00044,var("p.Ala152Thr")) causesNoChange sec(p(HGNC:MMP9))
p(HBP:HBP00044,var("p.Ala152Thr")) causesNoChange act(p(HGNC:MMP9))

SET Evidence = "ERK  phosphorylation is also not affected by A152T mutation
in 3R tau (Figure S4D)"
SET Confidence = "Medium"
p(HBP:HBP00044,var("p.Ala152Thr")) causesNoChange p(FPLX:ERK,pmod(Ph))

SET Evidence = "Taken together, these findings strongly suggest that 4R tau-A152T activates the ERK
pathway more strongly, which in turn increases the level and activity of secreted MMP-9. 4R tau-A152T expression
(Figures 4E and 4F) but not inhibition of ERK activity (Figures 4B and 4C) also increases the total activity of
secreted MMP-2, suggesting that tau-A152T increases the level and activity of secreted MMP-2, likely through
an ERK-independent pathway"
SET Confidence = "High"
p(HBP:HBP00045,var("p.Ala152Thr")) -> bp(GO:"ERK1 and ERK2 cascade")
p(HBP:HBP00045,var("p.Ala152Thr")) -> sec(p(HGNC:MMP9))
p(HBP:HBP00045,var("p.Ala152Thr")) -> act(p(HGNC:MMP9))
p(HBP:HBP00045,var("p.Ala152Thr")) -> sec(p(HGNC:MMP2))
p(HBP:HBP00045,var("p.Ala152Thr")) -> act(p(HGNC:MMP2))


##########################################################


UNSET STATEMENT_GROUP
