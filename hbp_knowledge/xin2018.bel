#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Clearance of Amyloid Beta and Tau in Alzheimer’s Disease:from Mechanisms to Therapy"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/7e4be528f12abd28be768b62402fba6e083eaf9e/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "29626319"}

SET Evidence = "Intracellular protein degradation is performed by UPS and ALS (Wong and Cuervo 2010"
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -> bp(GO:"cellular protein catabolic process")
bp(GO:"lysosomal microautophagy") -> bp(GO:"cellular protein catabolic process")

SET Evidence = "Under physiological conditions,
UPS, located in the cytosol and the nucleus in eukaryotic cells,
as a major intracellular short-lived protein degradation system (Schwartz and Ciechanover 2009), mediates the clearance of
misfolded or other abnormally modified proteins with the help of ubiquitin-activating enzyme (E1), ubiquitin-conjugating enzyme
(E2), ubiquitin ligase (E3), and the 26S proteasome for the sake of preventing the accumulation of toxic substances
(Shang and Taylor 2011)"
SET MeSHAnatomy = {"Cytosol","Cell Nucleus"}
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") reg deg(a(MESH:Proteins))
p(HGNC:UBA1) -> deg(a(MESH:Proteins))
p(SFAM:"UBE2 Family") -> deg(a(MESH:Proteins))
a(MESH:"Ubiquitin-Protein Ligases") -> deg(a(MESH:Proteins))
p(HGNC:PSMD7) -> deg(a(MESH:Proteins))
UNSET MeSHAnatomy

SET Evidence = "In addition, studies have shown that
the 20S proteasome can play a key role in digesting proteins
directly via an ATP-independent and ubiquitin-independent
pathway (Jariel-Encontre et al. 2008)"
a(HBP:"20 S Proteasome") -> bp(GO:"proteasomal ubiquitin-independent protein catabolic process")

SET Evidence = "And autophagy-lysosomal activity
is induced via two signal pathways: mammalian target of
rapamycin (mTOR)-dependent pathway and mTORindependent pathway (Tan et al. 2014b)"
complex(GO:"TOR complex") -> bp(GO:"lysosomal microautophagy")

SET Evidence = "Microglia, one type of glial cells, the equivalent of the macrophages
that exist in the brain and spinal cord, is the first and
also the main line of immune defense in CNS"
SET MeSHAnatomy = "Central Nervous System"
a(MESH:Microglia) -> bp(GO:"immune response")
UNSET MeSHAnatomy

SET MeSHDisease = "Alzheimer Disease"

SET Evidence = "Activated microglia has a double effect on AD progression (Li et al. 2014).
On the one hand, they can release some proinflammatory
cytokines, stimulating inflammatory response
and ultimately leading to neuronal injuries and death"
act(a(MESH:Microglia)) -> bp(GO:"cytokine production involved in inflammatory response")
act(a(MESH:Microglia)) -> bp(GO:"inflammatory response")
act(a(MESH:Microglia)) -> bp(HBP:Neurodegeneration)

SET Evidence = "On the other hand, they may show beneficial effects via facilitating
aberrant protein clearance by means of microglial migration to
the damaged, aberrant area, and phagocytosis of unnecessary
materials in the early stages of AD"
# they: activated microglia
act(a(MESH:Microglia)) -> bp(GO:phagocytosis)

UNSET MeSHDisease

SET Evidence = "In central nervous system (CNS), ISF proteins are transported
out of brain mainly through the BBB (Wong et al. 2013), and
CSF proteins enter blood by CSF absorption, including the
BCSFB and arachnoid villi (Pascale et al. 2011; Pollay 2010)"
SET MeSHAnatomy = "Central Nervous System"
tloc(a(MESH:Proteins),fromLoc(MESH:Brain),toLoc(MESH:Blood))
UNSET MeSHAnatomy

SET Evidence = "However, some proteins fail to be transported out
of the brain through intercellular tight junctions, and can only
be transported into blood by transporters expressed in the
capillary endothelium"
a(MESH:"Tight Junctions") -| tloc(a(MESH:Proteins),fromLoc(MESH:Brain),toLoc(MESH:Blood))

SET Evidence = "Unfortunately, the glymphatic system may be
impaired due to the loss of AQP4 after traumatic brain injury
(Iliff et al. 2014)"
path(MESH:"Brain Injuries, Traumatic") -| p(HGNC:AQP4)
path(MESH:"Brain Injuries, Traumatic") -| a(HP:"glymphatic system")
p(HGNC:AQP4) -> a(HP:"glymphatic system")

SET Evidence = "Under normal conditions, Aβ production in brain parenchyma
results from hydrolyzing amyloid precursor proteins via beta-secreted
enzymes and gamma-secreted enzymes, and the most
common subtypes of Aβ in human body are Aβ1–40 and Aβ1–42"
complex(SCOMP:"gamma Secretase Complex") -> a(CHEBI:"amyloid-beta")
p(HGNC:BACE1) -> a(CHEBI:"amyloid-beta")

SET Evidence = "Intracellular Aβ clearance can be achieved through UPS
and ALS, and extracellular Aβ is degraded by glial phagocytosis,
such as microglia, astrocytes, and proteases from neurons
and astrocytes (Fig. 2)"
SET MeSHAnatomy = "Intracellular Space"
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -> deg(a(CHEBI:"amyloid-beta"))
bp(GO:"lysosomal microautophagy") -> deg(a(CHEBI:"amyloid-beta"))
SET MeSHAnatomy = "Extracellular Space"
bp(GO:phagocytosis) -> deg(a(CHEBI:"amyloid-beta"))
UNSET MeSHAnatomy

SET Evidence = "Aβ in periphery
is mainly cleared by blood components, such as red cells
(RBCs) and monocytes, or some tissues and organs, such as
the liver and kidney (Fig. 2)"
a(MESH:Erythrocytes) -> deg(a(CHEBI:"amyloid-beta"))
a(MESH:Monocytes) -> deg(a(CHEBI:"amyloid-beta"))
a(MESH:Liver) -> deg(a(CHEBI:"amyloid-beta"))
a(MESH:Kidney) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Intracellular Aβ degradation pathways mainly contain two major
pathways: UPS and ALS (Vilchez et al. 2014)"
SET MeSHAnatomy = "Intracellular Space"
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -> deg(a(CHEBI:"amyloid-beta"))
bp(GO:"lysosomal microautophagy") -> deg(a(CHEBI:"amyloid-beta"))
UNSET MeSHAnatomy

SET Evidence = "However, their dysfunction in AD due to some factors will reduce Aβ clearance"
# their:UPS and ALS
path(MESH:"Alzheimer Disease") -| bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process")
path(MESH:"Alzheimer Disease") -| bp(GO:"lysosomal microautophagy")
path(MESH:"Alzheimer Disease") -| deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "As is known, the accumulation of frameshift
ubiquitin-B (UBB) mutant protein UBB (+1) can block the
26S proteasome in cell lines, and then can reduce Aβ clearance
(Hope et al. 2003)"
p(HGNC:UBB,var("?")) -| act(p(HGNC:PSMD7))
p(HGNC:UBB,var("?")) -| deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "The excessive deposition
of Aβ also induces oxidative stress and mitochondrial dysfunction,
which fails to offer ATP for the degradation of targeted
proteins by UPS in yeast (Chen and Petranovic 2015)"
a(CHEBI:"amyloid-beta") -> bp(GO:"response to oxidative stress")
a(CHEBI:"amyloid-beta") -> bp(HBP:"mitochondrial dysfunction")

SET Evidence = "In addition, there is accumulating evidence to prove that
Aβ can be cleared by ALS."
bp(GO:"lysosomal microautophagy") -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Transcriptional factor EB downregulates
Aβ levels by affecting autophagy-lysosome (Zhang and Zhao 2015)"
p(HGNC:TFEB) -| a(CHEBI:"amyloid-beta")

SET Evidence = "Xiao et al. have also obtained the consistent
conclusion that transcriptional factor EB, a master regulator of
lysosome biogenesis, improves lysosomal function in astrocytes, which may promote Aβ clearance and attenuate
plaque pathogenesis (Xiao et al. 2014)"
SET MeSHAnatomy = "Astrocytes"
p(HGNC:TFEB) -> act(a(GO:lysosome))
p(HGNC:TFEB) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:TFEB) -| path(MESH:"Plaque, Amyloid")
UNSET MeSHAnatomy

SET Evidence = "Recent evidence has
indicated that autophagy is damaged in astrocytes accompanied
by the expression of APOE4, which attenuates Aβ degradation
(Simonovitch et al. 2016)"
a(HBP:"APOE e4") -| deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "The proteolytic degradation is also a major pathway of Aβ clearance"
bp(GO:proteolysis) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Extracellular Aβ degrading enzymes include
neprilysin (NEP), insulin-degrading enzyme (IDE), matrix
metalloproteinases (MMPs), angiotensin converting enzyme
(ACE), endothelin-converting enzyme (ECE), and plasmin
(Baranello et al. 2015)"
p(HGNC:MME) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:IDE) -> deg(a(CHEBI:"amyloid-beta"))
p(FPLX:MMP) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:ACE) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:ECE1) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:SERPINF2) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "NEP, plasma membrane glycoprotein, is a zinc
metalloendopeptidase and the most efficient hydrolytic enzyme
in degrading Aβ in vitro (Shirotani et al. 2001)"
p(HGNC:MME) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "And in APP transgenic mice,
long-term gene therapy of NEP ameliorates behavior by lowering
the levels of Aβ (Spencer et al. 2008)"
p(HGNC:MME) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:MME) -> bp(GO:behavior)

SET Evidence = "IDE, a zinc endopeptidase, can degrade extracellular Aβ (Vekrellis et al. 2000)"
p(HGNC:IDE) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Meanwhile, an animal experiment showed that IDE expression will descend
with age and diabetes, then resulting in Aβ deposition (Kochkina et al. 2015)"
bp(GO:aging) negativeCorrelation p(HGNC:IDE)
bp(GO:aging) positiveCorrelation a(CHEBI:"amyloid-beta")
path(MESH:"Diabetes Mellitus") negativeCorrelation p(HGNC:IDE)
path(MESH:"Diabetes Mellitus") positiveCorrelation a(CHEBI:"amyloid-beta")

SET Evidence = "ACE, a membrane-bound zinc metalloprotease,
catalyzes the transformation of angiotensin I to angiotensin II,
which is beneficial to the maintenance of body fluid, blood
pressure, and sodium balance"
p(HGNC:ACE) -> rxn(reactants(a(CHEBI:"Angiotensin I")),products(a(CHEBI:"angiotensin II")))
p(HGNC:ACE) reg a(MESH:"Body Fluids")
p(HGNC:ACE) reg bp(MESH:"Blood Pressure")
p(HGNC:ACE) reg bp(GO:"sodium ion transmembrane transport")

SET Evidence = "In addition, ACE expression also
enhances Aβ clearance, and the levels and activity of ACE are
elevated in AD brains (Barnes et al. 1991; Hemming and Selkoe 2005)"
p(HGNC:ACE) -> deg(a(CHEBI:"amyloid-beta"))
path(MESH:"Alzheimer Disease") positiveCorrelation act(p(HGNC:ACE))

SET Evidence = "Brain plasmin degrades Aβ"
p(HGNC:SERPINF2) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "However, the levels and activity of plasmin are reduced in AD brains (Ledesma et al. 2000)"
path(MESH:"Alzheimer Disease") negativeCorrelation p(HGNC:SERPINF2)
path(MESH:"Alzheimer Disease") negativeCorrelation act(p(HGNC:SERPINF2))

SET Evidence = "Among MMPs, MMP-2, -3 and -9, stimulated by Aβ, play
important roles in degrading Aβ (Wang et al. 2014)"
a(CHEBI:"amyloid-beta") -> act(p(HGNC:MMP2))
act(p(HGNC:MMP2)) -> deg(a(CHEBI:"amyloid-beta"))
a(CHEBI:"amyloid-beta") -> act(p(HGNC:MMP3))
act(p(HGNC:MMP3)) -> deg(a(CHEBI:"amyloid-beta"))
a(CHEBI:"amyloid-beta") -> act(p(HGNC:MMP9))
act(p(HGNC:MMP9)) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Microglial cells, the key immune cells of the brain, play an important
part in the phagocytosis of Aβ"
a(MESH:Microglia) -> bp(GO:phagocytosis)
bp(GO:phagocytosis) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Aβ is cleared by receptor-mediated
microglial phagocytosis and degradation, such as scavenger
receptors, chemokine-like receptor 1, toll-like receptors,
and G protein-coupled receptors including formyl peptide receptor
2 (Yu and Ye 2015)"
bp(GO:phagocytosis) -> deg(a(CHEBI:"amyloid-beta"))
a(MESH:"Receptors, Scavenger") -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:CMKLR1) -> deg(a(CHEBI:"amyloid-beta"))
p(FPLX:TLR) -> deg(a(CHEBI:"amyloid-beta"))
a(MESH:"Receptors, G-Protein-Coupled") -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:FPR2) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "In addition, triggering receptor expressed
on myeloid cells 2 (TREM2), ATP-binding cassette transporter
A7, and CD33 also play key roles in microglial phagocytosis"
p(HGNC:TREM2) -- bp(GO:phagocytosis)
p(HGNC:ABCA7) -- bp(GO:phagocytosis)
p(HGNC:CD33) -- bp(GO:phagocytosis)

SET Evidence = "In addition, Aβ1–42 can increase the expression of TREM2, a
surface signaling receptor in microglia, and the up-regulation of
TRME2 can facilitate microglial phagocytosis of Aβ1–42"
a(CHEBI:"amyloid-beta polypeptide 42") -> p(HGNC:TREM2)
act(p(HGNC:TREM2)) -> deg(a(CHEBI:"amyloid-beta polypeptide 42"))
act(p(HGNC:TREM2)) -> bp(GO:phagocytosis)
bp(GO:phagocytosis) -> deg(a(CHEBI:"amyloid-beta polypeptide 42"))

SET Evidence = "However, due to the presence of the R47H mutation in AD,
TREM2 cannot effectively recognize the lipid ligands and then
fails to activate microglia, which leads to Aβ deposition (Jiang
et al. 2014a; Jiang et al. 2013)"
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:TREM2,var("p.Arg47His")) -| act(a(MESH:Microglia))
p(HGNC:TREM2,var("p.Arg47His")) -> a(CHEBI:"amyloid-beta")
UNSET MeSHDisease

SET Evidence = "A recent study has shown that ABCA7,
mainly expressed in human microglial cells, also regulates
microglial phagocytic function and decreases Aβ deposition
(Zhao et al. 2015a)"
SET Cell = "microglial cell"
p(HGNC:ABCA7) reg bp(GO:phagocytosis)
p(HGNC:ABCA7) -| a(CHEBI:"amyloid-beta")
UNSET Cell

SET Evidence = "However, current evidence showed that
ATP-binding cassette transporter A7 deficit can increase Aβ deposition
in brain by promoting Aβ-production through increasing
β-secretase 1 levels rather than influencing the clearance of Aβ
in APP/PS1 mice (Sakae et al. 2016)"
p(HGNC:ABCA7) -| a(CHEBI:"amyloid-beta")
p(HGNC:ABCA7) -| p(HGNC:BACE1)

SET Evidence = "Besides, CD33, most
abundantly expressed in microglia in AD, inhibits normal function
of immune cells and impairs microglia-mediated clearance
of Aβ (Jiang et al. 2014b)"
p(HGNC:CD33) -| deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "By collecting time-matched blood samples from cerebral vein,
femoral vein, and radial artery in patients to measure the concentration
of Aβ for every blood sample and figure out the
turnover of it from vein to artery, it has been shown that transport
of Aβ from brain to blood via the BBB and CSF absorption
accounts for half of the total clearance of Aβ in CNS in
humans, and furthermore, the clearance rate of Aβ via the
BBB and CSF absorption accounts for the same proportion (Roberts et al. 2014)"
tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:Brain),toLoc(MESH:Blood)) -| a(CHEBI:"amyloid-beta")

SET Evidence = "LRP1, efflux
transporter protein, is expressed mainly at the abluminal membrane
of the BBB and highly expressive LRP1 can elevate the
rate of Aβ clearance from brain to blood (Pflanzner et al. 2011)"
p(HGNC:LRP1) -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:Brain),toLoc(MESH:Blood))

SET Evidence = "PICALM, mainly expressed in endothelial cells of vascular
walls, contributes to the transport of Aβ across the BBB
into blood (Xu et al. 2015)"
p(HGNC:PICALM) -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:"Blood-Brain Barrier"),toLoc(MESH:Blood))

SET Evidence = "And in AD, the decreasing
expression of PICALM in brain endothelium reduces Aβ
clearance (Zhao et al. 2015b)"
SET MeSHDisease = "Alzheimer Disease"
path(MESH:"Alzheimer Disease") negativeCorrelation p(HGNC:PICALM)
p(HGNC:PICALM) -| a(CHEBI:"amyloid-beta")
UNSET MeSHDisease

SET Evidence = "GLUT1, glucose transporter
expressed in the BBB, regulates LRP1-dependent Aβ clearance
via increasing the expression of LRP1"
SET MeSHAnatomy = "Blood-Brain Barrier"
p(HGNC:SLC2A1) -> p(HGNC:LRP1)
p(HGNC:LRP1) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:SLC2A1) reg deg(a(CHEBI:"amyloid-beta"))
UNSET MeSHAnatomy

SET Evidence = "In addition, the
transport of GLUT1-mediated glucose into the brain is also
beneficial to maintaining the integrity of the BBB, thereby
ensuring the normal transport of Aβ from brain into blood
(Winkler et al. 2015)"
p(HGNC:SLC2A1) reg tloc(a(CHEBI:glucose),fromLoc(GO:"extracellular region"),toLoc(MESH:"Blood-Brain Barrier"))
p(HGNC:SLC2A1) reg a(MESH:"Blood-Brain Barrier")
p(HGNC:SLC2A1) reg tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:Brain),toLoc(MESH:Blood))

SET Evidence = "And meanwhile, P-glycoprotein (Pgp),
as an efflux transporter, highly expressed on the lumen
surface of the BBB, has been proven to transport Aβ out of
brain (van Assema et al. 2012; Wei et al. 2016)"
p(HGNC:ABCB1) -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(MESH:Brain),toLoc(GO:"extracellular region"))

SET Evidence = "However, recently, it has been reported that the expression and transport
activity of P-gp are impaired in sporadic AD as a result of its
ubiquitination, internalization, and proteasome-dependent
degradation derived from Aβ40 (Chiu et al. 2015; Hartz
et al. 2016), which will result in Aβ deposition"
path(MESH:"Alzheimer Disease") -| p(HGNC:ABCB1)
path(MESH:"Alzheimer Disease") -> a(CHEBI:"amyloid-beta")
p(HGNC:ABCB1) -| a(CHEBI:"amyloid-beta")

SET Evidence = "In addition,
the luminal residing receptor for advanced glycation end products
(RAGE) is an Aβ influx transporter (Deane et al. 2003)"
p(HGNC:AGER) -> tloc(a(CHEBI:"amyloid-beta"),fromLoc(GO:"extracellular region"),toLoc(GO:intracellular))

SET Evidence = "There is a study showing that the
BCSFB is the primary removal channel compared with arachnoid
villi, resulting from the receptor LRP1 expressed in epithelial
cells of choroid plexus in the BCSFB (Fujiyoshi et al. 2011)"
a(HP:"blood-cerebrospinal fluid barrier") -| a(CHEBI:"amyloid-beta")

SET Evidence = "CSF is produced mainly by choroid plexus"
a(MESH:"Choroid Plexus") -> a(MESH:"Cerebrospinal Fluid")

SET Evidence = "It has been shown
that choroid plexus dysfunction, due to the reductive expression
of epithelial aquaporin-1, a water channel protein, can induce
CSF production, which in turn damages Aβ clearance in a triple
transgenic mouse model of AD (Gonzalez-Marrero et al. 2015)"
p(HGNC:AQP1) -> act(a(MESH:"Choroid Plexus"))
p(HGNC:AQP1) -| a(CHEBI:"amyloid-beta")

SET Evidence = "In a similar manner, a recent experiment indicated that water
influx into the CSF is significantly reduced in AD-patients, which
may impair Aβ clearance (Suzuki et al. 2015)"
path(MESH:"Alzheimer Disease") -| tloc(a(CHEBI:water),fromLoc(GO:"extracellular region"),toLoc(MESH:"Cerebrospinal Fluid"))
tloc(a(CHEBI:water),fromLoc(GO:"extracellular region"),toLoc(MESH:"Cerebrospinal Fluid")) -| a(CHEBI:"amyloid-beta")

SET Evidence = "In addition, Aβ deposition in CP also blocks CSF production in AD (Serot et al. 2012)."
SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:"amyloid-beta") -| a(MESH:"Cerebrospinal Fluid")
UNSET MeSHDisease

SET Evidence = "Moreover, researchers reported that TTR, a transporter protein
mainly synthesized in the CP of the brain and secreted into
the CSF, can reduce the Aβ contents in brain (Ribeiro et al.
2014), which gives us inspiration that TTR bound to Aβ may
be a natural mechanism of brain Aβ clearance"
SET MeSHAnatomy = {"Cerebrospinal Fluid","Brain"}
complex(p(HGNC:TTR),a(CHEBI:"amyloid-beta")) -> deg(a(CHEBI:"amyloid-beta"))
UNSET MeSHAnatomy

SET Evidence = "The expression of APOE ε4 allele is related to the
reduction of Aβ clearance from the brain by impairing its arterial
perivascular drainage, accompanied by changes of protein levels
in cerebrovascular basement membrane (Hawkes et al. 2012)"
a(HBP:"APOE e4") -| deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "In addition, Jeffrey J. Iliff et al. have demonstrated that the Aβ in
brain interstitium can be eliminated from the parenchyma by the
bulk flow of interstitial fluid, which also depends on a water
channel aquaporin-4 (AQP4) expressed in astrocyte endfeet"
p(HGNC:AQP4) -- deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Consistent with the conclusion above, there is evidence that
glymphatic drainage of ISF bulk flow relying on water channel
AQP4 can decrease the levels of Aβ in brain (Iliff et al. 2012)"
SET MeSHAnatomy = "Brain"
p(HGNC:AQP4) -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy

SET Evidence = "In the meanwhile, a study supported this idea that AQP4 deficiency
can reduce the rate of Aβ clearance via glymphatic pathway (Iliff
and Nedergaard 2013)"
a(HP:"glymphatic system") -| a(CHEBI:"amyloid-beta")
p(HGNC:AQP4) -| a(CHEBI:"amyloid-beta")

SET Evidence = "After traumatic brain injury (TBI), the
expression and location of AQP4 will change, inducing its dysfunction
(Ren et al. 2013)"
path(MESH:"Brain Injuries, Traumatic") -| act(p(HGNC:AQP4))

SET Evidence = "Later, an experiment on live
mice using some special methods has also proved that natural
sleep or sleep resulting from anesthesia can increase interstitial
space by 60%, thereby speeding up the exchange of CSF-ISF
and finally increasing the elimination of Aβ (Xie et al. 2013)"
path(MESH:Sleep) -| a(CHEBI:"amyloid-beta")

SET Evidence = "A study suggested that Aβ is removed from CSF to cervical
lymph nodes via perineural space of the olfactory nerve
(Picken 2001; Pollay 2010)"
SET MeSHAnatomy = "Neck"
a(MESH:"Lymph Nodes") -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy

SET Evidence = "By measuring Aβ levels in superior vena cava and inferior
vena cava, it is clear thatAβ levels are getting lower and lower
along the direction of the vein blood flow, and the contents of
Aβ40 and total Aβ in artery are significantly less than those in
vein, suggesting a part of Aβ40 and total Aβ can be cleared
by peripheral organs and tissues, such as the liver, kidney,
skin, and the gastrointestinal tract, although there is no change
in Aβ42 concentrations (Xiang et al. 2015)"
SET MeSHAnatomy = "Arteries"
a(MESH:Liver) -| a(CHEBI:"amyloid-beta")
a(MESH:Liver) -| a(CHEBI:"amyloid-beta polypeptide 40")
a(MESH:Liver) cnc a(CHEBI:"amyloid-beta polypeptide 42")
a(MESH:Kidney) -| a(CHEBI:"amyloid-beta")
a(MESH:Kidney) -| a(CHEBI:"amyloid-beta polypeptide 40")
a(MESH:Kidney) cnc a(CHEBI:"amyloid-beta polypeptide 42")
a(MESH:Skin) -| a(CHEBI:"amyloid-beta")
a(MESH:Skin) -| a(CHEBI:"amyloid-beta polypeptide 40")
a(MESH:Skin) cnc a(CHEBI:"amyloid-beta polypeptide 42")
a(MESH:"Gastrointestinal Tract") -| a(CHEBI:"amyloid-beta")
a(MESH:"Gastrointestinal Tract") -| a(CHEBI:"amyloid-beta polypeptide 40")
a(MESH:"Gastrointestinal Tract") cnc a(CHEBI:"amyloid-beta polypeptide 42")
UNSET MeSHAnatomy

SET Evidence = "Among these peripheral organs and tissues mentioned above, the liver and
kidney are considered to be the major organs for the clearance
of Aβ in periphery (Ghiso et al. 2004)"
a(MESH:Liver) -| a(CHEBI:"amyloid-beta")
a(MESH:Kidney) -| a(CHEBI:"amyloid-beta")

SET Evidence = "It is known that RBCs can facilitate Aβ clearance relying on
complement C3b-dependent adherence to complement receptor
1(CR1) on RBCs (Rogers et al. 2006)"
# RBC: red blood cells
complex(a(MESH:Erythrocytes),p(HGNC:CR1)) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Monocytes in peripheral blood have been demonstrated to play an important
role in clearing Aβ that diffuses from brain to blood (Halle et al. 2015)"
a(MESH:Monocytes) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "In addition, a recent experiment
showed that a great deal of Aβ in the blood circulation may
combine with serum albumin (Stanyon and Viles 2012),
which provided a novel clearance pathway in periphery"
composite(a(MESH:"Serum Albumin"),a(CHEBI:"amyloid-beta")) -| a(CHEBI:"amyloid-beta")

SET Evidence = "Tau proteins, microtubule-associated proteins, take part in the
formation of microtubules for the sake of maintaining the
stability of microtubules"
p(HGNC:MAPT) -> a(GO:microtubule)

SET Evidence = "It has been indicated that intracellular tau proteins are also
degraded by autophagy and proteasomal pathways (Wang and Mandelkow 2012)"
bp(GO:autophagy) -> deg(p(HGNC:MAPT))
bp(GO:"proteasomal protein catabolic process") -> deg(p(HGNC:MAPT))

SET Evidence = "Dysfunction of UPS brings about the
deposition of hyperphosphorylated tau oligomers in synapses (Tai et al. 2012)"
SET MeSHAnatomy = "Synapses"
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -| p(HBP:"Tau oligomers",pmod(HBP:hyperphosphorylation))
UNSET MeSHAnatomy

SET Evidence = "On the one hand, it is beneficial to the clearance of tau aggregates"
# it:autophagy-lysosome system
bp(GO:"lysosomal microautophagy") -| a(HBP:"Tau aggregates")

SET Evidence = "On the other hand, it brings about tau fragmentation
into pro-aggregating forms due to the failure of F1 to
enter the lysosome"
# it:autophagy-lysosome system
bp(GO:"lysosomal microautophagy") -> p(HGNC:MAPT,frag("?"))

SET Evidence = "For example, like Aβ, clearance of
pTau/NFT also can be regulated by TFEB, which increases
the activity of autophagy and lysosome (Polito et al. 2014)"
p(HGNC:TFEB) reg deg(a(MESH:"Neurofibrillary Tangles"))
p(HGNC:TFEB) reg deg(p(HGNC:MAPT,pmod(Ph)))
p(HGNC:TFEB) reg deg(a(CHEBI:"amyloid-beta"))
p(HGNC:TFEB) -> bp(GO:autophagy)
p(HGNC:TFEB) -> act(a(GO:lysosome))

SET Evidence = "Human brain tau can be degraded by the proteases, such as
cathepsin-D, amino peptidases, human high temperature requirement
serine protease A1 (HTRA1), thrombin, caspases,
and calpains (Chesser et al. 2013; Kenessey et al. 1997)"
SET MeSHAnatomy = "Brain"
p(HGNC:CTSD) -> deg(p(HGNC:MAPT))
a(MESH:Aminopeptidases) -> deg(p(HGNC:MAPT))
p(HGNC:HTRA1) -> deg(p(HGNC:MAPT))
a(MESH:Thrombin) -> deg(p(HGNC:MAPT))
p(FPLX:Caspase) -> deg(p(HGNC:MAPT))
p(FPLX:CAPN) -> deg(p(HGNC:MAPT))
UNSET MeSHAnatomy

SET Evidence = "However, there was a study indicating that calpain-mediated
tau cleavage can result in the generation of tau fragments, which
may possess neurotoxicity in AD (Ferreira and Bigio 2011)"
SET MeSHDisease = "Alzheimer Disease"
p(FPLX:CAPN) -> p(HGNC:MAPT,frag("?"))
p(HGNC:MAPT,frag("?")) -> bp(HBP:neurotoxicity)
UNSET MeSHDisease

SET Evidence = "There was direct evidence to show that microglial phagocytosis
plays a pivotal role in clearance of tau in vitro and in vivo (Bolos et al. 2015)"
SET Cell = "microglial cell"
bp(GO:phagocytosis) -| p(HGNC:MAPT)
UNSET Cell

SET Evidence = "It has been reported that ISF tau can be eliminated by the
glymphatic system and the function of this clearance mechanism
may be impaired due to the loss of AQP4 after TBI,
which ultimately accelerates tau accumulation (Iliff et al.2014)"
SET MeSHAnatomy = "Extracellular Fluid"
a(HP:"glymphatic system") -| p(HGNC:MAPT)
p(HGNC:AQP4) -> a(HP:"glymphatic system")
path(MESH:"Brain Injuries, Traumatic") -| p(HGNC:AQP4)
path(MESH:"Brain Injuries, Traumatic") -> p(HGNC:MAPT)
path(MESH:"Brain Injuries, Traumatic") -| a(HP:"glymphatic system")
UNSET MeSHAnatomy

SET Evidence = "Tenuigenin could obviously reduce intracerebral Aβ1–40 accumulation
in AD mouse brain by increasing the content of 26S proteasome (Chen et al. 2015)"
a(MESH:tenuigenin) -| a(CHEBI:"amyloid-beta polypeptide 40")
a(MESH:tenuigenin) -> p(HGNC:PSMD7)

SET Evidence = "Impairment of 26S proteasome induced by tau can be prevented early in
disease through activation of cAMP-PKA signaling, and raising the
levels of cAMP with rolipram may enhance tau degradation (Myeku et al. 2016)"
p(HGNC:MAPT) -| p(HGNC:PSMD7)
bp(GO:"cAMP-mediated signaling") -> p(HGNC:PSMD7)
a(CHEBI:rolipram) -> a(CHEBI:"3',5'-cyclic AMP")
a(CHEBI:"3',5'-cyclic AMP") -> deg(p(HGNC:MAPT))
a(CHEBI:rolipram) -> deg(p(HGNC:MAPT))

SET Evidence = "Wogonin, rapamycin, and temsirolimus have been considered to improve
the activity of autophagy to increase Aβ clearance and
inhibit tau phosphorylation via targeting mTOR signaling
(Caccamo et al. 2010; Jiang et al. 2014c; Jiang et al. 2014d; Spilman et al. 2010; Zhu andWang 2015)"
a(CHEBI:wogonin) -> bp(GO:autophagy)
a(CHEBI:wogonin) -| a(CHEBI:"amyloid-beta")
a(CHEBI:wogonin) -| p(HGNC:MAPT,pmod(Ph))
a(CHEBI:sirolimus) -> bp(GO:autophagy)
a(CHEBI:sirolimus) -| a(CHEBI:"amyloid-beta")
a(CHEBI:sirolimus) -| p(HGNC:MAPT,pmod(Ph))
a(CHEBI:Temsirolimus) -> bp(GO:autophagy)
a(CHEBI:Temsirolimus) -| a(CHEBI:"amyloid-beta")
a(CHEBI:Temsirolimus) -| p(HGNC:MAPT,pmod(Ph))

SET Evidence = "Protein phosphatase 2A agonists are reported
to activate autophagy by affecting AMPK and mTORC1 signaling
pathways (Magnaudeix et al. 2013)"
p(HGNC:PPP2CA) -> bp(GO:autophagy)

SET Evidence = "Recent acknowledged drugs can improve autophagy by
acting on the process of autophagy-lysosome formation and
then increasing tau clearance, such as methylene blue, lithium,
and trehalose (Congdon et al. 2012; Kruger et al. 2012;
Shimada et al. 2012)"
a(CHEBI:"methylene blue") -> bp(GO:autophagy)
a(CHEBI:"lithium(1+)") -> bp(GO:autophagy)
a(CHEBI:"alpha,alpha-trehalose") -> bp(GO:autophagy)

SET Evidence = "Trehalose also can reduce Aβ levels in hippocampus (Du et al. 2013)"
SET MeSHAnatomy = "Hippocampus"
a(CHEBI:"alpha,alpha-trehalose") -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy

SET Evidence = "Epigallocatechin-3-gallate enhances the clearance of ADrelevant
phosphorylated tau species via increasing mRNA expression
of autophagy adaptor proteins NDP52 and p62
(Chesser et al. 2016)"
a(CHEBI:"(-)-epigallocatechin 3-gallate") -| p(HGNC:MAPT,pmod(Ph))
a(CHEBI:"(-)-epigallocatechin 3-gallate") -> p(HGNC:CALCOCO2)
a(CHEBI:"(-)-epigallocatechin 3-gallate") -> p(HGNC:SQSTM1)

SET Evidence = "Proteasome inhibitors and trehalose increase autophagy and
decrease tau content by up-regulating the expression of cochaperone
BAG3 targeting tau to the autophagy pathway for degradation (Lei et al. 2015)"
a(CHEBI:"proteasome inhibitor") -> bp(GO:autophagy)
a(CHEBI:"proteasome inhibitor") -| p(HGNC:MAPT)
a(CHEBI:"proteasome inhibitor") -> p(HGNC:BAG3)
a(CHEBI:"alpha,alpha-trehalose") -> bp(GO:autophagy)
a(CHEBI:"alpha,alpha-trehalose") -| p(HGNC:MAPT)
a(CHEBI:"alpha,alpha-trehalose") -> p(HGNC:BAG3)
p(HGNC:BAG3) -> deg(p(HGNC:MAPT))

SET Evidence = "Neuronal PAS domain protein 4
has been found to facilitate the autophagic clearance of endogenous
total and phosphorylated tau in cortical neurons of rats"
p(HGNC:NPAS4) -> bp(GO:autophagy)
bp(GO:autophagy) -> deg(p(HGNC:MAPT))
bp(GO:autophagy) -> deg(p(HGNC:MAPT,pmod(Ph)))
p(HGNC:NPAS4) -> deg(p(HGNC:MAPT))
p(HGNC:NPAS4) -> deg(p(HGNC:MAPT,pmod(Ph)))

SET Evidence = "Exercise training can increase extracellular Aβ clearance in
the brains of Tg2576 mice in a dose-dependent manner
through up-regulating NEP, IDE, MMP9, LRP1, and HSP70 (Moore et al. 2016)"
p(HGNC:MME) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:IDE) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:MMP9) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:LRP1) -> deg(a(CHEBI:"amyloid-beta"))
p(FPLX:HSPA) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Statin can lead to extracellular
IDE secretion from astrocytes in an autophagy-based unconventional
secretory pathway (Glebov and Walter 2012),
thus enhancing the extracellular removal of Aβ"
SET Cell = "astrocyte"
a(CHEBI:statin) -> sec(p(HGNC:IDE))
a(CHEBI:statin) -> deg(a(CHEBI:"amyloid-beta"))
UNSET Cell

SET Evidence = "Somatotatin also up-regulates the expression and secretion
of IDE in order to enhance Aβ clearance (Tundo et al.2012)"
a(CHEBI:somatostatin) -> p(HGNC:IDE)
a(CHEBI:somatostatin) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "A recent study showed that omega-3 polyunsaturated
fatty acids also increase Aβ degradation by proteases"
a(CHEBI:"omega-3 fatty acid") -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "For example, eicosapentaenoic acid (EPA) can induce the
activity of IDE and increase its gene expression, and
docosahexaenoic acid (DHA) also directly elevates IDE
activity and affects sorting by boosting exosome release of IDE (Grimm et al. 2016)"
a(CHEBI:"all-cis-5,8,11,14,17-icosapentaenoic acid") -> act(p(HGNC:IDE))
a(CHEBI:"docosahexaenoic acid") -> act(p(HGNC:IDE))

SET Evidence = "Simvastatin and atorvastatin
enhance extracellular Aβ degradation via increasing NEP
secretion from astrocytes by activating MAPK/Erk1/2 (Yamamoto et al. 2016)"
a(CHEBI:simvastatin) -> deg(a(CHEBI:"amyloid-beta"))
a(CHEBI:simvastatin) -> bp(GO:"ERK1 and ERK2 cascade")
a(CHEBI:atorvastatin) -> deg(a(CHEBI:"amyloid-beta"))
a(CHEBI:simvastatin) -> bp(GO:"ERK1 and ERK2 cascade")
SET MeSHAnatomy = "Astrocytes"
a(CHEBI:simvastatin) -> sec(p(HGNC:MME))
a(CHEBI:atorvastatin) -> sec(p(HGNC:MME))
UNSET MeSHAnatomy

SET Evidence = "Nobiletin, a flavone from citrus
depressa, leads to gene expression and improves the protein
level and activity of NEP in SK-N-SH cells, thus reducing
Aβ levels (Fujiwara et al. 2014)"
a(CHEBI:nobiletin) -> act(p(HGNC:MME))
a(CHEBI:nobiletin) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "And ginsenoside Rg1 and
granulocyte-colony stimulating factor may up-regulate activities
of NEP in retinal cells in an AD mouse model to
reduce tau protein pathology (He et al. 2014) (Doi et al.2014)."
a(CHEBI:"ginsenoside Rg1") -> act(p(HGNC:MME))
a(CHEBI:"ginsenoside Rg1") -| path(MESH:Tauopathies)
a(MESH:"Granulocyte Colony-Stimulating Factor") -> act(p(HGNC:MME))
a(MESH:"Granulocyte Colony-Stimulating Factor") -| path(MESH:Tauopathies)

SET Evidence = "Anti-inflammatory mediator annexin A1 (ANXA1)
can reduce Aβ content by increasing its degradation by
NEP (Ries et al. 2016)"
p(HGNC:ANXA1) -> deg(a(CHEBI:"amyloid-beta"))
p(HGNC:ANXA1) -> act(p(HGNC:MME))
act(p(HGNC:MME)) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Minocycline not only
suppresses pro-inflammatory phenotypes of microglia but also
promotes their phagocytic clearance of Aβ (El-Shimy et al.2015)"
a(CHEBI:minocycline) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Fractalkine can keep microglia in the right state via
interacting with CX3CR, thus contributing to Aβ clearance"
complex(a(MESH:"Chemokine CX3CL1"),p(HGNC:CX3CR1)) -> deg(a(CHEBI:"amyloid-beta"))

SET Evidence = "Rosiglitazone, a highaffinity
agonist for PPARγ, can clear Aβ by activating microglia
and promoting its phagocytosis via increasing the levels of
CD36, a receptor expressed in it (Escribano et al. 2010)"
a(CHEBI:rosiglitazone) -> act(p(HGNC:PPARG))
a(CHEBI:rosiglitazone) -> deg(a(CHEBI:"amyloid-beta"))
a(CHEBI:rosiglitazone) -> act(a(MESH:Microglia))

SET Evidence = "Moreover, pioglitazone seems to be an effective therapeutic approach
targeting Aβ clearance via similar mechanisms to those
of rosiglitazone (Mandrekar-Colucci et al. 2012)"
a(CHEBI:pioglitazone) -| a(CHEBI:"amyloid-beta")

SET Evidence = "It has been discovered that cromolyn sodium,
already used in the cure for asthma, can enter the central nervous
system and promote Aβ monomer clearance by microglial
phagocytosis (Hori et al. 2015)"
a(CHEBI:"disodium cromoglycate") -| a(CHEBI:"amyloid-beta")

SET Evidence = "1,25(OH)2D3, the active form of vitamin D, plays a key
role in enhancing transport of Aβ1–40 from the brain to
the blood by reducing RAGE levels at the BBB, and also
in contributing to periphery clearance by increasing levels
of LRP1 both in vivo and in vitro (Guo et al. 2016b)"
a(CHEBI:calcitriol) -> tloc(a(CHEBI:"amyloid-beta polypeptide 40"),fromLoc(MESH:Brain),toLoc(MESH:Blood))
SET MeSHAnatomy = "Blood-Brain Barrier"
a(CHEBI:calcitriol) -| p(HGNC:AGER)
UNSET MeSHAnatomy
a(CHEBI:calcitriol) -> p(HGNC:LRP1)

SET Evidence = "Oleocanthal, a special component of extra-virgin olive oil,
increases cerebral clearance of Aβ across the BBB by enhancing
the expression of important efflux transport proteins
at the BBB containing LRP1 and P-gp, and activating
the APOE-dependent Aβ clearance pathway in mice brains (Qosa et al. 2015)"
a(CHEBI:oleocanthal) -| a(CHEBI:"amyloid-beta")
SET MeSHAnatomy = "Blood-Brain Barrier"
a(CHEBI:oleocanthal) -> p(HGNC:LRP1)
a(CHEBI:oleocanthal) -> p(HGNC:ABCB1)
UNSET MeSHAnatomy

SET Evidence = "Cholinesterase inhibitors donepezil and rivastigmine, upregulating
transport proteins P-glycoprotein and LRP1, may
improve Aβ clearance in the liver of rats (Mohamed et al. 2015)"
a(CHEBI:donepezil) -> p(HGNC:ABCB1)
a(CHEBI:donepezil) -> p(HGNC:LRP1)
a(CHEBI:donepezil) -| a(CHEBI:"amyloid-beta")
a(CHEBI:rivastigmine) -> p(HGNC:ABCB1)
a(CHEBI:rivastigmine) -> p(HGNC:LRP1)
a(CHEBI:rivastigmine) -| a(CHEBI:"amyloid-beta")

SET Evidence = "Dietary pre-administration of docosahexaenoic acid
prevents RBCs from oxidative damage due to its antioxidative
characteristic and also increases Aβ degradation by RBC in a
lipid raft-dependent manner (Hashimoto et al. 2015)"
a(CHEBI:"docosahexaenoic acid") -> deg(a(CHEBI:"amyloid-beta"))
a(CHEBI:"docosahexaenoic acid") -> act(a(MESH:Erythrocytes))
a(MESH:Erythrocytes) -> deg(a(CHEBI:"amyloid-beta"))
