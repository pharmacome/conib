#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Naturally-expressed nicotinic acetylcholine receptor subtypes"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/501ceccdc9a27d97edbdc48a89ebe8e1dd3626e9/export/conso-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "21787755"}

SET Evidence = "Moreover, some nAChRs have been implicated in processes such as
the structuring and maintenance of neurites and synapses [18–20]
and even in modulation of neuronal viability/death [21–24]."
SET Confidence = "High"
p(FPLX:CHRN) -> bp(GO:"maintenance of synapse structure")
p(FPLX:CHRN) -> bp(GO:"neuron projection maintenance")
UNSET Confidence

SET Evidence = "nAChRs that bind radiolabeled nicotine with the highest afﬁnity
contain a4 subunits (a4*-nAChR; see reviews and/or tables by [1–7]."
SET Confidence = "Medium"
composite(a(CONSO:"alpha-4-containing nAChR"),a(CHEBI:nicotine)) -> complex(a(CONSO:"alpha-4-containing nAChR"),a(CHEBI:nicotine))
UNSET Confidence

SET Evidence = " a4b2-nAChRs have been
implicated in nicotine self-administration, reward, and depen-
dence, and in diseases such as Alzheimer’s and epilepsy [1–5,27–33]."
SET Confidence = "Medium"
complex(p(HGNC:CHRNA4),p(HGNC:CHRNB2)) -- path(MESH:"Tobacco Use Disorder")
complex(p(HGNC:CHRNA4),p(HGNC:CHRNB2)) -- path(MESH:Reward)
complex(p(HGNC:CHRNA4),p(HGNC:CHRNB2)) -- path(MESH:"Alzheimer Disease")
complex(p(HGNC:CHRNA4),p(HGNC:CHRNB2)) -- path(MESH:Epilepsy)
# ABOs to complex(p(HGNC:CHRNA4),p(HGNC:CHRNB2))
UNSET Confidence

SET Evidence = "a4 subunits can also assemble with b4 subunits to form
a4b4-nAChRs that have comparably high nicotine afﬁnity [34–36]."
SET Confidence = "Medium"
composite(p(HGNC:CHRNA4),p(HGNC:CHRNB4)) -> a(CONSO:"alpha-4 beta-4 nAChR")
composite(a(CONSO:"alpha-4 beta-4 nAChR"),a(CHEBI:nicotine)) -> complex(a(CONSO:"alpha-4 beta-4 nAChR"),a(CHEBI:nicotine))
#making statements elaborate
UNSET Confidence

SET Evidence = " b4 subunit mRNA colocalizes with a4 subunit mRNA in many
brain regions [37,38] that could be involved in complex behaviors
including nicotine dependence."
SET Confidence = "Low"
SET MeSHAnatomy = "Brain"
r(HGNC:CHRNA4) -- r(HGNC:CHRNB4)
composite(r(HGNC:CHRNA4), r(HGNC:CHRNB4)) -- path(MESH:"Tobacco Use Disorder")
#evidence says that mRNA colocalization in invovled in nicotine dependence.
UNSET MeSHAnatomy

SET Evidence = "In terms of
functional effects, nicotine acts acutely much in the way that ACh
does, causing opening of nAChR channels."
SET Confidence = "High"
a(CHEBI:nicotine) -> act(p(FPLX:CHRN))
a(CHEBI:acetylcholine) -> act(p(FPLX:CHRN))
UNSET Confidence

SET Evidence = "Another prominent nAChR subtype found in vertebrate central
and autonomic nervous systems contains a7 subunits (a7-
nAChRs). These sites have been known to exist for many years
based on their ability to bind the curaremimetic neurotoxin, a-
bungarotoxin (Bgt) [74–78]."
SET Confidence = "Medium"
SET MeSHAnatomy = {"Central Nervous System","Autonomic Nervous System"}
complex(a(CONSO:"alpha-7-containing nAChR"),a(CONSO:"alpha-Bungarotoxin"))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Subsequently, renewed
searches for functions of natural Bgt-binding nAChRs uncovered
short-lived, nicotine-gated, toxin-sensitive, inward currents and/
or elevations of intracellular Ca2+ in chick autonomic neurons [84],
in human ganglionic neuron-like clonal cells [85], or in rat CNS
neurons [16,40–44,86–91]."
SET Confidence = "High"
SET Cell = "neuron"
a(CONSO:"alpha-7-containing nAChR") -| tloc(a(CHEBI:"calcium(2+)"), MESH:"Extracellular Space", MESH:"Intracellular Space")
UNSET Cell
UNSET Confidence

SET Evidence = "By virtue of their unique subcellular localizations, channel
kinetics and Ca2+ permeability, a7-nAChRs appear to have novel
functional roles in addition to (i.e., distinct from) the mediation of
classical excitatory neurotransmission. "
SET Confidence = "High"
a(CONSO:"alpha-7-containing nAChR") reg bp(GO:"excitatory chemical synaptic transmission")
UNSET Confidence

SET Evidence = "For example, Bgt-sensitive
a7-nAChRs have been implicated in processes such as vicinal
control of neurotransmitter release [7,14], development and
maintenance of neurites and synapses [18–20], long-term
potentiation [95,96], seizures [97], and neuronal viability/death
[21–24]. These intriguing ﬁndings underscore the need for further
characterization of functional a7-nAChRs."
SET Confidence = "High"
a(CONSO:"alpha-7-containing nAChR") -- bp(GO:"neurotransmitter secretion")
a(CONSO:"alpha-7-containing nAChR") -- bp(GO:"maintenance of synapse structure")
a(CONSO:"alpha-7-containing nAChR") -- bp(GO:"neuron projection maintenance")
a(CONSO:"alpha-7-containing nAChR") -- bp(GO:"long-term synaptic potentiation")
a(CONSO:"alpha-7-containing nAChR") -- path(MESH:Seizures)
a(CONSO:"alpha-7-containing nAChR") -- bp(GO:"neuron death")
UNSET Confidence

SET Evidence = " The fact that oligomeric amyloid-b
seems to have high afﬁnity for a7b2-nAChRs expressed in the brain
region that is damaged early in Alzheimer’s disease may have
implications for disease etiopathogenesis."
SET Confidence = "High"
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
complex(a(CONSO:"alpha-7 beta-2 nAChR"),a(CHEBI:"amyloid-beta"))
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "a6 subunits are not widely expressed in the brain, but they are
prevalent in midbrain dopaminergic (DAergic) regions associated
with pleasure, reward, and mood control "
SET Confidence = "High"
SET Cell = "midbrain dopaminergic neuron"
a(CONSO:"alpha-6-containing nAChR") -- path(MESH:Pleasure)
a(CONSO:"alpha-6-containing nAChR") -- path(MESH:Reward)
a(CONSO:"alpha-6-containing nAChR") -- path(MESH:Affect)
UNSET Cell
UNSET Confidence

SET Evidence = " Recently, we reported a novel discovery that
functional a6*-nAChRs are located on GABAergic presynaptic
boutons associated with VTA DAergic neurons, where these a6*-
nAChRs mediate nicotinic modulation of GABA release onto those
DAergic neurons [122]."
SET Confidence = "High"
SET MeSHAnatomy =  {"Presynaptic Terminals","GABAergic Neurons"}
a(CONSO:"alpha-6-containing nAChR") reg sec(a(CHEBI:"gamma-aminobutyric acid"))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Initially, a-CtxMII was
thought to be highly selective for a3b2*-nAChRs [124] and became
a useful tool for investigating receptor structure [125]."
SET Confidence = "Medium"
a(MESH:"alpha-conotoxin MII") -| act(a(CONSO:"alpha-3 beta-2 nAChR"))
#evidence uses the phrase highly selective
UNSET Confidence

SET Evidence = "Other ‘‘a3b2-speciﬁc’’ antagonists, such as neuronal- or kappa-
bungarotoxin and a-CtxPnIA, have also turned out to block a6*-
nAChRs [129]."
SET Confidence = "High"
a(MESH:"neuronal bungarotoxin") -| act(a(CONSO:"alpha-3 beta-2 nAChR"))
a(CONSO:"kappa-Bungarotoxin") -| act(a(CONSO:"alpha-3 beta-2 nAChR"))
a(MESH:"alpha-conotoxin PnIA") -| act(a(CONSO:"alpha-3 beta-2 nAChR"))
a(MESH:"neuronal bungarotoxin") -| act(a(CONSO:"alpha-6-containing nAChR"))
a(CONSO:"kappa-Bungarotoxin") -| act(a(CONSO:"alpha-6-containing nAChR"))
a(MESH:"alpha-conotoxin PnIA") -| act(a(CONSO:"alpha-6-containing nAChR"))
UNSET Confidence

SET Evidence = "There is an emerging consensus that nAChR a6 subunit mRNA
and proteins are distributed in brain regions thought to be involved
in reward and drug reinforcement, in theory being involved in DA
release [135]. "
SET Confidence = "High"
SET MeSHAnatomy = "Brain"
r(HGNC:CHRNA6) -- path(MESH:Reward)
r(HGNC:CHRNA6) -- bp(MESH:"Drug Interactions")
r(HGNC:CHRNA6) -- sec(a(CHEBI:dopamine))
p(HGNC:CHRNA6) -- path(MESH:Reward)
p(HGNC:CHRNA6) -- bp(MESH:"Drug Interactions")
p(HGNC:CHRNA6) -- sec(a(CHEBI:dopamine))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = " This makes the hypothesis particularly intriguing
that a6*-nAChRs play roles in nicotine dependence."
SET Confidence = "High"
a(CONSO:"alpha-6-containing nAChR") -- path(MESH:"Tobacco Use Disorder")
UNSET Confidence

SET Evidence = "There is good evidence that a6*-nAChRs, in particular,
modulate neurotransmitter release in multiple brain regions."
SET Confidence = "High"
SET MeSHAnatomy = "Brain"
a(CONSO:"alpha-6-containing nAChR") reg bp(GO:"neurotransmitter secretion")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = " Also, Endo et al. [141] found
naturally-expressed a3b2- and a6b2-nAChRs on superior colli-
culus neurons, and these receptors are likely located on pre-
synaptic terminals of GABAergic neurons where they modulate
GABA release."
SET Confidence = "High"
SET MeSHAnatomy =  {"Presynaptic Terminals","GABAergic Neurons"}
a(CONSO:"alpha-3 beta-2 nAChR") reg sec(a(CHEBI:"gamma-aminobutyric acid"))
a(MESH:"alpha6beta2 nicotinic acetylcholine receptor") reg sec(a(CHEBI:"gamma-aminobutyric acid"))
UNSET MeSHAnatomy
UNSET Confidence
