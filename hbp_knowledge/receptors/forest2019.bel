#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Assessing Neuroprotective Agents for Aβ-Induced Neurotoxicity"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Trusha Adeshara"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
 DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
 DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
 DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
 #DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
 DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
 DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
 DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
 DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
 #DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
 #DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
 DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
 DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
 DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
 DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "31248781"}

SET Evidence = "Moreover, neurodegeneration correlates best with neurofibrillary tangle burden [3]"
SET Confidence = "High"
path(CONSO:Neurodegeneration) -- p(MESH:"Neurofibrillary Tangles")
UNSET Confidence

SET Evidence = "Abeta is produced via sequential proteolytic cleavage of the amyloid precursor protein (APP) by the processing enzymes, gamma-secretase and beta-secretase (BACE) [14]"
SET Confidence = "High"
composite(p(HGNC:APP),p(HGNC:BACE1),complex(CONSO:"gamma-secretase")) -> p(HGNC:APP,frag("?"))
p(HGNC:APP,frag("?")) -> a(CHEBI:"amyloid-beta")
UNSET Confidence

SET Evidence = "Additional neuroprotective strategies relevant to AD include regulating APP [43], neurotransmitter receptors (e.g., [44]), downregulating stress kinase signaling cascades [45–47], blocking activation of caspases (e.g., [48]), and/or upregulating cell survival pathways [49–52]."
SET Confidence = "High"
SET Disease = "Alzheimer's disease"
p(HGNC:APP) reg bp(MESH:Neuroprotection)
complex(GO:"neurotransmitter receptor complex") -- bp(MESH:Neuroprotection)
bp(GO:"stress-activated protein kinase signaling cascade") neg bp(MESH:Neuroprotection)
act(p(MESH:Caspases)) neg bp(MESH:Neuroprotection)
bp(MESH:"Cell Survival") pos bp(MESH:Neuroprotection)
UNSET Disease
UNSET Confidence

SET Evidence = "Behavioral deficits in AD extend to impaired attention, orientation (space and time), working memory, executive function and judgment, language function, mood (depression, anxiety, agitation), and sleep [56]."
SET Confidence = "High"
path(DOID:"Alzheimer's disease") -| path(MESH:Attention)
path(DOID:"Alzheimer's disease") -| path(MESH:Orientation)
path(DOID:"Alzheimer's disease") -| bp(GO:"learning or memory")
path(DOID:"Alzheimer's disease") -| path(MESH:"Executive Function")
path(DOID:"Alzheimer's disease") -| path(MESH:Judgment)
path(DOID:"Alzheimer's disease") -> path(MESH:"Language Disorders")
path(DOID:"Alzheimer's disease") -> path(DOID:"mental depression")
path(DOID:"Alzheimer's disease") -> path(MESH:"Anxiety Disorders")
path(DOID:"Alzheimer's disease") -> path(HP:Agitation)
path(DOID:"Alzheimer's disease") -> path(DOID:"sleep disorder")
UNSET Confidence

SET Evidence = "Aβ has been shown to interact with various receptors to induce synaptic and neuronal dysfunction and death. Targets notably include cellular prion protein (PrPc) [59,60] coupled to the metabotropic glutamate receptor 5 (mGluR5) [59,61], amylin receptors [62–64], NMDA-type glutamate receptors (and perhaps AMPA glutamate receptors) [65,66], and nicotinic acetylcholine receptors (nAChRs) [67–69]."
SET Confidence = "High"
composite(a(CHEBI:"amyloid-beta"),p(HGNC:PRNP),p(HGNC:GRM5)) -| bp(GO:"synaptic signaling")
composite(a(CHEBI:"amyloid-beta"),p(HGNC:PRNP),p(HGNC:GRM5)) -| bp(GO:"neuronal signal transduction")
composite(a(CHEBI:"amyloid-beta"),p(HGNC:PRNP),p(HGNC:GRM5)) -> bp(GO:"neuron death")
composite(a(CHEBI:"amyloid-beta"),p(MESH:"Receptors, Islet Amyloid Polypeptide")) -| bp(GO:"synaptic signaling")
composite(a(CHEBI:"amyloid-beta"),p(MESH:"Receptors, Islet Amyloid Polypeptide")) -| bp(GO:"neuronal signal transduction")
composite(a(CHEBI:"amyloid-beta"),p(MESH:"Receptors, Islet Amyloid Polypeptide")) -> bp(GO:"neuron death")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Glutamate ionotropic receptor NMDA type subunits")) -| bp(GO:"synaptic signaling")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Glutamate ionotropic receptor NMDA type subunits")) -| bp(GO:"neuronal signal transduction")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Glutamate ionotropic receptor NMDA type subunits")) -> bp(GO:"neuron death")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Glutamate ionotropic receptor AMPA type subunits")) -| bp(GO:"synaptic signaling")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Glutamate ionotropic receptor AMPA type subunits")) -| bp(GO:"neuronal signal transduction")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Glutamate ionotropic receptor AMPA type subunits")) -> bp(GO:"neuron death")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Cholinergic receptors nicotinic subunits")) -| bp(GO:"synaptic signaling")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Cholinergic receptors nicotinic subunits")) -| bp(GO:"neuronal signal transduction")
composite(a(CHEBI:"amyloid-beta"),p(HGNCGENEFAMILY:"Cholinergic receptors nicotinic subunits")) -> bp(GO:"neuron death")
UNSET Confidence

SET Evidence = "Amylin was originally described as a pancreatic peptide linked to diabetes via its glycemic regulation and pancreatic β-cell dysregulation [73]; however,receptors in the brain for amylin were later discovered [74], largely linked to autonomic regulation."
SET Confidence = "High"
SET Anatomy = "pancreas"
p(HGNC:IAPP) reg a(CHEBI:glucose)
p(HGNC:IAPP) -- path(DOID:"diabetes mellitus")
p(HGNC:IAPP) reg bp(GO:"negative regulation of type B pancreatic cell development")
UNSET Anatomy
SET Anatomy = "brain"
p(MESH:"Receptors, Islet Amyloid Polypeptide") reg a(MESH:"Autonomic Nervous System")
UNSET Anatomy
UNSET Confidence

SET Evidence = "Amylin receptors were found to be regulated by Aβ [62,64] and both peptides acting via amylin receptors trigger apoptotic neuronal death [75]."
SET Confidence = "High"
a(CHEBI:"amyloid-beta") reg p(MESH:"Receptors, Islet Amyloid Polypeptide")
complex(p(HGNC:IAPP),p(MESH:"Receptors, Islet Amyloid Polypeptide")) -> bp(GO:"neuron apoptotic process")
complex(p(CHEBI:"amyloid-beta"),p(MESH:"Receptors, Islet Amyloid Polypeptide")) -> bp(GO:"neuron apoptotic process")
UNSET Confidence

SET Evidence = "In AD models, the cAMP response element-binding protein (CREB), a constitutively expressed nuclear transcription factor that regulates neuronal survival and function, has been shown to be downregulated in hippocampal neurons [50–52]."
SET Confidence = "High"
SET Disease = "Alzheimer's disease"
SET Cell = "neuron"
p(HGNC:CREBBP) reg bp(MESH:"Cell Survival")
p(HGNC:CREBBP) reg act(a(MESH:Neurons))
UNSET Disease
SET MeSHAnatomy = "Hippocampus"
path(DOID:"Alzheimer's disease") -| p(HGNC:CREBBP)
UNSET MeSHAnatomy
UNSET Cell
UNSET Confidence

SET Evidence = "It has been shown that CREB is important in neurotrophin-mediated signaling linked to neuronal survival [79] and in neuroprotection via the PI3-kinase/Akt pathway [80]."
SET Confidence = "High"
SET Cell = "neuron"
p(HGNC:CREBBP) -- bp(GO:"neurotrophin signaling pathway")
bp(GO:"neurotrophin signaling pathway") -- bp(MESH:"Cell Survival")
p(HGNC:CREBBP) -- bp(MESH:"Cell Survival")
p(HGNC:CREBBP) -- bp(CONSO:"PI3K-Akt signaling pathway")
bp(CONSO:"PI3K-Akt signaling pathway") -- bp(MESH:Neuroprotection)
p(HGNC:CREBBP) -- bp(MESH:Neuroprotection)
UNSET Cell
UNSET Confidence

SET Evidence = "Contributing to a feedforward mechanism, CREB is believed to upregulate neurotrophin expression and activity, importantly, brain derived neurotrophic factor (BDNF), which is important in modulating synaptic plasticity [81], and a neurotrophin target receptor, the tropomyosin receptor kinase receptor B (TrkB) [82]"
SET Confidence = "High"
p(HGNC:CREBBP) => p(HGNCGENEFAMILY:Neurotrophins)
p(HGNC:CREBBP) => act(p(HGNCGENEFAMILY:Neurotrophins))
p(HGNC:CREBBP) => p(HGNC:BDNF)
p(HGNC:BDNF) reg bp(MESH:"Neuronal Plasticity")
p(HGNC:CREBBP) => p(HGNC:NTRK2)
UNSET Confidence

SET Evidence = "An example of an intriguing crossover to AD for an orally active free radical scavenger,edaravone, originally developed for acute ischemic stroke, was discovered wherein strong reductions in Aβ-induced oxidation, APP processing, Aβ deposition, neuroinflammation, neurite and dendritic loss, and synaptic and behavioral dysfunction were observed in in vitro and in vivo APP models [83]."
SET Confidence = "High"
a(DRUGBANK:Edaravone) -| path(HP:"Ischemic stroke")
a(DRUGBANK:Edaravone) -| act(a(CHEBI:"amyloid-beta"))
act(a(CHEBI:"amyloid-beta")) -> path(MESH:"Oxidative Stress")
a(DRUGBANK:Edaravone) -| path(MESH:"Oxidative Stress")
a(DRUGBANK:Edaravone) -| path(CONSO:"APP processing")
a(DRUGBANK:Edaravone) -| path(MESH:"Plaque, Amyloid")
a(DRUGBANK:Edaravone) -| path(CONSO:neuroinflammation)
a(DRUGBANK:Edaravone) -| deg(a(MESH:Neurites))
a(DRUGBANK:Edaravone) -| deg(a(MESH:Dendrites))
a(DRUGBANK:Edaravone) -> bp(GO:"synaptic signaling")
a(DRUGBANK:Edaravone) -> bp(GO:behavior)
UNSET Confidence

SET Evidence = "Aβ, primarily in monomeric form, appears to be neuroprotective [84]; but it would be difficult, if not impossible, to control the concentration and structural form of Aβ in vivo [85]."
SET Confidence = "High"
a(MESH:"Amyloid beta-Peptides") -> bp(MESH:Neuroprotection)
UNSET Confidence

SET Evidence = "Using mutational analysis together with physiological and behavioral assays in mouse models, it was determined the hydrophilic N terminal domain of Aβ is responsible for the neuromodulatory activity of full-length Aβ [86]."
SET Confidence = "High"
SET Species = "10090"
p(MESH:"Amyloid beta-Peptides",frag("1_?")) reg act(p(MESH:"Amyloid beta-Peptides",loc(MESH:Neurons)))
UNSET Species
UNSET Confidence

SET Evidence = "Consequently, a nontoxic fragment of Aβ (1–15/16) that is derived from the N terminal domain, naturally derived from Aβ by the action of α-secretase, and does not oligomerize [87], was shown to be more effective in regard to neuromodulatory activity compared with that observed for Aβ [86]."
SET Confidence = "High"
composite(p(MESH:"Amyloid Precursor Protein Secretases"),p(MESH:"Amyloid beta-Peptides")) -> p(MESH:"Amyloid beta-Peptides",frag("1_15"))
composite(p(MESH:"Amyloid Precursor Protein Secretases"),p(MESH:"Amyloid beta-Peptides")) -> p(MESH:"Amyloid beta-Peptides",frag("1_16"))
p(MESH:"Amyloid beta-Peptides",frag("1_15")) -| a(CONSO:"amyloid-beta oligomers")
p(MESH:"Amyloid beta-Peptides",frag("1_16")) -| a(CONSO:"amyloid-beta oligomers")
p(MESH:"Amyloid beta-Peptides",frag("1_15")) reg act(p(MESH:"Amyloid beta-Peptides",loc(MESH:Neurons)))
p(MESH:"Amyloid beta-Peptides",frag("1_16")) reg act(p(MESH:"Amyloid beta-Peptides",loc(MESH:Neurons)))
UNSET Confidence

SET Evidence = "Specifically, the N terminal Aβ fragment and the N-Aβ core rescued full length Aβ-induced mitochondrial dysfunction, oxidative stress, and apoptotic neuronal death in vitro in rodent neuron models, whether added as cotreatment or, notably, post-Aβ treatment [88]."
SET Confidence = "High"
SET Species = "10116"
p(MESH:"Amyloid beta-Peptides",frag("1_?")) -| path(CONSO:"mitochondrial dysfunction")
p(MESH:"Amyloid beta-Peptides",frag("1_?")) -| path(MESH:"Oxidative Stress")
p(MESH:"Amyloid beta-Peptides") -> path(CONSO:"mitochondrial dysfunction")
p(MESH:"Amyloid beta-Peptides") -> path(MESH:"Oxidative Stress")
p(MESH:"Amyloid beta-Peptides",frag("1_?")) -| bp(GO:"neuron apoptotic process")
p(MESH:"Amyloid beta-Peptides") -| bp(GO:"neuron apoptotic process")
UNSET Species
UNSET Confidence

SET Evidence = "Collectively, the findings indicate the potential of the N terminal Aβ peptides as effective neuroprotective agents against Aβ-linked neuronal toxicity, synaptic inhibition, and behavioral deficits (Box 1)."
SET Confidence = "High"
p(MESH:"Amyloid beta-Peptides",frag("1_?")) -> bp(MESH:Neuroprotection)
p(MESH:"Amyloid beta-Peptides",frag("1_?")) pos bp(GO:behavior)
SET Cell = "neuron"
p(MESH:"Amyloid beta-Peptides",frag("1_?")) -| path(MESH:"Cell Death")
UNSET Cell
p(MESH:"Amyloid beta-Peptides",frag("1_?")) -> bp(GO:"synaptic signaling")
UNSET Confidence
