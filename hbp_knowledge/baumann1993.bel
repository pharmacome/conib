#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright Â© 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY-SA 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Abnormal Alzheimer-like phosphorylation of tau-protein by cyclin-dependent kinases cdk2 and cdk5"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

###############
# Definitions #
###############

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY AS PATTERN "^\d+$"

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE MESH     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/1b20f0637c395f8aa89c2e2e342d7b704062c242/external/mesh-names.belns"
DEFINE NAMESPACE CHEBI    AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE   AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
DEFINE NAMESPACE NCBIGENE AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE GFAM     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE HGNC     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE MIRBASE  AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE HBP      AS URL "https://raw.githubusercontent.com/pharmacome/terminology/785431732a26d6809b4162d98b95687d16404e63/export/hbp.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel
# --------------------------------------------------------------------
DEFINE NAMESPACE CTO      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE MGI      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170725.belns"
DEFINE NAMESPACE SCOMP    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM     AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-20170731.belns"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "8282104"}

SET Evidence = "The Mr shifts up with time in several stages, very similar to our earlier
observations with the brain kinase activity, MAP kinase and GSK3 (Fig. 2,)"
#phosphorylation of tau by cdk2
SET Confidence = "High"
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph))
p(SFAM:"MAPK Erk1/2 Family") => p(HGNC:MAPT,pmod(Ph))
p(SFAM:"GSK3 Family") => p(HGNC:MAPT,pmod(Ph))
UNSET Confidence

SET Evidence = "The diagnostic antibodies AT8, TAU-1, SM131, SM134, and SM133 react to phosphorylation
similarly as with MAPK and GSK-3, indicating that SP motifs before the repeat region (S199
and/or S202, S235) and after the repeats (S396, S404) become phosphorylated (Fig. 2,-2,); note that AT8,
SMUl, and SM134 react with PHFs where the epitopes containing SP motifs are phosphorylated, while TAU-1
and SM133 react with normal tau where the epitopes are not phosphorylated"
SET Confidence = "High"
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,199))
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,202))
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,235))
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,396))
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,404))
UNSET Confidence

SET Evidence = "These fractions (7-9) contained several protein bands, including two with Mr = 25 and
31 kDa (Fig. 3D, lanes 3-5) and were capable of phosphorylating htau40, incorporating 3.2, 4.2 and 2.9 mol
Pi per mol htau40, respectively, and inducing an A4, shift of htau40 in SDS-PAGE"
# 31 kDa refers to CDK5
# 25 kDa refers to CDK5R1 p25
SET Confidence = "High"
p(HGNC:CDK5) => p(HBP:HBP00053,pmod(Ph))
a(HBP:HBP00172) => p(HBP:HBP00053,pmod(Ph))
UNSET Confidence

SET Evidence = "In conclusion, this cdc2-like kinase activity towards tau occurs in brain tissue and has an Mr of 31 kDa.
These features are suggestive of other cdc2-like kinases cloned recently from brain, termed nclk [ 141, PSSARLE
[28], or cdk5 [42,49]. Indeed, the 31 kDa protein reacted with an antibody specific for cdk5. Thus, the kinase
isolated here is very similar if not identical to cdk5."
SET Confidence = "High"
SET MeSHAnatomy = "Brain"
p(HGNC:CDK5) => p(HGNC:MAPT,pmod(Ph))
UNSET Confidence
UNSET MeSHAnatomy

SET Evidence = "We find that this kinase shows good activity towards tau only when complexed
with another protein of Mr 25 kDa, presumably a regulatory subunit analogous to one of the cyclins"
SET Confidence = "High"
complex(p(HGNC:CDK5),a(HBP:HBP00172)) -> p(HGNC:MAPT,pmod(Ph))
UNSET Confidence

SET Evidence = "The reaction with the diagnostic antibodies (Fig. 4) is similar
to the examples shown previously for cdk2 (Fig. 2), MAP kinase [8], or GSK-3 [26], indicating the phosphorylation
of the SP motifs for which these antibodies are sensitive (serines 199, 202, 235, 396, 404; see Fig. 1)"
# reaction with the diagnostic antibodies refers to cdk5
SET Confidence = "High"
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,199))
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,202))
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,235))
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,396))
p(HGNC:CDK2) => p(HGNC:MAPT,pmod(Ph,Ser,404))
p(HGNC:CDK5) => p(HGNC:MAPT,pmod(Ph,Ser,199))
p(HGNC:CDK5) => p(HGNC:MAPT,pmod(Ph,Ser,202))
p(HGNC:CDK5) => p(HGNC:MAPT,pmod(Ph,Ser,235))
p(HGNC:CDK5) => p(HGNC:MAPT,pmod(Ph,Ser,396))
p(HGNC:CDK5) => p(HGNC:MAPT,pmod(Ph,Ser,404))
p(SFAM:"MAPK Erk1/2 Family") => p(HGNC:MAPT,pmod(Ph,Ser,199))
p(SFAM:"MAPK Erk1/2 Family") => p(HGNC:MAPT,pmod(Ph,Ser,202))
p(SFAM:"MAPK Erk1/2 Family") => p(HGNC:MAPT,pmod(Ph,Ser,235))
p(SFAM:"MAPK Erk1/2 Family") => p(HGNC:MAPT,pmod(Ph,Ser,396))
p(SFAM:"MAPK Erk1/2 Family") => p(HGNC:MAPT,pmod(Ph,Ser,404))
p(SFAM:"GSK3 Family") => p(HGNC:MAPT,pmod(Ph,Ser,199))
p(SFAM:"GSK3 Family") => p(HGNC:MAPT,pmod(Ph,Ser,202))
p(SFAM:"GSK3 Family") => p(HGNC:MAPT,pmod(Ph,Ser,235))
p(SFAM:"GSK3 Family") => p(HGNC:MAPT,pmod(Ph,Ser,396))
p(SFAM:"GSK3 Family") => p(HGNC:MAPT,pmod(Ph,Ser,404))
UNSET Confidence

SET Evidence = "Fig. 5 shows that the 31 kDa kinase is included in the MAP fraction after
three or more cycles of microtubule assembly (lanes l-6), and is also associated with PHFs (lane 7)"
# cycles of temperature-dependent microtubule assembly and disassembly used to show association with microtubules
SET Confidence = "High"
p(HGNC:CDK5) -- a(GO:microtubule)
p(HGNC:CDK5) -- a(HBP:HBP00006)
UNSET Confidence

SET Evidence = "In this regard, cdk5 is similar to PKA [30], MAP kinase, and GSK-3 [12,17,26], but distinct from
PKC or CaMK which do not copurify with microtubules [34]"
# cycles of temperature-dependent microtubule assembly and disassembly used to show association with microtubules
SET Confidence = "High"
p(HGNC:PRKACA) -- a(GO:microtubule)
p(HGNC:PRKACA) -- a(HBP:HBP00006)
p(SFAM:"MAPK Erk1/2 Family") -- a(GO:microtubule)
p(SFAM:"MAPK Erk1/2 Family") -- a(HBP:HBP00006)
p(SFAM:"GSK3 Family") -- a(GO:microtubule)
p(SFAM:"GSK3 Family") -- a(HBP:HBP00006)
UNSET Confidence

SET Evidence = "Thus, we now know three kinases, cdk5, MAP kinase, and GSK-3, all of which are present in
neurons, which are capable of transforming tau into the Alzheimer-like state, and therefore could account
for the pathological phosphorylation of tau in Alzheimer brains"
SET MeSHAnatomy = "Neurons"
SET Confidence = "High"
p(HGNC:CDK5) -> rxn(reactants(p(HGNC:MAPT)), products(p(HGNC:MAPT,pmod(Ph))))
p(HGNC:MAPT,pmod(Ph)) positiveCorrelation path(MESH:"Alzheimer Disease")
p(HGNC:CDK5) positiveCorrelation path(MESH:"Alzheimer Disease")
p(SFAM:"MAPK Erk1/2 Family") -> rxn(reactants(p(HGNC:MAPT)), products(p(HGNC:MAPT,pmod(Ph))))
p(SFAM:"MAPK Erk1/2 Family") positiveCorrelation path(MESH:"Alzheimer Disease")
p(SFAM:"GSK3 Family") -> rxn(reactants(p(HGNC:MAPT)), products(p(HGNC:MAPT,pmod(Ph))))
p(SFAM:"GSK3 Family") positiveCorrelation path(MESH:"Alzheimer Disease")
UNSET Confidence
