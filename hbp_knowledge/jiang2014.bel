#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "M1 muscarinic acetylcholine receptor in Alzheimer’s disease"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Rana Aldisi"
SET DOCUMENT Description = "This file encodes the article M1 muscarinic acetylcholine receptor in Alzheimer’s disease by Jiang et al, 2014"

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/7e4be528f12abd28be768b62402fba6e083eaf9e/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "24590577"}

SET Evidence="Alzheimer’s disease (AD) is a debilitating neurodegenerative
disorder afflicting millions of people. It is
diagnosed by the progressive loss of cognitive function and
behavioral defi cits and is characterized by the presence of
neurofibrillary tangles (NFTs), senile plaques, cholinergic
neuron loss, and neuronal atrophy at autopsy"

path(MESH:"Alzheimer Disease") isA path(MESH:"Neurodegenerative Diseases")
path(MESH:"Alzheimer Disease") -| bp(GO:cognition)
path(MESH:"Alzheimer Disease") -- path(MESH:"Plaque, Amyloid")
path(MESH:"Alzheimer Disease") -- path(MESH:"Neurofibrillary Tangles")
path(MESH:"Alzheimer Disease") -| a(MESH:"Cholinergic Neurons")
path(MESH:"Alzheimer Disease") -> bp(GO:"neuron death")

SET Evidence="Senile plaques consist of
deposits of small peptides called beta-amyloid (Abeta). Multiple
lines of evidence suggest that the overproduction/
aggregation of neurotoxic Abeta in vulnerable brain regions
is the primary cause of AD"

a(HBP:"amyloid-beta aggregates") -> path(MESH:"Plaque, Amyloid")
a(HBP:"amyloid-beta aggregates") -> path(MESH:"Alzheimer Disease")

SET Evidence="NFTs are formed by
accumulation of hyperphosphorylated tau protein[7, 8]. Tau is
a microtubule-binding protein whose function is to stabilize
microtubules and facilitate fast axonal transport. Once
highly phosphorylated, tau dissociates from microtubules
and is prone to aggregate, forming paired helical fi laments
that aggregate into NFTs"

a(HBP:"Tau aggregates") -> path(MESH:"Neurofibrillary Tangles")
p(HGNC:MAPT) reg bp(GO:"microtubule anchoring")
p(HGNC:MAPT) reg bp(GO:"axonal transport")

SET Evidence="The third important hallmark of AD is cholinergic
hypofunction. The neurotransmitter acetylcholine
(ACh) exerts its physiological functions by activating
either ionotropic nicotinic ACh receptors (nAChRs) or
metabotropic muscarinic ACh receptors (mAChRs). It has
been reported that in AD brains there are (1) reduced
choline acetyltransferase levels accompanied by decreased
ACh synthesis; (2) significant loss of cholinergic neurons;
(3) reduction in the numbers of postsynaptic neurons
accessible to ACh; (4) cholinergic neuronal and axonal
abnormalities; and (5) reduction in nAChR levels"

path(MESH:"Alzheimer Disease") -| bp(GO:"synaptic transmission, cholinergic")
a(CHEBI:acetylcholine) -> act(p(HGNCGENEFAMILY:"Cholinergic receptors nicotinic subunits"))
a(CHEBI:acetylcholine) -> act(p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic"))
path(MESH:"Alzheimer Disease") -| p(HGNC:CHAT)
p(HGNC:CHAT) -| a(CHEBI:acetylcholine)
path(MESH:"Alzheimer Disease") -| a(MESH:"Cholinergic Neurons")
path(MESH:"Alzheimer Disease") -| p(HGNCGENEFAMILY:"Cholinergic receptors nicotinic subunits")

SET Evidence="Recent evidence indicates that cholinergic hypofunction
is closely linked to Abeta and tau pathologies[20]. As a
major receptor group for ACh, mAChRs have also been
implicated in the pathophysiology of AD."

bp(GO:"synaptic transmission, cholinergic") -- path(MESH:"Plaque, Amyloid")
bp(GO:"synaptic transmission, cholinergic") -- path(MESH:"Neurofibrillary Tangles")
p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") -- path(MESH:"Alzheimer Disease")

SET Evidence="mAChRs are seven-transmembrane G-proteincoupled
receptors. Upon binding to the endogenous
neurotransmitter ACh, mAChRs couple to G proteins to
transduct signals"

complex(p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic"), a(CHEBI:acetylcholine), p(FPLX:"G_protein")) -> bp(GO:"synaptic transmission, cholinergic")

SET Evidence="So far, five mAChR subtypes (M1–
M5) have been identified and are divided into two categories
based on the manner of signal transduction: M1, M3, and
M5 subtypes preferentially interact with the Gq/11 family
of G proteins, activating phospholipase C and mobilizing
intracellular calcium, while M2 and M4 subtypes couple to
the Go/i family, inhibiting adenylate cyclases and reducing
intracellular cAMP levels"

p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") hasMembers list(p(HGNC:CHRM1), p(HGNC:CHRM2), p(HGNC:CHRM3), p(HGNC:CHRM4), p(HGNC:CHRM5))
complex(p(HGNC:CHRM1), p(HGNC:GNA11)) -> act(p(FPLX:PLC))
complex(p(HGNC:CHRM1), p(HGNC:GNA11)) -> bp(GO:"calcium ion transport")
complex(p(HGNC:CHRM3), p(HGNC:GNA11)) -> act(p(FPLX:PLC))
complex(p(HGNC:CHRM3), p(HGNC:GNA11)) -> bp(GO:"calcium ion transport")
complex(p(HGNC:CHRM5), p(HGNC:GNA11)) -> act(p(FPLX:PLC))
complex(p(HGNC:CHRM5), p(HGNC:GNA11)) -> bp(GO:"calcium ion transport")
complex(p(HGNC:CHRM2), p(HGNC:GNAO1)) -| p(HGNCGENEFAMILY:"Adenylate cyclases")
complex(p(HGNC:CHRM2), p(HGNC:GNAO1)) -| a(CHEBI:"3',5'-cyclic AMP")
complex(p(HGNC:CHRM4), p(HGNC:GNAO1)) -| p(HGNCGENEFAMILY:"Adenylate cyclases")
complex(p(HGNC:CHRM4), p(HGNC:GNAO1)) -| a(CHEBI:"3',5'-cyclic AMP")

SET Evidence="Members of the mAChR family are widely expressed
in various regions in the central nervous system (CNS)
and in the peripheral system. They play crucial roles
in diverse physiological processes such as memory,
attention, nociception, motor control, sleep-wake
cycles, and cardiovascular, renal, and gastrointestinal
functions"

p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") -- bp(GO:memory)
p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") -- path(MESH:Attention)
p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") -- path(MESH:Nociception)
p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") -- bp(GO:"circadian sleep/wake cycle")
p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") -- a(MESH:"Cardiovascular System")
p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") -- a(MESH:Kidney)
p(HGNCGENEFAMILY:"Cholinergic receptors muscarinic") -- a(MESH:"Gastrointestinal Tract")

SET Evidence="Among the mAChR family members, the M1 subtype
makes up 50–60% of the total and is predominantly
expressed in all major areas of the forebrain, including
the hippocampus, cerebral cortex, corpus striatum, and
thalamus[36-38]. M1 mAChR-knockout mice show a series of
cognitive defi cits and impairments in long-term potentiation,
indicating that the M1 subtype is physiologically
linked to multiple functions such as synaptic plasticity,
neuronal excitability, neuronal differentiation during early
development, and learning and memory"

a(MESH:Hippocampus) -- p(HGNC:CHRM1)
a(MESH:"Cerebral Cortex") -- p(HGNC:CHRM1)
a(MESH:"Corpus Striatum") -- p(HGNC:CHRM1)
a(MESH:Thalamus) -- p(HGNC:CHRM1)

SET Species="10090"

p(MGI:Chrm1) -> bp(GO:cognition)
p(MGI:Chrm1) -> bp(GO:"long-term synaptic potentiation")

UNSET Species

p(HGNC:CHRM1) -- bp(GO:"regulation of synaptic plasticity")
p(HGNC:CHRM1) -- bp(GO:learning)
p(HGNC:CHRM1) -- bp(GO:memory)
p(HGNC:CHRM1) -- bp(GO:"neuronal signal transduction")
p(HGNC:CHRM1) -- bp(GO:"neuron differentiation")

SET Evidence="At the cellular
level, M1 mAChR is highly expressed in striatonigral,
striatopallidal, and glutamatergic pyramidal neurons,
especially in extrasynaptic regions. This localization of M1
mAChR is consistent with the cholinergic modulation of
glutamatergic neurotransmission"

SET Cell={"glutamatergic neuron"}

p(HGNC:CHRM1) reg bp(GO:"synaptic transmission, glutamatergic")

UNSET Cell

SET Evidence="M2 mAChR is expressed throughout the brain,
including the hippocampus and neocortex, and is
abundant in non-cholinergic neurons that project to these
areas. In the caudate-putamen, M2 mAChR acts as an
inhibitory modulator on dopaminergic terminals[46-48]."

a(MESH:Hippocampus) -- p(HGNC:CHRM2)
a(MESH:Neocortex) -- p(HGNC:CHRM2)

SET MeSHAnatomy="Neostriatum"

p(HGNC:CHRM2) -| act(a(CHEBI:dopamine))

UNSET MeSHAnatomy

SET Evidence="Mice deficient in M2 mAChR also show
a striking reduction in muscarinic-dependent antinociceptive
responses[49], suggesting a general antinociceptive effect."

SET Species="10090"

p(MGI:Chrm2) -| path(MESH:"Nociceptive Pain")

UNSET Species

SET Evidence="M3 mAChR is expressed at a relatively high level in the
hypothalamus, but is also found in many other regions
including the hippocampus[47]. Mice lacking M3 mAChR
appear hypophagic and lean, suggesting a general function
of M2 mAChR in regulating food intake[50]."
# There is a typo here! this Evidence talks about M3 not M2

a(MESH:Hypothalamus) -- p(HGNC:CHRM3)
a(MESH:Hippocampus) -- p(HGNC:CHRM3)

p(MGI:Chrm3) reg bp(GO:"response to food")

SET Evidence="Consistently,
mice with conditional knockout of M3 mAChR in the brain
exhibit a dwarf phenotype. They also exhibit hypoplasia
of the anterior pituitary gland and significantly decreased
hormones including pituitary prolactin and growth
hormone[51]. These fi ndings indicate that M3 mAChR plays
a critical role in promoting body growth."

SET Species="10090"

p(MGI:Chrm3) -> bp(GO:"pituitary gland development")
p(MGI:Chrm3) -> p(MGI:Prl)
p(MGI:Chrm3) -> p(MGI:Gh)
p(MGI:Chrm3) -> bp(GO:"multicellular organism growth")

UNSET Species

SET Evidence="M4 mAChR is mainly expressed in the corpus striatum
in the CNS and on various prejunctional nerve terminals
in the periphery. M4 mAChR has been suggested to play
a role in psychosis and to be a promising target for the
treatment of schizophrenia[52]. Indeed, the mixed M1/M4
mAChR agonist xanomeline has antipsychotic effects, and
M4 mAChR-knockout mice display increased sensitivity to
the disruptive effects of phencyclidine, a drug of abuse"

a(MESH:"Corpus Striatum") -- p(HGNC:CHRM4)
p(HGNC:CHRM4) -- path(MESH:"Psychotic Disorders")
a(CHEBI:xanomeline) -| path(MESH:"Psychotic Disorders")

SET Species="10090"

p(MGI:Chrm4) -| act(a(CHEBI:phencyclidine))

UNSET Species

SET Evidence="M4 mAChR is also involved in the pathology of Parkinson’s
disease, which is associated with the loss of dopaminergic
neurons projecting to the striatum and an imbalance
between cholinergic and dopaminergic systems. In the
corpus striatum, M4 mAChR is closely co-localized with
dopamine receptors on striatal-projecting neurons and the
striatal M4 mAChR inhibits dopamine D1 receptor function.
Mice lacking M4 mAChR show increased locomotor activity
and enhanced dopamine D1 receptor-mediated effects[55].
Consequently, selective M4 mAChR antagonists, such
as benzoxazines, have been developed for the treatment of
Parkinson’s disease"

p(HGNC:CHRM4) -- path(MESH:"Parkinson Disease")
path(MESH:"Parkinson Disease") -| act(a(MESH:"Dopaminergic Neurons"))

SET MeSHAnatomy="Corpus Striatum"

p(HGNC:CHRM4) -| act(p(HGNC:DRD1))

UNSET MeSHAnatomy

SET Species="10090"

p(MGI:Chrm4) -| bp(GO:"locomotor rhythm")
p(MGI:Chrm4) -| act(p(MGI:Drd1))

UNSET Species

a(CHEBI:benzoxazine) -| path(MESH:"Parkinson Disease")

SET Evidence="M5 mAChR is predominantly distributed in the pars
compacta of the substantia nigra, a structure that provides
dopaminergic innervation to the striatum, and in the
ventral tegmental area, a structure providing dopaminergic
innervation to the nucleus accumbens and other limbic
areas[26, 57]. These areas are well known to play a critical
role in the rewarding effects of several drugs of abuse. M5
mAChR-knockout mice are less sensitive to addictive drugs
such as morphine and cocaine[58]."

a(MESH:"Pars Compacta") -- p(HGNC:CHRM5)
p(HGNC:CHRM5) -> bp(GO:"response to drug")
p(HGNC:CHRM5) -> act(a(CHEBI:morphine))
p(HGNC:CHRM5) -> act(a(CHEBI:cocaine))

SET Evidence="Abeta, an important player in AD, is derived from beta-amyloid
precursor protein (APP) through sequential cleavages by beta-
and gamma-secretases: APP is cleaved by beta-secretase (BACE1)
to generate the large secreted derivative sAPPbeta and the
membrane-bound APP C-terminal fragment-beta; the latter
can be further cleaved by gamma-secretase to generate Abeta and
APP intracellular domain. Alternatively, APP can be cleaved
by alpha-secretase within the Abeta domain, which precludes Abeta
production and instead generates secreted sAPPalpha that
has been shown to be neuroprotective"

p(HGNC:BACE1) -> deg(p(HGNC:APP))
p(HGNC:BACE1) -> a(HBP:"sAPP-beta")
#APP C-terminal fragment-beta = C99
p(HGNC:BACE1) -> a(HBP:C99)
p(FPLX:"Gamma_secretase") -> deg(a(HBP:C99))
p(FPLX:"Gamma_secretase") -> a(CHEBI:"amyloid-beta")
p(FPLX:"Gamma_secretase") -> a(HBP:AICD)
p(HGNC:ADAM10) -> deg(p(HGNC:APP))
p(HGNC:ADAM10) -> a(HBP:"sAPP-alpha")
a(HBP:"sAPP-alpha") -> bp(MESH:Neuroprotection)

SET Evidence="Interestingly, stimulation of M1 mAChR by agonists has been found to
enhance sAPPalpha generation and reduce Abeta production[61-70].
Protein kinase C (PKC) is well-known to be activated upon
stimulation of M1 mAChR. PKC may promote the activity
of alpha-secretase[71] and the traffi cking of APP from the Golgi/
trans-Golgi network to the cell surface"

act(p(HGNC:CHRM1)) -> a(HBP:"sAPP-alpha")
act(p(HGNC:CHRM1)) -| a(CHEBI:"amyloid-beta")
act(p(HGNC:CHRM1)) -> act(p(FPLX:PKC))
act(p(FPLX:PKC)) -> act(p(HGNC:ADAM10))
act(p(FPLX:PKC)) -> tloc(p(HGNC:APP), fromLoc(GO:"Golgi apparatus"), toLoc(GO:"cell surface"))

SET Evidence="Some studies
suggest that M1 mAChR stimulation also leads to activation
of ERK1/2, which can modulate alpha-secretase activity and
APP processing[67, 73],"
# contradictory findings

act(p(HGNC:CHRM1)) -> bp(GO:"ERK1 and ERK2 cascade")
bp(GO:"ERK1 and ERK2 cascade") reg act(p(HGNC:ADAM10))
bp(GO:"ERK1 and ERK2 cascade") reg bp(HBP:"APP processing")

SET Evidence="though there are contradictory
findings showing that the alpha-secretase-mediated APP
processing via M1 mAChR stimulation is not modulated
by the ERK1/MEK cascade[71]. On the other hand, loss
of M1 mAChR increases amyloidogenic APP processing
in neurons and promotes brain Abeta plaque pathology in a
mouse model of AD"

act(p(HGNC:CHRM1)) cnc bp(GO:"ERK1 and ERK2 cascade")

SET MeSHDisease="Alzheimer Disease"
SET Species="10090"

act(p(MGI:Chrm1)) -| bp(HBP:"APP processing")
act(p(MGI:Chrm1)) -| path(MESH:"Plaque, Amyloid")

UNSET Species
UNSET MeSHDisease

SET Evidence="When APP/PS1/tau
triple transgenic (3×Tg) AD mice are treated with the
selective M1 mAChR agonist AF267B, the endogenous
level of BACE1 decreases via an unclear mechanism,
accompanied by a decreased Abeta level[77]. However, another
study found that stimulation of M1 mAChR upregulates
BACE1 levels in SK-SH-SY5Y cells via the PKC and MAPK
signaling cascades[78]. We recently found that M1 mAChR
directly interacts with BACE1 and mediates its proteasomal
degradation[79]. These results suggest that M1 mAChR
modulates BACE1 in a mixed manner."

SET Species="10090"

a(MESH:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| p(MGI:Bace1)
a(MESH:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| a(CHEBI:"amyloid-beta")

UNSET Species

act(p(HGNC:CHRM1)) -> p(HGNC:BACE1)
act(p(HGNC:CHRM1)) -> act(p(FPLX:PKC))
act(p(FPLX:PKC)) -> p(HGNC:BACE1)
act(p(HGNC:CHRM1)) -> bp(GO:"MAPK cascade")
bp(GO:"MAPK cascade") -> p(HGNC:BACE1)
complex(p(HGNC:CHRM1), p(HGNC:BACE1)) -> deg(p(HGNC:BACE1))
p(HGNC:CHRM1) reg p(HGNC:BACE1)

SET Evidence="In addition to inhibiting Abeta generation, activation of
M1 mAChR counteracts Abeta-induced neurotoxicity through
the Wnt signaling pathway, as Abeta impairs the Wnt pathway
and M1 mAChR stimulation inactivates GSK-3beta via PKC
activation, stabilizes beta-catenin, and induces the expression
of Wnt-targeting genes engrailed and cyclin-D1 for neuron
survival"

act(p(HGNC:CHRM1)) -| act(p(HGNC:GSK3B))
act(p(HGNC:CHRM1)) -> act(p(FPLX:PKC))
act(p(FPLX:PKC)) -| act(p(HGNC:GSK3B))
act(p(HGNC:CHRM1)) -> bp(HBP:neurotoxicity)
act(p(HGNC:CHRM1)) -> bp(GO:"Wnt signaling pathway")
bp(GO:"Wnt signaling pathway") -| bp(HBP:neurotoxicity)

SET Evidence="Stimulation
of M1 mAChR by two agonists, carbachol and AF102B,
time- and dose-dependently decreases tau phosphorylation
in PC12 cells[81]. Chronic treatment with AF267B also
alleviates tau pathology in 3×Tg AD mice, possibly by
activating PKC and inhibiting GSK-3beta"

SET CellLine="PC12"

a(CHEBI:carbachol) -> act(p(HGNC:CHRM1))
#cevimeline = AF102B
a(MESH:cevimeline) -> act(p(HGNC:CHRM1))
act(p(HGNC:CHRM1)) -| p(HGNC:MAPT, pmod(Ph))

UNSET CellLine

SET Species="10090"

a(MESH:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| p(HGNC:MAPT, pmod(Ph))

UNSET Species

SET Evidence="Activation of M1 mAChR also protects against
apoptotic factors in human neuroblastoma SH-SY5Y cells,
such as DNA damage, oxidative stress, caspase activation,
and mitochondrial impairment[83]. In addition, apoptosis
induced by serum deprivation is blocked by M1 mAChR
activation in a phosphoinositide 3-kinase- and MAPK/ERKindependent
manner"

SET CellLine="SH-SY5Y"

act(p(HGNC:CHRM1)) -| path(MESH:"DNA Damage")
act(p(HGNC:CHRM1)) -| path(MESH:"Oxidative Stress")
act(p(HGNC:CHRM1)) -| act(p(HGNCGENEFAMILY:"Caspases"))

UNSET CellLine

act(p(HGNC:CHRM1)) -| bp(GO:"cell death")

SET Evidence="In mice with
scopolamine-induced deficits, PQCA, a selective M1
mAChR positive allosteric modulator[87], improves not only
recognition memory, spatial working memory, and executive
function, but also blood-flow in the frontal cortex, though
the mechanism is not yet clear."

SET Species="10090"
#1-((4-cyano-4-(pyridine-2-yl)piperidin-1-yl)methyl)-4-oxo-4H-quinolizine-3-carboxylic acid = PQCA
a(MESH:"1-((4-cyano-4-(pyridine-2-yl)piperidin-1-yl)methyl)-4-oxo-4H-quinolizine-3-carboxylic acid") -> act(p(MGI:Chrm1))
a(MESH:"1-((4-cyano-4-(pyridine-2-yl)piperidin-1-yl)methyl)-4-oxo-4H-quinolizine-3-carboxylic acid") -> bp(GO:memory)
a(MESH:"1-((4-cyano-4-(pyridine-2-yl)piperidin-1-yl)methyl)-4-oxo-4H-quinolizine-3-carboxylic acid") -> path(MESH:"Executive Function")

UNSET Species

SET Evidence="In fact, Abeta has been shown to induce the uncoupling of M1
mAChR from G-protein, antagonizing the function of M1
mAChR under the pathological conditions of AD[96, 97]. Such
an uncoupling may result in decreased signal transduction,
reduced levels of sAPPalpha, and increased production of
Abeta, triggering a vicious cycle."

SET MeSHDisease="Alzheimer Disease"

a(CHEBI:"amyloid-beta") -| complex(p(HGNC:CHRM1), p(FPLX:"G_protein"))
a(CHEBI:"amyloid-beta") -| act(p(HGNC:CHRM1))
complex(p(HGNC:CHRM1), p(FPLX:"G_protein")) -> a(HBP:"sAPP-alpha")
complex(p(HGNC:CHRM1), p(FPLX:"G_protein")) -| a(CHEBI:"amyloid-beta")
complex(p(HGNC:CHRM1), p(FPLX:"G_protein")) -> bp(GO:"signal transduction")

UNSET MeSHDisease

SET Evidence="One example is xanomeline, an mAChR agonist with
selectivity for the M1 and M4 subtypes. Xanomeline
improves working memory in rodents and improves
cognition and reduces psychotic episodes in AD patients,
but it failed during phase-II clinical trial because of serious
side-effects, probably due to simultaneous activation of
M1 and M4 mAChRs (M4 > M1)"

a(CHEBI:xanomeline) -> bp(GO:memory)

SET MeSHDisease="Alzheimer Disease"

a(CHEBI:xanomeline) -> bp(GO:cognition)
a(CHEBI:xanomeline) -> act(p(HGNC:CHRM1))
a(CHEBI:xanomeline) -> act(p(HGNC:CHRM4))

UNSET MeSHDisease

SET Evidence="Chronic treatment with
AF267B reduces Abeta plaques and tau hyperphosphorylation
and rescues learning and memory impairments in 3×Tg
AD mice"

SET MeSHDisease="Alzheimer Disease"
SET Species="10090"

a(MESH:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| path(MESH:"Plaque, Amyloid")
a(MESH:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| p(HGNC:MAPT, pmod(Ph))
a(MESH:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -> bp(GO:memory)
a(MESH:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -> bp(GO:learning)

UNSET Species
UNSET MeSHDisease

SET Evidence="Administration of AF267B
and AF102B (Cevimeline, EVOXACTM), an M1 mAChRselective
agonist once prescribed for the treatment of
Sjogren’s syndrome, decreases Abeta42 levels in the cerebral
spinal fluid (CSF) of rabbits without affecting APP"

SET MeSHAnatomy="Cerebrospinal Fluid"

a(CHEBI:cevimeline) -| a(CHEBI:"amyloid-beta polypeptide 42")
a(MESH:"(S)-2-ethyl-8-methyl-1-thia-4,8-diazaspiro(4.5)decan-3-one") -| a(CHEBI:"amyloid-beta polypeptide 42")
a(CHEBI:cevimeline) -| path(MESH:"Sjogren's Syndrome")

UNSET MeSHAnatomy

SET Evidence="Moreover, AF102B administration decreased the total
CSF Abeta levels by 22% in 14 of 19 AD patients without
affecting sAPPalpha levels. However, AF102B has serious side effects
including gastrointestinal symptoms, diaphoresis,
confusion, diarrhea, and asthenia"

SET MeSHAnatomy="Cerebrospinal Fluid"
SET MeSHDisease="Alzheimer Disease"

a(CHEBI:cevimeline) -| a(CHEBI:"amyloid-beta")
a(CHEBI:cevimeline) cnc a(HBP:"sAPP-alpha")
a(CHEBI:cevimeline) -> path(MESH:Confusion)
a(CHEBI:cevimeline) -> path(MESH:Diarrhea)
a(CHEBI:cevimeline) -> path(MESH:Asthenia)
a(CHEBI:cevimeline) -> path(MESH:"Gastrointestinal Diseases")

UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence="Another M1
mAChR-selective agonist, talsaclidine, enhances nonamyloidogenic
processing of APP, resulting in increased
sAPPalpha release from both a transfected human astrocytoma
cell line and rat brain slices in a dose-dependent manner,
as well as significantly decreasing CSF Abeta in AD
patients[111]. However, talsaclidine at high doses had several
side-effects such as sweating and salivation"

a(MESH:"talsaclidine fumarate") -> a(HBP:"sAPP-alpha")

SET MeSHAnatomy="Cerebrospinal Fluid"

a(MESH:"talsaclidine fumarate") -| a(CHEBI:"amyloid-beta")

UNSET MeSHAnatomy

a(MESH:"talsaclidine fumarate") -> path(MESH:Sweating)
a(MESH:"talsaclidine fumarate") -> path(MESH:Salivation)

SET Evidence="Similarly,
the M1 agonist WAY-132983 at a low dose improves
cognitive status in animal models but at a high dose causes
side-effects such as salivation and hypothermia[102, 112]."

a(MESH:"WAY 132983") -> bp(GO:cognition)
a(MESH:"WAY 132983") -> path(MESH:Salivation)
a(MESH:"WAY 132983") -> path(MESH:Hypothermia)

SET Evidence="After brucine, several other M1-PAMs
have been discovered, including VU0029767, VU0090157,
and benzyl quinolone carboxylic acid (BQCA)[115-117]. These
compounds do not activate M1 mAChR directly but greatly
increase the affi nity of ACh for the M1 subtype. In addition,
BQCA is effective in restoring discrimination reversal
learning in a mouse model of AD and regulating nonamyloidogenic
APP processing[117]."

a(HBP:VU0029767) -> complex(p(HGNC:CHRM1), a(CHEBI:acetylcholine))
a(HBP:VU0090157) -> complex(p(HGNC:CHRM1), a(CHEBI:acetylcholine))
a(MESH:"1-(4-methoxybenzyl)-4-oxo-1,4-dihydroquinoline-3-carboxylic acid") -> complex(p(HGNC:CHRM1), a(CHEBI:acetylcholine))

SET MeSHDisease="Alzheimer Disease"
SET Species="10090"

a(MESH:"1-(4-methoxybenzyl)-4-oxo-1,4-dihydroquinoline-3-carboxylic acid") -> bp(GO:learning)
a(MESH:"1-(4-methoxybenzyl)-4-oxo-1,4-dihydroquinoline-3-carboxylic acid") reg bp(HBP:"APP processing")

UNSET Species
UNSET MeSHDisease

SET Evidence="Nevertheless, a compound developed later, TBPB,
selectively activates M1 mAChR in cell lines and shows no
agonist activity in any other mAChR subtype. Interestingly,
TBPB also potentiates the NMDA-evoked current in
hippocampal pyramidal neurons, which is considered to
be important for the effect of M1 mAChR on improving
cognition. In addition, TBPB shifts the processing of APP in
the non-amyloidogenic direction and thereafter decreases
neurotoxic Abeta production vitro[120]."

a(MESH:"1-(1'-(2-methylbenzyl)-1,4'-bipiperidin-4-yl)-1H-benzo(d)imidazol-2-(3H)-one") -> act(p(HGNC:CHRM1))

SET MeSHAnatomy={"Hippocampus", "Pyramidal Cells"}

a(MESH:"1-(1'-(2-methylbenzyl)-1,4'-bipiperidin-4-yl)-1H-benzo(d)imidazol-2-(3H)-one") -> bp(GO:"NMDA selective glutamate receptor signaling pathway")
act(p(HGNC:CHRM1)) -> bp(GO:cognition)
a(MESH:"1-(1'-(2-methylbenzyl)-1,4'-bipiperidin-4-yl)-1H-benzo(d)imidazol-2-(3H)-one") -| a(CHEBI:"amyloid-beta")
a(CHEBI:"amyloid-beta") -> path(HBP:neurotoxicity)

UNSET MeSHAnatomy

SET Evidence="Compound 77-LH-28-1 shows relatively
higher selectivity for the M1 than for the M2, M4, and M5
subtypes, but retains weak agonist activity for M3 mAChR
at high doses. Electrophysiological studies indicate that
77-LH-28-1 increases the activity of hippocampal CA1
pyramidal cells both in vitro and in vivo. Interestingly,
unlike other normal orthosteric agonists, 77-LH-28-1
appears to selectively activate M1 mAChR in a distinct
signaling pathway"

#1-(3-(4-butyl-1-piperidinyl)propyl)-3,4-dihydro-2(1H)-quinolinone = 77-LH-28-1
a(MESH:"1-(3-(4-butyl-1-piperidinyl)propyl)-3,4-dihydro-2(1H)-quinolinone") -> act(p(HGNC:CHRM1))

SET MeSHAnatomy="CA1 Region, Hippocampal"

a(MESH:"1-(3-(4-butyl-1-piperidinyl)propyl)-3,4-dihydro-2(1H)-quinolinone") -> act(a(MESH:"Pyramidal Cells"))

UNSET MeSHAnatomy

SET Evidence="The M1 mAChR agonist AC-260584 was
recently reported to be orally bioavailable with favorable
antipsychotic and cognition-enhancing effects[118, 122]."

a(MESH:"4-(3-(4-butylpiperidin-1-yl)propyl)-7-fluoro-4H-benzo(1,4)oxazin-3-one") -> bp(GO:cognition)
a(MESH:"4-(3-(4-butylpiperidin-1-yl)propyl)-7-fluoro-4H-benzo(1,4)oxazin-3-one") -- a(MESH:"Antipsychotic Agents")

SET Evidence="During the past few years, the M1 mAChR
allosteric agonists VU0184670 and VU0357017 have
been screened out, and have more exciting properties.
Both compounds have high solubility in aqueous solutions
as well as good CNS penetration, without any agonist or
antagonist activity for the M2 and M5 subtypes"

a(HBP:VU0184670) -> act(p(HGNC:CHRM1))
a(MESH:VU0357017) -> act(p(HGNC:CHRM1))

SET Evidence="Moreover,
VU0184670 potentiates neuronal NMDAR-mediated
currents in hippocampal brain slices and VU0357017
reverses the cognitive deficits induced by an mAChR
antagonist in a contextual fear conditioning paradigm,
exhibiting improvement of hippocampus-dependent
learning[110, 123]."

SET MeSHAnatomy="Hippocampus"

a(HBP:VU0184670) -> bp(GO:"NMDA selective glutamate receptor signaling pathway")
a(MESH:VU0357017) -| path(MESH:"Cognitive Dysfunction")
a(MESH:VU0357017) -> bp(GO:learning)

UNSET MeSHAnatomy
