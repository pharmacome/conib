#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Phosphatase: PP2A structural importance, regulation and its aberrant expression in cancer"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/cfd2ee89d1a71126da8b5f452544b7fe43be67e7/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/73688d6dc24e309fca59a1340dc9ee971e9f3baa/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/a5b84013a08880975ca84f40999e4404b14a97e2/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

# Add statements below this comment

SET Citation = {"PubMed", "Cancer Lett. 2013 Jul 10;335(1):9-18. doi: 10.1016/j.canlet.2013.02.036. Epub 2013 Feb 20.","23454242"}

SET Evidence = "In a typical cell, the functions of nearly one-third of the proteins are regulated via
phosphorylation and it controls various biological functions like cell division, growth and
development, survival, proliferation, and apoptosis."
bp(GO:phosphorylation) reg bp(GO:"cell division")
bp(GO:phosphorylation) reg bp(GO:"cell growth")
bp(GO:phosphorylation) reg bp(GO:"cell development")
bp(GO:phosphorylation) -> bp(GO:"negative regulation of apoptotic process")
bp(GO:phosphorylation) reg bp(GO:"cell population proliferation")
bp(GO:phosphorylation) reg bp(GO:"apoptotic process")

SET Evidence = "PP2A is an important player in many cellular functions. It controls cell metabolism by
regulating the activity of the enzymes involved in glycolysis, lipid metabolism and
catecholamine synthesis [8]. It also regulates various biological processes such as the cell
cycle (by mediating cdc2 kinase activation), DNA replication, transcription and translation,
signal transduction, cell proliferation, cytoskeleton dynamics and cell mobility and
apoptosis. It has also been shown to play a role in cell transformation and cancer [9-12]."
p(FPLX:PPP2) reg bp(GO:"glycolytic process")
p(FPLX:PPP2) reg bp(GO:"lipid metabolic process")
p(FPLX:PPP2) reg bp(GO:"catecholamine biosynthetic process")
p(FPLX:PPP2) reg bp(GO:"cell cycle")
p(FPLX:PPP2) reg bp(GO:"DNA replication")
p(FPLX:PPP2) reg bp(GO:"transcription, DNA-templated")
p(FPLX:PPP2) reg bp(GO:translation)
p(FPLX:PPP2) reg bp(GO:"signal transduction")
p(FPLX:PPP2) reg bp(GO:"cell population proliferation")
p(FPLX:PPP2) reg bp(GO:"cytoskeleton organization")
p(FPLX:PPP2) reg bp(GO:"cell motility")
p(FPLX:PPP2) reg bp(GO:"apoptotic process")
p(FPLX:PPP2) -- path(MESH:"Cell Transformation, Neoplastic")
p(FPLX:PPP2) -- path(MESH:Neoplasms)

SET Evidence = "The dimeric
form is known as the core enzyme and is composed of the catalytic and scaffold subunit,
while the trimeric form is an active holoenzyme complex which consists of three subunits:
catalytic (PP2Ac), scaffold (PP2AA) and regulatory subunits (PP2AB) [17](Figure.1)."
p(FPLX:PPP2) hasComponents list(p(FPLX:PPP2C), p(FPLX:"PPP2R_A"))
p(FPLX:PPP2) hasComponents list(p(FPLX:PPP2C), p(FPLX:"PPP2R_A"), p(FPLX:"PPP2R_B"))

SET Evidence = "PP2Ac exists in
two isoforms C α  and C β . They are encoded by two separate genes each comprised of seven
exons and six introns."
p(FPLX:PPP2C) hasComponents list(p(HGNC:PPP2CA), p(HGNC:PPP2CB))

SET Evidence = "The isoform C α  is predominantly expressed in the plasma membrane and C β  in
the cytoplasm and nucleus."
p(HGNC:PPP2CA) -- a(MESH:"Cell Membrane")
p(HGNC:PPP2CB) -- a(MESH:Cytosol)
p(HGNC:PPP2CB) -- a(MESH:"Cell Nucleus")

SET Evidence = "Similar to PP2Ac, the PP2A scaffold subunit is encoded by two distinct genes, PPP2R1A
and PPP2R1B, resulting in two isoforms, A α  and A β ."
p(FPLX:"PPP2R_A") hasComponents list(p(HGNC:PPP2R1A), p(HGNC:PPP2R1B))

SET Evidence = "The PP2A regulatory subunit is structurally diverse and has a
minimum of 26 different transcript and splice variants encoded by 15 different genes in the
human genome."
p(FPLX:"PPP2R_B") hasComponents list(p(HGNC:PPP2R2A), p(HGNC:PPP2R2B), p(HGNC:PPP2R2C), p(HGNC:PPP2R2D), p(HGNC:PPP2R3A), p(HGNC:PPP2R3B), p(HGNC:PTPA), p(HGNC:PPP2R5A), p(HGNC:PPP2R5B), p(HGNC:PPP2R5C), p(HGNC:PPP2R5D), p(HGNC:PPP2R5E))

SET Evidence = " It is observed that the expression of B55 γ  increases
and B55 β  decreases gradually after birth and are developmentally regulated [28]."
bp(GO:aging) -> p(HGNC:PPP2R2C)
bp(GO:aging) -| p(HGNC:PPP2R2B)

SET Evidence = "In neurons,
B55 α  and B55 β  are localized in the cytoplasm, whereas B55 γ  is localized in the
cytoskeletal fraction."
p(HGNC:PPP2R2A) -- a(MESH:Cytosol)
p(HGNC:PPP2R2B) -- a(MESH:Cytosol)
p(HGNC:PPP2R2C) -- a(MESH:Cytoskeleton)

SET Evidence = " It has been shown in yeast that CDC55 (B55 in human) is essential for
cytokinesis. "
p(HGNC:PPP2R2A) -> bp(GO:cytokinesis)
p(HGNC:PPP2R2B) -> bp(GO:cytokinesis)
p(HGNC:PPP2R2C) -> bp(GO:cytokinesis)

SET Evidence = "In mammals, B55 is associated with
cytoskeletal dynamics and nuclear translocation."
p(HGNC:PPP2R2A) -- bp(GO:"cytoskeleton organization")
p(HGNC:PPP2R2B) -- bp(GO:"cytoskeleton organization")
p(HGNC:PPP2R2C) -- bp(GO:"cytoskeleton organization")

SET Evidence = " Intracellular localization of
B56 isoforms vary, as B56 γ  is expressed in the nucleus while B56 α , B56 β , and B56 ε  are
expressed in the cytoplasm, and PR61 δ  appears to be expressed in both the nucleus and
cytoplasm [20]. "
p(HGNC:PPP2R5C) -- a(MESH:"Cell Nucleus")
p(HGNC:PPP2R5A) -- a(MESH:Cytosol)
p(HGNC:PPP2R5B) -- a(MESH:Cytosol)
p(HGNC:PPP2R5E) -- a(MESH:Cytosol)
p(HGNC:PPP2R5D) -- a(MESH:Cytosol)
p(HGNC:PPP2R5D) -- a(MESH:"Cell Nucleus")

SET Evidence = " This group also requires ATP and Mg2+ to
activate its own activity."
#this group: PP2A-B”
composite(a(CHEBI:ATP), a(CHEBI:"magnesium(2+)")) -> act(p(HGNC:PPP2R3A))

SET Evidence = " During meiosis I, Shugoshins binds to PP2A and
dephosphorylates cohesion, which prevents spindle microtubules disassembly [37].
PP2APR55 also dephosphorylates vimentin (intermediate filament) and protects
cytoskeleton disassembly [38]."
complex(p(HGNC:SGO1), p(FPLX:PPP2)) -| p(FPLX:Cohesin, pmod(Ph))
p(FPLX:Cohesin, pmod(Ph)) -> bp(GO:"spindle disassembly")
p(FPLX:PPP2) -| p(HGNC:VIM, pmod(Ph))
p(FPLX:PPP2) -> bp(GO:"cytoskeleton organization")

SET Evidence = "The phosphoprotein SET is a
potent inhibitor of PP2A activity [45]."
p(HGNC:SET) -| act(p(FPLX:PPP2))

SET Evidence = "In Alzheimer's disease, inhibition of PP2A
activity by SET leads to hyper phosphorylation of the Tau protein [47]."
path(MESH:"Alzheimer Disease") -> p(HGNC:SET)
path(MESH:"Alzheimer Disease") -| act(p(FPLX:PPP2))
act(p(FPLX:PPP2)) -| p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))

SET Evidence = "Potent tumor promoter, Okadaic acid (a microbial toxin), inhibits the enzymatic activity of
PP2Ac and thereby has facilitated various studies to understand the functional aspects of
PP2A and other phosphatases [12]. Other than Okadaic acid, calyculin A, microcystin,
cantharidin, nodularm, fostriecin and tautomycin are able to inhibit PP2A activity at
different IC50 values [48]."
a(CHEBI:"okadaic acid") -> path(MESH:Neoplasms)
a(CHEBI:"okadaic acid") -| act(p(FPLX:PPP2C))
a(PUBCHEM:"5311365") -| act(p(FPLX:PPP2))
a(CHEBI:microcystin) -| act(p(FPLX:PPP2))
a(CHEBI:cantharidin) -| act(p(FPLX:PPP2))
a(PUBCHEM:"4162") -| act(p(FPLX:PPP2))
a(PUBCHEM:"6913994") -| act(p(FPLX:PPP2))
a(CHEBI:Tautomycin) -| act(p(FPLX:PPP2))

SET Evidence = "In addition to microbial toxin, viral protein SV40 (potent
oncogene) also inactivates PP2A action by binding to the AC dimer and displacing the PR56
(PP2A-B’ γ ) subunit [35]."
a(PUBCHEM:"71361092") -> path(MESH:Neoplasms)
a(PUBCHEM:"71361092") -| act(p(FPLX:PPP2))

SET Evidence = "Endogenous CIP2A (cancerous inhibitor of PP2A) inhibits PP2Ac
activity via interacting with c-Myc (Ser62) and stabilizes it from proteolytic degradation
[49]. "
p(HGNC:CIP2A) -| act(p(FPLX:PPP2C))

SET Evidence = "Recently, a major indirect activation of PP2A by inhibiting CIP2A at both the
transcriptional and translational levels through the drug bortezomib was shown in triple
negative breast cancer cells [50]."
a(CHEBI:bortezomib) -| p(HGNC:CIP2A)
a(CHEBI:bortezomib) -> act(p(FPLX:PPP2C))

SET Evidence = "Methylation [52] and phosphorylation [53] are two major
modifications that have been shown to modulate PP2A catalytic efficiency."
p(FPLX:PPP2, pmod(M)) -- act(p(FPLX:PPP2))
p(FPLX:PPP2, pmod(Ph)) -- act(p(FPLX:PPP2))

SET Evidence = "Phosphorylation of Y307 by
receptor associated tyrosine kinases effectively decreases the PP2A activity by inhibiting the
interaction of PP2Ac with the PP2A-PR55/PR61 subunit [54]."
p(FPLX:PPP2, pmod(Ph,Tyr,307)) -| act(p(FPLX:PPP2))

SET Evidence = " The addition of a methyl group by LCMT1 at L309 enhances the binding affinity
of the core dimer (A&C subunit) toward distinct regulatory subunits and provides specific
activity to the holoenzyme [56]."
p(HGNC:LCMT1) -> p(FPLX:PPP2, pmod(M,Leu,309))
p(FPLX:PPP2, pmod(M,Leu,309)) -> act(p(FPLX:PPP2))

SET Evidence = " Akt plays an
important role in cell growth, proliferation and apoptosis."
p(FPLX:AKT) reg bp(GO:"cell growth")
p(FPLX:AKT) reg bp(GO:"cell population proliferation")
p(FPLX:AKT) reg bp(GO:"apoptotic process")

SET Evidence = "Phosphorylation at Thr-308 and
Ser-473 leads to activation of Akt and it was found that deregulation of Akt is associated
with various human malignancies. "
p(FPLX:AKT, pmod(Ph,Thr,308)) -> act(p(FPLX:AKT))
p(FPLX:AKT, pmod(Ph,Ser,473)) -> act(p(FPLX:AKT))

SET Evidence = "Co-immunoprecipitation and in-vitro pull down assay
using pro-lymphoid FL5.12 cells showed a direct association of the PP2A-B55 holoenzyme
with Akt, which selectively regulates phosphorylation of Akt at Thr-308 and regulates cell
proliferation and survival [58]."
p(FPLX:PPP2) -| p(FPLX:AKT, pmod(Ph,Thr,308))
p(FPLX:PPP2) reg bp(GO:"cell population proliferation")
p(FPLX:PPP2) reg bp(GO:"apoptotic process")

SET Evidence = "PP2A also plays a major role in the Wnt signaling pathway. "
p(FPLX:PPP2) -- bp(GO:"Wnt signaling pathway")

SET Evidence = "In Xenopus, PP2A-
B56 is involved in  β -catenin dephosphorylation and degradation and its phosphorylation
directs activation of the Wnt pathway [43]."
p(FPLX:PPP2) -| p(HGNC:CTNNB1, pmod(Ph))
p(FPLX:PPP2) -> deg(p(HGNC:CTNNB1))
p(HGNC:CTNNB1, pmod(Ph)) -> bp(GO:"Wnt signaling pathway")

SET Evidence = "Phosphorylation of BAD suppresses,
and its dephosphorylation by PP2A promotes pro-apoptotic activity [59]. Additionally,
phosphorylation of Bcl-2 activates, and its dephosphorylation by PP2A suppresses anti-
apoptotic activity."
p(HGNC:BAD, pmod(Ph)) -| bp(GO:"apoptotic process")
p(FPLX:PPP2) -| p(HGNC:BAD, pmod(Ph))
p(HGNC:BCL2, pmod(Ph)) -| bp(GO:"apoptotic process")
p(FPLX:PPP2) -| p(HGNC:BCL2, pmod(Ph))

SET Evidence = "PP2A is considered as a tumor suppressor and is thought to be functionally inactivated in
cancer."
p(FPLX:PPP2) -| path(MESH:Neoplasms)
path(MESH:Neoplasms) neg act(p(FPLX:PPP2))
