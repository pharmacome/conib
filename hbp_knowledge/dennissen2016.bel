#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Adenosine A1 receptor antagonist 64627 alleviates axonopathy caused by human Tau ΔK280"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/7e4be528f12abd28be768b62402fba6e083eaf9e/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "27671637"}

# p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) : proaggregant
# two additional prolines in the hexapeptide motifs of the repeat domain of the Tau protein, which serve as β-sheet breakers (human Tau ΔK280-PP)
# deltaK280 plus two proline mutations in the hexapeptide motifs (Tau(RD)/deltaK280/I277P/I308P)
# p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) : antiaggregant

SET Species = "10090"

SET Evidence = "In organotypic hippocampal slices, both proaggregant and antiaggregant Tau is
missorted to the somatodendritic compartment as has been shown before (Fig. 1 A and B, asterisks)"
SET MeSHAnatomy = "Hippocampus"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -- a(GO:"somatodendritic compartment")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> bp(GO:"protein localization to somatodendritic compartment")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -- a(GO:"somatodendritic compartment")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> bp(GO:"protein localization to somatodendritic compartment")

SET Evidence = "Only proaggregant Tau transgenic slices reveal Taupositive
beaded structures in the neuropil oriented mostly perpendicular
to the apical dendrites of the CA1 pyramidal cells (Fig. 1 C
and D, arrowheads) resembling grains in human AGD (16)"
SET MeSHAnatomy = "CA1 Region, Hippocampal"
SET Cell = "pyramidal neuron"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -- a(HP:"Argyrophilic inclusion bodies")
UNSET MeSHAnatomy
UNSET Cell

SET Evidence = "This suggests that proaggregant Tau accumulates in the axons as grains"
SET MeSHAnatomy = "Axons"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) pos a(HP:"Argyrophilic inclusion bodies")
UNSET MeSHAnatomy

SET Evidence = "The axons of transfected neurons (Fig. 1 G and I) clearly
reveal small inclusions of Tau (∼1 μm in size, arrowheads), although
presynaptic boutons (e.g., giant mossy fiber boutons) are
only marginally stained for Tau (Fig. 1I and Fig. S2 A and B;
arrow), indicating that Tau does not accumulate at presynaptic
boutons in these slices"
# transfected with proaggregant tau
a(MESH:Axons) pos p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))
a(MESH:"Presynaptic Terminals") cnc p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))

SET Evidence = "Furthermore, we did not see colocalization
of the grains of Tau and presynaptic marker synaptophysin (Fig. S3)"
a(HP:"Argyrophilic inclusion bodies") cnc p(MGI:Syp)

SET Evidence = "We also observed that Tau missorts into a subgroup of proximal
dendrites, which correlates with a dramatic spine loss in the affected
dendrites (Fig. S2 C and F)"
SET MeSHAnatomy = "Dendrites"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) neg a(GO:"dendritic spine")
UNSET MeSHAnatomy

SET Evidence = "By contrast, dendrites that do not
contain Tau are richly decorated with spines (>1 spines per μm),
indicating that there is only local impairment of dendritic function
in case of proaggregant Tau missorting"
SET MeSHAnatomy = "Dendrites"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| act(a(GO:dendrite))
UNSET MeSHAnatomy

SET Evidence = "Phosphorylation of these serines
is known to induce detachment of Tau from microtubules and
to promote missorting of Tau (17)."
# pSer262/pSer356
p(MGI:Mapt,pmod(Ph,Ser,262),pmod(Ph,Ser,356)) -| complex(p(MGI:Mapt),a(GO:microtubule))

SET Evidence = "When corrected for the
difference in total Tau, 12E8 phosphorylation does not differ
between proaggregant and antiaggregant Tau (Fig. 2C)"
# 12E8 tau epitope
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -- a(HBP:"Tau epitope, 12E8")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -- a(HBP:"Tau epitope, 12E8")

SET Evidence = "The PHF-1 epitope (pSer396+pSer404, Fig. 2 D–F) is seen in both types of
Tau transgenic slices where it appears in the somatodendritic
compartment (asterisks) and in the axonal grains (arrowheads)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -- a(HBP:"Tau epitope, PHF1")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -- a(HBP:"Tau epitope, PHF1")

SET Evidence = "The antibody AT180 (Tau pThr231) (18) shows (very) weak
staining in the cell soma of both types of Tau transgenic slices (Fig. 2 G and H, asterisks) contrasting the high degree of Tau phosphorylated
at Ser202/Thr205 [asterisks (somata) and long arrows (apical dendrites)] (AT8 antibody, Fig. 2 I and J)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) neg a(HBP:"Tau epitope, AT180")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) pos a(HBP:"Tau epitope, AT8")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) neg a(HBP:"Tau epitope, AT180")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) pos a(HBP:"Tau epitope, AT8")

SET Evidence = "MC-1–positive Tau accumulates in the axonal grains
of proaggregant Tau as described above (arrowheads), whereas
antiaggregant slices remain unstained"
# MC-1 antibody detects neurofibrillary tangles
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) pos a(GO:"neurofibrillary tangle")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) cnc a(GO:"neurofibrillary tangle")

SET Evidence = "Surprisingly, the axonal grains of Tau appear to
resist protein degradation because they are negative for markers of
degradation (vimentin, ubiquitin, Lamp1, Sqstm1/P62, Hsc70, and
Tia-1) (Fig. S5)"
composite(p(MGI:Mapt),a(HP:"Argyrophilic inclusion bodies")) neg bp(GO:"cellular protein catabolic process")

SET Evidence = "Proaggregant Tau transgenic slices showed a significant reduction
of spines compared with littermate control slices, whereas spine
density of antiaggregant Tau transgenic slices was similar to
controls (Fig. 3 A and B)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(GO:"dendritic spine")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) cnc a(GO:"dendritic spine")

SET Evidence = "We determined mitochondrial
movements in live organotypic slices because aggregation-prone
Tau is known to impair mitochondrial transport (Fig. 3 D and E)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| bp(GO:"mitochondrial transport")

SET Evidence = "Mitochondria transport is similar in both kinds of Tau transgenic
slices (Fig. 3E) with only a moderately lower mitochondrial density
in proaggregant Tau transgenic slices compared with antiaggregant
slices (Fig. 3F and Table S2)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(GO:mitochondrion)
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -| a(GO:mitochondrion)

SET Evidence = "ATP is reduced
in the proaggregant transgenic slices, matching the lower
mitochondrial density, compared with littermate controls or
antiaggregant Tau transgenic slices (Fig. 3H)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(CHEBI:ATP)
a(CHEBI:ATP) pos a(GO:mitochondrion)

SET Evidence = "This suggests that
the energy status of the neurons is compromised by proaggregant
but not by antiaggregant Tau"
SET MeSHAnatomy = "Neurons"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(CHEBI:ATP)
UNSET MeSHAnatomy

SET Evidence = "Surprisingly, expression of neuronal activity
marker cFos, astrocytic activity marker Gfap, and oxidative stress
marker Hmox1 were reduced in the proaggregant Tau transgenic
slices, whereas antiaggregant Tau transgenic slices were not different
from littermate controls (Fig. 4A)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) neg act(a(MESH:Neurons))
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) neg p(MGI:Fos)
p(MGI:Fos) biomarkerFor act(a(MESH:Neurons))
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) neg bp(GO:"astrocyte activation")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) neg p(MGI:Gfap)
p(MGI:Gfap) biomarkerFor bp(GO:"astrocyte activation")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) neg bp(GO:"response to oxidative stress")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) neg p(MGI:Hmox1)
p(MGI:Hmox1) biomarkerFor bp(GO:"response to oxidative stress")

SET Evidence = "We observed a typical
paired-pulse facilitation (PPF) response in littermate controls
and antiaggregant Tau transgenic slices, whereas in proaggregant
Tau transgenic slices, the same stimulus paradigm resulted in a
paired-pulse depression (Fig. 4B)"
# PPF used to study axonal (presynaptic) functioning
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| act(a(GO:presynapse))
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) cnc act(a(GO:presynapse))

SET Evidence = "Adenosine downmodulates
neuronal activity (cFos levels), impairs the presynapse,
and attenuates long-term potentiation (LTP) via the A1 receptor (21)"
a(CHEBI:adenosine) -> act(p(MGI:Adora1))
a(CHEBI:adenosine) -| act(a(MESH:Neurons))
act(p(MGI:Adora1)) -| act(a(MESH:Neurons))
a(CHEBI:adenosine) -| act(a(GO:presynapse))
act(p(MGI:Adora1)) -| act(a(GO:presynapse))
a(CHEBI:adenosine) -| bp(GO:"long-term synaptic potentiation")
act(p(MGI:Adora1)) -| bp(GO:"long-term synaptic potentiation")

SET Evidence = "64627 increases neuronal activity (Fos mRNA)
both in proaggregant Tau transgenic slices and controls, although
in case of the proaggregant slices neuronal activity is almost
doubled, yielding levels similar to those of treated littermate
control slices (Fig. 4D)"
# 64627 antagonist of Adora1
a(PUBCHEM:64627) -| act(p(MGI:Adora1))
a(PUBCHEM:64627) -> act(a(MESH:Neurons))

SET Evidence = "In line with these observations, the presynaptic
impairment in proaggregant Tau transgenic slices can be
reversed by 64627 or BSc3094 without causing adverse effects
in controls (Fig. 4F and Fig. S6)"
a(MESH:"BSc 3094") -| a(HBP:"Tau aggregates")
a(MESH:"BSc 3094") -> act(a(GO:presynapse))
a(PUBCHEM:64627) -> act(a(GO:presynapse))

SET Evidence = "Indeed, the reduced level of spines seen in
proaggregant Tau transgenic slices are normalized when treated
with 64627, whereas no significant changes are found in
antiaggregant Tau transgenic slices or littermate controls (Fig. 5 A and B)"
a(PUBCHEM:64627) -> a(GO:"dendritic spine")
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> a(GO:"dendritic spine")
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro"))) cnc a(GO:"dendritic spine")

SET Evidence = "The axonal density of mitochondria, which is slightly lower
in proaggregant compared with antiaggregant Tau transgenic slices,
is marginally decreased by 64627 treatment albeit in a
genotype-independent manner (Fig. S7)"
SET CellStructure = "Mitochondria"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(MESH:Axons)
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -| a(MESH:Axons)
a(PUBCHEM:64627) -| a(MESH:Axons)

SET Evidence = "Having observed that
64627 restores presynaptic functioning (i.e., PPF, Fig. 4F),
neuronal activity (induction of Fos, Fig. 4D), and dendritic
spine levels in proaggregant Tau transgenic organotypic slices
(Fig. 5 A and B), we tested whether we could restore long-term
spatial memory in proaggregant Tau transgenic mice as well"
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> act(a(GO:presynapse))
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> act(a(MESH:Neurons))
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> p(MGI:Fos)
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> a(GO:"dendritic spine")

SET Evidence = "64627 reestablished novel arm preference in
proaggregant mice, suggesting that 64627 restores spatial
memory in these animals"
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> path(MESH:"Spatial Memory")

SET Evidence = "64627 treatment improved
long-term object recognition memory in proaggregant
Tau transgenic mice, as shown by increased novel object preference"
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> bp(GO:"long-term memory")

SET Evidence = "This result suggests an impaired learning association
between the sound and the foot shock in proaggregant mice, which
can be rescued by 64627 treatment"
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> bp(GO:"associative learning")

SET Evidence = "Compared with untreated proaggregant Tau transgenic
mice, treated mice (proaggregant Tau transgenics and littermate
controls) have significantly larger maximal excitatory postsynaptic
potential amplitudes (Fig. 5 G–I)"
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> bp(GO:"excitatory postsynaptic potential")

SET Evidence = "The slope of the input/output
(I/O) curve is significantly reduced in proaggregant Tau transgenic
mice compared with controls, indicative of impaired basal synaptic
transmission (Fig. 5I)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| bp(GO:"spontaneous synaptic transmission")

SET Evidence = "Treatment with 64627 increases the slope
of the I/O curve in both proaggregant Tau transgenic slices and
littermate controls (Fig. 5I)."
composite(a(PUBCHEM:64627),p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))) -> bp(GO:"spontaneous synaptic transmission")

UNSET Species
