#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright Â© 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "A role for b2* nicotinic receptors in a model of local amyloid pathology induced in dentate gyrus"
SET DOCUMENT Version     = "1.0.0" 
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DNSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "27522251"} 

# hAPP-SLA: human sequence of the APP harboring 3 pathogenic mutations, the Swedish double
# mutation (KM670/671NL), the London mutation (V717I) and the Austrian mutation (T714I) 

# 1st group: injected with the hAPP-SLA lentivirus (APP-WT; APP-alpha7, and APP-beta2 groups)
# 2nd group: injected with a lentivirus expressing only GFP (GFP-WT;GFP-alpha7, and GFP-beta2 groups) [control]

SET Species = "10090"
SET MeSHAnatomy = "Dentate Gyrus"

SET Evidence = " However, the analysis of the recognition percentage 
for the same set of data showed that GFP-WT spent significantly more 
time exploring the novel object compared with APP-WT (p = 0.0032; Fig. 2E), 
suggesting the presence of a memory deficit in the APP-WT group."
# NOR task
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) -> path(MESH:"Memory Disorders")

SET Evidence = "The locomotor
behavior during the NOR habituation phase was measured by the
total distance traveled: APP-WT showed higher locomotor activity
compared with GFP-WT (p = 0.0027; Fig. 2F)"
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) -> bp(GO:locomotion)

SET Evidence = "We finally measured
anxiety levels using the LDB paradigm. No differences were
observed between both groups for the index of time spent in the lit
compartment (p = 0.7) as well as for the number of transitions (p = 0.2; Fig. 2GeH)"
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) causesNoChange path(MESH:Anxiety)

SET Evidence = "Hence, the transduction of hAPP-SLA in DG induces
recognition memory deficits as supported by NPR and NOR tasks"
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) -> path(MESH:"Memory Disorders")

SET Evidence = "We did not detect any glia or microglia activation in WT-APP (Fig. 3C and F)
compared with WT-GFP (Fig. 3B and E), meaning that the neuroinflammation
does not play a role in the memory deficit we observed"
bp(GO:"inflammatory response") causesNoChange path(MESH:"Memory Disorders")
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) causesNoChange bp(GO:"microglial cell activation")
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) causesNoChange act(a(MESH:Neuroglia))

SET Evidence = "In addition, viral transduction did not induce any neuroinflammation,
as shown in WT noninjected mice (Fig. 3A and D)"
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) causesNoChange bp(GO:"inflammatory response")

SET Evidence = "In the NPR task (7 months p.i.),
the GFP-beta2 spent significantly more time exploring the novel
compartment (p = 0.003; Fig. 4A), as well as APP-beta2 (p = 0.017; Fig. 4B)."
# beta2 subunit KO mice
composite(p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")),p(MGI:Chrnb2)) -| path(MESH:"Memory Disorders")
p(MGI:Chrnb2) -| path(MESH:"Memory Disorders")

SET Evidence = "Similarly, in the NOR task (15 months p.i.), both the GFP-beta2
and the APP-beta2 displayed higher exploration of the novel object
(p < 0.0001 and p = 0.0001, respectively; Fig. 4C and D), with no
differences between the 2 groups in the recognition index (p = 0.7;
Fig. 4E), meaning that beta2 mice injected with hAPP-SLA did not
exhibit the recognition memory deficit observed in APP-WT"
# beta2 subunit KO mice
composite(p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")),p(MGI:Chrnb2)) -| path(MESH:"Memory Disorders")
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) -> path(MESH:"Memory Disorders")

SET Evidence = "The hAPP-SLA transduction in DG did not induce a
memory deficit in beta2 KO, meaning that the Abeta/beta2-nAChR interaction
is required to drive the memory deficit in this model"
# beta2 subunit KO mice
complex(p(MGI:Chrnb2),a(CHEBI:"amyloid-beta")) -> path(MESH:"Memory Disorders")
complex(a(CHEBI:"amyloid-beta"),p(FPLX:CHRN)) -> path(MESH:"Memory Disorders")

SET Evidence = "At 7 months p.i., both GFP-alpha7 (p = 0.9; Fig. 5A) and
APP-alpha7 groups (p = 0.3; Fig. 5B) displayed a memory deficit in the NPR task."
# alpha7 subunit KO mice
composite(p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")),p(MGI:Chrna7)) -| path(MESH:"Memory Disorders")

SET Evidence = "Noninjected mice
spent the same amount of time exploring novel and familiar
compartments during the test session (p = 0.6; Fig. 5C),
confirming the presence of a constitutive recognition memory
deficit in age-matched alpha7 KO mice"
p(MGI:Chrna7) -| path(MESH:"Memory Disorders")

SET Evidence = "In the NOR task (15 months p.i.) as well, 
both GFP-alpha7 and APP-alpha7 displayed a cognitive impairment (Fig. 5DeE)"
# alpha7 subunit KO mice
composite(p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")),p(MGI:Chrna7)) -| path(MESH:"Cognitive Dysfunction")
p(MGI:Chrna7) -| path(MESH:"Cognitive Dysfunction")

SET Evidence = "The analysis of the total exploration time
during the NOR task showed that GFP-alpha7 spent similar time
exploring the familiar and the novel objects (p = 0.76; Fig. 5D),
whereas APP-alpha7 spent significantly more time exploring the
novel object compared with the familiar one (p = 0.01; Fig. 5E)"
# alpha7 subunit KO mice
composite(p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")),p(MGI:Chrna7)) -| path(MESH:"Memory Disorders")

SET Evidence = "Because the alpha7 KO had a memory deficit independent 
of hAPP-SLA expression, we could not draw any conclusion on the role of alpha7 in the
memory deficit observed"
p(MGI:Chrna7) -| path(MESH:"Memory Disorders")

SET Evidence = "However, we identify a possible role of
the alpha7 subunit in the normal aging process that should be further investigated"
p(MGI:Chrna7) -- bp(GO:aging)

UNSET MeSHAnatomy 

SET Evidence = "Several studies showed that in AD animal models, the appearance
of the cognitive deficits precedes plaque deposition (Casas
et al., 2004; Gouras et al., 2000; Kumar et al., 2013; Wirths et al., 2004)"
SET MeSHDisease = "Alzheimer Disease"
path(MESH:"Alzheimer Disease") -> path(MESH:"Cognitive Dysfunction") 
path(MESH:"Cognitive Dysfunction") -> path(MESH:"Plaque, Amyloid")
UNSET MeSHDisease

SET Evidence = "The presence of oligomeric Abeta in APP-WT was confirmed with the antibody VHH
31-1, specific for oligomeric forms of Abeta (Lafaye et al., 2009)"
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) -> a(HBP:HBP00022)

SET MeSHAnatomy = "Dentate Gyrus"

SET Evidence = "Abeta was mainly found in the polymorphic layer of the DG (Fig. 6, arrows)"
# Abeta in APP-WT
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) -> a(HBP:HBP00022)

SET Evidence = "Abeta intracellular accumulation in DG polymorphic
layer was also confirmed with the rat monoclonal 7H3D6 antibody,
also specific for oligomeric Abeta (Kumar et al., 2013) (Fig. 7, arrow)"
# Abeta in APP-WT
p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")) -> a(HBP:HBP00022)

UNSET MeSHAnatomy 

SET Evidence = "We then investigated the presence of Abeta aggregates in the
hippocampus of APP-beta2 and APP-alpha7. In APP-beta2, a positive staining
for Abeta oligomers using the VHH 31-1 antibody was found (Fig. 8, arrows)"
# beta2 subunit KO mice
SET MeSHAnatomy = "Hippocampus"
composite(p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")),p(MGI:Chrnb2)) -| a(HBP:HBP00022)

SET MeSHAnatomy = "Dentate Gyrus"

SET Evidence = "We observed intracellular Abeta staining in the polymorphic
layer of the DG that was absent in GFP-beta2 (Fig. 8)"
p(MGI:Chrnb2) -> a(CHEBI:"amyloid-beta")

SET Evidence = "Immunofluorescence performed with VHH 31-1 on brain slices of APP-alpha7 mice
also showed intracellular Abeta oligomers in the polymorphic layer
(Fig. 8, arrows), whereas GFP-alpha7 mice did not show any Abeta oligomers"
composite(p(MGI:App,var("p.Lys670Asn"),var("p.Met671Leu"),var("p.Thr714Ile"),var("p.Val717Ile")),p(MGI:Chrna7)) -| a(HBP:HBP00022)
p(MGI:Chrna7) -> a(HBP:HBP00022)

UNSET Species
UNSET MeSHAnatomy
