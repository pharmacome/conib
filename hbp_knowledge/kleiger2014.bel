#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Perilous journey: a tour of the ubiquitin–proteasome system"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/77c0796c3ca82a80cb0eaf1c8380d6ccd7f323af/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET STATEMENT_GROUP = "Sandra"

# Add statements below this comment

SET Citation = {"PubMed", "Trends Cell Biol. 2014 Jun;24(6):352-9. doi: 10.1016/j.tcb.2013.12.003", "24457024"}

SET Evidence = "An E1 enzyme must first activate ubiquitin,
a highly conserved, 76 amino acid polypeptide, in an ATP-dependent manner."
complex(p(HGNC:UBA1),a(CHEBI:ATP)) -> act(a(MESH:Ubiquitin))

SET Evidence = "The thioesterified ubiquitin passes from the
E1 active site to the next member of the cascade, the E2 or
ubiquitin-conjugating enzyme"
complex(p(HGNC:UBA1),a(MESH:Ubiquitin)) -> complex(p(HGNC:UBA2),a(MESH:Ubiquitin))

SET Evidence = "Finally, the E3 ubiquitin
ligase binds to both the E2-bound ubiquitin and the protein
substrate, promoting the transfer of ubiquitin onto the substrate"
complex(p(HGNC:UBA3),p(HGNC:UBA2),a(MESH:Ubiquitin),a(MESH:Proteins)) -> bp(GO:"protein ubiquitination")

SET Evidence = "For instance, ubiquitin can recruit other factors to mediate
various cellular responses such as signaling, gene regulation,
endocytosis, macro-autophagy, and DNA repair"
a(MESH:Ubiquitin) reg bp(GO:signaling)
a(MESH:Ubiquitin) reg bp(GO:endocytosis)
a(MESH:Ubiquitin) reg bp(GO:macroautophagy)
a(MESH:Ubiquitin) reg bp(GO:"DNA repair")

SET Evidence = "E1s are multidomain enzymes that must activate ubiquitin
and efficiently transfer it to the E2 active site"
complex(p(HGNC:UBA1),a(CHEBI:ATP)) -> act(a(MESH:Ubiquitin))
complex(p(HGNC:UBA1),a(MESH:Ubiquitin)) -> complex(p(HGNC:UBA2),a(MESH:Ubiquitin))

SET Evidence = "This function is crucial for cellular homeostasis because failure
to activate ubiquitin, as seen by the chemical inhibition of
E1 activity in the cell, results in the almost immediate
shutdown of the entire UPS"
act(a(MESH:Ubiquitin)) reg bp(GO:"cellular homeostasis")

SET Evidence = "However, earlier biochemical investigations on the SCF
(Skp, Cullin, F-box containing) ubiquitin ligase complex,
the archetypal member of the cullin–RING ligases, indicated
that E3s can also positively influence the rate of
ubiquitin transfer from the E2 to the protein substrate [17,18]."
p(HGNC:UBA3) -> bp(GO:"protein ubiquitination")

SET Evidence = "Indeed, recent studies have provided insight into
the mechanism of E3-mediated activation of E2s by trapping
and either co-crystallizing or characterizing by
nuclear magnetic resonance (NMR) the unstable and transient
complexes between E2~ubiquitin and E3."
p(HGNC:UBA3) -> act(p(HGNC:UBA2))

SET Evidence = "These studies demonstrated that both E2s and E3s can affect
the conformation of the ubiquitin on the E2 surface to
promote its transfer to the substrate"
p(HGNC:UBA3) -> bp(GO:"protein ubiquitination")
p(HGNC:UBA2) -> bp(GO:"protein ubiquitination")

SET Evidence = "It was originally shown that the E2 Ubc1 (ubiquitin-conjugating
enzyme E2 1) can form a non-covalent interface
with a ubiquitin that is thioesterified to the active site [22]"
p(HGNC:UBE2K) -> complex(p(HGNC:UBE2K),a(MESH:Ubiquitin))

SET Evidence = "Recent work has shown that the E2s Cdc34 (cell
division cycle 34) and Ube2S (ubiquitin-conjugating enzyme E2S)
also form noncovalent interfaces with ubiquitin
in addition to the covalent thioester bond [23,24],
suggesting that E2s catalyze ubiquitin transfer at least in
part by holding ubiquitin against an interface on the E2
surface that optimizes the position of the thioester bond in
the active site"
p(HGNC:CDC34) -> complex(p(HGNC:CDC34),a(MESH:Ubiquitin))
p(HGNC:UBE2S) -> complex(p(HGNC:UBE2S),a(MESH:Ubiquitin))

SET Evidence = "Some E3s can also stabilize the conformation between
ubiquitin and the E2, thereby accelerating further the rate
of conjugation of ubiquitin to proteins"
p(HGNC:UBA3) reg complex(p(HGNC:UBA2),a(MESH:Ubiquitin))
p(HGNC:UBA3) -> bp(GO:"protein ubiquitination")

SET Evidence = "Indeed, three recent structural studies
observed the E2~ubiquitin conformation in the presence
of either RING domains or the structurally related U-box
domain [19–21]"
path(MESH:"RING Finger Domains") -> complex(p(HGNC:UBA2),a(MESH:Ubiquitin))
p(INTERPRO:"U box domain") -> complex(p(HGNC:UBA2),a(MESH:Ubiquitin))

SET Evidence = "In summary, mechanisms
of E2 activation involve the binding of ubiquitin to
surfaces on the E2 and/or E3, thereby allowing an optimal
conformation of ubiquitin on the E2 surface"
complex(p(HGNC:UBA2),a(MESH:Ubiquitin)) -> act(p(HGNC:UBA2))
complex(p(HGNC:UBA3),a(MESH:Ubiquitin)) -> act(p(HGNC:UBA2))

SET Evidence = "Both HECT and RBR E3s are unique in comparison to the
RING ligases because they catalyze an additional transthioesterification
step, where ubiquitin is transferred from the E2 to the E3 before its
conjugation to the protein substrate (Figure 1A)"
p(INTERPRO:"HECT domain") -> complex(p(HGNC:UBA3),a(MESH:Ubiquitin))
p(INTERPRO:"E3 ubiquitin ligase RBR family") -> complex(p(HGNC:UBA3),a(MESH:Ubiquitin))

SET Evidence = "One key take-home message is that many E3 ligases likely evolved
to be processive, meaning that they can conjugate multiple
ubiquitins onto a substrate before it is released from enzyme"
p(HGNC:UBA3) reg bp(GO:"protein polyubiquitination")

SET Evidence = "Misfolded proteins can aggregate with time, and protein quality-control pathways
are tasked with their ubiquitination and subsequent degradation [46,47]."
p(MESH:Proteins,pmod(HBP:misfolding)) -> bp(HBP:"protein aggregation")
complex(p(MESH:Proteins,pmod(HBP:misfolding)),a(MESH:Ubiquitin)) -> deg(p(MESH:Proteins,pmod(HBP:misfolding)))

SET Evidence = "For example, yeast Cdc48 (p97 in mammalian cells) is a conserved multisubunit enzyme that
plays a major role in dissociating ubiquitinated proteins from their binding
partners to promote their degradation by the proteasome [52]"
#p97 = VCP
p(HGNC:VCP) -> bp(GO:"proteasomal protein catabolic process")

SET Evidence = "Cdc48/p97
generally acts downstream of ubiquitin ligases, although
its activity may also promote ubiquitination in some cases;
a major challenge is to delineate further the mechanism of
action of this multipurpose enzyme"
p(HGNC:VCP) -> bp(GO:"protein ubiquitination")

SET Evidence = "Specifically, Bag6 recognizes
and binds to long hydrophobic stretches of the polypeptide
chain of misfolded proteins targeted by the ERAD (endoplasmic
reticulum associated protein degradation) pathway [56–58]"
p(HGNC:BAG6) -> complex(p(HGNC:BAG6),p(MESH:Proteins,pmod(HBP:misfolding)))
bp(GO:"ERAD pathway") -- complex(p(HGNC:BAG6),p(MESH:Proteins,pmod(HBP:misfolding)))

SET Evidence = "Because Bag6 interacts with both E3 ligases as
well as the proteasome [57,58,60], it may promote both the
ubiquitination and delivery of misfolded proteins to the
proteasome while preventing their aggregation"
complex(p(HGNC:BAG6),p(HGNC:UBA3)) -> deg(p(MESH:Proteins,pmod(HBP:misfolding)))
complex(p(HGNC:BAG6),p(HGNC:UBA3)) -> bp(GO:"protein ubiquitination")
complex(p(HGNC:BAG6),p(HGNC:UBA3)) -| bp(HBP:"protein aggregation")
complex(p(HGNC:BAG6),a(GO:"proteasome complex")) -> deg(p(MESH:Proteins,pmod(HBP:misfolding)))
complex(p(HGNC:BAG6),a(GO:"proteasome complex")) -| bp(HBP:"protein aggregation")
complex(p(HGNC:BAG6),a(GO:"proteasome complex")) -> bp(GO:"protein ubiquitination")

SET Evidence = "These enzymes, referred to as deubiquitinases
(DUBs), cleave ubiquitin from conjugated proteins
or edit ubiquitin chains by trimming their lengths [62]"
a(MESH:"Deubiquitinating Enzymes") -| complex(a(MESH:Proteins),a(MESH:Ubiquitin))

SET Evidence = "It was initially believed that the presence of DUBs at
the proteasome (such as Uch37/UchL5 and Usp14) would
promote the degradation of proteins through facilitating
substrate processing (see also the next section)"
complex(a(GO:"proteasome complex"),p(HGNC:UCHL5)) -> bp(GO:"protein catabolic process")
complex(a(GO:"proteasome complex"),p(HGNC:USP14)) -> bp(GO:"protein catabolic process")

SET Evidence = "However, recent studies have shown that, by
contrast, upon chemical inhibition of the proteasome-bound
DUB Usp14, the degradation of aggregation-prone
substrates in mammalian tissue culture cells significantly
increased [65]"
complex(a(GO:"proteasome complex"),p(HGNC:USP14)) -| bp(GO:"protein catabolic process")

SET Evidence = "Similarly, the inactivation of the evolutionarily
related DUB in yeast, Ubp6, increases the cellular
fitness of aneuploid cells, which have been shown to incur
increased proteotoxic stresses due to chromosomal imbalance [66]."
# Ubp6 = USP6
act(p(HGNC:USP6)) -| path(MESH:Aneuploidy)

SET Evidence = "First, ubiquitinated
proteins are docked to the proteasome through receptors
associated to the 19S [67,68], including the proteasome
proteins Rpn10 (proteasome regulatory particle base subunit;
also known as Psmd4 – proteasome 26S subunit, non-
ATPase, 4) and Rpn13 (also known as Adrm1, adhesion
regulating molecule 1), that bind to the poly-ubiquitin
chains [69,70]."
p(HGNC:PSMD4) -> complex(a(GO:"proteasome complex"),p(MESH:Proteins,pmod(ubiquitination)))
p(HGNC:ADRM1) -> complex(a(GO:"proteasome complex"),p(MESH:Proteins,pmod(ubiquitination)))

SET Evidence = "Second, Rpn11/Poh1 (also known as
Psmd14) is a subunit in the 19S regulatory particle that
cleaves the entire, intact ubiquitin chain from the protein
substrate [71,72]."
p(HGNC:PSMD14) -| complex(a(MESH:Proteins),a(MESH:Ubiquitin))

SET Evidence = "This activity promotes both the recycling
of chains back into the free cellular pool of ubiquitin and
creates space for the protein substrate to enter the 20S core."
#this refers to activity of Psmd14
act(p(HGNC:PSMD14)) -> tloc(a(MESH:Proteins),fromLoc(GO:cytosol),toLoc(HBP:"20 S Proteasome"))

SET Evidence = "This observation suggests that chain
binding and cleavage from the protein substrate are tightly
coordinated by the proteasome, and may help to prevent
situations such as premature chain cleavage by Rpn11,
which could result in the release of the substrate before it
becomes actively engaged to the proteasome."
complex(GO:"proteasome complex") reg complex(a(MESH:Proteins),a(MESH:Ubiquitin))
p(HGNC:PSMD14) -| complex(a(MESH:Proteins),a(MESH:Ubiquitin))

SET Evidence = "Another key early finding was that the cleavage of the
ubiquitin chain from the substrate was ATP-dependent
and was coupled to the translocation of the protein substrate
into the 20S core"
a(CHEBI:ATP) -| complex(a(MESH:Proteins),a(MESH:Ubiquitin))
a(CHEBI:ATP) -> tloc(a(MESH:Proteins),fromLoc(GO:cytosol),toLoc(HBP:"20 S Proteasome"))

SET Evidence = "Thus, the binding
of substrate to the 26S and its concomitant translocation
into the 20S results in the activation of Rpn11 through
a conformational change."
complex(a(MESH:Proteins),a(GO:"proteasome complex")) -> tloc(a(MESH:Proteins),fromLoc(GO:cytosol),toLoc(HBP:"20 S Proteasome"))
tloc(a(MESH:Proteins),fromLoc(GO:cytosol),toLoc(HBP:"20 S Proteasome")) -> act(p(HGNC:PSMD14))

##########################################################


UNSET STATEMENT_GROUP
