#######################
# Document Properties #
#######################
SET DOCUMENT Copyright = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses = "CC BY-SA 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name = "Multivalent cross-linking of actin filaments and microtubules through the microtubule-associated protein Tau"
SET DOCUMENT Version = "1.0.0"
SET DOCUMENT Description = ""
SET DOCUMENT Authors = "Sandra Spalek"

###############
# Definitions #
###############

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY AS PATTERN "^\d+$"

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE MESH     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/1b20f0637c395f8aa89c2e2e342d7b704062c242/external/mesh-names.belns"
DEFINE NAMESPACE CHEBI    AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE   AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
DEFINE NAMESPACE NCBIGENE AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE GFAM     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE HGNC     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE MIRBASE  AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE HBP      AS URL "https://raw.githubusercontent.com/pharmacome/terminology/785431732a26d6809b4162d98b95687d16404e63/export/hbp.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel
# --------------------------------------------------------------------
DEFINE NAMESPACE CTO      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE MGI      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170725.belns"
DEFINE NAMESPACE SCOMP    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM     AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-20170731.belns"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############


SET STATEMENT_GROUP = "Sandra" 

# Add statements below this comment

SET Citation = {"PubMed", "Nat Commun. 2017 Dec 7;8(1):1981. doi: 10.1038/s41467-017-02230-8", "29215007"} 

SET Evidence = "Addition of an equimolar concentration of Tau caused bundling of F-actin
(Fig. 1b), although some single filaments remained"
SET Confidence = "High"
p(HGNC:MAPT) -> bp(GO:"actin filament bundle assembly")

SET Evidence = "Quantification of the intensity of different bands indicated that ~63% of Tau remained in the
supernatant. In addition, ~8% of Tau molecules were bound to single filaments, while ~29% of Tau was found together 
with actin bundles. The data demonstrate that Tau bundles actin filaments"
SET Confidence = "High"
p(HGNC:MAPT) -> bp(GO:"actin filament bundle assembly")

SET Evidence = "The analysis reveals that the proline-rich regions P1–P2 and the four
pseudo-repeats contribute to the Tau/F-actin interaction"
SET Confidence = "High"
a(HBP:HBP00166) -> complex(p(HGNC:MAPT),a(GO:"filamentous actin"))

SET Evidence = "The strongest NMR signal attenuation was observed for K254-V287,
K294-K331, and E342-D358 (Fig. 1f), suggesting that these Tau
residues have the highest affinity toward F-actin"
SET Confidence = "High"
complex(p(HGNC:MAPT,frag("254_287")),a(GO:"filamentous actin"))
complex(p(HGNC:MAPT,frag("294_331")),a(GO:"filamentous actin"))
complex(p(HGNC:MAPT,frag("342_358")),a(GO:"filamentous actin"))

SET Evidence = "One of these binding partners is cofilin, a 21 kDa eukaryotic protein, which binds 
to F-actin with a Kd < 0.05 μM33 and results in disassembly of F-actin34, 35. Cryo-electron microscopy further
showed that the binding site of cofilin on F-actin is highly similar to its binding site on G-actin (Fig. 2a)32"
SET Confidence = "Medium"
composite(p(HGNC:CFL1),complex(GO:"filamentous actin")) -| complex(GO:"filamentous actin")

SET Evidence = "The PRE profile contained seven peaks corresponding to Tau
residues L243-A246, K259-K267, V275-L284, S289-S293, S305-
V313, S320-H330, and K375-F378 (Fig. 2e, Supplementary
Fig. 4c)"
#support binding of Tau to the hydrophobic pocket between subdomains 1 and 3 of actin
#complex(p(HGNC:MAPT,frag("243_246")),a(GO:"filamentous actin"))
#complex(p(HGNC:MAPT,frag("259_267")),a(GO:"filamentous actin"))
#complex(p(HGNC:MAPT,frag("275_284")),a(GO:"filamentous actin"))
#complex(p(HGNC:MAPT,frag("289_293")),a(GO:"filamentous actin"))
#complex(p(HGNC:MAPT,frag("305_313")),a(GO:"filamentous actin"))
#complex(p(HGNC:MAPT,frag("320_330")),a(GO:"filamentous actin"))
#complex(p(HGNC:MAPT,frag("375_378")),a(GO:"filamentous actin"))

SET Evidence = "The combined data—(i) competition of binding
of Tau to F-actin by cofilin, which interacts with actin’s
hydrophobic pocket, and (ii) residue-specific PRE effects in the
repeat domain of Tau by preferential MTSSL-labeling of C374 in
proximity to the hydrophobic pocket—suggest that Tau binds to
the solvent-exposed hydrophobic pocket that is located between
subdomains 1 and 3 of actin"
SET Confidence = "Medium"
a(HBP:HBP00169) positiveCorrelation complex(p(HGNC:MAPT),a(GO:"filamentous actin"))
a(HBP:HBP00169) positiveCorrelation complex(p(HGNC:CFL1),a(GO:"filamentous actin"))
a(HBP:HBP00169) positiveCorrelation complex(p(HGNC:CFL1),a(MESH:Actins))

SET Evidence = "Truncation of Tau by caspases and endopeptidases has been suggested 
to constitute an important pathogenic step in AD"
SET Confidence = "High"
a(MESH:Caspases) -> p(HGNC:MAPT,var("?"))
a(MESH:Endopeptidases) -> p(HGNC:MAPT,var("?"))
p(HGNC:MAPT,var("?")) positiveCorrelation path(MESH:"Alzheimer Disease") 

# Tau K18 consists of four repeat regions (residues Gln244–Glu372 , plus initial Met243) 

SET Evidence = "Indeed, K18 bound with a Kd of 110 ± 1 nM to F-actin (Supplementary Fig. 1)"
SET Confidence = "High"
complex(p(HGNC:MAPT,frag("244_372")),a(GO:"filamentous actin"))

SET Evidence = "In addition, K18 was capable of bundling F-actin (Supplementary Fig. 5b)"
SET Confidence = "High"
p(HGNC:MAPT,frag("244_372")) -> bp(GO:"actin filament bundle assembly")

SET Evidence = "To gain insight into the structure,
which these segments adopt in complex with F-actin, we used the
peptide Tau(254–290), which bundles filaments (Fig. 3a)"
SET Confidence = "High"
p(HGNC:MAPT,frag("254_290")) -> bp(GO:"actin filament bundle assembly")

SET Evidence = "In contrast, medium-range NOE cross-peaks, which are specific for secondary
structure, were observed for residues ~260 to ~268 and residues ~277 to ~283"
SET Confidence = "High"
p(HGNC:MAPT,frag("260_268")) -> bp(MESH:"Protein Structure, Secondary")
p(HGNC:MAPT,frag("277_283")) -> bp(MESH:"Protein Structure, Secondary")

SET Evidence = "Analysis of secondary structure in the lowest energy conformation using
STRIDE47, identifies α-helix for residues 261–268 and 3–10 helix for residues 277–280"
SET Confidence = "High"
p(HGNC:MAPT,frag("261_268")) -> bp(MESH:"Protein Conformation, alpha-Helical")
p(HGNC:MAPT,frag("277_280")) -> a(HBP:HBP00170)

SET Evidence = "In some of the other conformers of Tau
(254–290), however, residues 261–265 were identified not as α-
helix but as 3–10 helix, while residues 277–283 were assigned to α-helix"
SET Confidence = "High"
p(HGNC:MAPT,frag("261_265")) -> a(HBP:HBP00170)
p(HGNC:MAPT,frag("277_283")) -> bp(MESH:"Protein Conformation, alpha-Helical") 

SET Evidence = "The analysis suggests that the experimental NOE data witness the formation of helical 
structure in these regions upon binding to F-actin, but the number of detected restraints is not
sufficient to define a unique conformation"
SET Confidence = "High"
complex(a(GO:"filamentous actin"),p(HGNC:MAPT,frag("261_268"))) -> bp(MESH:"Protein Conformation, alpha-Helical")
complex(a(GO:"filamentous actin"),p(HGNC:MAPT,frag("277_280"))) -> a(HBP:HBP00170)
complex(a(GO:"filamentous actin"),p(HGNC:MAPT,frag("261_265"))) -> a(HBP:HBP00170)
complex(a(GO:"filamentous actin"),p(HGNC:MAPT,frag("277_283"))) -> bp(MESH:"Protein Conformation, alpha-Helical") 

SET Evidence = "Within the highest scoring cluster (Supplementary Fig. 8), the N-terminal
helical region of Tau(254–290) was bound to the hydrophobic pocket of F-actin (Fig. 3e)"
SET Confidence = "High"
complex(p(HGNC:MAPT,frag("254_290")),a(HBP:HBP00169))

#SET Evidence = "The docking model (Fig. 3e) suggests an important role of the
#Tau residue L266 for binding to F-actin"

#coding of specific residue without any modifaction

SET Evidence = "When compared to the wild-type peptide, Tau(254–290)-L266E showed
a decreased STD signal (Supplementary Figure 10c–e), indicating that the mutation attenuated F-actin binding"
SET Confidence = "High"
p(HGNC:MAPT,var("p.Leu256Glu")) -| complex(p(HGNC:MAPT,frag("254_290")),a(GO:"filamentous actin"))
p(HGNC:MAPT,frag("254_290")) -> complex(p(HGNC:MAPT,frag("254_290")),a(GO:"filamentous actin"))

SET Evidence = "In agreement with a decrease in affinity of Tau(254–290)-L266E for binding to
F-actin (Supplementary Fig. 1), the mutant peptide was less
efficient in promoting F-actin bundling (Supplementary Fig. 10f)"
SET Confidence = "High"
p(HGNC:MAPT,var("p.Leu256Glu")) -| bp(GO:"actin filament bundle assembly")

SET Evidence = "Alternatively, individual Tau (254–290) molecules can bind to F-actin either through residues
~260 to ~268 or through residues ~277 to ~283, while the other region will not be bound"
SET Confidence = "High"
complex(p(HGNC:MAPT,frag("260_268")),a(GO:"filamentous actin")) 
complex(p(HGNC:MAPT,frag("277_283")),a(GO:"filamentous actin"))

SET Evidence = "Tau(292–319) comprises the residues of repeat R2 and R3,
which experience strong signal attenuation upon addition of F-actin
to full-length Tau (Fig. 1e, f). In agreement with the ability
of Tau(292–319) to bind to F-actin, the peptide promotes
bundling of actin filaments (Supplementary Fig. 5c)"
SET Confidence = "High"
complex(p(HGNC:MAPT,frag("292_319")),a(GO:"filamentous actin"))
p(HGNC:MAPT,frag("292_319")) -> bp(GO:"actin filament bundle assembly")

SET Evidence = "In most structures a 3–10 helix was identified for residues 315–318, although in the
lowest energy structure residues 315–318 are in α-helical conformation (Supplementary Figure 12c)"
SET Confidence = "High"
p(HGNC:MAPT,frag("315_318")) -> a(HBP:HBP00170)

# peptide Tau(254–268)

SET Evidence = "Sequential resonance assignment of the peptide
(Supplementary Table 3), followed by NOE analysis (Supplementary
Fig. 13) and structure calculations (Supplementary
Fig. 14) showed that the short peptide folds into an α-helix
(residues 259–265) in complex with F-actin that is similar to the
one found in Tau(254–290) (Supplementary Fig. 14b)"
SET Confidence = "High"
complex(p(HGNC:MAPT,frag("254_268")),a(GO:"filamentous actin")) -> bp(MESH:"Protein Conformation, alpha-Helical") 
complex(p(HGNC:MAPT,frag("254_290")),a(GO:"filamentous actin")) -> bp(MESH:"Protein Conformation, alpha-Helical") 

SET Evidence = "In contrast to Tau(254–290), however, Tau(254–268) was not able to bundle
F-actin (Supplementary Fig. 5d)"
SET Confidence = "High"
p(HGNC:MAPT,frag("254_268")) -| bp(GO:"actin filament bundle assembly")
p(HGNC:MAPT,frag("254_290")) -> bp(GO:"actin filament bundle assembly")

SET Evidence = "An important class of kinases that phosphorylate Tau at S262 and the other
KXGS motifs in the repeat domain are the microtubule-associated
protein/microtubule affinity-regulating kinases (MARKs)"
SET Confidence = "Medium"
kin(p(HGNC:MARK2)) -> p(HGNC:MAPT,pmod(Ph,Ser,262))
kin(p(HGNC:MARK2)) -> p(HBP:HBP00098,pmod(Ph))

SET Evidence = "We therefore phosphorylated full-length Tau by MARK2. The
downfield chemical shift of phosphorylated residues (Fig. 4a) is in
agreement with previous reports and confirms phosphorylation at
S262, S293, S305, S324, S356, and S416"
SET Confidence = "High"
p(HGNC:MARK2) -> p(HGNC:MAPT,pmod(Ph,Ser,262))
p(HGNC:MARK2) -> p(HGNC:MAPT,pmod(Ph,Ser,293))
p(HGNC:MARK2) -> p(HGNC:MAPT,pmod(Ph,Ser,305))
p(HGNC:MARK2) -> p(HGNC:MAPT,pmod(Ph,Ser,324))
p(HGNC:MARK2) -> p(HGNC:MAPT,pmod(Ph,Ser,356))
p(HGNC:MARK2) -> p(HGNC:MAPT,pmod(Ph,Ser,416))

SET Evidence = "The NMR experiments demonstrate that MARK2-
phosphorylation of Tau attenuates its binding to F-actin.
Consistent with a reduced affinity, MARK2-phosphorylated Tau
failed in bundling actin filaments (Fig. 4e)"
SET Confidence = "High"
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| complex(p(HGNC:MAPT),a(GO:"filamentous actin"))
p(HGNC:MAPT,pmod(Ph,Ser,293)) -| complex(p(HGNC:MAPT),a(GO:"filamentous actin"))
p(HGNC:MAPT,pmod(Ph,Ser,305)) -| complex(p(HGNC:MAPT),a(GO:"filamentous actin"))
p(HGNC:MAPT,pmod(Ph,Ser,324)) -| complex(p(HGNC:MAPT),a(GO:"filamentous actin"))
p(HGNC:MAPT,pmod(Ph,Ser,356)) -| complex(p(HGNC:MAPT),a(GO:"filamentous actin"))
p(HGNC:MAPT,pmod(Ph,Ser,416)) -| complex(p(HGNC:MAPT),a(GO:"filamentous actin"))
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| bp(GO:"actin filament bundle assembly")
p(HGNC:MAPT,pmod(Ph,Ser,293)) -| bp(GO:"actin filament bundle assembly")
p(HGNC:MAPT,pmod(Ph,Ser,305)) -| bp(GO:"actin filament bundle assembly")
p(HGNC:MAPT,pmod(Ph,Ser,324)) -| bp(GO:"actin filament bundle assembly")
p(HGNC:MAPT,pmod(Ph,Ser,356)) -| bp(GO:"actin filament bundle assembly")
p(HGNC:MAPT,pmod(Ph,Ser,416)) -| bp(GO:"actin filament bundle assembly")

SET Evidence = "In addition,
attachment of a phosphate group to S262 in the peptide Tau
(254–284) decreased the affinity of the peptide for F-actin
(Supplementary Fig. 1) and lowered the amount of Tau
(254–284)-promoted actin bundles (Fig. 4f)"
SET Confidence = "High"
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| complex(p(HGNC:MAPT,frag("254_284")),a(GO:"filamentous actin"))
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| bp(GO:"actin filament bundle assembly")



########################################################## 

UNSET STATEMENT_GROUP

