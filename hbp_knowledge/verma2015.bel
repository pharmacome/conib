#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Chronic Anatabine Treatment Reduces Alzheimer ’ s Disease (AD)-Like Pathology and Improves Socio-Behavioral Deficits in a Transgenic Mouse Model of AD"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "26010758"}

SET Evidence = " We have shown previously that
anatabine displays some anti-inflammatory properties and reduces microgliosis and tau
phosphorylation in a pure mouse model of tauopathy."
a(CHEBI:Anatabine) -| bp(GO:"inflammatory response")
a(CHEBI:Anatabine) -| path(MESH:Gliosis)
SET MeSHDisease = "Tauopathies"
SET Species = "10090"
a(CHEBI:Anatabine) -| p(HGNC:MAPT, pmod(Ph))
UNSET Species
UNSET MeSHDisease

SET Evidence = " Anatabine has a chemical structure closely related to nicotine
and is a full agonist of α7 and α4β2 nicotinic acetylcholine receptors (nAChR) but is a more
potent α7 nAChR agonist than nicotine [15,16]."
a(CHEBI:Anatabine) -> act(p(HGNC:CHRNA7))

SET Evidence =  "We have shown previously that anatabine
displays some anti-inflammatory properties by reducing the activation of NFκB and STAT3
[17,18]."
a(CHEBI:Anatabine) -| bp(GO:"inflammatory response")
a(CHEBI:Anatabine) -| act(p(FPLX:NFkappaB))
a(CHEBI:Anatabine) -| act(p(HGNC:STAT3))

SET Evidence = "Transgenic mouse models of AD overexpressing Aβ peptides generally show greater locomotor
activity and disinhibition in the elevated plus maze compared to non-transgenic mice, suggest-
ing hyperactivity and a lower level of anxiety [28–30]. "
SET Species = "10090"
SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:"amyloid-beta") -> bp(GO:locomotion)
a(CHEBI:"amyloid-beta") -| a(MESH:Anxiety)
UNSET MeSHDisease

SET Evidence = "Interestingly, the hyper-
active behavior of Tg PS1/APPswe mice was suppressed with the anatabine treatment at a dos-
age of 20 mg/Kg/Day (Fig 2B and 2C). "
SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:Anatabine) -| bp(GO:locomotion)
UNSET MeSHDisease

SET Evidence = "The disinhibition affecting Tg PS1/APPswe mice was suppressed following
treatment with anatabine at a dosage of either 10 or 20 mg/Kg/Day at both the time points
(Figs 2D and 3D)."
SET MeSHDisease = "Alzheimer Disease"
a(CHEBI:Anatabine) -> a(MESH:Anxiety)
UNSET MeSHDisease

SET Evidence = "Unlike wild-type mice, Tg PS1/APPswe mice elicited social interaction deficits and spent an
equal amount of time in the chamber containing the empty cage or the chamber containing
the unfamiliar (Stranger 1) mouse (Fig 4A). Anatabine at a dosage of 20mg/Kg/Day restored
sociability in Tg PS1/APPswe mice as Tg PS1/APPswe mice treated with anatabine spent
significantly more time in the chamber containing the unfamiliar mouse (Stranger 1) and
less time in the chamber containing the empty cage (Fig 4B)."
SET MeSHDisease= "Alzheimer Disease"
a(CHEBI:"amyloid-beta") -| a(MESH:"Interpersonal Relations")
a(CHEBI:Anatabine) -> a(MESH:"Interpersonal Relations")
UNSET MeSHDisease

SET Evidence = " A significant increase in Iba-1 bur-
den was observed in the cortex and hippocampus of Tg PS1/APPswe mice compared to their
control wild-type littermates, suggesting increased Iba-1 immunopositive microglia in the
brain of Tg PS1/APPswe mice (Fig 5A). "
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = {"Cerebral Cortex","Hippocampus"}
a(CHEBI:"amyloid-beta") -> p(HGNC:AIF1)
p(HGNC:AIF1) pos bp(GO:"microglial cell activation")
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "Interestingly, Tg PS1/APPswe mice treated with anata-
bine at a dosage of 20 mg/Kg/Day, but not 10mg/Kg/day showed a significant reduction in Iba-
1 burden in the hippocampus compared to untreated Tg PS1/APPswe mice (Fig 5A and 5B),
suggesting that anatabine suppresses microgliosis in the brain of Tg PS1/APPswe mice."
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = "Hippocampus"
a(CHEBI:Anatabine) -| p(HGNC:AIF1)
a(CHEBI:Anatabine) -| bp(GO:"microglial cell activation")
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "A significant increase in CD45 immunopositive
microglia/macrophage was observed in the cortex of Tg PS1/APPswe mice compared to wild-
type mice (CD45 immunonegative) (Fig 5C). "
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = "Cerebral Cortex"
SET Cell = {"microglial cell","macrophage"}
a(CHEBI:"amyloid-beta") -> p(HGNC:PTPRC)
UNSET Cell
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = " However, we observed a significant reduction in
CD45 immunopositive microglia/macrophage in Tg PS1/APPswe mice receiving anatabine at
a dosage of 10 and 20 mg/Kg/Day compared to Tg PS1/APPswe mice receiving regular drink-
ing water (Fig 5C and 5D)."
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = "Cerebral Cortex"
SET Cell = {"microglial cell","macrophage"}
a(CHEBI:Anatabine) -| p(HGNC:PTPRC)
UNSET Cell
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "We have previously shown that anatabine inhibits STAT3 and NFκB activation [18] resulting
in decreased neuroinflammation in a mouse model of multiple sclerosis."
SET MeSHDisease = "Multiple Sclerosis"
a(CHEBI:Anatabine) -| act(p(FPLX:NFkappaB))
a(CHEBI:Anatabine) -| act(p(HGNC:STAT3))
SET MeSHAnatomy = "Neurons"
a(CHEBI:Anatabine) -| bp(GO:"inflammatory response")
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "We ob-
served an elevation of STAT3 phosphorylation in the hippocampus and cortex of Tg PS1/
APPswe compared to their control wild-type littermates (Fig 6A). "
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = {"Cerebral Cortex","Hippocampus"}
a(CHEBI:"amyloid-beta") -> p(HGNC:STAT3, pmod(Ph))
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "A significant reduction in
STAT3 phosphorylation was observed in the hippocampus and cortex of Tg PS1/APPswe mice
treated with 10 or 20 mg/Kg/Day of anatabine compared to Tg PS1/APPswe receiving regular
drinking water (Fig 6B) showing that anatabine prevents STAT3 activation in the brain of Tg
PS1/APPswe mice. "
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = {"Cerebral Cortex","Hippocampus"}
a(CHEBI:Anatabine) -| p(HGNC:STAT3, pmod(Ph))
a(CHEBI:Anatabine) -| act(p(HGNC:STAT3))
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "We also observed elevation of NFκB activation in the vicinity of Aβ deposits
in the brain of Tg PS1/APPswe mice (Fig 7A)."
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
a(CHEBI:"amyloid-beta") -> act(p(FPLX:NFkappaB))
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = " Interestingly, we found a significant reduction in
the expression of amyloid plaque associated phospho-p65 NFκB immunopositive cells in Tg
PS1/APPswe mice treated with anatabine at either 10 or 20 mg/Kg/Day (Fig 7B) showing that
anatabine prevents NFκB activation in the brain of Tg PS1/APPswe mice."
#Phospho-NF-κB p65 (Ser536)
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
a(CHEBI:Anatabine) -| p(FPLX:NFkappaB, pmod(Ph,Ser,536))
a(CHEBI:Anatabine) -| act(p(FPLX:NFkappaB))
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "We observed that anatabine reduces brain Aβ burden both
in the cortex and the hippocampus of Tg PS1/APPswe mice using immunostaining with the
antibody 4G8 which recognizes Aβ (Fig 8A and 8B)."
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = {"Cerebral Cortex","Hippocampus"}
a(CHEBI:Anatabine) -| a(CHEBI:"amyloid-beta")
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = " Additionally, we found that Bace1 mRNA
expression is significantly increased in the brain of Tg PS1/APPswe mice compared to wild-
type littermates, whereas a significant reduction in Bace1 mRNA levels is observed in Tg PS1/
APPswe receiving 20 mg/Kg/Day of anatabine in their drinking water showing that at this
dosage anatabine can mitigate the upregulation of Bace1 expression in Tg PS1/APPswe mice
(Fig 9)."
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
a(CHEBI:"amyloid-beta") -> r(HGNC:BACE1)
a(CHEBI:Anatabine) -| r(HGNC:BACE1)
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "We found a significant reduction in Cox-2 mRNA expression in Tg PS1/
APPswe mice treated with anatabine at a dosage of 20 mg/Kg/Day (Fig 10)."
SET MeSHDisease= "Alzheimer Disease"
SET MeSHAnatomy = "Brain"
a(CHEBI:Anatabine) -| r(HGNC:PTGS2)
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "A dose dependent
suppression of iNOS transcription was observed in Tg PS1/APPswe mice treated with anata-
bine at a dosage of 10 or 20 mg/Kg/Day compared to untreated Tg PS1/APPswe mice (Fig 10)."
SET MeSHDisease= "Alzheimer Disease"
a(CHEBI:Anatabine) -| r(HGNC:NOS2)
UNSET MeSHDisease
