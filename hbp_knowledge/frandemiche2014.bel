#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "frandemiche2014"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Rana Aldisi"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/90e1cb9e5e882703380c9db8d4915ac6f3cba137/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed", "24760868"}

SET TextLocation="Results"

SET Evidence="The merged image
showed that filamentous actin and PSD-95 are both localized
in dendritic spines (Fig. 1A). We then cotransfected LifeAct-
RFP with EGFP-tau in cortical neurons and observed that
EGFP-tau fluorescence is restricted to axonal and dendritic
shafts in the resting condition (Fig. 1B)."

p(HGNC:DLG4, loc(MESH:"Dendritic Spines"))
a(MESH:Actins, loc(MESH:"Dendritic Spines"))
p(HGNC:MAPT, loc(GO:"dendritic shaft"))

SET Evidence="Therefore, during a long-lasting synaptic activation,
we observed an increase in tau, fyn, actin, GluA1, and
PSD-95 content in the PSD-positive fraction, which is consistent
with the characteristic features of synaptic plasticity (Ehlers,
2003)."

bp(GO:"long-term synaptic potentiation") pos p(HGNC:MAPT, loc(GO:synapse))
bp(GO:"long-term synaptic potentiation") pos p(MESH:Actins, loc(GO:synapse))
bp(GO:"long-term synaptic potentiation") pos p(HGNC:FYN, loc(GO:synapse))
bp(GO:"long-term synaptic potentiation") pos p(HGNC:GRIA1, loc(GO:synapse))
bp(GO:"long-term synaptic potentiation") pos p(HGNC:DLG4, loc(GO:synapse))

SET Evidence="These results suggest that tau translocates from the dendritic
shaft to the synapse during activation
and probably takes part in the
activity-driven synaptic reorganization
that underlies synaptic plasticity"

bp(GO:"long-term synaptic potentiation") pos tloc(p(HGNC:MAPT), fromLoc(GO:"dendritic shaft"), toLoc(GO:synapse))
tloc(p(HGNC:MAPT), fromLoc(GO:"dendritic shaft"), toLoc(GO:synapse)) -- bp(MESH:"Neuronal Plasticity")

SET Evidence="We observed a similar LTPinduced
increase in tau content within the
PSD-enriched fraction from CA1 synaptosomes
(29.86 +-4.86 to 70.15 +- 4.86,
**p = 0.0011; Fig. 3B). As expected, actin
and GluA1 were also increased, strengthening
the idea that tau is involved in synaptic
reorganization processes necessary
for synaptic plasticity "

SET MeSHAnatomy="Post-Synaptic Density"

bp(GO:"long-term synaptic potentiation") pos p(HGNC:MAPT)
bp(GO:"long-term synaptic potentiation") pos  p(HGNC:GRIA1)
bp(GO:"long-term synaptic potentiation") pos p(MESH:Actins)
p(HGNC:MAPT) reg bp(MESH:"Neuronal Plasticity")

UNSET MeSHAnatomy

SET Evidence="Although tau alone
were observed only in the supernatant in both experimental conditions,
ruling out a nonspecific coaggregation, we found that tau
coprecipitated with the pellet obtained from high- and low-speed
centrifugation, illustrating its direct interaction with both F-actin
(Fig. 4A) and actin bundles (Fig. 4B)."

p(HGNC:MAPT) -> complex(complex(GO:"filamentous actin"), p(HGNC:MAPT))
p(HGNC:MAPT) -> complex(complex(GO:"actin filament bundle"), p(HGNC:MAPT))

SET Evidence="These results show that the amount
of tau collected is proportional to neuronal F-actin content, suggesting
a close link between F-actin and tau."

SET MeSHAnatomy="Neurons"

p(HGNC:MAPT) -- complex(GO:"filamentous actin")

UNSET MeSHAnatomy

SET Evidence="For this, we
treated our primary cortical neurons with jasplakinolide (1 M),
a compound that promotes actin polymerization (Lazaro-
Dieguez et al., 2008), or with a latrunculin A (at 500 nM), a compound
that depolymerizes F-actin into soluble globular actin (Gactin;
Coue´ et al., 1987; Fig. 5C). After jasplakinolide application,
we observed a large increase in synaptic EGFP-tau fluorescence"

a(MESH:jasplakinolide) -> bp(GO:"actin filament polymerization")
a(CHEBI:"latrunculin A") -> bp(GO:"actin filament depolymerization")
a(MESH:jasplakinolide) -> p(HGNC:MAPT)

SET Evidence="Latrunculin treatment produced rapid actin
depolymerization and the corresponding disappearance of
LifeAct-RFP fluorescence in every spine studied; no synaptic
EGFP-tau fluorescence was observed (data not shown)."

a(CHEBI:"latrunculin A") -> bp(GO:"actin filament depolymerization")
a(CHEBI:"latrunculin A") -| p(HGNC:MAPT)

SET Evidence="We analyzed actin and tau in the PSD-enriched fraction from
primary cortical neurons treated with jasplakinolide (Fig. 5E).
We observed that increased neuronal F-actin content promotes
concurrent tau enrichment (*p0.0150, 2-tailed Student’s t test;
control 17.49  0.7755 vs jasplakinolide 27.02  2719, N  4
independent cultures; Fig. 5F). GLUA1, the membrane trafficking
of which is known to be actin dependent, was increased (*p
0.0279, 2-tailed Student’s t test; control 16.91  1015 vs jasplakinolide
31.00  4.778, N  4 independent cultures). The
amount of Fyn in the PSD was decreased (*p  0.0265, 2-tailed
Student’s t test; control 27.25 5.003 vs jasplakinolide 11.71 
1.786, N  4 independent cultures)."

SET MeSHAnatomy="Post-Synaptic Density"

a(MESH:jasplakinolide) -> bp(GO:"actin filament polymerization")
bp(GO:"actin filament polymerization") -> complex(GO:"filamentous actin")
complex(GO:"filamentous actin") -> p(HGNC:MAPT)
a(MESH:jasplakinolide) -> p(HGNC:GRIA1)
a(MESH:jasplakinolide) -| p(HGNC:FYN)

UNSET MeSHAnatomy

SET Evidence="Together, these results suggest
that tau translocation to the synapse depends on the F-actin
stabilization that promotes their interaction."

complex(GO:"filamentous actin") -> p(HGNC:MAPT, loc(GO:synapse))

SET Evidence="Abetao exposure induced
a translocation of tau into the PSD fraction (***p 
0.0002, 2-tailed Student’s t test; control 20.12  1228 vs Abetao
29.74  1.748, N  12 independent culture). There was also an
increase of PSD-95 (***p0.0006, 2-tailed Student’s t test; control
19.10  2.557 vs Abetao 33.3  2153, N  9 independent
culture), GluA1 (**p  0.0078, 2-tailed Student’s t test; control
18.841.930 vs Abetao 26.221.475,N9 independent culture)
and fyn (**p  0.0041, 2-tailed Student’s t test; control 19.42 
1.337 vs Abetao 29.67  2.181, N  6 independent cultures; Fig.
6D)."
#abetao = Amyloid-beta oligomers
a(HBP:"amyloid-beta oligomers") -> p(HGNC:MAPT, loc(MESH:"Post-Synaptic Density"))
a(HBP:"amyloid-beta oligomers") -> p(HGNC:DLG4)
a(HBP:"amyloid-beta oligomers") -> p(HGNC:GRIA1)
a(HBP:"amyloid-beta oligomers") -> p(HGNC:FYN)

SET Evidence="Preceded by 15 min Abetao treatment,
synaptic activation disrupted LifeAct-RFP fluorescence, suggesting
an alteration of F-actin organization and no additional EGFPtau
recruitment at the synapse."

a(HBP:"amyloid-beta oligomers") -| bp(GO:"actin filament polymerization")

SET Evidence="After Abetao treatment, synaptic activation did
not trigger any increase in synaptic markers and in fact decreased
synaptic actin (***p  0.0009, 2-tailed Student’s t test; Abetao
29.64  1.495, Abetao 	 Bic/4-AP 18.56 2.030, N  7 independent
cultures; Fig. 7C), PSD-95 (***p0.0007, 2-tailed Student’s
t test; Abetao 33.37  2.153, Abetao 	 Bic/4-AP 19.25 2.550, N  7
independent cultures) and tau levels (**p0.0014, 2-tailed Student’s
t test; Abetao 29.74 1.748, Abetao 	 Bic/4-AP 20.68 1.751,
N  12 independent cultures)."

a(HBP:"amyloid-beta oligomers") -| a(MESH:Actins, loc(GO:synapse))
a(HBP:"amyloid-beta oligomers") -| p(HGNC:DLG4, loc(GO:synapse))
a(HBP:"amyloid-beta oligomers") -| p(HGNC:MAPT, loc(GO:synapse))

SET Evidence="Finally, we performed a phalloidin precipitation
assay after a 15 min Abetao treatment on our neuron culture (Fig.
8A) and observed that tau/F-actin content was increased (**,*p
0.05 relative to control, #p  0.05 relative to Abetao, 1-way
ANOVA; control 15.45  1.529, Abetao 32.90  3.181, Abetao	 Bic/
4-AP 20.182671 for actin; control 16.342.618, Abetao 31.77
1.952, Abetao	Bic/4-AP 17.704.080 for tau,N5 independent
cultures Fig. 8B). A subsequent synaptic activation did alter tau
interaction with F-actin."

SET MeSHAnatomy="Neurons"

a(HBP:"amyloid-beta oligomers") -> complex(complex(GO:"filamentous actin"), p(HGNC:MAPT))
bp(GO:"long-term synaptic potentiation") -- complex(complex(GO:"filamentous actin"), p(HGNC:MAPT))

UNSET MeSHAnatomy

SET Evidence="The recovery curves indicated 3 pools of tau:
a mobile, unbleached fraction comprising 9.72  1.03%, a
dynamic fraction at 47.08  3.06%, and a stable, unrecoverable
fraction at 42.75  2.78%. This stable fraction suggests
that a large portion of tau is anchored in the spine."
#Discuss

a(MESH:"Dendritic Spines") -- tloc(p(HGNC:MAPT))
a(MESH:"Dendritic Spines") -- p(HGNC:MAPT)
a(MESH:"Dendritic Spines") -- complex(p(HGNC:MAPT), a(MESH:"Dendritic Spines"))

SET Evidence="When we
investigated Abetao-driven tau translocation to the synapse, we
did not see any change in half-life recovery (4.729 s) from
those measured with synaptic activation. However, the plateau
value was drastically modified (71.20%), illustrating that,
whereas Abetao induced tau translocation and subsequently its
interaction with actin filament, the resulting synaptic tau is less stable."

a(HBP:"amyloid-beta oligomers") -| complex(p(HGNC:MAPT), a(MESH:"Dendritic Spines"))
a(HBP:"amyloid-beta oligomers") -> tloc(p(HGNC:MAPT))
a(HBP:"amyloid-beta oligomers") -> complex(complex(GO:"filamentous actin"), p(HGNC:MAPT))

SET Evidence="Thr-205 phosphorylated tau
was only increased under synaptic activation in the PSD
fraction (control 24.57  0.9754 vs Bic/4-AP 38.90  1.936;
Fig. 9E), whereas it was decreased after Abetao treatment (control
24.57  0.9754 vs Abeta 13.64  2.416). Synaptic activation
after Abetao exposure did not produce any significant Thr-205
phosphorylation of tau (Abeta 	 Bic/4-AP 22.892.796 vs Bic/
4-AP 38.90  1.93 vs Abeta 13.642.50)."

SET MeSHAnatomy="Post-Synaptic Density"

bp(GO:"long-term synaptic potentiation") -> p(HGNC:MAPT, pmod(Ph,Thr,205))
a(HBP:"amyloid-beta oligomers") -| p(HGNC:MAPT, pmod(Ph,Thr,205))

UNSET MeSHAnatomy

SET Evidence="Conversely, only Abetao
exposure promoted significant tau phosphorylation on Ser
404 (**p0.05, 1-way ANOVA; control 15.672.418 vs Abetao
32.65  3.76 vs Bic/4-AP 26.75  1.17 vs Abetao 	 Bic/4-AP
24.97  4.48, N  4). These results revealed that, although
synaptic activation or Abetao promote tau translocation to PSD
fractions, the synaptic tau displays a different phosphorylation
profile that may be responsible for the conditional tau properties
observed."

SET MeSHAnatomy="Post-Synaptic Density"

a(HBP:"amyloid-beta oligomers") -> p(HGNC:MAPT, pmod(Ph,Ser,404))
a(HBP:"amyloid-beta oligomers") reg p(HGNC:MAPT, pmod(Ph))
p(HGNC:MAPT, pmod(Ph)) -- act(p(HGNC:MAPT))

UNSET MeSHAnatomy

SET Evidence="We observed
that synaptic activation promoted EGFP-Tau T205A
translocation to the spine but FRAP experiments revealed a
shorter tau turnover time in the spine (Fig. 9B), whereas Abetao driven
translocation to the spine was no longer observable in
EGFP-Tau S404A-transfected neurons (Fig. 9F,G). These experiments
highlight the pivotal role of these phosphorylations
in tau translocation features to the spine."

bp(GO:"long-term synaptic potentiation") -> p(HGNC:MAPT, var("p.T205A"), loc(MESH:"Dendritic Spines"))
a(HBP:"amyloid-beta oligomers") cnc p(HGNC:MAPT, var("p.S404A"), loc(MESH:"Dendritic Spines"))
p(HGNC:MAPT, pmod(Ph,Thr,205)) -> p(HGNC:MAPT, loc(MESH:"Dendritic Spines"))
p(HGNC:MAPT, pmod(Ph,Ser,404)) -> p(HGNC:MAPT, loc(MESH:"Dendritic Spines"))

UNSET TextLocation
