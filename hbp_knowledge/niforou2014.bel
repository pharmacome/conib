#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Molecular chaperones and proteostasis regulation during redox imbalance"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/0c9853baf883545b922ff1b138a9f297a70b7951/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET STATEMENT_GROUP = "Esther"

# Add statements below this comment

SET Citation = {"PubMed","Redox Biol. 2014 Jan 30;2:323-32. doi: 10.1016/j.redox.2014.01.017. eCollection 2014.","24563850"}

SET Evidence = "Excessive amounts of free radicals and
radical-derived reactive species may also arise from the activity of
NAD(P)H oxidases (NOx) and/or xanthine oxidase, as well as from
nitric oxide synthase (NOS), P450 metabolism and peroxisomes."
act(p(FPLX:"NADPH_oxidase")) -> a(CHEBI:radical)
act(p(HGNC:XDH)) -> a(CHEBI:radical)
act(p(FPLX:NOS)) -> a(CHEBI:radical)
act(p(HGNCGENEFAMILY:"Cytochrome P450 family 1")) -> a(CHEBI:radical)
act(a(MESH:Peroxisomes)) -> a(CHEBI:radical)
act(p(FPLX:"NADPH_oxidase")) -> a(CHEBI:"reactive oxygen species")
act(p(HGNC:XDH)) -> a(CHEBI:"reactive oxygen species")
act(p(FPLX:NOS)) -> a(CHEBI:"reactive oxygen species")
act(p(HGNCGENEFAMILY:"Cytochrome P450 family 1")) -> a(CHEBI:"reactive oxygen species")
act(a(MESH:Peroxisomes)) -> a(CHEBI:"reactive oxygen species")
act(p(FPLX:"NADPH_oxidase")) -> a(CHEBI:"reactive nitrogen species")
act(p(HGNC:XDH)) -> a(CHEBI:"reactive nitrogen species")
act(p(FPLX:NOS)) -> a(CHEBI:"reactive nitrogen species")
act(p(HGNCGENEFAMILY:"Cytochrome P450 family 1")) -> a(CHEBI:"reactive nitrogen species")
act(a(MESH:Peroxisomes)) -> a(CHEBI:"reactive nitrogen species")

SET Evidence = " Notably, free radicals may also arise from exogenous sources
including various types of radiations (e.g. UV light, X-rays or
gamma rays), atmospheric pollutants and metal-catalyzed reac-
tions [1–3]."
bp(GO:"response to radiation") -> a(CHEBI:radical)
a(MESH:"Air Pollution") -> a(CHEBI:radical)

SET Evidence = "Free radicals were originally believed to be harmful, but it has
been realized that in physiological concentrations they serve as
redox messengers, which are essential in the regulation of intra-
cellular signalling and significant cellular processes including meta-
bolism, antioxidant defenses and responses to pathogens [1,4]."
a(CHEBI:radical) -- bp(GO:"detection of redox state")
a(CHEBI:radical) reg bp(GO:"intracellular receptor signaling pathway")
a(CHEBI:radical) reg bp(MESH:Metabolism)
a(CHEBI:radical) -| a(MESH:Antioxidants)
a(CHEBI:radical) -- bp(MESH:"Host-Pathogen Interactions")

SET Evidence = "Proteome is modified post-translationally by either numerous
highly regulated enzymatic protein modifications (EPMs) (e.g.
phosphorylation, acetylation, ubiquitination, methylation, etc.) or
by non-enzymatic protein modifications (NEPMs), which are mostly
stochastic and increase with ageing or in age-related diseases
(Fig. 1)."
bp(HBP:"non-enzymatic protein modification") positiveCorrelation bp(GO:aging)

SET Evidence = "EPMs alter the targeted proteins, which however
remain fully functional, while NEPMs may induce protein unfolding
or misfolding resulting in increased proteome instability."
bp(HBP:"non-enzymatic protein modification") -> bp(HBP:misfolding)

SET Evidence = " Free
radicals-derived protein modification can result in either gain- or
loss-of-function due to the protein misfolding or unfolding. "
a(CHEBI:radical) -> bp(HBP:"non-enzymatic protein modification")
a(CHEBI:radical) -> bp(HBP:misfolding)

SET Evidence = "In those organismal states (e.g. ageing or diseases) where
the chaperone network becomes deregulated, the accumulating
non-native, misfolded or unfolded proteins can form (among
others) fibrils, amyloids or large amorphous aggregates [15]. "
bp(HBP:misfolding) -> path(MESH:"Protein Aggregation, Pathological")

SET Evidence = "These
chaperones provide high-affinity binding platforms for unfolded
proteins and prevent protein aggregation specifically during stress
conditions."
p(HGNCGENEFAMILY:"Small heat shock proteins") -| path(MESH:"Protein Aggregation, Pathological")

SET Evidence = "sHSPs are tightly
regulated by EPMs (e.g. phosphorylation) that enable them to
respond upon stress and to perform their chaperone activities
[25,26]."
bp(GO:"post-translational protein modification") reg p(HGNCGENEFAMILY:"Small heat shock proteins")

SET Evidence = "Moreover, HSP22 stimu-
lates autophagy-mediated degradation of protein aggregates in an
eIF2α-dependent manner [30]."
p(HGNC:HSPB8) -> bp(GO:"chaperone-mediated autophagy")
bp(GO:"chaperone-mediated autophagy") -| path(MESH:"Protein Aggregation, Pathological")
p(HGNC:HSPB8) -- p(HGNC:EIF2A)

SET Evidence = "sHSPs are overexpressed upon
many different types of stress as they are key components of the
PN and PDR."
bp(GO:"response to stress") -> p(HGNCGENEFAMILY:"Small heat shock proteins")

SET Evidence = "Specifically, they have been found overexpressed in
many different diseases including various types of cancer where
they contribute in reducing proteotoxic stress [7]. "
path(MESH:Neoplasms) -> p(HGNCGENEFAMILY:"Small heat shock proteins")

SET Evidence = " HSP70 chaperones have a diverse
array of cellular functions but their major role is to ensure correct
folding of newly synthesized proteins and to perform the refolding
of proteins that are misfolded and/or aggregated. "
p(FPLX:HSPA) -> bp(GO:"'de novo' protein folding")
p(FPLX:HSPA) -> bp(GO:"protein folding")
p(FPLX:HSPA) -| bp(HBP:misfolding)
p(FPLX:HSPA) -| path(MESH:"Protein Aggregation, Pathological")

SET Evidence = "n general, HSP90s are more specialized than other
chaperones and are essential for survival in eukaryotic cells as they
also are capable of binding non-native polypeptides (at the late
stages of their folding) and preventing their aggregation [14]."
p(FPLX:HSP90) -| path(MESH:"Protein Aggregation, Pathological")

SET Evidence = " Upon
ER proteotoxic stress, GRP78 dissociates from its binding partners,
which are then free to trigger the Unfolded Protein Response (UPR) by
regulating specific gene responses aiming to restore ER proteome
stability. "
#GRP78 is HSPA5
bp(GO:"response to endoplasmic reticulum stress") -> act(p(HGNC:HSPA5))

SET Evidence = " The three sensors of ER proteotoxic stress facilitate contra-
dictory responses since they either promote cell survival by decreasing
the misfolded protein and/or oxidative load, or, if UPR fails, they
promote the activation of apoptotic pathways that eventually result in
cell death [57]."
bp(GO:"response to endoplasmic reticulum stress") -| bp(HBP:misfolding)
bp(GO:"response to endoplasmic reticulum stress") -| bp(GO:"cell death")
bp(GO:"response to endoplasmic reticulum stress") -> bp(GO:"apoptotic process")
bp(GO:"response to endoplasmic reticulum stress") -> bp(GO:"cell death")

SET Evidence = "Moreover, it
was shown that the concentration of the extracellular heat shock
protein 72 (eHSP72) increases during exercise-heat stress [65]."
bp(GO:"cellular response to heat") -> p(HGNC:HSPA1A, loc(MESH:"Extracellular Space"))

SET Evidence = "Excessive amounts of ROS may also
arise from inﬂammatory processes [75]."
bp(GO:"inflammatory response") -> a(CHEBI:"reactive oxygen species")

SET Evidence = "Specifically, ROS have been shown to
regulate a wide variety of signalling pathways including anti-
inﬂammatory responses and adaptation to hypoxia [77,78], autop-
hagy [79], immune cell function [80], cellular differentiation [81],
integrins [82], as well as oncogenes signalling [83]."
a(CHEBI:"reactive oxygen species") -> bp(GO:"negative regulation of inflammatory response")
a(CHEBI:"reactive oxygen species") reg bp(GO:"response to hypoxia")
a(CHEBI:"reactive oxygen species") reg bp(GO:"autophagy")
a(CHEBI:"reactive oxygen species") reg bp(GO:"leukocyte activation")
a(CHEBI:"reactive oxygen species") reg bp(GO:"cell differentiation")
a(CHEBI:"reactive oxygen species") reg bp(GO:"integrin activation")

SET Evidence = " Primary cellular defensive
mechanisms include enzymes like the superoxide dismutases,
SOD1 (Cu–Zn SOD) and SOD2 (MnSOD) that convert superoxide
to H 2 O 2 and catalase or glutathione peroxidase that convert H 2 O 2
to H 2 O;"
p(HGNC:SOD1) -> rxn(reactants(a(CHEBI:superoxide)),products(a(CHEBI:"hydrogen peroxide"), a(CHEBI:dioxygen)))
p(HGNC:SOD2) -> rxn(reactants(a(CHEBI:superoxide)),products(a(CHEBI:"hydrogen peroxide"), a(CHEBI:dioxygen)))
p(HGNC:CAT) -> rxn(reactants(a(CHEBI:"hydrogen peroxide")),products(a(CHEBI:"water"),a(CHEBI:dioxygen)))
p(HGNC:GPX1) -> rxn(reactants(a(CHEBI:"hydrogen peroxide")),products(a(CHEBI:"water")))

SET Evidence = "Oxidative stress abrogates the
Keap1-mediated degradation of Nrf2 which in turn accumulates in
the nucleus where it heterodimerizes with a small musculoapo-
neurotic fibrosarcoma (Maf) protein on antioxidant response
elements (AREs) to stimulate the expression of a wide arrays of
phase II and antioxidant enzymes including NAD(P)H quinone
oxidoreductase 1 (Nqo1), heme oxygenase 1 (Hmox1), glutamane-
cysteine ligase (GCL) and glutathione S transferases (GSTs)
[84,85,87,88]."
bp(GO:"response to oxidative stress") -| act(p(HGNC:KEAP1))
act(p(HGNC:KEAP1)) -> deg(p(HGNC:NFE2L2))
bp(GO:"response to oxidative stress") -> p(HGNC:NFE2L2)
complex(p(HGNC:NFE2L2),p(HGNC:MAF)) -> p(HGNC:NQO1)
complex(p(HGNC:NFE2L2),p(HGNC:MAF)) -> p(HGNC:HMOX1)
complex(p(HGNC:NFE2L2),p(HGNC:MAF)) -> p(HGNC:GCLC)
complex(p(HGNC:NFE2L2),p(HGNC:MAF)) -> p(FPLX:GST)

SET Evidence = " Moreover, Nrf2 contributes to cellular proteostasis
by regulating the expression of molecular chaperones [89], as well
as of additional players of proteome stability and maintenance,
namely the proteasome subunits [90–92]."
p(HGNC:NFE2L2) -> bp(HBP:Proteostasis)

SET Evidence = "Proteome oxidation and
instability has been associated with ageing and the progression of
several age-related diseases, including cardiovascular disorders, neu-
rodegeneration, and cancer [7,95,96]."
a(CHEBI:radical) -> bp(GO:"protein oxidation")
bp(GO:"protein oxidation") -- bp(GO:aging)
bp(GO:"protein oxidation") -- path(MESH:"Cardiovascular Diseases")
bp(GO:"protein oxidation") -- bp(HP:"Neurodegeneration")
bp(GO:"protein oxidation") -- path(MESH:Neoplasms)

SET Evidence = "A frequent
oxidative modification of proteins is irreversible carbonylation which
can occur by either direct oxidation where oxidants act and leave a
functional carbonyl group on amino acid side chains or in the protein
backbone, or, indirectly, by protein conjugation with oxidation pro-
ducts of polyunsaturated fatty acids and carbohydrates [98]."
a(CHEBI:radical) -> bp(MESH:"Protein Carbonylation")

SET Evidence = "Heavily carbonylated proteins tend to form
aggregates that are resistant to degradation and accumulate as
unfolded or damaged proteins [101]."
bp(MESH:"Protein Carbonylation") -> a(MESH:"Protein Aggregates")

SET Evidence = " HSF1 binds to a consensus heat shock
element (HSE) within the promoter regions of HSP genes resulting
in the activation of HSPs' gene expression and the control of cellular
responses to oxidative and proteotoxic stress [108]."
p(HGNC:HSF1) -> a(MESH:"Heat-Shock Proteins")
a(MESH:"Heat-Shock Proteins") reg bp(GO:"response to oxidative stress")

SET Evidence = "During elevated stress the chaperones within
the repressive HSF1-containing multi-chaperone complexes bind
the unfolded proteins and thus the liberated monomeric HSF1s
undergo phosphorylation, trimerization and nuclear localization
with increased transcriptional activity [109]."
bp(GO:"response to stress") -> p(HGNC:HSF1, pmod(Ph))
bp(GO:"response to stress") -> p(HGNC:HSF1, loc(MESH:"Cell Nucleus"))
bp(GO:"response to stress") -> tscript(p(HGNC:HSF1))

SET Evidence = "PDI is a redox sensitive chaperone that acts not
only as a sensor but also as a protein involved in the processing of
oxidized proteins and in preventing misfolding and/or aggregation
of proteins."
SET MeSHAnatomy = "Endoplasmic Reticulum"
p(HGNCGENEFAMILY:"Protein disulfide isomerases") -| bp(GO:"protein oxidation")
p(HGNCGENEFAMILY:"Protein disulfide isomerases") -| bp(HBP:misfolding)
p(HGNCGENEFAMILY:"Protein disulfide isomerases") -| a(MESH:"Protein Aggregates")
UNSET MeSHAnatomy

SET Evidence = "Additional studies in mammalian
peroxiredoxins showed that over-oxidation induces the formation
of high molecular weight oligomers which function as potent
chaperones and prevent protein aggregation [128,129];"
bp(GO:"response to oxidative stress") -> act(p(FPLX:PRDX))
act(p(FPLX:PRDX)) -| a(MESH:"Protein Aggregates")
