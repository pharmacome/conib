#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Axonal Transport, Tau Protein, and Neurodegeneration in Alzheimer’s Disease"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/cfd2ee89d1a71126da8b5f452544b7fe43be67e7/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/73688d6dc24e309fca59a1340dc9ee971e9f3baa/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/a5b84013a08880975ca84f40999e4404b14a97e2/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

# Add statements below this comment

SET Citation = {"PubMed", "Neuromolecular Med. 2002;2(2):151-65.","12428809"}

SET Evidence = "Tau protein is a typical microtubule-associated
protein (MAP) and thus is directly implicated in
maintaining the integrity and stability of the micro-
tubules and involved in axonal transport. On the other
hand, recent findings propose a direct role for
APP in axonal transport, as APP can link to kinesins
moving along the microtubules (Kamal et al., 2001)."
p(HGNC:MAPT) -> bp(GO:"microtubule cytoskeleton organization")
p(HGNC:MAPT) -- bp(GO:"axonal transport")
p(HGNC:APP) -- bp(GO:"axonal transport")

SET Evidence = "An important function of tau protein involves the
stabilization and spacing of microtubules, proba-
bly by linking to a number of tubulin subunits,
thereby preventing or slowing microtubule depoly-
merization (Drubin and Kirschner, 1986). "
p(HGNC:MAPT) -| bp(GO:"microtubule depolymerization")

SET Evidence = "The microtubule-binding properties of tau protein
are believed to be important for a number of
processes, e.g., the formation and maintenance of
axons and for fast axonal transport (FAT)."
p(HGNC:MAPT) -> bp(GO:"axon development")
p(HGNC:MAPT) -> bp(GO:"axonal transport")

SET Evidence = " Among the specific “proline-dependent”
kinases that can phosphorylate protein tau at dif-
ferent sites in vitro, special attention is dedicated to
glycogen-synthase kinase-3β (GSK-3β) (Michel et
al., 1998), mitogen-activated protein kinase (MAPK)
(Drewes et al., 1992), stress-activated protein kinases
(SAPKs) (Goedert et al., 1997) and cyclin-dependent
kinases (CDKs) including cdc2 and cdk5 (Baumann
et al., 1993; Patrick et al., 1999)."
p(HGNC:GSK3B) -> p(HGNC:MAPT, pmod(Ph))
p(FPLX:ERK) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:MAPK9) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:CDK1) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:CDK5) -> p(HGNC:MAPT, pmod(Ph))

SET Evidence = "Other serine and threonine residues, not followed
by proline, are phosphorylated by other protein
kinases, including microtubule-affinity-regulating
kinase (MARK) (Drewes et al., 1993), calcium/
calmodulin kinase II (CAMKII), cAMP-dependent
kinase (PKA) (Johnson et al., 1992), and casein kinase
II (Greenwood et al., 1994)."
p(HGNC:MARK1) -> p(HGNC:MAPT, pmod(Ph))
p(HGNC:CAMK2B) -> p(HGNC:MAPT, pmod(Ph))
p(FPLX:PKA) -> p(HGNC:MAPT, pmod(Ph))
p(FPLX:CK2) -> p(HGNC:MAPT, pmod(Ph))

SET Evidence = " Tau protein is rapidly
dephosphorylated by endogenous phosphatases
such as protein phosphatases 1, 2A, and 2B (cal-
cineurin) that are all present in the brain, and effec-
tively dephosphorylate tau protein in vitro. "
p(FPLX:PPP1) -| p(HGNC:MAPT, pmod(Ph))
p(FPLX:PPP2) -| p(HGNC:MAPT, pmod(Ph))
p(FPLX:PPP3) -| p(HGNC:MAPT, pmod(Ph))

SET Evidence = " TSubsequently, Wallerian degeneration and severe
muscle wasting and motoric problems demon-
strated the extensive neurodegeneration caused by
the overexpression of human tau protein in a gene-
dosage fashion."
#transgenic: overexpression of htau-2N, 4R
SET Species = "10090"
p(HBP:"4R tau") -> path(MESH:"Wallerian Degeneration")
p(HBP:"4R tau") -> path(MESH:"Muscular Atrophy")
p(HBP:"4R tau") -> path(HP:Neurodegeneration)
UNSET Species

SET Evidence = ". In ALS,
accumulation of NFs is a prominent feature
(Rouleau et al., 1996), and it has been demonstrated
that NFs contribute heavily to the axonopathy of
tau transgenic mice (Ishihara et al., 2001a)."
path(MESH:"Amyotrophic Lateral Sclerosis") pos a(MESH:"Intermediate Filaments")
SET Species = "10090"
a(MESH:"Intermediate Filaments") neg a(MESH:Axons)
UNSET Species

SET Evidence = "For the sake of completeness, we also refer to tau-
3R transgenic mice that developed another type of
pathology in the hippocampus, e.g., straight fila-
ments formed in aged mice older than 18 mo (Ishi-
hara, 2001b), which was proposed to be relevant for
AD, given the age-dependence."
SET Species = "10090"
p(HBP:"3R tau") -> a(HBP:"straight filaments")
a(HBP:"straight filaments") -- path(MESH:"Alzheimer Disease")
UNSET Species

SET Evidence = "Evidently, this argues for critical levels of
protein tau-4R in the pathology of FTD and by exten-
sion, in AD."
p(HBP:"4R tau") pos path(MESH:"Frontotemporal Dementia")
p(HBP:"4R tau") pos path(MESH:"Alzheimer Disease")

SET Evidence = "The following proposition has been recently reit-
erated that axonal transport in AD could become
disrupted by increased neuronal concentrations of
tau protein"
path(MESH:"Alzheimer Disease") neg bp(GO:"axonal transport")

SET Evidence = "The amount of Aβ produced could
be altered by delayed axonal transport, as well as
the precise species of metabolites of APPproduced—
e.g., Aβ40 or 42, monomeric Aβ, or Aβ-oligomers or
Aβ-derived diffusible ligands (ADDLs) (Lambert et
al., 1998; Walsh et al., 2000)."
bp(GO:"axonal transport") -- a(CHEBI:"amyloid-beta")

SET Evidence = "Neuronally overexpressed GSK-3β was
demonstrated to hyperphosphorylate tau protein
in vivo in the brain and spinal cord of double-
transgenic mice (Spittaels et al., 2000) thereby
reducing the amount of protein tau associated with
microtubules by 50% (Spittaels et al., 2000)."
SET Species = "10090"
SET MeSHAnatomy = {"Brain", "Spinal Cord"}
p(HGNC:GSK3B) -> p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
p(HGNC:GSK3B) -| complex(p(HGNC:MAPT), a(MESH:Microtubules))
UNSET Species
UNSET MeSHAnatomy

SET Evidence = " More-
over, the unbound tau protein was hyperphospho-
rylated and especially at the AD-2 epitope, e.g., an
epitope shown to contain serine 396 and serine 404."
SET Species = "10090"
SET MeSHAnatomy = {"Brain", "Spinal Cord"}
p(HGNC:GSK3B) -> p(HGNC:MAPT, pmod(Ph,Ser,396), pmod(Ph,Ser,404))
p(HGNC:MAPT, pmod(Ph,Ser,396), pmod(Ph,Ser,404)) -- path(MESH:"Alzheimer Disease")
UNSET Species
UNSET MeSHAnatomy

SET Evidence = "Surprisingly, the axonopathy was rescued in the
tau x GSK-3β double-transgenic mice, together
with a near-total normalization of the functional
disabilities."
p(HGNC:GSK3B) -> a(MESH:Axons)

SET Evidence = "Although much further work
is needed, these additional data demonstrate that
GSK-3β is intimately involved in the architecture
of axons and other neuronal processes, providing
indirect support for its role in tau-mediated con-
trol of axonal transport."
p(HGNC:GSK3B) -- bp(GO:"axonal transport")

SET Evidence = "However, it is believed that the secreted form of
APP plays important roles in neuronal plasticity
and survival (Mattson, 1997)."
sec(p(HGNC:APP)) -- bp(GO:"regulation of neuronal synaptic plasticity")
sec(p(HGNC:APP)) -- bp(GO:"negative regulation of neuron apoptotic process")

SET Evidence = "Most recently, this role became even more puz-
zling, since APP was implemented as a kinesin-1
receptor (Kamal et al., 2000; 2001). APP apparently
binds to the light chain of kinesin-1, which itself is
responsible for anterograde axonal transport and
consists of two light chains (KLC) associated with
two heavy chains (KIF5B)."
complex(p(HGNC:APP),p(HGNC:KLC1))
p(HGNC:KLC1) -> bp(GO:"anterograde axonal transport")

SET Evidence = "Deletion  of  the
C-terminus of APP695 or APPL, including the
kinesin-binding region, disrupted axonal transport
of APP695 and APPL."
p(HGNC:APPL) -> bp(GO:"anterograde axonal transport")
