#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "TFEB enhances astroglial uptake of extracellular tau species and reduces tau spreading."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
# DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
# DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
# DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "30108137"}

SET Evidence = "TFEB activation has been linked to lysosomal stress and the accumulation
of aberrant protein aggregates, indicated by TFEB’s
nuclear localization in lysosomal storage disorders (Sardiello et
al., 2009; Song et al., 2013)."
SET Confidence = "Medium"
SET MeSHAnatomy = "Lysosomes"
act(p(HGNC:TFEB)) -- path(MESH:"Stress, Physiological")
UNSET MeSHAnatomy
act(p(HGNC:TFEB)) -- p(CONSO:"protein aggregates")
path(DOID:"lysosomal storage disease") -- p(HGNC:TFEB,loc(GO:nucleus))
UNSET Confidence

SET Evidence = "Transcriptional levels of TFEB and several of
its well-known lysosomal targets were stratified by no dementia,
mild cognitive impairment, and dementia, showing that
these transcript levels positively correlate with cognitive decline
(Fig. 1 A and Fig. S1, A–C)."
SET Confidence = "Medium"
r(HGNC:TFEB) -- path(DOID:dementia)
r(HGNC:TFEB) neg bp(GO:cognition)
UNSET Confidence

SET Evidence = "Further, transcriptional levels of these
same genes were also positively correlated with increasing Braak
score, indicating a relationship between the TFEB lysosomal
pathway and worsening pathology (Fig. 1 B and Fig. S1, D–F)."
SET Confidence = "High"
r(HGNC:TFEB) pos a(CONSO:"Braak_Stage VI")
UNSET Confidence

SET Evidence = "In
addition, human brains diagnosed with frontotemporal dementia
(FTD) demonstrated an increase in transcriptional levels of
TFEB and TFEB lysosomal target gene LAMP1 (Fig. 1 C)."
SET Confidence = "High"
SET MeSHAnatomy = "Brain"
path(DOID:"frontotemporal dementia") -> r(HGNC:TFEB)
path(DOID:"frontotemporal dementia") -> r(HGNC:LAMP1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "On the
protein level, TFEB lysosomal target genes LAMP1 and Cathepsin
D (CTSD) are also increased in the FTD frontal cortex relative
to normal subjects, and TFEB protein levels trended increased
with statistical significance in the CBD group (Fig. 1, D and E)."
#CBD - corticobasal degeneration
SET Confidence = "High"
SET MeSHAnatomy = {"Cerebral Cortex","Frontal Lobe"}
path(DOID:"frontotemporal dementia") -> p(HGNC:LAMP1)
path(DOID:"frontotemporal dementia") -> p(HGNC:CTSD)
path(CONSO:"Corticobasal Degeneration") -> p(HGNC:TFEB)
UNSET MeSHAnatomy
UNSET Confidence

SET Species = "10090"

SET Evidence = "In the rTg4510 mouse model of tauopathy (Ramsden et al., 2005;
SantaCruz et al., 2005), gene set enrichment analysis (GSEA) of
microarray data revealed a similar enrichment of TFEB’s transcriptional
profile when comparing 4-mo-old transgenic mice
with widespread NFTs to wild-type mice, indicating TFEB’s
activation with tau pathology (Fig. 1 F)."
#rTg4510 4-month old
SET Confidence = "High"
SET MeSHDisease = "Tauopathies"
bp(GO:"neurofibrillary tangle assembly") -> g(HGNC:TFEB)
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "Examining the protein
levels in another tauopathy mouse model, hippocampal lysates
from PS19 mice (Yoshiyama et al., 2007) at 10 mo of age showed
increases in TFEB and lysosomal proteins LAMP1 and CTSD compared
with wild-type mice (Fig. S1, G and H)."
#PS19 10-month old
SET Confidence = "High"
SET MeSHAnatomy = "Hippocampus"
path(DOID:tauopathy) -> p(HGNC:TFEB)
path(DOID:tauopathy) -> p(HGNC:LAMP1)
path(DOID:tauopathy) -> p(HGNC:CTSD)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "This
cell type specific data suggests that TFEB is activated in astrocytes
in response to tauopathy."
#rTg4510
SET Confidence = "High"
SET MeSHAnatomy = "Astrocytes"
path(DOID:tauopathy) -> act(p(HGNC:TFEB))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Quantitation
of TFEB-3×FLAG nuclear localization, a marker of TFEB
activation, in these mice revealed a significant increase in TFEB
nuclear localization with tau pathology (Fig. 1, H and I)."
SET Confidence = "High"
path(DOID:tauopathy) -> p(HGNC:TFEB,loc(GO:nucleus))
UNSET Confidence

UNSET Species

SET Species = "10090"
SET CellLine = "mouse C57BL/6 inbred strain"

SET Evidence = "Likewise, protein expression of lysosomal
TFEB targets LAMP1, CTSB, and the mature form of CTSD were
increased (Fig. 2, C and D)."
#TFEB-transduced astrocytes mice (C57/BL6)
SET Confidence = "Low"
SET MeSHAnatomy = "Astrocytes"
p(HGNC:TFEB) -> p(HGNC:LAMP1)
p(HGNC:TFEB) -> p(HGNC:CTSD)
p(HGNC:TFEB) -> p(HGNC:CTSB)
UNSET MeSHAnatomy
#composite(p(HGNC:TFEB),a(MESH:Astrocytes)) -> p(HGNC:LAMP1)
#composite(p(HGNC:TFEB),a(MESH:Astrocytes)) -> p(HGNC:CTSD)
#composite(p(HGNC:TFEB),a(MESH:Astrocytes)) -> p(HGNC:CTSB)
UNSET Confidence

SET Evidence = "To determine if TFEB overexpression
increases overall lysosomal biogenesis, we stained AAV-transduced
primary astrocytes with LysoTracker red dye and demonstrated
an increase in the abundance of acidic organelles such
as lysosomes and late endosomes in TFEB-transduced astrocytes
(Fig. 2 E)."
SET Confidence = "High"
SET MeSHAnatomy = "Astrocytes"
p(HGNC:TFEB) -> a(MESH:Lysosomes)
p(HGNC:TFEB) -> complex(GO:"late endosome")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In sum, these results demonstrate that TFEB regulates
the lysosomal pathway in primary astrocytes."
SET Confidence = "High"
SET MeSHAnatomy = "Astrocytes"
p(HGNC:TFEB) reg act(a(MESH:Lysosomes))
UNSET MeSHAnatomy
UNSET Confidence
UNSET CellLine
UNSET Species

SET Evidence = "TFEB not only increased the
proportion of cells taking up beads, but also increased the proportion
of cells taking up multiple beads, suggesting an enhanced
phagocytic capacity (Fig. 2, F–H)."
#beads - red fluorescent latex beads
SET Confidence = "High"
p(HGNC:TFEB) -> bp(GO:phagocytosis)
UNSET Confidence

SET Evidence = "Our results demonstrate a statistically significant increase of 12%
in the uptake of pffs in TFEB transduced astrocytes relative to
control at the 1-h time point, with a 22% relative increase in uptake
in the TFEB transduced cells at 4 h (Fig. 2 I)."
#in synthetic tau fibrils of truncated human tau with P301L mutataion(K18 P301L)
#pff - preformed fibrils of tau
SET Confidence = "High"
composite(p(HGNC:TFEB),a(MESH:Astrocytes)) -> p(CONSO:"Tau fibrils",loc(MESH:Astrocytes))
UNSET Confidence

SET Evidence = "Using FLAG immunostaining for the transduced FLAG-tagged
TFEB construct, confocal microscopy revealed that under
basal primary astrocyte culture conditions, TFEB was predominantly
cytoplasmic in its inactive state (Fig. 2 J)."
SET Confidence = "High"
SET MeSHAnatomy = "Astrocytes"
p(HGNC:TFEB,loc(GO:cytoplasm)) neg act(p(HGNC:TFEB))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "mTORC1 phosphorylates TFEB, which
sequesters the transcription factor in the cytoplasm (Peña-Llopis
et al., 2011; Martina et al., 2012; Roczniak-Ferguson et al., 2012;
Settembre et al., 2012)."
SET Confidence = "High"
p(HGNCGENEFAMILY:"MTOR complex 1") -> p(HGNC:TFEB,pmod(Ph))
p(HGNC:TFEB,pmod(Ph)) -> p(HGNC:TFEB,loc(GO:cytoplasm))
p(HGNCGENEFAMILY:"MTOR complex 1") -> p(HGNC:TFEB,loc(GO:cytoplasm))
UNSET Confidence

SET Evidence = "Inhibiting mTORC1 blocks the phosphorylation
of TFEB, allowing unphosphorylated TFEB to translocate
to the nucleus and become transcriptionally active (Settembre
et al., 2012)."
SET Confidence = "High"
p(HGNC:TFEB) -> tloc(p(HGNC:TFEB),fromLoc(GO:cytoplasm),toLoc(GO:nucleus))
p(HGNC:TFEB,loc(GO:nucleus)) -> act(p(HGNC:TFEB),ma(tscript))
UNSET Confidence

SET Evidence = "Confocal microscopy of FLAG immunostaining for
FLAG-tagged TFEB demonstrates the nuclear localization of
TFEB following Torin1 treatment as shown by colabeling with
DAPI (Fig. 2 J)."
#torin 1 is inhibitor of mTORC1
SET Confidence = "High"
a(CHEBI:"torin 1") -| p(HGNCGENEFAMILY:"MTOR complex 1")
a(CHEBI:"torin 1") -> p(HGNC:TFEB,loc(GO:nucleus))
UNSET Confidence

SET Evidence = "Additional LAMP1 staining also reveals TFEB’s
colocalization with the lysosome with Torin1 treatment, an indication
of TFEB temporarily trapped on the lysosomal surface
due to its interaction with the recently inactivated mTORC1
(Fig. 2 J; Settembre et al., 2012)."
SET Confidence = "High"
a(CHEBI:"torin 1") -> composite(p(HGNC:TFEB,loc(GO:"lysosomal membrane")),p(HGNC:LAMP1,loc(GO:"lysosomal membrane")))
UNSET Confidence

SET Evidence = "Flow
cytometry revealed that Torin1 treatment of TFEB transduced
astrocytes increased dye-conjugated pff uptake 63% relative to
EGFP transduced controls as shown by median fluorescence intensity,
while under basal conditions, the TFEB overexpressing
astrocytes increased uptake just 18% relative to EGFP expressing
controls (Fig. 2 K)."
SET Confidence = "High"
composite(p(HGNC:TFEB),a(MESH:Astrocytes),a(CHEBI:"torin 1")) -> p(CONSO:"Tau fibrils",loc(MESH:Astrocytes))
UNSET Confidence

SET Species = "10090"

SET Evidence = "qRT-PCR demonstrated undetectable transcript
levels of TFEB as well as a reduction in LAMP1 mRNA in TFEB
KO astrocytes compared with littermate controls (Fig. 2 L)."
#nestin-cre tcfeb knockout (TFEB KO)
SET Confidence = "High"
SET MeSHAnatomy = "Astrocytes"
r(HGNC:TFEB) -> p(HGNC:TFEB)
p(HGNC:TFEB) -> r(HGNC:LAMP1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In
addition, the TFEB KO astrocyte culture demonstrated a modest
reduction in LysoTracker Red staining, indicating a reduction in
lysosomes and/or lysosomal acidity (Fig. 2 M)."
SET Confidence = "High"
p(HGNC:TFEB) -> a(MESH:Lysosomes)
p(HGNC:TFEB) pos bp(GO:"lysosomal lumen acidification")
UNSET Confidence
UNSET Species

SET MeSHAnatomy = "Astrocytes"

SET Evidence = "Confocal microscopy revealed an expected increase in
lysosomal marker LAMP1 staining, as well as an increase in
dye-conjugated fibril fluorescence signal in TFEB transduced
astrocytes (Fig. 3 A)."
SET Confidence = "Medium"
SET MeSHAnatomy = "Astrocytes"
p(HGNC:TFEB) -> p(HGNC:LAMP1)
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "These findings indicate TFEB increases
the incidence of phagocytosed pffs inside the lysosome."
SET Confidence = "High"
p(HGNC:TFEB) -> p(CONSO:"Tau fibrils",loc(MESH:Lysosomes))
p(CONSO:"Tau fibrils",loc(MESH:Lysosomes)) -> deg(p(CONSO:"Tau fibrils"))
UNSET Confidence

SET Evidence = "Flow cytometry analysis of
primary astrocytes incubated with DQ-BSA revealed an ∼60%
increase in median fluorescence for TFEB-transduced astrocytes,
indicating that TFEB enhances uptake and proteolysis
of DQ-BSA (Fig. 3 D)."
#DQ-BSA - boron-dipyrromethene dye–conjugated BSA (102513886)
SET Confidence = "High"
composite(p(HGNC:TFEB),a(MESH:Astrocytes)) -> composite(a(PUBCHEM:102513886,loc(MESH:Lysosomes)),p(MESH:"Serum Albumin, Bovine",loc(MESH:Lysosomes)))
SET MeSHAnatomy = "Lysosomes"
composite(a(PUBCHEM:102513886),p(MESH:"Serum Albumin, Bovine")) -> deg(a(PUBCHEM:102513886))
composite(a(PUBCHEM:102513886),p(MESH:"Serum Albumin, Bovine")) -> deg(p(MESH:"Serum Albumin, Bovine"))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In sum, these data suggest that
TFEB stimulates uptake and trafficking of pffs to the lysosome
for degradation."
SET Confidence = "High"
p(HGNC:TFEB) -> p(CONSO:"Tau fibrils",loc(MESH:Lysosomes))
p(CONSO:"Tau fibrils",loc(MESH:Lysosomes)) -> deg(p(CONSO:"Tau fibrils"))
p(HGNC:TFEB) -> deg(p(CONSO:"Tau fibrils"))
UNSET Confidence

SET Evidence = "Confocal microscopy
revealed that heparin treatment reduces the fluorescence
signal of pffs in primary astrocytes transduced with TFEB
(Fig. 3 A)."
SET Confidence = "High"
SET MeSHAnatomy = "Astrocytes"
a(CHEBI:heparin) -| p(CONSO:"Tau fibrils",loc(MESH:Lysosomes))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In addition, flow cytometry analysis demonstrated
that heparin reduces dye-conjugated pff uptake in both TFEB
and EGFP transduced astrocytes, suggesting that macropinocytosis
is responsible for astroglial pff uptake (Fig. 3 B)."
SET Confidence = "High"
bp(GO:macropinocytosis) -> tloc(p(CONSO:"Tau fibrils"),fromLoc(GO:cytoplasm),toLoc(MESH:Lysosomes))
UNSET Confidence
SET Species = "10090"
#mouse pups at P3
SET MeSHAnatomy = "Hippocampus"

SET Evidence = "Confocal microscopy of costaining for FLAG-tagged TFEB and
astrocytic marker GFAP in the hippocampus showed AAV-mediated
expression of TFEB occurred in astrocytes as signified
by the colocalization of the two antibodies (Fig. 4 A)."
SET Confidence = "High"
complex(MESH:Dependovirus) -> p(HGNC:TFEB,loc(MESH:Astrocytes))
UNSET Confidence

SET Evidence = "Hippocampal brain extract
from TFEB-injected mice exhibited an ∼13-fold increase in
TFEB transcripts as well as significant increases in the transcript
levels of TFEB target genes compared with EGFP-injected
littermates (Fig. 4, B and C)."
SET Confidence = "High"
complex(MESH:Dependovirus) -> r(HGNC:TFEB)
complex(MESH:Dependovirus) -> act(p(HGNC:TFEB),ma(tscript))
UNSET Confidence

SET Evidence = "On the protein level, LAMP1 and
both immature and mature forms of CTSD were significantly
increased in the hippocampus of TFEB-injected mice (Fig. 4, D
and E). In addition, protein homogenate from the hippocampus
of TFEB-injected mice had increased CTSD enzyme activity
relative to EGFP-injected littermates (Fig. 4 F)."
SET Confidence = "High"
p(HGNC:TFEB) -> p(HGNC:LAMP1)
p(HGNC:TFEB) -> p(HGNC:CTSD)
p(HGNC:TFEB) -> act(p(HGNC:CTSD),ma(GO:"peptidase activity"))
UNSET Confidence
UNSET MeSHAnatomy

SET Evidence = "Previous work
indicates that neuronal activity influences the rate of synaptic
tau release, which may impact tau spreading (Wu et al., 2016),
and astrocytes can impact neuronal signaling through lysosomal
exocytosis of ATP (Zhang et al., 2007; Lalo et al., 2016)."
SET Confidence = "High"
SET MeSHAnatomy = "Synapses"
act(a(MESH:Neurons)) -> sec(p(HGNC:MAPT))
sec(p(HGNC:MAPT)) -- path(CONSO:"Pathological Tau Spreading")
UNSET MeSHAnatomy
a(MESH:Astrocytes) -> act(a(MESH:Lysosomes))
act(a(MESH:Lysosomes)) -> bp(GO:"ATP export")
bp(GO:"ATP export") -- bp(GO:"neuron-neuron synaptic transmission")
UNSET Confidence

SET Evidence = "Neither sEPSCs amplitude nor frequency show statistically significant
differences between AAV-GFAP-EGFP and AAV-GFAP-TFEB
mice (Fig. 4, I and J). Thus, AAV-mediated astroglial TFEB
expression does not impact astrocyte reactivity/morphology or
neuronal activity in wild-type mice, excluding these possible
indirect effects on tau release."
#EPSC - excitatory post-synaptic currents
SET Confidence = "High"
SET MeSHAnatomy = "Dentate Gyrus"
complex(MESH:Dependovirus) -> p(HGNC:TFEB,loc(MESH:Astrocytes))
p(HGNC:TFEB,loc(MESH:Astrocytes)) cnc bp(GO:"excitatory postsynaptic potential")
p(HGNC:TFEB,loc(MESH:Astrocytes)) cnc bp(GO:"astrocyte activation")
p(HGNC:TFEB,loc(MESH:Astrocytes)) cnc act(a(MESH:Neurons))
UNSET MeSHAnatomy
UNSET Confidence
#in rTg4510 tauopathy model
SET MeSHDisease = "Tauopathies"

SET Evidence = "Immunoblotting
for total tau and phospho-tau antibodies, PHF1 and CP13,
demonstrated no statistical difference in hippocampal brain lysate
from TFEB- versus EGFP-injected rTg4510 male or female
mice (Fig. 5, A–C)."
SET Confidence = "High"
SET Gender = {"Female","Male"}
SET MeSHAnatomy = "Hippocampus"
p(HGNC:TFEB) cnc p(HGNC:MAPT)
p(HGNC:TFEB) cnc p(HGNC:MAPT,pmod(Ph))
UNSET MeSHAnatomy
UNSET Gender
UNSET Confidence

SET Evidence = "Similarly, immunofluorescence staining for
an early pathological conformation of tau using the MC1 antibody
showed no significant difference in area fluorescence in the hippocampus
or cortex of TFEB and EGFP groups, nor did staining
with phospho-tau antibody AT8 (Fig. 5, D–I)."
SET Confidence = "High"
SET MeSHAnatomy = {"Hippocampus","Cerebral Cortex"}
p(HGNC:TFEB) cnc p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In addition, immunofluorescence
staining for astro- and microgliosis markers GFAP
and Iba1, respectively, showed no change in area fluorescence in
TFEB- or EGFP-injected male mice (Fig. S3)."
SET Confidence = "High"
SET MeSHAnatomy = "Microglia"
bp(GO:"reactive gliosis") -> p(HGNC:AIF1)
UNSET MeSHAnatomy
SET MeSHAnatomy = "Astrocytes"
bp(GO:"reactive gliosis") -> p(HGNC:GFAP)
UNSET MeSHAnatomy
p(HGNC:TFEB) cnc bp(GO:"reactive gliosis")
UNSET Confidence

#in PS19 taupathy model
SET Evidence = "Following fractionation, immunoblotting
for phospho and total tau species in the soluble pool similarly
showed a significant reduction in phospho-tau species recognized
by CP13 and PHF1 antibodies, with no change in total tau
(Fig. 6, C and D)."
#in both soluble and insoluble pool
SET Confidence = "High"
p(HGNC:TFEB) -| p(HGNC:MAPT)
p(HGNC:TFEB) -| p(HGNC:MAPT,pmod(Ph))
UNSET Confidence

SET Evidence = "In contrast, cell
body staining was absent in the CA3 region of AAV-TFEB–injected
mice with instead prominent mossy fiber staining present
(Fig. 6 F)(Shi et al. (2017))."
SET Confidence = "High"
SET MeSHAnatomy = "CA3 Region, Hippocampal"
p(HGNC:TFEB) -> complex(GO:"hippocampal mossy fiber to CA3 synapse")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "The distribution of the
CP13 staining patterns indicated patterns associated with later
stage pathology were more abundant in EGFP-injected mice,
while earlier stage, less severe phospho-tau staining patterns
were present in the TFEB-injected mice (Fig. 6 G)."
SET Confidence = "High"
p(HGNC:TFEB) -| path(DOID:tauopathy)
UNSET Confidence

SET Evidence = "With respect
to phospho-tau staining in the cortex of these mice, a trending
reduction in the area of CP13 staining is observed in the TFEB-injected
group (Fig. 6 H)."
SET Confidence = "High"
SET MeSHAnatomy = "Cerebral Cortex"
p(HGNC:TFEB) -| p(HGNC:MAPT,pmod(Ph))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In line with a reduction in tau pathology,
gliosis was also reduced in the hippocampus though not in the
cortex, as shown by GFAP and Iba1 immunostaining (Fig. 6, I–L)."
SET Confidence = "High"
SET MeSHAnatomy = "Hippocampus"
p(HGNC:TFEB) -| bp(GO:"reactive gliosis")
UNSET MeSHAnatomy
SET MeSHAnatomy = "Cerebral Cortex"
p(HGNC:TFEB) cnc bp(GO:"reactive gliosis")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Quantifying
the area fluorescence of MC1 staining for both hippocampi from
sections representing the entire volume of hippocampus showed
that astroglial TFEB had no impact on MC1 staining on the ipsilateral
side, but significantly reduced the area of MC1 staining
on the contralateral side (Fig. 7, A and C)."
SET Confidence = "High"
SET MeSHAnatomy = "Hippocampus"
p(HGNC:TFEB,loc(MESH:Astrocytes)) -| p(HGNC:MAPT,pmod(Ph))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In addition, X-34 staining, which labels β sheet
structures in tangle-like pathology, was reduced in the TFEB-injected
mice 2 mo after pff injection on both the ipsilateral and
contralateral side (Fig. S5, D and G)."
SET Confidence = "High"
p(HGNC:TFEB) -| p(CONSO:"Tau fibrils")
p(HGNC:TFEB) -| complex(GO:"neurofibrillary tangle")
UNSET Confidence

SET Evidence = "In addition
to a reduction in tau pathology/tau spreading, the TFEB-injected
mice also demonstrate an increased incidence of astrocytes with
AT8 positive phospho-tau (Fig. 8, A–C)."
SET Confidence = "High"
p(HGNC:TFEB) -| path(CONSO:"Pathological Tau Spreading")
SET MeSHAnatomy = "Astrocytes"
p(HGNC:TFEB) -> p(HGNC:MAPT,pmod(Ph))
UNSET MeSHAnatomy

UNSET MeSHDisease
UNSET Species
UNSET Confidence
