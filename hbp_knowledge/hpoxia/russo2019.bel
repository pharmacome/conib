#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Hypoxia and Inflammation as a Consequence of β-Fibril Accumulation: A Perspective View for New Potential Therapeutic Targets."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
 DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
 DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
 DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
# DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"
 DEFINE NAMESPACE NCIT           AS PATTERN "^C\d+$"
 DEFINE NAMESPACE OMIT           AS PATTERN "^\d{7}$"
###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
 DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
# DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
# DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
# DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
# DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
# DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "31346362"}

SET Evidence = "A constant feature of amyloidoses is the progressive accumulation
of β-fibrils in the intra- and extracellular space of
involved tissues and organs as reported in Figure 1."
#Fig 1
path(DOID:amyloidosis) -> p(CONSO:"Tau aggregates",loc(GO:cytosol))
path(DOID:amyloidosis) -> p(CONSO:"huntingtin aggregates",loc(GO:nucleus))
path(DOID:amyloidosis) -> p(CONSO:"amyloid-beta fibrils",loc(GO:"endoplasmic reticulum"))
path(DOID:amyloidosis) -> sec(p(CONSO:"amyloid-beta fibrils"))
path(DOID:amyloidosis) -> p(CONSO:"amyloid-beta fibrils",loc(GO:"extracellular space"))
path(DOID:amyloidosis) -> p(CONSO:"amyloid-beta fibrils",loc(MESH:Plasma))

SET Evidence = "β-Fibrils exhibit a green birefringence
at polarized light under an optical microscope after red
Congo staining and more importantly a strong resistance to
proteases that normally provide to their turnover in order
to avoid accumulation in cells and tissues [2]."
p(HGNCGENEFAMILY:Aminopeptidases) -| p(CONSO:"amyloid-beta fibrils")
p(HGNCGENEFAMILY:Aminopeptidases) cnc path(DOID:amyloidosis)

SET Evidence = "Intracellular β-fibrils are able to activate various pathways of
oxidative metabolism with production of ROS which contribute substantially to the cell damage."
p(CONSO:"amyloid-beta fibrils",loc(GO:intracellular)) -> bp(GO:"cellular respiration")
p(CONSO:"amyloid-beta fibrils",loc(GO:intracellular)) -> bp(GO:"reactive oxygen species biosynthetic process")
p(CONSO:"amyloid-beta fibrils",loc(GO:intracellular)) -> bp(GO:"cell death")

SET Evidence = "Moreover, in systemic amyloidoses, 15% of
amyloidosic substance is constituted by SAP or serum amyloid
protein, a member of the major acute-phase protein family,
i.e., pentraxins, which strongly interacts with β-fibrils,
probably through chemical patterns usually recognized by
pentraxins."
p(CONSO:"amyloid-beta fibrils") hasComponents list(p(HGNC:APCS),p(HGNCGENEFAMILY:"Long pentraxins"),p(HGNC:APP))

SET Evidence = "This interaction may protect β-aggregates from
proteolysis on one side and stimulate their phagocytosis by
macrophages on the other side (Figure 1) [19, 20]."
#this interaction - between beta fibrils and pentraxins
complex(p(CONSO:"amyloid-beta aggregates"),p(HGNCGENEFAMILY:"Long pentraxins")) -| bp(GO:proteolysis)
complex(p(CONSO:"amyloid-beta aggregates"),p(HGNCGENEFAMILY:"Long pentraxins")) -> bp(GO:phagocytosis)

SET Evidence = "Systemic amyloidoses are characterized by
the deposition of culprit proteins such as immunoglobulin
light chains (intact or fragments) and many acute-phase proteins
(SAA, transthyretin, fibrinogen α-chain, β2-macroglobulin,
lysozyme, and gelsolin) [1, 21]."
#NCIT:C16720 - Immunoglobulin Light chains
path(DOID:amyloidosis) -> p(NCIT:C16720)
path(DOID:amyloidosis) -> p(NCIT:C16720,frag("?"))
path(DOID:amyloidosis) -> p(HGNC:SAA1)
path(DOID:amyloidosis) -> p(HGNC:TTR)
path(DOID:amyloidosis) -> p(HGNC:FGA)
path(DOID:amyloidosis) -> p(HGNC:B2M)
path(DOID:amyloidosis) -> p(HGNC:LYZ)
path(DOID:amyloidosis) -> p(HGNC:GSN)

SET Evidence = "As a consequence, in this area, a hypoxic environment
is generated, leading to HIF-1α activation that is
accompanied by NFκB activation. Importantly, both
HIF-1α and NFκB trigger and amplify the inflammatory reparative
response and cellular damage [28]."
#this area - beta aggregated tissues
p(CONSO:"amyloid-beta aggregates") -> p(HGNC:HIF1A)
p(CONSO:"amyloid-beta aggregates") -> p(HGNCGENEFAMILY:"NF-kappa B complex subunits")
p(HGNC:HIF1A) -> bp(GO:"inflammatory response")
p(HGNC:HIF1A) -> bp(GO:"cell death")
p(HGNCGENEFAMILY:"NF-kappa B complex subunits") -> bp(GO:"inflammatory response")
p(HGNCGENEFAMILY:"NF-kappa B complex subunits") -> bp(GO:"cell death")

SET Evidence = "O2 shortage causes inhibition
of ATP production, rapid fall of the energy charge, and
loss of ionic gradients with the alteration of cytosolic calcium
homeostasis [29]."
a(CHEBI:dioxygen) -> bp(GO:"ATP biosynthetic process")
a(CHEBI:dioxygen) -> bp(GO:"energy homeostasis")
a(CHEBI:dioxygen) -> bp(GO:"establishment or maintenance of transmembrane electrochemical gradient")
a(CHEBI:dioxygen) reg bp(GO:"cellular calcium ion homeostasis")

SET Evidence = "The increase of cytosolic Ca++ concentration
above 10-6 M dramatically activates the peroxidative
metabolism, an irreversible contraction and degradation of
the cytoskeleton and the activation of Ca++-dependent
cytosolic proteases (calpains) and DNAases, leading to an
irreversible and rapid cell degradation [30]."
#above 10^-6 M
a(CHEBI:"calcium(2+)") -> bp(GO:"membrane lipid catabolic process")
bp(GO:"membrane lipid catabolic process") -> deg(a(MESH:Cytoskeleton))
a(CHEBI:"calcium(2+)") -> act(p(HGNCGENEFAMILY:Calpains))
a(CHEBI:"calcium(2+)") -> act(ma(GO:"deoxyribonuclease I activity"))
act(p(HGNCGENEFAMILY:Calpains)) -> bp(GO:"cell death")
act(ma(GO:"deoxyribonuclease I activity")) -> bp(GO:"cell death")

SET Evidence = "This adaptation to hypoxia occurs through the activation of hypoxic inducible factors
(HIFs) [32] and the expression of a number of HIF-1α-
dependent genes, involved in vital pathways, such as angiogenesis
(vascular endothelial growth factors or VEGFs),
metabolism (glucose transporter 1 or Glut1, hexokinase II
or HKII, and glycolysis) [33], inflammation (Toll-like receptors
or TLRs and other receptors for alarmins, cytokines, and
matrix metalloproteinases or MMPs), and repair (autophagocytosis
for disposal of damaged cell components, telomerase
reverse transcriptase (TERT), and stemness genes to
increase the stem cell compartment to substitute death cells)
[34], transforming growth factor β (TGF-β) and fibrosis
pathways [33]."
p(HGNC:HIF1A) -> g(HGNC:VEGFA)
g(HGNC:VEGFA) -> bp(GO:angiogenesis)
p(HGNC:HIF1A) -> bp(GO:angiogenesis)
p(HGNC:HIF1A) -> g(HGNC:SLC2A1)
p(HGNC:HIF1A) -> g(HGNC:HK2)
p(HGNC:SLC2A1) -> bp(GO:"glycolytic process")
p(HGNC:HK2) -> bp(GO:"glycolytic process")
p(HGNC:HIF1A) -> bp(GO:"glycolytic process")
p(HGNC:HIF1A) -> g(HGNCGENEFAMILY:"Toll like receptors")
p(HGNC:HIF1A) -> a(MESH:Alarmins)
p(HGNC:HIF1A) -> a(MESH:Cytokines)
p(HGNC:HIF1A) -> a(MESH:"Matrix Metalloproteinases")
a(MESH:Alarmins) -> bp(GO:"inflammatory response")
a(MESH:Cytokines) -> bp(GO:"inflammatory response")
a(MESH:"Matrix Metalloproteinases") -> bp(GO:"inflammatory response")
p(HGNC:HIF1A) -> bp(GO:"inflammatory response")
p(HGNC:HIF1A) -> bp(GO:autophagy)
p(HGNC:HIF1A) -> g(HGNC:TERT)
p(HGNC:HIF1A) -> g(HGNC:TGFB1)
p(HGNC:HIF1A) -> p(CONSO:"amyloid-beta fibrils")

SET Evidence = "As a consequence, alarmins released by necrotic cells
(β-fibrils, high-mobility group box 1 or HMGB1, ATP/ADP,
membrane debris, nucleic acids, etc.) bind to these newly
expressed receptors producing an additional activation of
transcription factors (NFκB, STAT3, AP1, etc.) driving IRR
gene transcription not only in resident inflammatory cells
but also in remodeled parenchymal cells [26, 27]."
#Receptors - RAGE, P2X7, NOD-like, Inflammasome
#NCIT:C706 - Nucleic acids
bp(GO:"necrotic cell death") -> a(MESH:Alarmins)
p(CONSO:"amyloid-beta fibrils") -> a(MESH:Alarmins)
p(HGNC:HMGB1) -> a(MESH:Alarmins)
bp(GO:"ATP generation from ADP") -> a(MESH:Alarmins)
p(NCIT:C706) -> a(MESH:Alarmins)
SET Anatomy = "parenchyma"
complex(a(MESH:Alarmins),p(HGNC:AGER)) -> act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits"))
complex(a(MESH:Alarmins),p(HGNC:AGER)) -> act(p(HGNC:STAT3))
complex(a(MESH:Alarmins),p(HGNC:AGER)) -> act(complex(GO:"transcription factor AP-1 complex"))
complex(a(MESH:Alarmins),p(HGNC:P2RX7)) -> act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits"))
complex(a(MESH:Alarmins),p(HGNC:P2RX7)) -> act(p(HGNC:STAT3))
complex(a(MESH:Alarmins),p(HGNC:P2RX7)) -> act(complex(GO:"transcription factor AP-1 complex"))
complex(a(MESH:Alarmins),p(HGNCGENEFAMILY:"NLR family")) -> act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits"))
complex(a(MESH:Alarmins),p(HGNCGENEFAMILY:"NLR family") -> act(p(HGNC:STAT3))
complex(a(MESH:Alarmins),p(HGNCGENEFAMILY:"NLR family") -> act(complex(GO:"transcription factor AP-1 complex"))
complex(a(MESH:Alarmins),complex(GO:"inflammasome complex")) -> act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits"))
complex(a(MESH:Alarmins),complex(GO:"inflammasome complex")) -> act(p(HGNC:STAT3))
complex(a(MESH:Alarmins),complex(GO:"inflammasome complex")) -> act(complex(GO:"transcription factor AP-1 complex"))
UNSET Anatomy

SET Evidence = "The latter
further activate oxidative metabolism, produce mediators
and other proinflammatory cytokines, and maintain a
chronic inflammatory status in the affected tissue."
#IRR gene

SET Evidence = "Unfortunately,
clinical consequences of this prolonged IRR activation
include cell/tissue damage, continuous repair and fibrosis
with progressive deterioration of the function leading to
organ insufficiency, and neuronal damages, the typical final
outcome of amyloidoses [5, 7, 36]."

SET Evidence = "Extracellular
β-fibril-alarmin receptor binding can then activate NFκB
pathways for oxidative metabolism and apoptosis [40, 41]
as well as phagocytic activity of macrophages (Figure 1(c))."
complex(a(MESH:Alarmins),a(CONSO:"amyloid-beta fibrils"),loc(GO:"extracellular region")) -> act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits"))
act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits")) -> bp(GO:"cellular respiration")
act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits")) -> bp(GO:"programmed cell death")
act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits")) -> bp(GO:phagocytosis)
complex(a(MESH:Alarmins),a(CONSO:"amyloid-beta fibrils"),loc(GO:"extracellular region")) -> bp(GO:"cellular respiration")
complex(a(MESH:Alarmins),a(CONSO:"amyloid-beta fibrils"),loc(GO:"extracellular region")) -> bp(GO:"programmed cell death")
complex(a(MESH:Alarmins),a(CONSO:"amyloid-beta fibrils"),loc(GO:"extracellular region")) -> bp(GO:phagocytosis)

SET Evidence = "On the other hand, intracellular β-fibrils are able to activate
inflammasomes [42, 43]."
#for the same recpetor
complex(a(MESH:Alarmins),a(CONSO:"amyloid-beta fibrils"),loc(GO:intracellular)) -> act(complex(GO:"inflammasome complex"))

SET Evidence = "Through these mechanisms,
inflammation and ROS contribute substantially to the cell/
tissue damage and severity of the disease."
#these mechanisms - actvation of inflammasome,activation of NF-kB
act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits")) -> bp(GO:"inflammatory response")
act(complex(GO:"inflammasome complex")) -> bp(GO:"inflammatory response")
act(p(HGNCGENEFAMILY:"NF-kappa B complex subunits")) -> a(CHEBI:"reactive oxygen species")
act(complex(GO:"inflammasome complex")) -> a(CHEBI:"reactive oxygen species")

SET Evidence = "Although intracellular β-fibrils
may undergo autophagocytosis, they still display a clear
insensitivity to the intralysosomal acidic proteases, leading
to lipofuscin accumulation that can be observed in aged
and amyloidosic tissues [46]."
#with respect to aggregation of beta fibrils
p(CONSO:"amyloid-beta aggregates",loc(GO:intracellular)) -> bp(GO:autophagy)
p(CONSO:"amyloid-beta aggregates",loc(GO:intracellular)) -> a(MESH:Lipofuscin)
path(DOID:amyloidosis) -> a(MESH:Lipofuscin)

SET Evidence = "In
conclusion, as it has been underlined previously, progressive
accumulation of amyloid fibrils is responsible for a local hypoxia
and of a chronic long lasting inflammation, which in
turn produces tissue damage leading to continuous repair,
fibrosis, and organ insufficiency."
#NCIT:C82901 - chronic inflammation
p(CONSO:"amyloid-beta aggregates") -> path(EFO:hypoxia)
p(CONSO:"amyloid-beta aggregates") -> path(NCIT:C82901)

SET Evidence = "Recently, it has been shown
[50] that recombinant APC (activated protein C, a plasma
protease) or its analogs, inhibiting the neuronal β-secretase,
may strongly slow or block the generation of β-amyloid
protein protecting from Alzheimer’s disease or slowing its
progression [51, 52]"
act(p(HGNC:PROC,var("?"))) -| p(HGNC:BACE1,loc(GO:"neuron part"))
act(p(HGNC:PROC,var("?"))) -| p(HGNC:APP)
p(HGNC:APP) -> path(DOID:"Alzheimer's disease")
act(p(HGNC:PROC,var("?"))) -| path(DOID:"Alzheimer's disease")

SET Evidence = "Using chaperones [53, 54], small molecules [54], and
aspirin [55] slows or inhibits polymer formation [50, 55, 56]
and β-fibril aggregation and accumulation, preventing the
disease and attenuating its progression and symptoms."
#OMIT:0018975 - Molecular chaperon
p(OMIT:0018975) -| p(MESH:Proteins,pmod(MESH:Polymers))
a(CHEBI:"acetylsalicylic acid") -| p(MESH:Proteins,pmod(MESH:Polymers))
p(OMIT:0018975) -| p(CONSO:"amyloid-beta aggregates")
a(CHEBI:"acetylsalicylic acid") -| p(CONSO:"amyloid-beta aggregates")

SET Evidence = "Although the mechanism(s)
is still unclear, it has been demonstrated that aspirin is able
to donate its acetyl group to the culprit proteins, increasing
their acetylation and reducing the tendency of phosphorylation."
a(CHEBI:"acetylsalicylic acid") -> p(MESH:Proteins,pmod(Ac))
p(MESH:Proteins,pmod(Ac)) -| p(MESH:Proteins,pmod(Ph))
a(CHEBI:"acetylsalicylic acid") -| p(MESH:Proteins,pmod(Ph))

SET Evidence = "Aspirin may, as well, contribute with other beneficial
mechanisms, such as inhibition of NFκB and cyclooxygenase,
reducing the damaging impact of the inflammatory reparative
response."
a(CHEBI:"acetylsalicylic acid") -| p(HGNCGENEFAMILY:"NF-kappa B complex subunits")
a(CHEBI:"acetylsalicylic acid") -| p(MESH:"Prostaglandin-Endoperoxide Synthases")

SET Evidence = "A long list of potential HIF-1α
inhibitors is now available, such as digoxin, acriflavine,
doxorubicin, and chetomin [57–59]"
#443101 - acriflavine
a(CHEBI:digoxin) -| p(HGNC:HIF1A)
a(PUBCHEM:443101) -| p(HGNC:HIF1A)
a(CHEBI:doxorubicin) -| p(HGNC:HIF1A)
a(CHEBI:chetomin) -| p(HGNC:HIF1A)

SET Evidence = "The utilization of NFκB inhibitors, anti-inflammatory
drugs, and anti-inflammasome agents is aimed at modulating
and inhibiting chronic inflammation and ROS production
responsible for continuous cell damage, repair, and
fibrosis [31, 60]"
a(MESH:"Anti-Inflammatory Agents") neg path(NCIT:C82901)
a(MESH:"Anti-Inflammatory Agents") neg a(CHEBI:"reactive oxygen species")
complex(GO:"inflammasome complex") pos path(NCIT:C82901)
complex(GO:"inflammasome complex") pos a(CHEBI:"reactive oxygen species")

SET Evidence = "As a consequence,
natural nutrients [64] such as curcumin and in
particular vanillin, a degradation product of curcumin, have
been shown to reduce the formation of advanced glycation
end products (AGEs) [65]."
a(CHEBI:curcumin) -| p(MESH:"Glycation End Products, Advanced")
deg(a(CHEBI:curcumin)) -> a(CHEBI:vanillin)
a(CHEBI:vanillin) -| p(MESH:"Glycation End Products, Advanced")

SET Evidence = "Recently, physical exercise has been shown to be able
to prevent Alzheimer’s disease and substantially slow its
progression."
path(EFO:"physical activity") -| path(DOID:"Alzheimer's disease")

SET Evidence = "In particular, the release of survival factors, such as
BDGF (neurons), IGF-1 (systemic or organ-specific release),
and testosterone, and the activation of survival/repairing
pathways, such as sirtuin-dependent transcription factors,
are able to reduce apoptosis and to repair abnormal cell
components, rescuing sublethally damaged postmitotic cells,
such as neurons [66], myocardiocytes, skeletal muscle, and
hepatocytes [67]."
p(HGNC:BDNF) -| bp(GO:"programmed cell death")
p(HGNC:IGF1) -| bp(GO:"programmed cell death")
a(CHEBI:testosterone) -| bp(GO:"programmed cell death")
act(p(HGNC:SIRT1)) -| bp(GO:"programmed cell death")
