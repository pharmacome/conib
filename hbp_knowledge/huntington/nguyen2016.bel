#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Impaired TrkB Signaling Underlies Reduced BDNF-Mediated Trophic Support of Striatal Neurons in the R6/2 Mouse Model of Huntington's Disease"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Trusha Adeshara"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
DEFINE NAMESPACE MGI           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
DEFINE NAMESPACE RGD           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "27013968"}

##paper is comparing differences between WT and mHTT transgenic mice. To indicate this, mHTT is represented by Huntington Disease or p(HGNC:HTT,pmod(HBP:"CAG repeats")) in Statements.

SET Evidence = "Huntington’s disease (HD) is a progressive autosomal dominant disease characterized by early hyperkinetic movements and behavioral changes (Roze et al., 2011). HD patients carry an expanded CAG-repeat in exon 1 of the IT15/HTT gene that yields an elongated polyglutamine stretch in the N-terminal domain of the huntingtin (htt) protein (Bates, 2005)."

path(MESH:"Huntington Disease") -> path(HP:"Autosomal dominant inheritance")
path(MESH:"Huntington Disease") -> path(MESH:Hyperkinesis)
path(MESH:"Huntington Disease") -> path(HP:"Behavioral abnormality")
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -> path(MESH:"Huntington Disease")

SET Evidence = "A hallmark of HD is the early degeneration of medium spiny neurons (MSNs) in the striatum, which constitute >95% of the neuronal population in the striatum. MSNs are the principal projection neurons of the striatum, which is the initial relay nucleus of the basal ganglia network that processes cortical and thalamic glutamatergic and midbrain dopaminergic signals that regulate locomotor and motivational behavior (Tepper et al., 2007)."

SET Anatomy = "striatum"
SET Cell = "medium spiny neuron"
a(MESH:"Basal Ganglia") -> act(a(GO:"glutamatergic synapse",loc(MESH:Thalamus)))
act(a(GO:"glutamatergic synapse",loc(MESH:Thalamus))) -> bp(GO:"locomotory behavior")
SET Anatomy = "midbrain"
a(MESH:"Basal Ganglia") -> act(a(GO:"dopaminergic synapse"))
act(a(GO:"dopaminergic synapse")) -> bp(GO:"locomotory behavior")
UNSET Cell
UNSET Anatomy

SET Evidence = "In HD patients and animal models mutant huntingtin (mhtt) is expressed by a wide variety of neuronal populations with no clear preference for the vulnerable MSNs population (Trottier et al., 1995; Bhide et al., 1996)."

SET Cell = "neuron"
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -- path(MESH:"Huntington Disease")
UNSET Cell

SET Evidence = "Evidence in cellular and animal models of HD and in HD (Raymond et al., 2011; Francelle et al., 2014) indicate mhtt expression is associated with forebrain abnormalities in glutamatergic and dopaminergic transmission (Bibb et al., 2000; Cepeda et al., 2014), dysregulation of gene transcription (Neueder and Bates, 2014), altered striatal enriched proteins (e.g., PDE10A, STEP, Rhes; Subramaniam et al., 2009; Saavedra et al., 2011; Leuti et al., 2013; Mealer et al., 2014; Fusco and Giampà, 2015) and impaired neurotrophic support (e.g., BDNF-TrkB pathway; Zuccato et al., 2001; Wild and Tabrizi, 2014)."

SET Anatomy = "forebrain"
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -| bp(GO:"synaptic transmission, dopaminergic")
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -| bp(GO:"synaptic transmission, glutamatergic")
UNSET Anatomy
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -| bp(GO:"mRNA transcription")
SET Anatomy = "striatum"
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -- p(HGNC:PDE10A)
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -- p(HGNC:PTPN5)
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -- p(HGNC:RASD2)
UNSET Anatomy
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -- act(p(HGNC:BDNF), ma(GO:"brain-derived neurotrophic factor binding"))
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -- act(p(HGNC:NTRK2), ma(GO:"neurotrophin TRKB receptor binding"))

SET Evidence = "Importantly, since the survival and maintenance of MSNs are especially dependent on the BDNF-TrkB signaling pathway (Ivkovic and Ehrlich, 1999; Baydyuk et al., 2011), reduced neurotrophic support may underlie the early vulnerability of MSNs to degeneration in HD."
#BDNF-TRKB signaling pathway = complex(p(HGNC:BDNF),p(HGNC:NTRK2))
#TrkB = NTRK2
SET Cell = "medium spiny neuron"
complex(p(HGNC:BDNF),p(HGNC:NTRK2)) -> bp(MESH:"Cell Survival")
complex(p(HGNC:BDNF),p(HGNC:NTRK2)) -| path(MESH:"Huntington Disease")
UNSET Cell

SET Evidence = "Striatal MSNs express TrkB, the cognate receptor for brain derived neurotrophic factor (BDNF), but do not produce BDNF. They therefore rely on a supply from cortical, dopaminergic and thalamic afferents for neurotrophic maintenance of survival and phenotypic function (Altar et al., 1997; Sadikot et al., 2005)."

SET Anatomy = "striatum"
SET Cell = "medium spiny neuron"
g(HGNC:NTRK2) -> p(HGNC:NTRK2)
complex(p(HGNC:BDNF),p(HGNC:NTRK2))
UNSET Anatomy
UNSET Cell
SET MeSHAnatomy = {"Cerebrum","Dopaminergic Neurons","Thalamus"}
g(HGNC:BDNF) -> p(HGNC:BDNF)
UNSET MeSHAnatomy

SET Evidence = " Low BDNF protein levels are reported in postmortem striatal tissue from HD patients at symptomatic disease stages (Ferrer et al., 2000; Zuccato et al., 2008)"

SET Anatomy = "striatum"
path(MESH:"Huntington Disease") -| p(HGNC:BDNF)
UNSET Anatomy

SET Evidence = "Cellular models show that BDNF transcription and transport can be impaired by overexpression or knock-in of mhtt (Zuccato et al., 2001; Gauthier et al., 2004)."

p(HGNC:HTT, pmod(HBP:"CAG repeats")) -| r(HGNC:BDNF)
SET Anatomy = "striatum"
SET Cell = "medium spiny neuron"
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -| p(HGNC:BDNF)
UNSET Anatomy
UNSET Cell

SET Evidence = "Accordingly, in vivo deficits of striatal BDNF levels are shown to correlate with reduced cortical BDNF mRNA expression in some studies of Q175 knock-in and YAC72 transgenic HD mouse models (Zuccato et al., 2001; Ma et al., 2015)."

SET Species = "10090"
tloc(p(HGNC:BDNF),fromLoc(MESH:Cerebrum),toLoc(MESH:"Corpus Striatum"))
UNSET Species

SET Evidence = "Low striatal fl-TrkB protein levels are reported in postmortem tissue from HD patients at late disease stages (Ginés et al., 2006; Brito et al., 2013)."

path(MESH:"Huntington Disease") -| p(HGNC:NTRK2)

SET Evidence = "As an important caveat, observations at later stages may be confounded by loss of volume or number of striatal neurons as disease progresses (Stack et al., 2005; Samadi et al., 2013)."

SET Anatomy = "striatum"
path(MESH:"Huntington Disease") -> bp(GO:"neuron death")
UNSET Anatomy

SET Evidence = "On the other hand, normal TrkB levels observed at early stages may not necessarily reflect normal receptor function. For example, defects in transport and scaffolding proteins associated with TrkB signal transduction down the Erk1/2 pathway are demonstrated in mhtt knock-in STHdhQ111 cells and HdhQ111 striatal cultures independent of changes in TrkB expression (Ginés et al., 2010; Liot et al., 2013)."

path(MESH:"Huntington Disease") -| act(p(HGNC:NTRK2))
p(HGNC:HTT, pmod(HBP:"CAG repeats")) -| bp(GO:"ERK1 and ERK2 cascade")

SET Evidence = "Our assays on presymptomatic R6/2 mice indicated that striatal morphology and protein levels of BDNF and fl-TrkB are normal but levels of activated phospho-TrkB are reduced compared to WT mice."

SET Species = "10090"

path(MESH:"Huntington Disease") cnc p(HGNC:BDNF)
path(MESH:"Huntington Disease") cnc p(HGNC:NTRK2)
path(MESH:"Huntington Disease") -| p(HGNC:NTRK2,pmod(Ph))

SET Evidence = "We found that the normal trophic effects of BDNF on striatal neurons were attenuated in R6/2 cultures. Moreover, this was associated with impaired BDNF-mediated activation of phospho-TrkB despite normal expression of fl-TrkB in R6/2 cultures."

SET Anatomy = "striatum"
path(MESH:"Huntington Disease") -| act(p(HGNC:BDNF))
UNSET Anatomy
p(HGNC:BDNF) -> act(p(HGNC:NTRK2,pmod(Ph)))
complex(p(HGNC:BDNF),p(HGNC:NTRK2)) -> p(HGNC:NTRK2,pmod(Ph))

SET Evidence = "Finally, downstream activation of Erk1/2 was also impaired in R6/2 cultures."

path(MESH:"Huntington Disease")  -| bp(GO:"ERK1 and ERK2 cascade")

SET Evidence = "The estimated total number of neurons in the striatum was similar in WT and R6/2 mice (1,144,207 ± 55,809 vs. 1,333,955 ± 55,522 cells, respectively; p > 0.05)."
#R6/2 mice are transgenic mice with human mutant HTT transgene with 120-130 CAG repeats
#R6/2 young mice
SET Anatomy = "striatum"

path(MESH:"Huntington Disease") cnc a(MESH:Neurons)

SET Evidence = "Striatal BDNF and fl-TrkB levels were found to be similar in R6/2 and WT mice (Figures 1C,D)."
#R6/2 young mice
path(MESH:"Huntington Disease") cnc p(HGNC:BDNF)
path(MESH:"Huntington Disease") cnc p(HGNC:NTRK2)

SET Evidence = "However, levels of activated phospho-TrkB were lower in R6/2 compared to WT mice (Figures 1C,D)."
#R6/2 young mice
path(MESH:"Huntington Disease") -| p(HGNC:NTRK2,pmod(Ph))

SET Evidence = "Since activation of phospho-TrkB signaling regulates expression of the dopamine and cAMP regulated neuronal phosphoprotein 32 kDa (DARPP-32; an MSN-specific protein; Ivkovic and Ehrlich, 1999), we also found that striatal DARPP-32 levels were low in young R6/2 mice (Figures 1C,D)."

act(p(HGNC:NTRK2,pmod(Ph))) reg a(CHEBI:dopamine)

SET Cell = "medium spiny neuron"
act(p(HGNC:NTRK2,pmod(Ph))) reg a(CHEBI:"2',3'-cyclic AMP")
a(CHEBI:"2',3'-cyclic AMP") reg p(PFAM:"DARPP-32")
UNSET Cell

#R6/2 young mice
path(MESH:"Huntington Disease") -| p(PFAM:"DARPP-32")

SET Evidence = "Counts of DAPI+ staining, a DNA label that identifies all cell nuclei in culture, revealed that total cell density was similar in WT and R6/2 cultures (211 ± 22 and 208 ± 14 cells/mm2, respectively; Figures 2A,B) after 7 DIV in basal media. Concurrently, MAP2+ staining (microtubule associated protein 2; a neuron-specific cytoskeletal protein) indicated a similar sized population of neuronal cells in WT and R6/2 cultures (100 ± 14 and 103 ± 7 cells/mm2, respectively; Figures 2A,B)."

#R6/2 cell culture
path(MESH:"Huntington Disease") cnc bp(GO:"maintenance of cell number")
SET Cell = "neuron"
path(MESH:"Huntington Disease") cnc bp(GO:"maintenance of cell number")
UNSET Cell

SET Evidence = "Furthermore, DARPP-32+ staining indicated a similar sized population of MSN-like cells in WT and R6/2 cultures after 7 DIV in basal media (40 ± 8 and 40 ± 5 cells/mm2, respectively; Figures 2A,B)."

#R6/2 cell culture
SET Cell = "medium spiny neuron"
path(MESH:"Huntington Disease") cnc bp(GO:"maintenance of cell number")
UNSET Cell

SET Evidence = "For example, striatal cultures exposed to BDNF (10–100 ng/mL; 7 DIV) exhibit greater neuronal survival and MSN phenotype (e.g., DARPP-32 expression) compared to untreated cultures (Mizuno et al., 1994; Nakao et al., 1995; Ventimiglia et al., 1995)."

#R6/2 cell culture
SET Cell = "neuron"
p(HGNC:BDNF) -> bp(MESH:"Cell Survival")
UNSET Cell

SET Cell = "medium spiny neuron"
p(HGNC:BDNF) -> p(PFAM:"DARPP-32")
UNSET Cell

SET Evidence = "There were significant main effects of both BDNF dose and genotype on the population of neurons in culture (Figures 3A,B; ANOVA: dose- F(3,167), p < 0.01); genotype- F(1,167), p < 0.01)."

#R6/2 cell culture
SET Cell = "neuron"
composite(p(HGNC:BDNF),p(HGNC:HTT, pmod(HBP:"CAG repeats"))) -> bp(GO:"maintenance of cell number")
UNSET Cell

SET Evidence = "Striatal neurons in R6/2 cultures were therefore resistant to BDNF survival effects compared to neurons in WT cultures."

p(HGNC:BDNF) -> bp(MESH:"Cell Survival")
#R6/2 cell culture
SET Cell = "neuron"
composite(p(HGNC:BDNF),p(HGNC:HTT, pmod(HBP:"CAG repeats"))) -| bp(MESH:"Cell Survival")
UNSET Cell

SET Evidence = "BDNF is therefore less effective in rescuing cells from apoptosis in R6/2 cultures compared to WT cultures."

p(HGNC:BDNF) -| bp(MESH:Apoptosis)
#R6/2 cell culture
composite(p(HGNC:BDNF),p(HGNC:HTT, pmod(HBP:"CAG repeats"))) -> bp(MESH:Apoptosis)

SET Evidence = "BDNF-TrkB signaling is well known to promote expression of DARPP-32 (Nakao et al., 1995; Ivkovic et al., 1997), a striatal-enriched protein essential for proper function of striatal MSNs (Bibb et al., 2000)."

complex(p(HGNC:BDNF),p(HGNC:NTRK2)) -> p(PFAM:"DARPP-32")

SET Cell = "medium spiny neuron"
p(PFAM:"DARPP-32") -> bp(GO:"cellular process")
UNSET Cell

SET Evidence = "These results indicate that R6/2 striatal cultures are less sensitive to BDNF-stimulated expression of DARPP-32."

p(HGNC:BDNF) -> p(PFAM:"DARPP-32")
#R6/2 cell culture
composite(p(HGNC:BDNF),p(HGNC:HTT, pmod(HBP:"CAG repeats"))) -| p(PFAM:"DARPP-32")

SET Evidence = "Exposure to BDNF elevated phospho-TrkB levels over control conditions in both WT and R6/2 cultures (Figures 4A,D). Importantly, BDNF yielded higher phospho-TrkB levels in WT compared to R6/2 cultures, indicating that BDNF-induced activation of TrkB is blunted in R6/2 cultures."

p(HGNC:BDNF) -> p(HGNC:NTRK2,pmod(Ph))
#R6/2 cell culture
composite(p(HGNC:BDNF),p(HGNC:HTT, pmod(HBP:"CAG repeats"))) -| p(HGNC:NTRK2,pmod(Ph))

SET Evidence = "In control conditions total Erk1/2 and phospho-Erk1/2 levels were similar in WT and R6/2 cultures (Figures 4A,C). Acute exposure to BDNF increased phospho-Erk1/2 levels over control conditions in both WT and R6/2 cultures (Figures 4A,E)."

p(HGNC:HTT, pmod(HBP:"CAG repeats")) cnc p(HGNC:MAPK3)
p(HGNC:HTT, pmod(HBP:"CAG repeats")) cnc p(HGNC:MAPK1)
p(HGNC:HTT, pmod(HBP:"CAG repeats")) cnc p(HGNC:MAPK3,pmod(Ph))
p(HGNC:HTT, pmod(HBP:"CAG repeats")) cnc p(HGNC:MAPK1,pmod(Ph))
composite(p(HGNC:BDNF),p(HGNC:HTT, pmod(HBP:"CAG repeats"))) -> p(HGNC:MAPK1,pmod(Ph))
composite(p(HGNC:BDNF),p(HGNC:HTT, pmod(HBP:"CAG repeats"))) -> p(HGNC:MAPK3,pmod(Ph))

SET Evidence = "However, BDNF yielded higher phospho-Erk1/2 levels in WT compared to R6/2 cultures, indicating that TrkB signal transduction down the Erk1/2 pathway is attenuated in R6/2 cultures."

act(p(HGNC:NTRK2)) -> bp(GO:"ERK1 and ERK2 cascade")
#R6/2 cell culture
path(MESH:"Huntington Disease") -| act(p(HGNC:NTRK2))
path(MESH:"Huntington Disease") -| bp(GO:"ERK1 and ERK2 cascade")

UNSET Species
UNSET Anatomy
