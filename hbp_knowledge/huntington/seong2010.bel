#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Huntingtin facilitates polycomb repressive complex 2"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
# DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
 DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
 DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
 DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
 DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
 DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
 DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
 DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
 DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
 DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
 DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
 DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
 DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
 DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
 DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
 DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
 DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
 DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
 DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "19933700"}

SET MeSHAnatomy = "Embryoid Bodies"

SET Evidence = "PR65/A, for example, facilitates diverse phosphatase holoenzymes
involved in cell signaling and metabolism (16), while
importin-beta serves distinct nuclear transport complexes that
engage a variety of cargos (17,18)."
#KPNB1 : Importins beta
p(HGNC:"PPP2R1A") -- bp(GO:"cell-cell signaling")
p(HGNC:"PPP2R1A") -- path(MESH:"Metabolism")
p(HGNC:"KPNB1") -- bp(GO:"nuclear transport")

SET Evidence = "Consistent with a flexible segmental a-helical domain
organization, limited tryptic digestion of the recombinant
protein readily yielded two large domains (approx 150 kDa and approx 200 kDa fragments) (Fig. 1B) and mass spectrometry
(Supplementary Material, Tables S1 and S2) located a major
accessible hinge region between residues 1184–1254."
#appox 150kDa seqment : 1-1184aa
#approx. 200kDa seqment : 1254 - end
a(CHEBI:"Trypsin") -> complex(p(HGNC:"HTT", frag("1_1184")), p(HGNC:"HTT", frag("1254_*")))

SET Evidence = "Though Hdhex4/5 homozygote embryos at embryonic stage
E7.5 have previously been shown to properly localize stage  appropriate
markers such as Otx2, Hnf3b and Hesx1 (29),
the results of whole mount in situ hybridization, in
Figure 2A, revealed that huntingtin null embryos failed to
properly repress PRC2 regulated Hox gene expression, as evidenced
by ectopic Hoxb1, Hoxb2 and Hoxb9 mRNA"
#Huntingtin null cells : Hdh ex4/5
p(HGNC:"HTT") -| r(HGNC:"HOXB1")
p(HGNC:"HTT") -| r(HGNC:"HOXB2")
p(HGNC:"HTT") -| r(HGNC:"HOXB9")
p(HGNCGENEFAMILY:"Polycomb repressive complex 2") reg r(HGNC:"HOXB1")
p(HGNCGENEFAMILY:"Polycomb repressive complex 2") reg r(HGNC:"HOXB2")
p(HGNCGENEFAMILY:"Polycomb repressive complex 2") reg r(HGNC:"HOXB9")

SET Evidence = "Moreover,
female huntingtin null embryos exhibited decreased
differentiation of trophoblast giant cells, marked by PL-1
mRNA expression, which normally requires proper obligatory
silencing of the paternally inherited X chromosome."
#PL-1 : 2350
SET Gender = "female"
SET Cell = "trophoblast giant cell"
p(HGNC:"HTT") -> path(MESH:"Cell Differentiation")
path(MESH:"Cell Differentiation") -> r(NCBIGENE:"2350")
r(NCBIGENE:"2350") -> path(MESH:"X Chromosome Inactivation")
UNSET Cell
UNSET Gender

SET Evidence = "Investigation of histone H3K27 methylation in Hdhex4/5 null
EBs, developing in cell culture from embryonic stem cells,
demonstrated that huntingtin was required for efficient
re-establishment of global tri-methylated histone H3K27."
#Hdhex4/5 null
SET MeSHAnatomy = "Embryonic Stem Cells"
p(HGNC:"HTT") -- p(HBP:"Histone H3", pmod(Me3, Lys, 27))
UNSET MeSHAnatomy

SET Evidence = "Notably, global levels of di-methylated histone
H3K27 were not altered in huntingtin null EBs (Supplementary
Material, Fig. S3B), indicating that lack of huntingtin
specifically affected H3K27 tri-methylation."
p(HGNC:"HTT") cnc p(HBP:"Histone H3", pmod(Me2, Lys, 27))

SET Evidence = "Furthermore, the
results of analysis of nuclear extracts by gel filtration chromatography
demonstrated that full-length huntingtin was
co-eluted with PRC2 subunits Ezh2 and Suz12 (Supplementary
Material, Fig. S3D)."
p(HGNC:"HTT") -> complex(p(HGNC:"HTT"), p(HGNC:"EZH2"), p(HGNC:"SUZ12"))

SET Evidence = "As shown in the immunoblot in Figure 4A, full-length
FLAG-tag Q23 huntingtin added to recombinant PRC2 was
co-immunoprecipitated with Ezh2 and Suz12, and, as illustrated
in Figure 4B, the recombinant protein significantly increased
PRC2-specific histone H3K27 methylation, as judged by the
intensity of bands of incorporated tritium."
#Q23 htt
#tri-methylation of H3K27 enhanced
p(HGNCGENEFAMILY:"Polycomb repressive complex 2", var("?")) ->  p(HBP:"Histone H3", pmod(Me3, Lys, 27))

UNSET MeSHAnatomy
