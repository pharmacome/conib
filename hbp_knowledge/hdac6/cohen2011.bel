#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "The acetylation of tau inhibits its function and promotes pathological tau aggregation."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
# DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
# DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
# DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
# DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
# DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
 DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "21427723"}

#in tau Tg mouse
SET Species = "10090"

#tau-K18 : fragment of Tau compassing four MT-binding repeats
#tau-T40 :Full lenght recombinant tau

SET Evidence = "Although tau-K18 and
tau-T40 displayed low-level auto-acetylation frequently seen with high-affinity substrates,
CBP enhanced the acetylation of both full-length T40 and the K18 fragment, suggesting
acetylated lysine residues lie within the MT-binding region (Fig. 1a)."
SET Confidence = "High"
p(MGI:Mapt) -| p(MGI:Mapt,pmod(Ac))
p(MGI:Mapt,frag("?")) -| p(MGI:Mapt,pmod(Ac,Lys))
p(MGI:Crebbp) -> p(MGI:Mapt,pmod(Ac))
p(MGI:Crebbp) -> p(MGI:Mapt,frag("?"),pmod(Ac,Lys))
UNSET Confidence

SET Evidence = "Tau-T40 was
fibrillized in vitro to generate tau fibrils in which the MT-binding region forms the fibril
core. These tau fibrils could not be acetylated indicating lysine residues are not accessible to
acetylation in fibrillized tau."
SET Confidence = "High"
p(MGI:Mapt) -> p(CONSO:"Tau fibrils")
p(CONSO:"Tau fibrils") -| p(MGI:Mapt,pmod(Ac))
UNSET Confidence

SET Evidence = "As expected, native
K18 promoted tubulin assembly into MTs as determined by absorbance at 350 nm. In
contrast, acetylated K18 failed to promote MT assembly, similar to the negative control K18
containing the K311D mutation, previously shown to impair tau–MT interactions [14,15]."
SET Confidence = "High"
p(MGI:Mapt,frag("?")) pos bp(GO:"tubulin complex assembly")
p(MGI:Mapt,frag("?"),pmod(Ac)) neg bp(GO:"tubulin complex assembly")
p(MGI:Mapt,frag("?"),var("p.Lys311Asp")) neg bp(GO:"tubulin complex assembly")
p(MGI:Mapt,frag("?"),var("p.Lys311Asp")) -| act(p(MGI:Mapt),ma(GO:"microtubule binding"))
UNSET Confidence

SET Evidence = "Using a heparin-induced tau fibrillization assay quantified by
Thioflavin-T (ThT) binding, native K18 did not fibrillize during the 4-h incubation period,
whereas acetylated K18 fibrillized rapidly by 3 h indicating acetylation enhanced the K18
fibrillization rate (Fig. 1d).Similar enhancement of tau-T40 aggregation was also observed
(Supplementary Fig. S1)."
SET Confidence = "High"
p(MGI:Mapt,frag("?")) -| p(CONSO:"Tau fibrils")
p(MGI:Mapt,frag("?"),pmod(Ac)) -> p(CONSO:"Tau fibrils")
p(MGI:Mapt) -| p(CONSO:"Tau fibrils")
p(MGI:Mapt,pmod(Ac)) -> p(CONSO:"Tau fibrils")
UNSET Confidence

SET Evidence = "As expected, the negative
control K18-K311D mutant did not fibrillize using both ThT and sedimentation assays."
SET Confidence = "High"
p(MGI:Mapt,frag("?"),var("p.Lys311Asp")) -| p(CONSO:"Tau fibrils")
UNSET Confidence

#in HEK-293 cells
SET CellLine = "HEK293"

SET Evidence = "Although low-level tau
acetylation was observed in untreated HEK-T40 cells, treatment with the pan histone
deacetylase (HDAC) inhibitor trichostatin A (TSA), but not the Sir2 class inhibitor
nicotinamide [20], resulted in a dramatic increase in acetylated tau levels."
#HEK-T40 : HEK-293 cell expressing full lenght tau-T40
SET Confidence = "High"
a(CHEBI:"trichostatin A") -| p(MESH:"Histone Deacetylases")
a(CHEBI:"trichostatin A") -> p(MGI:Mapt,pmod(Ac))
a(CHEBI:nicotinamide) -| p(MGI:Sirt1)
a(CHEBI:nicotinamide) -| p(MGI:Mapt,pmod(Ac))
UNSET Confidence

SET Evidence = "CBP transfection alone promoted detectable tau acetylation, however, CBP expression in
combination with TSA treatment led to a dramatic accumulation of acetylated tau."
SET Confidence = "High"
p(MGI:Crebbp) -> p(MGI:Mapt,pmod(Ac))
composite(p(MGI:Crebbp),a(CHEBI:"trichostatin A")) -> p(MGI:Mapt,pmod(Ac))
UNSET Confidence

SET Evidence = "Notably,
acetylated tau can be modified by HDAC activity as co-expression of CBP with wild-type
(WT) HDAC6, but not a catalytically dead HDAC6 mutant, resulted in tau deacetylation
(Fig. 2c)."
#the variant doesnt cause deacetylation while the wild type does
SET Confidence = "Medium"
composite(p(MGI:Crebbp),p(MGI:Hdac6)) -- p(MGI:Mapt,pmod(Ac))
composite(p(MGI:Crebbp),p(MGI:Hdac6,var("?"))) -| p(MGI:Mapt,pmod(Ac))
UNSET Confidence

SET Evidence = "Significant acetylation was detected
on the double lysine K280/K281 within the peptide V–Q–I–I–N–K–K in the second MT-binding
repeat as well as K163 and K369 within the proline-rich region and MT-binding
region, respectively (see Table 1)."
#in CBP-transfected HEK-T40 cells
SET Confidence = "High"
p(MGI:Crebbp) -> p(MGI:Mapt,pmod(Ac,Lys,280),pmod(Ac,Lys,281))
p(MGI:Crebbp) -> p(MGI:Mapt,pmod(Ac,Lys,163))
p(MGI:Crebbp) -> p(MGI:Mapt,pmod(Ac,Lys,396))
UNSET Confidence

SET Evidence = "Western analysis of lysine -> arginine mutants
demonstrated that mutation of all four lysine residues (tau-4KR) dramatically reduced tau
acetylation in cells, indicating that K163/280/281/369 represent the major sites of tau
acetylation (Fig. 2d)."
SET Confidence = "High"
p(MGI:Mapt,var("p.Lys163Arg"),var("p.Lys280Arg"),var("p.Lys281Arg"),var("p.Lys369Arg")) -| p(MGI:Mapt,pmod(Ac))
UNSET Confidence

SET Evidence = "Consistent with previous reports [21],
WT tau overexpression stabilized MTs and promotes MT bundling, as detected by
immunofluorescence (IF) using an anti-acetyl-tubulin antibody (Fig. 2e–g)."
SET Confidence = "High"
act(p(MGI:Mapt)) -> a(MESH:Microtubules)
act(p(MGI:Mapt)) pos bp(GO:"microtubule bundle formation")
UNSET Confidence

SET Evidence = "However,
expression of a tau-4KQ acetylation mimetic (K163/280/281/369Q), but not a tau-4KR nonmimetic
mutant, showed a dramatic reduction in MT bundling activity (WT ~55% versus
4KQ ~7% versus 4KR ~50% bundling), consistent with loss of tau function induced by
acetylation (Fig. 2h–j)."
SET Confidence = "High"
p(MGI:Mapt,var("p.Lys163Arg"),var("p.Lys280Arg"),var("p.Lys281Arg"),var("p.Lys369Arg")) -| act(p(MGI:Mapt))
p(MGI:Mapt,var("p.Lys163Arg"),var("p.Lys280Arg"),var("p.Lys281Arg"),var("p.Lys369Arg")) neg bp(GO:"microtubule bundle formation")
UNSET Confidence

SET Evidence = "Interestingly, MT bundling observed with the single K280Q mimetic
was significantly reduced (~40% bundling), indicating acetylation of a single-lysine residue,
that is, K280, is critical in regulating tau function (Fig. 2k)."
SET Confidence = "High"
p(MGI:Mapt,var("p.Lys280Arg")) -| act(p(MGI:Mapt))
p(MGI:Mapt,var("p.Lys280Arg")) neg bp(GO:"microtubule bundle formation")
p(MGI:Mapt,pmod(Ac,Lys,280)) reg act(p(MGI:Mapt))
UNSET Confidence

UNSET CellLine

SET MeSHAnatomy = "Brain"

SET Evidence = "Genetic studies have shown that deletion of K280 (ΔK280) is associated with FTDP-17
familial tauopathy [11]"
SET Confidence = "High"
p(MGI:Mapt,var("p.Lys280del")) -- path(DOID:"frontotemporal dementia")
p(MGI:Mapt,var("p.Lys280del")) -- path(DOID:tauopathy)
UNSET Confidence

SET Evidence = "Although the disease mechanism associated with ΔK280 is not fully
understood, in vitro analysis of ΔK280 demonstrated impaired MT assembly [22] and
enhanced PHF aggregation [19,23]"
SET Confidence = "High"
p(MGI:Mapt,var("p.Lys280del")) neg bp(GO:"microtubule polymerization")
p(MGI:Mapt,var("p.Lys280del")) -> p(CONSO:"paired helical filaments")
UNSET Confidence

SET Evidence = "Thus, K280 acetylation functionally mimics ΔK280,
potentially by inhibition of electrostatic tau–MT interactions."
SET Confidence = "High"
p(MGI:Mapt,pmod(Ac)) -- p(MGI:Mapt,var("p.Lys280del"))
p(MGI:Mapt,pmod(Ac)) -| complex(p(MGI:Mapt),a(MESH:Microtubules))
UNSET Confidence

SET Evidence = "Moreover, expression of an HDAC6 small interfering RNA
led to increased levels of ac-K280, further confirming the specificity of the anti-ac-K280
antibody and providing additional evidence that HDAC6 is a tau deacetylase
(Supplementary Fig. S3)."
SET Confidence = "High"
p(MGI:Hdac6) -> p(MGI:Mapt,pmod(Ac,Lys,280))
UNSET Confidence
#in PS19 cells with P301S mutant tau and PDAPP exprwssing APP V717F mutant

SET Evidence = "Monogenic PS19 tau Tg mice revealed moderate
neuronal ac-K280 immunoreactivity in both cortex and hippocampus when compared with
non-Tg mice, although abundant hyperphosphorylated tau was detected by AT8, a phosphotau
specific antibody (Fig. 3a–d)."
#monogenic PS19 - P301S muatnt tau
SET Confidence = "High"
SET MeSHAnatomy = {"Hippocampus","Cerebral Cortex"}
p(MGI:Mapt,var("p.Pro301Ser")) -> p(MGI:Mapt,pmod(CONSO:hyperphosphorylation))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "However, bigenic PS19;PDAPP mice showed profound
age-dependent increase in ac-K280 tau pathology (see Supplementary Fig. S4), particularly
in the hippocampus, in which the intensity of ac-K280 staining was increased relative to
monogenic PS19 mice (Fig. 3e,f)."
SET Confidence = "High"
SET MeSHAnatomy = "Hippocampus"
p(MGI:App,var("p.Val717Phe")) -> p(MGI:Mapt,pmod(Ac,Lys,280))
bp(GO:aging) pos p(MGI:Mapt,pmod(Ac,Lys,280))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Remarkably, anti-ac-K280-positive NFTs and neuropil threads were
prominently observed in AD cortex and resemble pathology detected by PHF-1 (Fig. 4a–b)."
SET Confidence = "High"
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Cerebral Cortex"
p(GO:"neurofibrillary tangle") -> p(MGI:Mapt,pmod(Ac,Lys,280))
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "CBD has extensive grey and white matter tau pathology of neuronal and glial tau inclusions,
and anti-ac-K280 immunostained these tau pathologies similar to PHF-1 (Fig. 4c–d)."
#CBD - Corticobasal degenration
SET Confidence = "High"
SET MeSHAnatomy = {"Gray Matter","White Matter","Neurons"}
SET Cell = "glial cell"
path(CONSO:"Corticobasal Degeneration") -> p(MGI:Mapt,pmod(Ac,Lys,280))
UNSET Cell
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Indeed, anti-ac-K280- positive tau
aggregates were observed in all other tauopathies examined including progressive
supranuclear palsy, argyrophilic grain disease, tangle predominant senile dementia and
Guam parkinsonism–dementia complex (Supplementary Fig. S5)."
SET Confidence = "High"
path(DOID:"progressive supranuclear palsy") -> p(MGI:Mapt,pmod(Ac,Lys,280))
path(DOID:dementia) -> p(MGI:Mapt,pmod(Ac,Lys,280))
path(DOID:"amyotrophic lateral sclerosis") -> p(MGI:Mapt,pmod(Ac,Lys,280))
UNSET Confidence

SET Evidence = "Thus, K280 acetylation is
a feature found in a variety of human 4R or 3R/4R tauopathies including AD, but not 3R tauopathies
such as PiD."
SET Confidence = "High"
path(CONSO:"4R tauopathy") -- path(DOID:"Alzheimer's disease")
path(CONSO:"4R tauopathy") pos p(MGI:Mapt,pmod(Ac,Lys,280))
complex(p(CONSO:"3R tau"),p(CONSO:"4R tau")) -- path(DOID:"Alzheimer's disease")
complex(p(CONSO:"3R tau"),p(CONSO:"4R tau")) pos p(MGI:Mapt,pmod(Ac,Lys,280))
p(CONSO:"3R tau") -- path(DOID:"Niemann-Pick disease")
p(CONSO:"3R tau") cnc p(MGI:Mapt,pmod(Ac,Lys,280))
UNSET Confidence

UNSET Species
