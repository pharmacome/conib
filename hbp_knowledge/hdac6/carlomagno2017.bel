#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "An acetylation-phosphorylation switch that regulates tau aggregation propensity and function."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
# DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
# DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
# DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
# DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
# DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
# DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
 DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
# DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "28760828"}

SET Evidence = "By immunoblot, we confirmed that
robust deacetylation of tau was observed in the presence of
HDAC6, which was prevented upon the addition of ACY-738
(supplemental Fig. S1)."
#in 4R0N isoform
SET Confidence = "High"
p(HGNC:HDAC6) -| p(HGNC:MAPT,pmod(Ac))
a(PUBCHEM:57381425) -| act(p(HGNC:HDAC6))
a(PUBCHEM:57381425) -> p(HGNC:MAPT,pmod(Ac))
UNSET Confidence

SET Evidence = "Of note, all of tau’s KXGS motifs were
responsive to HDAC6 in vitro with the exception of Lys-290 in
the second microtubule binding repeat domain, which could
not be determined, given that p300 did not acetylate this site
under the current conditions."
SET Confidence = "High"
p(HGNC:HDAC6) cnc p(HGNC:MAPT,pmod(Ac,Lys,290))
p(HGNC:EP300) cnc p(HGNC:MAPT,pmod(Ac,Lys,290))
UNSET Confidence

SET Evidence = "Nonetheless, based on our findings that
p300 and HDAC6 coordinately modulate the acetylation of residues
that determine tau aggregation propensity (7), we focused
all subsequent analyses on tau acetylation sites that are responsive
to both p300 and HDAC6 to identify the residues critical
for tau assembly."
SET Confidence = "High"
act(p(HGNC:EP300)) -- p(CONSO:"Tau aggregates")
act(p(HGNC:HDAC6)) -- p(CONSO:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac)) -- p(CONSO:"Tau aggregates")
UNSET Confidence

SET Evidence = "As observed previously, mimicking
acetylation on all four of tau’s KXGS motifs (K259Q/K290Q/
K321Q/K353Q (4KQ)) significantly impaired tau polymerization
(Fig. 1a)."
SET Confidence = "High"
p(HGNC:MAPT,var("p.Lys259Gln"),pmod(Ac,Gln,259),var("p.Lys290Gln"),pmod(Ac,Gln,290),var("p.Lys321Gln"),pmod(Ac,Gln,321),var("p.Lys352Gln"),pmod(Ac,Gln,353)) neg p(CONSO:"Tau aggregates")
UNSET Confidence

SET Evidence = "Given that K290Q/K321Q (2KQ) also decreased
tau aggregation, acetylation of tau’s internal KCGS motifs
appears to be most critical to inhibit tau filament assembly."
SET Confidence = "High"
p(HGNC:MAPT,var("p.Lys290Gln"),pmod(Ac,Gln,290),var("p.Lys321Gln"),pmod(Ac,Gln,321)) -| p(CONSO:"Tau aggregates")
UNSET Confidence

SET Evidence = "To
further assess the extent to which acetylation at each KXGS
motif impacts tau assembly, we generated mutant proteins containing
a single mutation, revealing that K321Q dramatically
inhibits tau filament formation (Fig. 1a)."
SET Confidence = "High"
p(HGNC:MAPT,var("p.Lys321Gln"),pmod(Ac,Gln,321)) -| p(CONSO:"Tau fibrils")
UNSET Confidence

SET Evidence = "Of note, whereas mimicking
acetylation at most non-KXGS sites had minimal impact
on tau aggregation, K274Q also significantly decreased tau
assembly in vitro, albeit to a lesser extent than K321Q (Fig. 1b)."
SET Confidence = "High"
p(HGNC:MAPT,var("p.Lys163Gln"),pmod(Ac,Gln,163)) cnc p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys174Gln"),pmod(Ac,Gln,174)) cnc p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys180Gln"),pmod(Ac,Gln,180)) cnc p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys234Gln"),pmod(Ac,Gln,234)) cnc p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys240Gln"),pmod(Ac,Gln,240)) cnc p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys274Gln"),pmod(Ac,Gln,274)) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys280Gln"),pmod(Ac,Gln,280)) cnc p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys311Gln"),pmod(Ac,Gln,311)) cnc p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys369Gln"),pmod(Ac,Gln,369)) cnc p(CONSO:"Tau aggregates")
UNSET Confidence

SET Evidence = "Whereas mutating Lys-321 to arginine to maintain the charge
but prevent acetylation has no significant impact on filament
formation compared with wild-type tau (Fig. 2 (a, c, and d–f)
and supplemental Fig. S2 (a and b)), K321Q dramatically alters
tau filament assembly and aggregation (Fig. 2 (a and b and d–f)
and supplemental Fig. S2 (a and b))."
SET Confidence = "High"
p(HGNC:MAPT,var("p.Lys321Arg")) -| p(HGNC:MAPT,pmod(Ac))
p(HGNC:MAPT,var("p.Lys321Arg")) cnc p(CONSO:"Tau fibrils")
p(HGNC:MAPT) cnc p(CONSO:"Tau fibrils")
p(HGNC:MAPT,var("p.Lys321Gln")) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT,var("p.Lys321Gln")) -| p(CONSO:"Tau fibrils")
UNSET Confidence

#in HEK293T cells
SET CellLine = "HEK293T"

SET Evidence = "As anticipated,WT tau was phosphorylated at both
Ser-324 (pSer-324) and Ser-262/356 (12E8) when co-expressed
with MARK2 (Fig. 3b)."
SET Confidence = "High"
composite(p(HGNC:MAPT),p(HGNC:MARK2)) -> p(HGNC:MAPT,pmod(Ph,Ser,324))
composite(p(HGNC:MAPT),p(HGNC:MARK2)) -> p(HGNC:MAPT,pmod(Ph,Ser,262),pmod(Ph,Ser,356))
UNSET Confidence

SET Evidence = "However, the pseudoacetylated K321Q
mutant was resistant to MARK2-mediated phosphorylation at
Ser-324, whereas phosphorylation at the 12E8 epitope was
unaffected (Fig. 3b)."
SET Confidence = "High"
composite(p(HGNC:MAPT,var("p.Lys321Gln"),pmod(Ac)),p(HGNC:MARK2)) -| p(HGNC:MAPT,pmod(Ph,Ser,324))
composite(p(HGNC:MAPT,var("p.Lys321Gln"),pmod(Ac)),p(HGNC:MARK2)) cnc p(HGNC:MAPT,pmod(Ph,Ser,262),pmod(Ph,Ser,356))
UNSET Confidence

SET Evidence = "These findings indicate that similar to
KIGS motifs, the ability of MARK2 to phosphorylate KCGS
motifs is regulated by acetylation."
SET Confidence = "High"
p(HGNC:MAPT,pmod(Ac)) reg act(p(HGNC:MARK2))
UNSET Confidence

SET Evidence  = "Whereas both WT
and S324A tau were capable of supporting the polymerization
of microtubules, the pseudophosphorylated S324D and S324E
mutants exhibited an impaired capacity to drive microtubule
assembly (Fig. 3c)."
SET Confidence = "High"
p(HGNC:MAPT) -> bp(GO:"microtubule polymerization")
p(HGNC:MAPT,var("p.Ser324Ala")) -> bp(GO:"microtubule polymerization")
p(HGNC:MAPT,var("p.Ser324Asp"),pmod(Ph)) -| bp(GO:"microtubule polymerization")
p(HGNC:MAPT,var("p.Ser324Glu"),pmod(Ph)) -| bp(GO:"microtubule polymerization")
UNSET Confidence

SET Evidence = "Given that phosphorylation of Ser-262 in
tau’s first KIGS motif has also been shown to impair tau’s ability
to promote microtubule assembly (11), the phosphorylation
status of all KXGS motifs may act to regulate tau function."
SET Confidence = "High"
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| bp(GO:"microtubule polymerization")
p(HGNC:MAPT,pmod(Ph),loc(CONSO:"KXGS motif")) reg act(p(HGNC:MAPT))
UNSET Confidence

UNSET CellLine

SET Species = "10090"
#expressing human tau

SET Evidence = "Whereas deposition of tau species phosphorylated on KIGS
motifs (pSer-262/356) increases with age in the well-characterized
rTg4510 mouse model (7, 12)."
SET Confidence = "High"
bp(GO:aging) -> p(HGNC:MAPT,pmod(Ph,Ser,262),pmod(Ph,Ser,356))
UNSET Confidence

SET Evidence = "At 6 months of age, mice injected with human
mutant P301L tau-AAV recapitulate several behavioral and
neuropathological hallmarks of tauopathy, including the accumulation
of hyperphosphorylated and abnormally folded, protease-
resistant tau species (13)."
#age - 6 months
SET Confidence = "High"
p(HGNC:MAPT,var("p.Pro301Leu")) -> p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))
p(HGNC:MAPT,var("p.Pro301Leu")) -| deg(p(HGNC:MAPT))
UNSET Confidence

SET Evidence = "At this same time point, we also
detect abundant pSer-324 deposition in both the cortex and
CA1, brain regions in which high levels of human tau expression
are observed in this model (Fig. 4, a–f)."
SET Confidence = "High"
SET MeSHAnatomy = {"Cerebral Cortex","CA1 Region, Hippocampal","Brain"}
p(HGNC:MAPT,var("p.Pro301Leu")) -> p(HGNC:MAPT,pmod(Ph,Ser,324))
UNSET MeSHAnatomy
UNSET Confidence
UNSET Species

SET Evidence = "Similar
to the 12E8 epitope (pSer-262/356) (7), pSer-324 is detected
biochemically in patients with AD but is absent from controls
(Fig. 5a and supplemental Fig. S5)."
SET Confidence = "High"
SET MeSHAnatomy = "Brain"
path(DOID:"Alzheimer's disease") -> p(HGNC:MAPT,pmod(Ph,Ser,262),pmod(Ph,Ser,356))
path(DOID:"Alzheimer's disease") -> p(HGNC:MAPT,pmod(Ph,Ser,324))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Consistent with previous studies (7, 15, 16), HDAC6 inhibition
leads to a significant reduction in tau levels as detected by
the human tau-specific antibody E1 (Fig. 6 (a and c) and
supplemental Fig. S6)."
SET Confidence = "High"
p(HGNC:HDAC6) -> p(HGNC:MAPT)
UNSET Confidence

SET Evidence = "We also observed a striking decrease in
phosphorylation at Ser-324, which was statistically significant
even when normalizing to E1 to control for the reduction
in tau levels (Fig. 6 (a and b) and supplemental Fig. S6)."
#during HDAC6 inhibiton
SET Confidence = "High"
p(HGNC:HDAC6) -> p(HGNC:MAPT,pmod(Ph,Ser,324))
UNSET Confidence

SET Evidence = "These findings indicate that accumulation of
pSer-324 –positive tau species is sensitive to HDAC6 activity
in neurons."
SET Confidence = "High"
SET MeSHAnatomy = "Neurons"
act(p(HGNC:HDAC6)) -> p(HGNC:MAPT,pmod(Ph,Ser,324))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Our findings indicate that several acetylation sites in tau
are responsive to HDAC6 and that acetylation on Lys-321
(within a KCGSmotif) is both essential for acetylation-mediated
inhibition of tau aggregation in vitro and a molecular tactic for
preventing phosphorylation on the downstream Ser-324 residue."
SET Confidence = "High"
p(HGNC:HDAC6) -> p(HGNC:MAPT,pmod(Ac))
p(HGNC:MAPT,pmod(Ac,Lys,321)) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac,Lys,321)) -| p(HGNC:MAPT,pmod(Ph,Ser,324))
p(HGNC:MAPT,pmod(Ph,Ser,324)) -> p(CONSO:"Tau aggregates")
UNSET Confidence
