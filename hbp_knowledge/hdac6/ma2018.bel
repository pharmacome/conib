#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Pharmacological inhibition of HDAC6 reverses cognitive impairment and tau pathology as a result of cisplatin treatment."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
# DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
# DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
# DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
# DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
# DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
# DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
 DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
# DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "30270813"}

SET Species = "10090"
SET Gender = "Male"
#aged 8-10 weeks

SET Evidence = "The cumulative dose
of cisplatin has been shown to effectively control tumor
growth in mice [47]."
#2.3 mg/kg for 5 days
a(CHEBI:cisplatin) neg path(EFO:neoplasm)

SET Evidence = "Cisplatin-treated mice showed an impaired performance
in the Y maze test, as evidenced by a reduction in the
rate of perfect alternations (Fig. 1b), implying a decreased
spatial working memory function (post-hoc test,
p = 0.0086)."
a(CHEBI:cisplatin) -| path(MESH:"Spatial Memory")

SET Evidence = "Treatment with 14 doses of the HDAC6 inhibitor
ACY-1083 starting 3 days after completion of cisplatin
treatment, completely reversed this cognitive
deficit (post-hoc test, p = 0.0074) (Fig. 1b)."
#5 mg/kg ACY-1083 after 3 day treatment of cisplatin
#ACY-1083 - 91844718
a(PUBCHEM:91844718) -> path(MESH:"Spatial Memory")

SET Evidence = "Of note, we
showed previously that ACY-1083 and ACY-1215 are
equally effective in reversing cisplatin-induced peripheral
neuropathy [31]."
#ACY-1215 - 53340666
#5mg/kg ACY-1083 and 30mg/kg ACY-1215
a(CHEBI:cisplatin) -> path(EFO:"peripheral neuropathy")
a(PUBCHEM:91844718) -| path(EFO:"peripheral neuropathy")
a(PUBCHEM:53340666) -| path(EFO:"peripheral neuropathy")

SET Evidence = "However, treatment with
ACY-1215 in the same schedule that reversed peripheral
neuropathy did not restore cognitive function (Fig. 1c)
(two-way ANOVA, F (1, 20) = 0.3809, p = 0.54)."
#defect in cognition caused due to cisplatin
a(PUBCHEM:53340666) cnc bp(GO:cognition)
a(CHEBI:cisplatin) neg bp(GO:cognition)

SET Evidence = "No differences
were detected in the total interaction time with objects
(two-way ANOVA, F (1, 31) = 0.6425, p = 0.4289) (Fig.
2b), indicating that cisplatin and ACY-1083 had no effect
on locomotor activity or interest in the objects.."
a(CHEBI:cisplatin) cnc bp(GO:"locomotory behavior")
a(PUBCHEM:91844718) cnc bp(GO:"locomotory behavior")

SET Evidence = "Taken together, these findings indicate that cisplatin
impaired spatial working memory, executive functioning,
and problem-solving abilities in mice and, more importantly,
that the brain-penetrating HDAC6 inhibitor
ACY-1083 reversed these deficits."
a(CHEBI:cisplatin) -|  path(MESH:"Spatial Memory")
a(CHEBI:cisplatin) -|  path(MESH:"Executive Function")
a(CHEBI:cisplatin) -|  path(MESH:"Problem Solving")
a(PUBCHEM:91844718) -| p(HGNC:HDAC6)
a(PUBCHEM:91844718) ->  path(MESH:"Spatial Memory")
a(PUBCHEM:91844718) ->  path(MESH:"Executive Function")
a(PUBCHEM:91844718) ->  path(MESH:"Problem Solving")

SET MeSHAnatomy = "Brain"

SET Evidence = "Figure 3a shows that
cisplatin treatment decreased α-tubulin acetylation in
the brain (post-hoc test, p = 0.0408) and that this was reversed
by ACY-1083 treatment (post-hoc test, p =
0.0170)."
a(CHEBI:cisplatin) -| p(INTERPRO:"Alpha tubulin",pmod(Ac))
a(PUBCHEM:91844718) -> p(INTERPRO:"Alpha tubulin",pmod(Ac))

SET Evidence = "As shown in
Fig. 3b, cisplatin did not change the protein level of
HDAC6 in the brain (two-way ANOVA, F (1, 12) =
0.7129, p = 0.4150)."
a(CHEBI:cisplatin) cnc p(HGNC:HDAC6)

SET Evidence = "Figs. 4a–e, cisplatin treatment led to morphological damage
to synaptosomal mitochondria, as evidenced by an increased
percentage of atypical mitochondria that were swollen and/
or had abnormal cristae structure (post-hoc test, p = 0.0256)."
SET MeSHAnatomy = "Synaptosomes"
a(CHEBI:cisplatin) -> bp(MESH:"Mitochondrial Size")
a(CHEBI:cisplatin) neg bp(GO:"cristae formation")
UNSET MeSHAnatomy

SET Evidence = "Notably, these morphological changes were completely
reversed by 2 weeks of ACY-1083 treatment (post-hoc test,
p = 0.0060)"
#changes mentioned above
#2 week treatment
SET MeSHAnatomy = "Synaptosomes"
a(PUBCHEM:91844718) reg bp(MESH:"Mitochondrial Size")
a(PUBCHEM:91844718) pos bp(GO:"cristae formation")
UNSET MeSHAnatomy

SET Evidence = "Furthermore, cisplatin induced significant decreases in
parameters measuring synaptosomal mitochondrial bioenergetics,
including the maximal respiratory capacity
(MRC) (post-hoc test, p = 0.0416) (Fig. 4f ) and spare respiratory
capacity (SRC) (post-hoc test, p = 0.0147) (Fig.
4g). ACY-1083 treatment also restored cisplatin-induced
impairment in MRC (post-hoc test, p = 0.0345) and SRC
(post-hoc test, p = 0.0291) (Fig. 4f and g)."
SET MeSHAnatomy = "Synaptosomes"
a(CHEBI:cisplatin) -| path(MESH:"Energy Metabolism")
a(CHEBI:cisplatin) -| path(EFO:"maximal oxygen uptake measurement")
a(PUBCHEM:91844718) -> path(MESH:"Energy Metabolism")
a(PUBCHEM:91844718) -> path(EFO:"maximal oxygen uptake measurement")
UNSET MeSHAnatomy

SET Evidence = "Cisplatin and
ACY-1083 did not affect basal respiration (two-way
ANOVA, F (1, 20) = 0.6219, p = 0.4396) (Fig. 4h) or
ATP-coupled respiration (two-way ANOVA, F (1, 20) =
0.02046, p = 0.8877; n = 6) (Fig. 4i)."
a(CHEBI:cisplatin) cnc bp(GO:"respiratory gaseous exchange by respiratory system")
a(PUBCHEM:91844718) cnc bp(GO:"respiratory gaseous exchange by respiratory system")
a(CHEBI:cisplatin) cnc bp(GO:"cellular respiration")
a(PUBCHEM:91844718) cnc bp(GO:"cellular respiration")

SET MeSHAnatomy = {"CA1 Region, Hippocampal","Dentate Gyrus"}

SET Evidence = "The results in Figs. 5
demonstrate that cisplatin reduced the expression of the
presynaptic marker synaptophysin (post-hoc, p = 0.0005 for
CA1, p < 0.0001 for DG) and the postsynaptic marker
PSD95 (post-hoc, p = 0.0138 for CA1, p= 0.0065 for DG) in
the CA1 and DG regions of the hippocampus."
#DG - Dentate Gyrus
a(CHEBI:cisplatin) -| p(HGNC:SYP)
a(CHEBI:cisplatin) -| p(HGNC:DLG4)

SET Evidence = "Two weeks
of ACY-1083 treatment normalized expression of both the
presynaptic (post-hoc, p < 0.0001 for CA1, p < 0.0001 for
DG) and postsynaptic markers (post-hoc, p = 0.0434 for
CA1, p= 0.0275 for DG)."
#2 week treatment
a(PUBCHEM:91844718) -> p(HGNC:SYP)
a(PUBCHEM:91844718) -> p(HGNC:DLG4)

SET Evidence = "We detected significant upregulation of
tau phosphorylation with the AT8 antibody in the CA1
(post-hoc, p = 0.0003) (Figs. 6a–d) and DG (post-hoc, p =
0.0002) (Figs. 6e–h) regions of cisplatin-treated mice."
#AT8 antibody used
a(CHEBI:cisplatin) -> p(HGNC:MAPT,pmod(Ph))

SET Evidence = "Notably,
ACY-1083 treatment completely reversed tau hyperphosphorylation
induced by cisplatin treatment (post-hoc,
p = 0.0002 for CA1, p = 0.0013 for DG) (Fig. 6i and j)."
a(PUBCHEM:91844718) -| p(HGNC:MAPT,pmod(Ph))

UNSET MeSHAnatomy

UNSET Gender
UNSET Species
