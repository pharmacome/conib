#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Degradation or aggregation: the ramifications of post-translational modifications on tau."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
# DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
# DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
# DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
# DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
# DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
# DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "29661268"}

SET Evidence = "Subsequent findings, that tau phosphorylation
occurs mainly in PRDs (∼22 sites out of 93 amino acids
in the PRD can be phosphorylated), reduces tau affinity to
microtubules, facilitates tau self-fibrillization, and subsequently
results in microtubule destabilization led to the fundamental
hypothesis that blocking tau phosphorylation would delay the
pathological development of tauopathies."
p(HGNC:MAPT,pmod(Ph)) -| composite(a(MESH:Microtubules),p(HGNC:MAPT))
p(HGNC:MAPT,pmod(Ph)) -> p(CONSO:"Tau aggregates")
p(HGNC:MAPT,pmod(Ph)) -> bp(GO:"microtubule severing")

SET Evidence = "These negative data do not necessarily preclude
the strategy of targeting tau kinases, but rather indicate that the
effects of tau phosphorylation on its aggregation are dependent
on the kinases involved or the modification site."
p(MESH:Phosphotransferases) -- p(CONSO:"Tau aggregates",pmod(Ph))

SET Evidence = "Histone
acetyltransferase cAMP-response element binding protein
(CREB)-binding protein (CBP) and the highly homologous
p300 proteins are the key tau acetylases (21-23)."
act(p(HGNC:CREBBP),ma(GO:"acetyltransferase activity")) -> p(HGNC:MAPT,pmod(Ub))
act(p(HGNC:EP300),ma(GO:"acetyltransferase activity")) -> p(HGNC:MAPT,pmod(Ub))

SET Evidence = "Notably, tau
protein may intrinsically possess auto-acetyltransferase activity
capable of acetylating its own Lys residues, using its MBD as
the key catalytic site.Therefore, Cys291 and Cys322, the only
two Cys residues of tau, have two competing functions: as the
key residues in the catalytic center, they enable tau
auto-acetylation even in the absence of p300/CBP, and as the
residues in the dimerization site of tau, they enable aggregate
nuclei formation."
p(HGNC:MAPT) -> p(HGNC:MAPT,pmod(Ac,Lys))
p(HGNC:MAPT) -> p(HGNC:MAPT,pmod(Ac,Cys,291))
p(HGNC:MAPT) -> p(HGNC:MAPT,pmod(Ac,Cys,322))

SET Evidence = "Because
acetylation and ubiquitination may use the same Lys residues,
acetylation is also thought to directly reduce tau ubiquitination
and subsequently block its proteasomal degradation (26)."
p(HGNC:MAPT,pmod(Ac,Lys)) -| p(HGNC:MAPT,pmod(Ub,Lys))
p(HGNC:MAPT,pmod(Ac,Lys)) -| deg(p(HGNC:MAPT))
p(HGNC:MAPT,pmod(Ub,Lys)) -> deg(p(HGNC:MAPT))

SET Evidence = "The tau deacetylase, histone deacetylase 6 (HDAC6), can
directly bind to the MBD and antagonize acetylation of tau,
especially on the KXGS motifs (27)."
p(HGNC:HDAC6) -> complex(p(HGNC:MAPT,loc(CONSO:"microtubule-binding region")),p(HGNC:HDAC6))
complex(p(HGNC:MAPT,loc(CONSO:"microtubule-binding region")),p(HGNC:HDAC6)) -> act(p(HGNC:HDAC6))
act(p(HGNC:HDAC6)) -| p(HGNC:MAPT,pmod(Ac),loc(CONSO:"KXGS motif"))

SET Evidence = "High HDAC6 activity is
correlated not only with reduced tau acetylation, but also
elevated phosphorylation at the Ser residues within the KXGS
motifs (28)."
act(p(HGNC:HDAC6)) -> p(HGNC:MAPT,pmod(Ph,Ser),loc(CONSO:"KXGS motif"))

SET Evidence = "Therefore, the consequence of HDAC6-mediated
deacetylation is the generation of less microtubule-binding,
and more aggregation-prone forms of tau (29, 30)."
act(p(HGNC:HDAC6)) -| act(p(HGNC:MAPT),ma(GO:"microtubule binding"))
act(p(HGNC:HDAC6)) -> p(CONSO:"Tau aggregates")

SET Evidence = "However, contrary to the effect of
HDAC6, decreased activity of SIRT1 was associated with
enhanced PHF-tau accumulation in AD patients (31),
indicating a negative correlation between the regulation of
SIRT1 and tau accumulation."
SET MeSHDisease = "Alzheimer Disease"
act(p(HGNC:SIRT1)) neg p(HGNC:MAPT)
UNSET MeSHDisease

SET Evidence = "Consistent with the function of acetylation that may
preclude Lys residues from ubiquitination, mutations altering
Lys residues to Gln, mimicking acetylation on human tau
proteins, attenuated tau clearance and induced behavioral
abnormality in vivo, such as memory and learning impairments
(32, 33)."
p(HGNC:MAPT,var("p.Lys?Gln")) -- p(HGNC:MAPT,pmod(Ac))
p(HGNC:MAPT,var("p.Lys?Gln")) -| deg(p(HGNC:MAPT))
p(HGNC:MAPT,var("p.Lys?Gln")) neg bp(GO:memory)
p(HGNC:MAPT,var("p.Lys?Gln")) neg bp(GO:learning)

SET Evidence = "Similarly, the Gan group reported that abnormal
acetylation of Lys274 and Lys281 on tau was found in human
AD brains and transgenic mice expressing the same mutations
also exhibited AD-related memory deficits and impaired
hippocampal long-term potentiation (LTP) (34)."
SET Species = {"10090","9606"}
path(DOID:"Alzheimer's disease") pos p(HGNC:MAPT,pmod(Ac,Lys,274))
path(DOID:"Alzheimer's disease") pos p(HGNC:MAPT,pmod(Ac,Lys,281))
p(HGNC:MAPT,pmod(Ac,Lys,274)) neg bp(GO:memory)
p(HGNC:MAPT,pmod(Ac,Lys,281)) neg bp(GO:memory)
SET MeSHAnatomy = "Hippocampus"
p(HGNC:MAPT,pmod(Ac,Lys,274)) neg bp(GO:"long-term synaptic potentiation")
p(HGNC:MAPT,pmod(Ac,Lys,281)) neg bp(GO:"long-term synaptic potentiation")
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "Acetylation of
tau at Lys174, Lys274 and Lys280 has been detected
post-mortem in AD, FTDP-17, and PiD, suggesting acetylation
at these sites may be pathological (33, 35)."
path(DOID:"Alzheimer's disease") pos p(HGNC:MAPT,pmod(Ac,Lys,174))
path(DOID:"Alzheimer's disease") pos p(HGNC:MAPT,pmod(Ac,Lys,274))
path(DOID:"Alzheimer's disease") pos p(HGNC:MAPT,pmod(Ac,Lys,280))
path(DOID:"frontotemporal dementia") pos p(HGNC:MAPT,pmod(Ac,Lys,174))
path(DOID:"frontotemporal dementia") pos p(HGNC:MAPT,pmod(Ac,Lys,274))
path(DOID:"frontotemporal dementia") pos p(HGNC:MAPT,pmod(Ac,Lys,280))
path(DOID:"Niemann-Pick disease") pos p(HGNC:MAPT,pmod(Ac,Lys,174))
path(DOID:"Niemann-Pick disease") pos p(HGNC:MAPT,pmod(Ac,Lys,274))
path(DOID:"Niemann-Pick disease") pos p(HGNC:MAPT,pmod(Ac,Lys,280))

SET Evidence = "In addition to its
competing function with ubiquitination, acetylation of Lys280
of tau appears to have a negative role on tau-mediated
stabilization of microtubules and, conceivably, alters its
phosphorylation at both proximal and distal residues (36)."
p(HGNC:MAPT,pmod(Ac,Lys,280)) -| p(HGNC:MAPT,pmod(Ub,Lys,280))
p(HGNC:MAPT,pmod(Ac,Lys,280)) pos bp(GO:"microtubule severing")
p(HGNC:MAPT,pmod(Ac,Lys,280)) neg p(HGNC:MAPT,pmod(Ph))

SET Evidence = "The
K280Q mutation mimicking tau acetylation in Drosophila and
mice showed reduced microtubule bundling, elevated degrees
of tau phosphorylation at nearby Ser262 and Thr212/214
residues, and increased total tau amounts (25, 37)."
SET Species = {"7227","10090"}
p(HGNC:MAPT,var("p.Lys280Gln")) -- p(HGNC:MAPT,pmod(Ac))
p(HGNC:MAPT,var("p.Lys280Gln")) -| bp(GO:"microtubule bundle formation")
p(HGNC:MAPT,var("p.Lys280Gln")) -> p(HGNC:MAPT,pmod(Ph,Ser,262))
p(HGNC:MAPT,var("p.Lys280Gln")) -> p(HGNC:MAPT,pmod(Ph,Thr,212),pmod(Ph,Thr,214))
p(HGNC:MAPT,var("p.Lys280Gln")) -> p(HGNC:MAPT)
UNSET Species

SET Evidence = "Acetylation
of tau at Lys280 is expected to occur before pathological tau is
fibrillized into PHFs."
p(HGNC:MAPT,pmod(Ac,Lys,280)) -> p(CONSO:"paired helical filaments")

SET Evidence = "Immunohistopathological studies revealed
that acetylation at Lys280 is observed in the brain of AD and
other tauopathies, but not in normal brains, in a distribution
pattern similar to hyperphosphorylated tau (38)."
a(MESH:Brain) -> p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))
SET MeSHAnatomy = "Brain"
path(DOID:"Alzheimer's disease") -> p(HGNC:MAPT,pmod(Ac,Lys,280))
path(DOID:tauopathy) -> p(HGNC:MAPT,pmod(Ac,Lys,280))
UNSET MeSHAnatomy

SET Evidence = "Acetylation of Lys residues in the KXGS
motifs (Lys259, Lys290, Lys321, and Lys353; Fig. 2A) has been
reported to prevent phosphorylation at nearby Ser/Thr residues
(for example, the reciprocal inhibitory relationship between
Lys321 acetylation and Ser324 phosphorylation) in the MBD
and to decrease the aggregation propensity of recombinant tau
in vitro (27, 39)."
p(HGNC:MAPT,pmod(Ac,Lys,259),loc(CONSO:"KXGS motif")) -| p(HGNC:MAPT,pmod(Ph,Ser),pmod(Ph,Thr),loc(CONSO:"microtubule-binding region"))
p(HGNC:MAPT,pmod(Ac,Lys,259),loc(CONSO:"KXGS motif")) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac,Lys,290),loc(CONSO:"KXGS motif")) -| p(HGNC:MAPT,pmod(Ph,Ser),pmod(Ph,Thr),loc(CONSO:"microtubule-binding region"))
p(HGNC:MAPT,pmod(Ac,Lys,290),loc(CONSO:"KXGS motif")) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac,Lys,321),loc(CONSO:"KXGS motif")) -| p(HGNC:MAPT,pmod(Ph,Ser),pmod(Ph,Thr),loc(CONSO:"microtubule-binding region"))
p(HGNC:MAPT,pmod(Ac,Lys,321),loc(CONSO:"KXGS motif")) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac,Lys,353),loc(CONSO:"KXGS motif")) -| p(HGNC:MAPT,pmod(Ph,Ser),pmod(Ph,Thr),loc(CONSO:"microtubule-binding region"))
p(HGNC:MAPT,pmod(Ac,Lys,353),loc(CONSO:"KXGS motif")) -| p(CONSO:"Tau aggregates")
p(HGNC:MAPT,pmod(Ac,Lys,321),loc(CONSO:"KXGS motif")) -| p(HGNC:MAPT,pmod(Ph,Ser,324),loc(CONSO:"microtubule-binding region"))

SET Evidence = "On the contrary, tau
acetylation at Lys280 and Lys281 (also in the MBD) resulted in
microtubule destabilization and promoted tau fibrillization
both in tau transgenic mice and human patients (25), suggesting
a causative, instead of a protective, role in tauopathies."
SET Species = {"10090","9606"}
p(HGNC:MAPT,pmod(Ac,Lys,280),loc(CONSO:"microtubule-binding region")) -> bp(GO:"microtubule severing")
p(HGNC:MAPT,pmod(Ac,Lys,280),loc(CONSO:"microtubule-binding region")) -> p(CONSO:"Tau fibrils")
p(HGNC:MAPT,pmod(Ac,Lys,281),loc(CONSO:"microtubule-binding region")) -> bp(GO:"microtubule severing")
p(HGNC:MAPT,pmod(Ac,Lys,281),loc(CONSO:"microtubule-binding region")) -> p(CONSO:"Tau fibrils")
UNSET Species

SET Evidence = "Identified tau kinases include glycogen
synthase kinase 3 beta (GSK3B), cyclin-dependent kinase 5
(CDK5), CDK2, Ca2＋/calmodulin-dependent protein kinase II
(CaMKII), microtubule-affinity regulating kinase (MARK),
protein kinase A (PKA), PKB/AKT, PKC, and tyrosine kinases
such as Src family kinases, including Src, Fyn, and c-Abl (46)."
act(p(HGNC:GSK3B),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:CDK5),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:CDK2),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:CAMK2A),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:MARK1),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(MESH:"Cyclic AMP-Dependent Protein Kinases"),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:AKT1),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(MESH:"Protein Kinase C"),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:SRC),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:FYN),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:ABL1),ma(kin)) -> p(HGNC:MAPT,pmod(Ph))

SET Evidence = "For
instance, the antibody AT8 recognizes tau phosphorylation at
Ser202, Thr205, and Ser208 and is widely used as a marker for
pre-tangle tau aggregates for assessing the AD-related
neurofibrillary pathology (47)."
SET MeSHDisease = "Alzheimer Disease"
p(CONSO:"Tau epitope, AT8") -> complex(p(CONSO:"Tau epitope, AT8"),a(CONSO:pretangles))
UNSET MeSHDisease

SET Evidence = "Tau filaments can also be
immunohistochemically detected by the AT100 antibodies
recognizing Thr212 and Ser214."
p(CONSO:"Tau epitope, AT100") -> complex(p(CONSO:"Tau epitope, AT100"),a(CONSO:"Tau fibrils"))

SET Evidence = "GSK3B can modify more than 30 phosphorylation sites and
its overexpression in mouse brain resulted in tau
hyperphosphorylation and neurodegeneration (48)."
SET Species = "10090"
SET MeSHAnatomy = "Brain"
p(HGNC:GSK3B) -> p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))
p(HGNC:GSK3B) -> path(DOID:"neurodegenerative disease")
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "Treatment
with lithium, a classic inhibitor of GSK3 and an FDA-approved
approved drug for bipolar disorder, produced beneficial
cognitive effects in mouse models of AD and even in AD
patients (49-51)."
a(CHEBI:"lithium atom") -| p(HGNC:GSK3B)
a(CHEBI:"lithium atom") -| path(DOID:"bipolar disorder")
SET Species = {"10090","9606"}
a(CHEBI:"lithium atom") pos bp(GO:cognition)
UNSET Species

SET Evidence = "Hyperphosphorylation of tau may originate from reduced
activity of phosphatases instead of elevation of kinase activity."
act(p(HGNCGENEFAMILY:"Protein phosphatases")) -| p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))

SET Evidence = "Protein phosphatases (PP) 1, PP2A, PP2B, PP2C, PP3, and PP5
are possible phosphatases responsible for tau dephosphorylation
(54)."
p(HGNC:PPA1) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP2CA) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP3CA) -| p(HGNC:MAPT,pmod(Ph))
p(HGNCGENEFAMILY:"Protein phosphatases, Mg2+/Mn2+ dependent") -| p(HGNC:MAPT,pmod(Ph))
p(MESH:Calcineurin) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP5C) -| p(HGNC:MAPT,pmod(Ph))

SET Evidence = "Decreased PP2A activity observed in AD brain may
contribute to abnormal tau hyperphosphorylation (56, 57)."
path(DOID:"Alzheimer's disease") -| act(p(HGNC:PPP2CA))
SET MeSHDisease = "Alzheimer Disease"
act(p(HGNC:PPP2CA)) -| p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))
UNSET MeSHDisease

SET Evidence = "Okadaic acid is often used to inhibit cellular PP1 and PP2A
activity and subsequently elevate the levels of hyperphosphorylated
tau in cultured cell and animal models (58-61)."
a(CHEBI:"okadaic acid") -| act(p(HGNC:PPA1))
a(CHEBI:"okadaic acid") -| act(p(HGNC:PPP2CA))
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))

SET Evidence = "Notably, the mammalian target of rapamycin (mTOR), which
negatively modulates autophagy by phosphorylating Atg1, an
autophagy initiating factor (62), also negatively regulates PP2A
activity."
a(CHEBI:sirolimus) -> p(HGNC:ULK1,pmod(Ph))
p(HGNC:ULK1,pmod(Ph)) neg bp(GO:autophagy)
p(HGNC:ULK1) pos bp(GO:autophagy)
a(CHEBI:sirolimus) neg bp(GO:autophagy)
a(CHEBI:sirolimus) neg act(p(HGNC:PPP2CA))

SET Evidence = "Therefore, inhibition of mTOR activity upon nutrient
starvation or carbon depletion, may result in both PP2A
activation and autophagy upregulation, relieving the
pathological consequences of tau hyperphosphorylation and
aggregation."
a(CHEBI:nutrient) -> act(p(HGNC:MTOR))
a(CHEBI:"carbon atom") -> act(p(HGNC:MTOR))
act(p(HGNC:MTOR)) -| p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))
act(p(HGNC:MTOR)) -| p(CONSO:"Tau aggregates")

SET Evidence = "A positive regulatory relation between the phosphorylation
and acetylation of tau has been proposed; phosphorylation in
the MBDs activates the autoacetyltransferase activity of tau and
expands acetylation of tau (63)."
p(HGNC:MAPT,pmod(Ph),loc(CONSO:"microtubule-binding region")) -> act(p(HGNC:MAPT),ma(GO:"acetyltransferase activity"))
act(p(HGNC:MAPT),ma(GO:"acetyltransferase activity")) -> p(HGNC:MAPT,pmod(Ac))

SET Evidence = "Phosphorylation of tau at the
KXGS motifs negatively regulates tau ubiquitination; phospho-
Ser interferes with its interaction with the tau ubiquitin ligase
C-terminus of Hsc70 interacting protein (CHIP), subsequently
elevating levels of free tau and tau aggregates (64, 65)."
p(HGNC:MAPT,pmod(Ph),loc(CONSO:"KXGS motif")) neg p(HGNC:MAPT,pmod(Ub))
complex(p(HGNC:MAPT,pmod(Ph,Ser)),p(HGNC:STUB1)) -> p(HGNC:MAPT)
complex(p(HGNC:MAPT,pmod(Ph,Ser)),p(HGNC:STUB1)) -> p(CONSO:"Tau aggregates")

SET Evidence = "Compared to
non-AD controls, tau proteins were highly ubiquitinated in the
brain of human AD patients."
path(DOID:"Alzheimer's disease") -> p(HGNC:MAPT,pmod(Ub))

SET Evidence = "PHF-tau isolated from human AD brains was
co-immunoprecipitated with various proteasome subunits,
probably indicating inefficient processing at the proteasome
(76)."
SET MeSHDisease = "Alzheimer Disease"
complex(p(HGNC:MAPT),p(CONSO:"paired helical filaments")) -> complex(p(HGNC:MAPT),p(CONSO:"paired helical filaments"),p(MESH:"Proteasome Endopeptidase Complex"))
complex(p(HGNC:MAPT),p(CONSO:"paired helical filaments"),p(MESH:"Proteasome Endopeptidase Complex")) -| act(p(MESH:"Proteasome Endopeptidase Complex"))
complex(p(HGNC:MAPT),p(CONSO:"paired helical filaments"),p(MESH:"Proteasome Endopeptidase Complex")) -| deg(p(HGNC:MAPT))
act(p(MESH:"Proteasome Endopeptidase Complex")) -> deg(p(MESH:Proteins))
UNSET MeSHDisease

SET Evidence = "This may be a reason for the observation that
overall degradative activity of UPS was significantly impaired
in post-mortem human AD brains (77, 78)."
#this - inefficient prcessing of proteasome
SET MeSHAnatomy = "Brain"
path(DOID:"Alzheimer's disease") neg bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process")
UNSET MeSHAnatomy

SET Evidence = "Therefore, the significance of these ubiquitinated tau (Ub-tau)
species is evident from the fact that the impairment of
UPS-mediated proteolysis clearly contributes to tau
homeostasis and ultimately to AD pathology."
p(HGNC:MAPT,pmod(Ub)) neg bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process")
p(HGNC:MAPT,pmod(Ub)) -> p(HGNC:MAPT)
p(HGNC:MAPT,pmod(Ub)) pos path(DOID:"Alzheimer's disease")

SET Evidence =  "Increased levels
of Ub-tau were found in cerebrospinal fluid (CSF) of AD
patients as well (81, 82)."
SET MeSHAnatomy = "Cerebrospinal Fluid"
path(DOID:"Alzheimer's disease") -> p(HGNC:MAPT,pmod(Ub))
UNSET MeSHAnatomy

SET Evidence = "Prior to proteasomal
degradation, tau ubiquitination, especially at Lys254, Lys311,
and Lys353, all of which are located on MBDs, may first
weaken the tau-microtubule binding affinity (Fig. 2A)."
p(HGNC:MAPT,pmod(Ub,Lys,254),loc(CONSO:"microtubule-binding region")) -| act(p(HGNC:MAPT),ma(GO:"microtubule binding"))
p(HGNC:MAPT,pmod(Ub,Lys,311),loc(CONSO:"microtubule-binding region")) -| act(p(HGNC:MAPT),ma(GO:"microtubule binding"))
p(HGNC:MAPT,pmod(Ub,Lys,353),loc(CONSO:"microtubule-binding region")) -| act(p(HGNC:MAPT),ma(GO:"microtubule binding"))

SET Evidence = "Several E3 ligases are reported to ubiquitinate tau, which
include CHIP, tumor necrosis factor receptor-associated factor
6 (TRAF6), and axotrophin/MARCH7 (83-86)."
p(HGNC:STUB1) -> p(HGNC:MAPT,pmod(Ub))
p(HGNC:TRAF6) -> p(HGNC:MAPT,pmod(Ub))
p(HGNC:MARCH7) -> p(HGNC:MAPT,pmod(Ub))

SET Evidence = "By promoting tau
quality control, overexpression of CHIP attenuated the toxicity
of hyperphosphorylated tau and maintained neuronal survival
(86, 87)."
p(HGNC:STUB1) reg p(HGNC:MAPT)
p(HGNC:STUB1) -| p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))
p(HGNC:STUB1) neg bp(GO:"neuron death")

SET Evidence = "Consistent with this, a quantitative analysis using
mouse and human brains revealed that levels of CHIP and
Hsp70 had inverse correlation with the levels of insoluble tau
aggregates (88), strongly suggesting that CHIP-mediated
ubiquitination of tau is an important negative regulator of
tauopathies."
SET Species = {"10090","9606"}
SET MeSHAnatomy = "Brain"
p(HGNC:STUB1) neg p(CONSO:"Tau aggregates")
p(HGNCGENEFAMILY:"Heat shock 70kDa proteins") neg p(CONSO:"Tau aggregates")
p(HGNC:STUB1) neg path(DOID:tauopathy)
p(HGNC:STUB1) -> p(HGNC:MAPT,pmod(Ub))
p(HGNC:MAPT,pmod(Ub)) neg path(DOID:tauopathy)
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "When
excess tau exists in cells, tau is expected to be actively
ubiquitinated by CHIP and removed by 26S proteasomes."
p(HGNC:MAPT) -> act(p(HGNC:STUB1))
p(HGNC:STUB1) -> p(HGNC:MAPT,pmod(Ub))
p(HGNC:MAPT,pmod(Ub)) -> act(p(MESH:"Proteasome Endopeptidase Complex"))
act(p(MESH:"Proteasome Endopeptidase Complex")) -> deg(p(HGNC:MAPT))

SET Evidence = "If tau proteins are accumulated beyond the threshold of the
proteasomal capacity, ubiquitinated tau may lead to a vicious,
reciprocal aggregation cycle because excessive levels of tau
proteins may inhibit proteasome activity via the “clogging”
effect (90, 91)."
#accumulated beyond threshold
p(HGNC:MAPT,pmod(Ub)) -> p(CONSO:"Tau aggregates")
p(CONSO:"Tau aggregates") -| act(p(MESH:"Proteasome Endopeptidase Complex"))
p(HGNC:MAPT,pmod(Ub)) -| act(p(MESH:"Proteasome Endopeptidase Complex"))

SET Evidence = "Newly formed PHFs can directly interact with
various proteasome subunits to further block the normal
proteasome function preventing the clearance of tau (76, 91)."
composite(p(CONSO:"paired helical filaments"),p(MESH:"Proteasome Endopeptidase Complex")) -| act(p(MESH:"Proteasome Endopeptidase Complex"))

SET Evidence = "More
specifically, CHIP-mediated tau ubiquitination may depend on
its phosphorylation states, both physiological and diseased
(64), and may promote phosphorylation and aggregation
events of tau."
p(HGNC:MAPT,pmod(Ph)) reg act(p(HGNC:STUB1))
act(p(HGNC:STUB1)) -> p(HGNC:MAPT,pmod(Ph))
act(p(HGNC:STUB1)) -> p(CONSO:"Tau aggregates")

SET Evidence = "Therefore, tau
ubiquitination occurring after its hyperphosphorylation, may
facilitate the aggregation process, rather than leading to its
Ub-dependent proteasomal degradation, especially AD
patients with decreased proteasome activity."
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation),pmod(Ub)) -> p(CONSO:"Tau aggregates")
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation),pmod(Ub)) neg bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process")
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -> deg(p(HGNC:MAPT))
UNSET MeSHDisease
