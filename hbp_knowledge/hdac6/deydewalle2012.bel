#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "HDAC6 at the Intersection of Neuroprotection and Neurodegeneration."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
# DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
# DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
# DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "22372633"}

SET Evidence = "Pharmacological inhibition or downregulation of
HDAC6 in cellular and animal models increases acetylated
tubulin abundance (6,8–13)."
p(HGNC:HDAC6) -| p(INTERPRO:"Alpha tubulin",pmod(Ac))

SET Evidence = "The Akt-dependent glycogen synthase kinase-3β (GSK-3β)
increases the deacetylase activity of HDAC6 on tubulin as
shown in hippocampal neurons (Figure 2)(12)."
SET MeSHAnatomy = {"Brain","Hippocampus"}
p(HGNC:GSK3B) -> act(p(HGNC:HDAC6),ma(GO:"deacetylase activity"))
act(p(HGNC:HDAC6),ma(GO:"deacetylase activity")) -| p(INTERPRO:"Alpha tubulin",pmod(Ac))
UNSET MeSHAnatomy

SET Evidence = "Alternatively, the tubulin polymerization-promoting
protein (TPPP/p25) is an unstructured protein that aligns
along the microtubule network and affects microtubule
dynamics and cell motility (14)."
p(HGNC:TPPP) reg act(a(MESH:Microtubules))
p(HGNC:TPPP) reg bp(GO:"cell motility")

SET Evidence = "Binding of TPPP/p25
to HDAC6 directly inhibits the deacetylating function
of HDAC6 in HeLa cells (Figure 2) (14)."
SET CellLine = "HeLa"
complex(p(HGNC:TPPP),p(HGNC:HDAC6)) -| act(p(HGNC:HDAC6),ma(GO:"deacetylase activity"))
UNSET CellLine

SET Evidence = "Finally,
the microtubule-associated protein Tau, which ensures
stabilization of the microtubular network, also binds to
HDAC6 and decreases the deacetylase activity of HDAC6
in neuronal cell lines (Figure 2) (15,16)."
SET MeSHAnatomy = "Neurons"
complex(p(HGNC:HDAC6),p(HGNC:MAPT)) -| act(p(HGNC:HDAC6),ma(GO:"deacetylase activity"))
UNSET MeSHAnatomy

SET Evidence = "Recently, it has been found
that Tau is deacetylated mainly by sirtuin-1, a class III
HDAC, and to a lesser extent by sirtuin-2 and HDAC6
(17)."
p(HGNC:SIRT1) -| p(HGNC:MAPT,pmod(Ac))
p(HGNC:SIRT2) -| p(HGNC:MAPT,pmod(Ac))
p(HGNC:HDAC6) -| p(HGNC:MAPT,pmod(Ac))

SET Evidence = "It has been proposed that HDAC6
might regulate axonal transport either directly by binding
to motor proteins or indirectly by altering the acetylation
state of microtubules."
p(HGNC:HDAC6) reg bp(GO:"axonal transport")
complex(p(HGNC:HDAC6),p(MESH:"Molecular Motor Proteins")) reg bp(GO:"axonal transport")
p(HGNC:HDAC6) -- p(MESH:Microtubules,pmod(Ac))
p(MESH:Mircotubule,pmod(Ac)) -- bp(GO:"axonal transport")

SET Evidence = "Moreover, inhibition or knockdown of
HDAC6 in cultured neurons increases the axonal transport
of mitochondria by increasing acetylated tubulin amounts
and the concomitant recruitment of motor protein dynein
and kinesin-1 to microtubules (10,12)."
SET MeSHAnatomy = "Neurons"
p(HGNC:HDAC6) neg bp(GO:"axonal transport of mitochondrion")
p(HGNC:HDAC6) -| p(INTERPRO:"Alpha tubulin",pmod(Ac))
p(HGNC:HDAC6) -| composite(p(MESH:Dyneins),a(MESH:Microtubules),p(MESH:Kinesin))
p(INTERPRO:"Alpha tubulin",pmod(Ac)) pos bp(GO:"axonal transport of mitochondrion")
composite(p(MESH:Dyneins),a(MESH:Microtubules),p(MESH:Kinesin)) pos bp(GO:"axonal transport of mitochondrion")
UNSET MeSHAnatomy

SET Evidence = "Microtubules containing high levels of
acetylated tubulin are more resistant to drug-induced
depolymerization indicating that tubulin acetylation is
a marker for stable microtubules in fibroblasts (8)."
SET MeSHAnatomy = "Microtubules"
p(INTERPRO:"Alpha tubulin",pmod(Ac)) -|  bp(GO:"microtubule depolymerization")
SET MeSHAnatomy = "Fibroblasts"
p(INTERPRO:"Alpha tubulin",pmod(Ac)) -- a(MESH:Microtubules)
UNSET MeSHAnatomy

SET Evidence = "Overexpression of HDAC6 induces depolymerization and
subsequent deacetylation, indicating that tubulin dimers
and monomers are deacetylated, while acetylation only
occurs in polymerized microtubules (8)."
p(HGNC:HDAC6) pos bp(GO:"microtubule depolymerization")
p(HGNC:HDAC6) -| p(INTERPRO:"Alpha tubulin",pmod(Ac))
p(HGNC:HDAC6) -| complex(p(INTERPRO:"Alpha tubulin"),p(INTERPRO:"Alpha tubulin"),pmod(Ac))
p(HGNC:HDAC6) -> p(MESH:Microtubules,pmod(Ac))

SET Evidence = "Moreover, inhibition or downregulation of
HDAC6 results in reduced axonal length and decreased
growth and shrinkage velocity of microtubules (9,11)."
#these changes in microtubule are due to EB-1 and Arp1 proteins
p(HGNC:HDAC6) -| p(HGNC:MAPRE1)
p(HGNC:HDAC6) -| p(HGNC:ACTR1A)
SET MeSHAnatomy = "Axons"
p(HGNC:MAPRE1) -> bp(MESH:Growth)
p(HGNC:ACTR1A) -> bp(MESH:Growth)
UNSET MeSHAnatomy

SET Evidence = "Upon stimulation with growth
factors, HDAC6 translocates to actin-enriched membrane
ruffles where it binds and deacetylates cortactin (Figure 2)
(19,20)."
SET MeSHAnatomy = "Microtubules"
a(CHEBI:"growth hormone") -> p(HGNC:HDAC6,loc(GO:membrane))
SET MeSHAnatomy = "Cell Membrane"
complex(p(HGNC:HDAC6),p(HGNC:CTTN)) -| p(HGNC:CTTN,pmod(Ac))
UNSET MeSHAnatomy


SET Evidence = "The latter protein is a cortical actin-binding protein
and promotes the polymerization and rearrangement of
the actin cytoskeleton, especially the actin cortex around
the cellular periphery."
#latter protein - CTTN
p(HGNC:CTTN) -> bp(GO:"actin filament reorganization")
p(HGNC:CTTN) -> bp(GO:"actin filament organization")

SET Evidence = "Deacetylation of cortactin changes
its binding capacity to F-actin, eventually altering actin
polymerization (20)."
p(HGNC:CTTN,pmod(Ac)) -> act(p(HGNC:CTTN),ma(GO:"actin filament binding"))
act(p(HGNC:CTTN),ma(GO:"actin filament binding")) reg bp(GO:"actin filament organization")

SET Evidence = "Acetylation of PRX increases their reducing activity,
their resistance to superoxidation and to transition to
high-molecular mass complexes."
p(HGNC:PRX,pmod(Ac)) -> act(p(HGNC:PRX),ma(GO:"antioxidant activity"))

SET Evidence = "Inhibition of HDAC6
deacetylase activity with a consequent accumulation of
acetylated PRX could lead to a beneficial increase in
antioxidant activity (21)."
act(p(HGNC:HDAC6),ma(GO:"deacetylase activity")) -| p(HGNC:PRX,pmod(Ac))

SET Evidence = "In HDAC6-deficient
regulatory T cells, HSF1-regulated genes including HSP90
were upregulated (25,26)."
SET MeSHAnatomy = "T-Lymphocytes, Regulatory"
p(HGNC:HDAC6) -| p(HGNC:HSF1)
p(HGNC:HSF1) -> p(HGNCGENEFAMILY:"Heat shock 90kDa proteins")
UNSET MeSHAnatomy

SET Evidence = "HDAC6 senses the presence of
ubiquitinated aggregates and consequently induces the
expression of major cellular chaperones by triggering
the dissociation of a repressive HDAC6/HSF1/HSP90
complex and subsequent HSF1 activation (27)."
p(HGNC:HDAC6) -| complex(p(HGNC:HDAC6),p(HGNC:HSF1),p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"))
complex(p(HGNC:HDAC6),p(HGNC:HSF1),p(HGNCGENEFAMILY:"Heat shock 90kDa proteins")) -| act(p(HGNC:HSF1))
p(HGNC:HDAC6) -> act(p(HGNC:HSF1))

SET Evidence = "Inhibition
of HDAC6 also induces a reduction in binding of HSP90
to vascular endothelial growth factor receptor 1 and 2
(VEGFR1/Flt1 and VEGFR2/Flk1, respectively), followed
by increased binding of HSP70 to these receptors (28)."
p(HGNC:HDAC6) -| act(p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"),ma(GO:"vascular endothelial growth factor receptor 1 binding"))
act(p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"),ma(GO:"vascular endothelial growth factor receptor 1 binding")) -> act(p(HGNCGENEFAMILY:"Heat shock 70kDa proteins"),ma(GO:"vascular endothelial growth factor receptor 1 binding"))
p(HGNC:HDAC6) -| act(p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"),ma(GO:"vascular endothelial growth factor receptor 1 binding"))
act(p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"),ma(GO:"vascular endothelial growth factor receptor 2 binding")) -> act(p(HGNCGENEFAMILY:"Heat shock 70kDa proteins"),ma(GO:"vascular endothelial growth factor receptor 2 binding"))

SET Evidence = "Knockdown of HDAC6 leads to a depletion of VEGFR1/Flt1
and VEGFR2/Flk1 (28)."
p(HGNC:HDAC6) -| p(HGNC:FLT1)
p(HGNC:HDAC6) -| p(HGNC:KDR)

SET Evidence = "Genetic ablation of HDAC6 leads to
hyperacetylation of microtubules and HSP90 without
any obvious abnormal effects on development and
survival of these mice (33)."
SET Species = "10090"
p(HGNC:HDAC6) -| p(INTERPRO:"Alpha tubulin",pmod(Ac))
p(HGNC:HDAC6) -| p(HGNCGENEFAMILY:"Heat shock 90kDa proteins",pmod(Ac))
p(HGNC:HDAC6) cnc bp(MESH:"Cell Survival")
p(HGNC:HDAC6) cnc bp(MESH:Growth)
UNSET Species

SET Evidence = "Another strain of HDAC6
knockout mice demonstrate ubiquitin-positive aggregates
and apoptotic cell death, both pathogenic hallmarks of
neurodegeneration, in the brain from 6 months of age
on (34)."
#6 months
SET Species = "10090"
p(HGNC:HDAC6) -| p(CONSO:"protein aggregates",pmod(Ub))
p(HGNC:HDAC6) -| bp(GO:"programmed cell death")
SET MeSHAnatomy = "Brain"
p(HGNC:HDAC6) -| path(DOID:"neurodegenerative disease")
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "These chaperones are intrinsically associated
with ubiquitinated misfolded proteins that are prone
to aggregation."
#HSF1/HSP90 complex
complex(p(HGNC:HSF1),p(HGNCGENEFAMILY:"Heat shock 90kDa proteins")) -> p(MESH:Proteins,pmod(CONSO:misfolded),pmod(Ub))
p(MESH:Proteins,pmod(CONSO:misfolded)) -> p(CONSO:"protein aggregates")

SET Evidence = "VCP/p97 promotes ubiquitin
chain turnover and protein degradation through the UPS,
whereas HDAC6 favors the accumulation of ubiquitinated
proteins and subsequent perinuclear aggregation
(27,40,41)."
p(HGNC:VCP) -| bp(GO:"protein ubiquitination")
bp(GO:"protein ubiquitination") -> bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process")
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") -> deg(p(MESH:Proteins))
p(HGNC:HDAC6) -> bp(GO:"protein ubiquitination")
bp(GO:"protein ubiquitination") -> p(CONSO:"protein aggregates")

SET Evidence = "An elegant
study demonstrated that HDAC6 associates with the
dynein motor protein complex and recruits ubiquitinated
protein aggregates to dynein (Figure 3) (39)."
complex(p(HGNC:HDAC6),p(MESH:Dyneins)) -> complex(p(CONSO:"protein aggregates"),a(MESH:Dyneins))

SET Evidence = "Pharmacological
inhibition and ablation of HDAC6 interferes with
the recruitment of Atg-8/LC3 to the aggresome (Figure 3)
(37)."
p(HGNC:HDAC6) -- p(HGNC:MAP1LC3A,loc(GO:aggresome))

SET Evidence = "HDAC6 also stimulates autophagosome–lysosome
fusion by promoting F-actin remodeling in a cortactin-dependent
manner (Figure 3) (34)."
p(HGNC:HDAC6) -> act(p(HGNC:CTTN),ma(GO:"actin filament binding"))
act(p(HGNC:CTTN),ma(GO:"actin filament binding")) -> bp(GO:"actin filament reorganization")
bp(GO:"actin filament reorganization") -> bp(GO:"autophagosome-lysosome fusion")
p(HGNC:HDAC6) -> bp(GO:"autophagosome-lysosome fusion")

SET Evidence = "Conversely, HDAC6 deficiency leads to failure of
autophagosome maturation, protein aggregate build-up
and neurodegeneration (34)."
p(HGNC:HDAC6) pos bp(GO:"autophagosome maturation")
p(HGNC:HDAC6) -> p(CONSO:"protein aggregates")
p(HGNC:HDAC6) -> path(DOID:"neurodegenerative disease")

SET Evidence = "Like autophagy, SGs are formed when
the UPS is impaired, suggesting that both autophagy
and translational suppression occur simultaneously (43)."
#stress granule
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") neg bp(GO:"stress granule assembly")
bp(GO:autophagy) neg bp(GO:"stress granule assembly")

SET Evidence = "HDAC6 appears to colocalize and interact with Ras-GAP
SH3 domain-binding protein (G3BP), an SG marker, indicating
that HDAC6 might contribute to SG formation (Figure
3) (42)."
bp(GO:"stress granule assembly") -> p(HGNC:G3BP1)
composite(p(HGNC:G3BP1),p(HGNC:HDAC6)) -> bp(GO:"stress granule assembly")

SET Evidence = "Pharmacological inhibition
or genetic disruption of HDAC6 indeed abolishes SG
formation (42)."
p(HGNC:HDAC6) -> bp(GO:"stress granule assembly")

SET Evidence = "Blocking
the dynein motor proteins does not affect the localization
of HDAC6, while it does impair SG formation (42)."
p(MESH:Dyneins) cnc p(HGNC:HDAC6)
p(MESH:Dyneins) -> bp(GO:"stress granule assembly")

SET Evidence = "Silencing either TDP-43 or FUS/TLS
reduces HDAC6 mRNA levels, indicating a crucial link
between these RNA-binding proteins and HDAC6 (44)."
p(HGNC:TARDBP) -> r(HGNC:HDAC6)
p(HGNC:FUS) -> r(HGNC:HDAC6)

SET Evidence = "Finally, HDAC6 overexpression is able to rescue the phenotype
of a Drosophila model of spinobulbar muscular
atrophy, indicating that HDAC6 might partially counteract
the pathogenesis of neurodegenerative disorders characterized
by an impaired UPS (36)."
SET Species = "7227"
p(HGNC:HDAC6) -| path(MESH:"Bulbo-Spinal Atrophy, X-Linked")
bp(GO:"proteasome-mediated ubiquitin-dependent protein catabolic process") neg path(DOID:"neurodegenerative disease")
p(HGNC:HDAC6) neg path(DOID:"neurodegenerative disease")
UNSET Species

SET Evidence = "HDAC6 thus might be a promising
therapeutic target for ciliopathies that give rise to a wide
variety of symptoms such as multiorgan failure with
variable central nervous system involvement (46–48)."
p(HGNC:HDAC6) -| path(DOID:ciliopathy)

SET Evidence = "Although HDAC6 appears
to be upregulated, acetylated tubulin levels are increased
in neurons containing neurofibrillary Tau pathology (15,16)."
SET MeSHAnatomy = "Neurons"
p(CONSO:"Tau fibrils") -> p(HGNC:HDAC6)
p(CONSO:"Tau fibrils") -> p(INTERPRO:"Alpha tubulin",pmod(Ac))
UNSET MeSHAnatomy

SET Evidence = "Inhibition of the proteasome system in cellular models
potentiates the interaction between HDAC6 and Tau, and
facilitates perinuclear aggregation of Tau independently of
the deacetylase activity (15)."
complex(GO:"proteasome complex") -| composite(p(HGNC:HDAC6),p(HGNC:MAPT))
composite(p(HGNC:HDAC6),p(HGNC:MAPT)) -> p(CONSO:"Tau aggregates")
act(p(HGNC:HDAC6),ma(GO:"deacetylase activity")) cnc p(CONSO:"Tau aggregates")

SET Evidence = "Cellular models demonstrated that
HDAC6 expression increases and colocalizes with mutant
α-synuclein, one of the genetic causes of familial PD, in
the perinuclear region forming aggresome-like structures
(49)."
p(HGNC:SNCA,var("?")) -> p(HGNC:HDAC6,loc(GO:"perinuclear region of cytoplasm"))
p(HGNC:SNCA,var("?")) -> complex(a(HGNC:HDAC6),p(HGNC:SNCA),loc(GO:"perinuclear region of cytoplasm"))

SET Evidence = "HDAC6 deficiency blocks the formation of aggregates
and interferes with autophagy (49)."
#aggregates of SNCA
p(HGNC:HDAC6) -> p(CONSO:"alpha-synuclein aggregates")
p(HGNC:HDAC6) -- bp(GO:autophagy)

SET Evidence = "Parkin, associated
with early-onset PD, promotes mitophagy by catalyzing
mitochondrial ubiquitination (51). This recruits ubiquitin binding
autophagic components HDAC6 and p62, leading
to mitochondrial clearance (51)."
SET MeSHDisease = "Parkinson Disease"
p(HGNC:PRKN) pos bp(GO:mitophagy)
complex(p(HGNC:HDAC6),p(HGNC:SQSTM1),a(MESH:Mitochondria)) -> deg(a(MESH:Mitochondria))
UNSET MeSHDisease

SET Evidence = "In a Drosophila model
for PD, depletion of HDAC6 significantly increases the
loss of dopaminergic neurons, retinal degeneration and
locomotor dysfunction (52)."
SET Species = "7227"
SET MeSHDisease = "Parkinson Disease"
p(HGNC:HDAC6) pos bp(GO:"synaptic transmission, dopaminergic")
p(HGNC:HDAC6) neg path(DOID:"retinal degeneration")
p(HGNC:HDAC6) pos bp(MESH:Locomotion)
UNSET MeSHDisease
UNSET Species

SET Evidence = "Conversely, overexpression
of HDAC6 promotes inclusion formation and suppresses
the α-synuclein-induced loss of neurons and retinal
degeneration (52)."
#SNCA mutant refered
p(HGNC:HDAC6) -> p(HGNC:SNCA,loc(GO:"inclusion body"))
p(HGNC:SNCA,var("?")) neg bp(GO:"synaptic transmission, dopaminergic")
p(HGNC:SNCA,var("?")) pos path(DOID:"retinal degeneration")

SET MeSHDisease = "Huntington Disease"

SET Evidence = "In
vitro models of HD demonstrated that HDAC6 inhibition
restores acetylated tubulin abundance and subsequently
the axonal transport defects observed in diseased states
(10)."
p(HGNC:HDAC6) -| p(INTERPRO:"Alpha tubulin",pmod(Ac))
p(HGNC:HDAC6) neg bp(GO:"axonal transport")

SET Evidence = "HDAC6 deficiency in HD
mice also does not influence the disease progression at
the behavioral level (53)."
#HD disease
SET Species = "10090"
p(HGNC:HDAC6) cnc path(DOID:"Huntington's disease")
UNSET Species

UNSET MeSHDisease

SET Evidence = "This notion is supported by
the association of mutations in TDP-43 and FUS/TLS
as a cause of familial ALS, which form a complex and
bind to HDAC6 mRNA in SGs."
p(HGNC:TARDBP,var("?")) -> complex(p(HGNC:TARDBP),r(HGNC:HDAC6),loc(GO:"cytoplasmic stress granule"))
p(HGNC:FUS,var("?")) -> complex(p(HGNC:FUS),r(HGNC:HDAC6),loc(GO:"cytoplasmic stress granule"))

SET Evidence = "Treatment with an HDAC inhibitor also delays
disease progression and increases survival in a mouse
model of ALS, further supporting the notion that HDAC6 is
a central enzyme in the disease (58)."
SET Species = "10090"
p(HGNC:HDAC6) pos path(DOID:"amyotrophic lateral sclerosis")
p(HGNC:HDAC6) -| bp(MESH:"Cell Survival")
UNSET Species

SET Evidence = "Moreover, rapid muscle
denervation due to axonal loss and defects in axonal
transport is observed in cellular and animal models of ALS
in which HDAC6 also might play a role (18,59)."
SET MeSHDisease = "Amyotrophic Lateral Sclerosis"
p(HGNC:HDAC6) pos path(MESH:"Muscle Denervation")
p(HGNC:HDAC6) -| a(MESH:Axons)
a(MESH:Axons) neg path(MESH:"Muscle Denervation")
p(HGNC:HDAC6) neg bp(GO:"axonal transport")
UNSET MeSHDisease

SET Evidence = "Although the exact
link between mutant HSPB1 and HDAC6 remains
elusive, pharmacological inhibition of HDAC6 rescues the
phenotype of mutant HSPB1 mice and restores the axonal
transport defects (13)."
SET Species = "10090"
p(HGNC:HDAC6) -> p(HGNC:HSPB1,var("?"))
UNSET Species

SET Evidence = "Increased expression of HDAC6 is observed after injury
to neurons (66)."
path(MESH:"Brain Injuries") -> p(HGNC:HDAC6)
