#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Acetylation: a new key to unlock tau's role in neurodegeneration.."
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
 DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
 DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
 DEFINE NAMESPACE EFO            AS URL "https://raw.githubusercontent.com/pharmacome/conso/5d2c2ea6f7112421cc8089a3f021a75ab60bb373/external/efo-names.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
 DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
 DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/conso/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
 DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
 DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
# DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
 DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "25031639"}

SET Evidence = "Cohen and
collagues [4] utilized the acetyltransferase CREB-binding
protein (CBP) to acetylate a fragment of tau
comprising the microtubule-binding domain (commonly
referred to as K18), and observed an increase in aggregation
of the K18 fragment."
p(HGNC:CREBBP) -> p(HGNC:MAPT,frag("?"),pmod(Ac,Lys,18))
p(HGNC:MAPT,frag("?"),pmod(Ac,Lys,18)) -> p(HGNC:MAPT,frag("?"),pmod(Ac,Lys,18),pmod(CONSO:"protein aggregates"))

SET Evidence = "We subsequently performed a
similar analysis but using full-length tau and the acetyltransferase
p300; we detected a decrease in filament
assembly following tau acetylation, the extent of which
correlated with the concentration of p300 [11]"
p(HGNC:EP300) -> p(HGNC:MAPT,pmod(Ac))
p(HGNC:MAPT,pmod(Ac)) -| p(HGNC:MAPT,pmod(CONSO:"protein aggregates"))

SET Evidence = "We also
observed a complete reversal of p300-mediated acetylation
and inhibition of tau assembly upon addition of
the deacetylase histone deacetylase (HDAC)6 [11]."
p(HGNC:HDAC6) -| act(p(HGNC:EP300))
p(HGNC:HDAC6) -| p(HGNC:MAPT,pmod(Ac))
p(HGNC:HDAC6) -> p(HGNC:MAPT,pmod(CONSO:"protein aggregates"))

SET Evidence = "In addition, tau
hyperphosphorylation has been reported to lead to conformational
changes that decrease flexibility and affinity
for microtubules, thereby promoting accumulation of tau
in the cytosol and driving the formation of PHFs and
NFTs [18,24]."
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation)) -| act(p(HGNC:MAPT),ma(GO:"microtubule binding"))
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation)) -> p(CONSO:"Tau aggregates",loc(GO:cytosol))
p(CONSO:"Tau aggregates",loc(GO:cytosol)) -> p(MESH:"Neurofibrillary Tangles")
p(CONSO:"Tau aggregates",loc(GO:cytosol)) -> p(CONSO:"paired helical filaments")
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation)) -> p(MESH:"Neurofibrillary Tangles")
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation)) -> p(CONSO:"paired helical filaments")

SET Evidence = "The polymerization and accumulation of
hyperphosphorylated tau (p-tau) has also been linked
with impaired axonal transport and synaptic dysfunction,
two early events associated with the neuronal degeneration
observed in AD and other tauopathies [18,25]."
#polmerization : aggregates
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation)) neg bp(GO:"axonal transport")
p(CONSO:"Tau aggregates") neg bp(GO:"axonal transport")
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation)) neg bp(GO:"synaptic signaling")
p(CONSO:"Tau aggregates") neg bp(GO:"synaptic signaling")
path(DOID:"Alzheimer's disease") neg bp(GO:"axonal transport")
path(DOID:"Alzheimer's disease") neg bp(GO:"axonal transport")
path(DOID:tauopathy) -- bp(GO:"axonal transport")
path(DOID:tauopathy) -- bp(GO:"axonal transport")

SET Evidence = "Given
that acetylation of tau at K280 is highest at late stages of
disease, it is possible increased acetylation at this epitope
is a response to the disease process, rather than a pathogenic
mechanism responsible for tangle formation."
p(HGNC:MAPT,pmod(Ac,Lys,280)) pos path(DOID:"Alzheimer's disease")

SET Evidence = "We demonstrate that elevated HDAC6
activity increases phosphorylation of tau at the 12E8
epitope (pS262/356), a phospho-epitope present within
the KXGS motifs of tau’s microtubule-binding domain."
act(p(HGNC:HDAC6)) -> p(HGNC:MAPT,pmod(Ph,Ser,262))
act(p(HGNC:HDAC6)) -> p(HGNC:MAPT,pmod(Ph,Ser,356))
p(CONSO:"Tau epitope, 12E8") partOf p(CONSO:"KXGS motif")

SET Evidence = "The phosphorylation of KXGS motifs within tau by the
kinase Par-1/MARK2 is required for tau proteotoxicity in
Drosophila [29], observed at very early stages of NFT
formation in AD brain [30], and appears to prime tau for
subsequent phosphorylation events [29,31]."
SET Species = "7227"
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:MARK2) -> p(CONSO:"KXGS motif",pmod(Ph))
p(CONSO:"KXGS motif",pmod(Ph)) -> path(MESH:Tauopathies)
p(CONSO:"KXGS motif",pmod(Ph)) -> p(MESH:"Neurofibrillary Tangles")
UNSET MeSHDisease
UNSET Species

SET Evidence = "Tau species
phosphorylated on KXGS motifs are not recognized by
cellular degradation machinery, including the tau ubiquitin
ligase C-terminus of Hsc70 interacting protein (CHIP)
and the heat shock protein 70/90 (Hsp70/90) chaperone
complex [32,33], and are therefore particularly prone to
accumulate."
#STUB1 - CHIP
p(CONSO:"KXGS motif",pmod(Ph)) -| complex(p(HGNC:STUB1),p(HGNC:MAPT))
complex(p(HGNC:STUB1),p(HGNC:MAPT)) -> deg(p(HGNC:MAPT))
p(CONSO:"KXGS motif",pmod(Ph)) -| complex(p(HGNCGENEFAMILY:"Heat shock 70kDa proteins"),p(HGNC:MAPT))
complex(p(HGNCGENEFAMILY:"Heat shock 70kDa proteins"),p(HGNC:MAPT)) -> deg(p(HGNC:MAPT))
p(CONSO:"KXGS motif",pmod(Ph)) -| complex(p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"),p(HGNC:MAPT)))
complex(p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"),p(HGNC:MAPT))) -> deg(p(HGNC:MAPT))
p(CONSO:"KXGS motif",pmod(Ph)) -> p(CONSO:"Tau aggregates")

SET Evidence = "Given
that tau species phosphorylated on KXGS motifs are
resistant to degradation and accumulate in NFTs [30],
fail to bind and stabilize microtubules [36], and are also
primed for phosphorylation by other kinases [29,31],
hyperactivation of HDAC6 would be expected to considerably
enhance tau pathology."
p(CONSO:"KXGS motif",pmod(Ph)) -> p(CONSO:"Tau aggregates",loc(MESH:"Neurofibrillary Tangles"))
p(CONSO:"KXGS motif",pmod(Ph)) -| act(p(HGNC:MAPT),ma(GO:"microtubule binding"))
p(HGNC:HDAC6) pos path(DOID:tauopathy)

SET Evidence = "As mentioned above, this acetylation
decreases the ability of tau to aggregate in in vitro assays
[11]."
p(HGNC:MAPT,pmod(Ac)) -| p(CONSO:"Tau aggregates")

SET Evidence = "We also detect a competitive relationship between
acetylation and phosphorylation on KXGS motifs, such
that treatment with HDAC6 inhibitors simultaneously
increases acetylation of tau, while blocking phosphorylation
at these crucial motifs in mice [11]."
SET Species = "10090"
a(MESH:"Histone Deacetylase Inhibitors") -> p(HGNC:MAPT,pmod(Ac))
UNSET Species

SET Evidence = "Furthermore,
KXGS motifs are hypoacetylated and hyperphosphorylated
in patients with AD and in a progressive and
well-characterized mouse model of tauopathy (rTg4510)
[11,37]."
#cell line - rTg4510
SET Species = "10090"
path(DOID:"Alzheimer's disease") -> p(CONSO:"KXGS motif",pmod(Ph))
path(DOID:"Alzheimer's disease") -| p(CONSO:"KXGS motif",pmod(Ac))
UNSET Species

SET Evidence = "In particular,
previous studies have demonstrated that the tau ubiquitin
ligase, CHIP, is unable to bind and ubiquitinate tau
species phosphorylated by Par-1/MARK2 on the 12E8
epitope (S262/356) [33], a p-tau species that is also
resistant to degradation upon treatment with Hsp90
inhibitors [32,33]."
p(HGNC:MARK2) -> p(HGNC:MAPT,pmod(Ph,Ser,262))
p(HGNC:MARK2) -> p(HGNC:MAPT,pmod(Ph,Ser,365))
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| complex(p(HGNC:STUB1),p(HGNC:MAPT,pmod(Ph,Ser,262)))
p(HGNC:MAPT,pmod(Ph,Ser,365)) -| complex(p(HGNC:STUB1),p(HGNC:MAPT,pmod(Ph,Ser,356)))
p(HGNC:STUB1) -> complex(p(HGNC:STUB1),p(HGNC:MAPT))
complex(p(HGNC:STUB1),p(HGNC:MAPT)) -> p(HGNC:MAPT,pmod(Ub))
p(HGNC:STUB1) -> p(HGNC:MAPT,pmod(Ub))
p(HGNC:MAPT,pmod(Ub)) -> deg(p(HGNC:MAPT))
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| deg(p(HGNC:MAPT))
p(HGNC:MAPT,pmod(Ph,Ser,262)) -| deg(p(HGNC:MAPT))

SET Evidence = "Tau phosphorylated at the PHF1
epitope (S396/404) is still susceptible to degradation
following Hsp90 inhibition and actually exhibits an
enhanced interaction with Hsp90 [33]."
complex(p(HGNC:MAPT,pmod(Ph,Ser,396)),p(HGNCGENEFAMILY:"Heat shock 90kDa proteins")) -> p(HGNC:MAPT)
complex(p(HGNC:MAPT,pmod(Ph,Ser,404)),p(HGNCGENEFAMILY:"Heat shock 90kDa proteins")) -> p(HGNC:MAPT)
p(HGNC:MAPT,pmod(Ph,Ser,396)) -> complex(p(HGNC:MAPT,pmod(Ph,Ser,396)),p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"))
p(HGNC:MAPT,pmod(Ph,Ser,404)) -> complex(p(HGNC:MAPT,pmod(Ph,Ser,404)),p(HGNCGENEFAMILY:"Heat shock 90kDa proteins"))

SET Evidence = "HDAC6 disrupts
this cascade by potentiating Par-1/MARK2-mediated
phosphorylation of KXGS motifs (detected by the 12E8
antibody), an effect that is eliminated by pseudoacetylation
of KXGS motifs [11]."
p(HGNC:HDAC6) -> act(p(HGNC:MARK2))
act(p(HGNC:MARK2)) -> p(CONSO:"KXGS motif",pmod(Ph))
p(HGNC:HDAC6) -| p(CONSO:"KXGS motif",pmod(Ac))

SET Evidence = "In addition, our recent findings
indicate that HDAC6 directly modulates tau polymerization
and acetylation, and this relationship is dependent on
the ability of HDAC6 to deacetylate tau specifically on
KXGS motifs [11]."
p(HGNC:HDAC6) reg p(HGNC:MAPT,pmod(MESH:Polymerization))
p(HGNC:HDAC6) reg p(HGNC:MAPT,pmod(Ac))

SET Evidence = "These results support the hypothesis
that decreased HDAC6 activity increases acetylation of
KXGS motifs and, in so doing, prevents phosphorylation
of serine residues within the same motif."
act(p(HGNC:HDAC6)) -> p(CONSO:"KXGS motif",pmod(Ph))
p(CONSO:"KXGS motif",pmod(Ac)) -| p(CONSO:"KXGS motif",pmod(Ph))
act(p(HGNC:HDAC6)) -| p(CONSO:"KXGS motif",pmod(Ac))

SET Evidence = "Given that progressive hypoacetylation
and hyperphosphorylation of KXGS motifs is
observed in rTg4510 mice with aging [11], the fact that
tau turnover also decreases with aging in rTg4510 mice
[43] may indicate that the relationship between acetylation
and phosphorylation on KXGS motifs regulates tau
turnover."
#rTg4510
bp(GO:aging) -| p(CONSO:"KXGS motif",pmod(Ac))
bp(GO:aging) -> p(CONSO:"KXGS motif",pmod(CONSO:hyperphosphorylation))
bp(GO:aging) -| p(HGNC:MAPT)

SET Evidence = "While hyperphosphorylated tau is ubiquitinated in
patients with AD [38,44] (Figure 1), indicating that
pathological tau species may be successfully targeted
for degradation, the accumulation of ubiquitinated tau
species in those patients suggests dysfunction of either
proteasomal or lysosomal degradation pathways contributes
to NFT formation in disease."
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation),pmod(Ub)) -> p(HGNC:MAPT,pmod(Ub))
p(HGNC:MAPT,pmod(Ub)) neg bp(GO:"proteasomal protein catabolic process")
p(HGNC:MAPT,pmod(Ub)) neg bp(GO:"lysosomal protein catabolic process")
p(HGNC:MAPT,pmod(Ub)) -> p(MESH:"Neurofibrillary Tangles")
UNSET MeSHDisease

SET Evidence = "Given that aggregated tau within NFTs is ubiquitinated
[38,44], it is possible that excessive ubiquitination of tau
actually prevents acetylation, exacerbating tau aggregation."
p(HGNC:MAPT,pmod(Ub),pmod(CONSO:"Tau aggregates")) -| p(HGNC:MAPT,pmod(Ac))

SET Evidence = "Specifically, recent evidence indicates that acetylation of
tau on KXGS motifs under conditions of HDAC6 inhibition
not only prevents aggregation, but also blocks
phosphorylation on this same motif, thereby favoring
tau clearance [11]."
p(HGNC:HDAC6) -| p(CONSO:"KXGS motif",pmod(Ac))
p(CONSO:"KXGS motif",pmod(Ac)) -| p(CONSO:"Tau aggregates")
p(CONSO:"KXGS motif",pmod(Ac)) -| p(HFNC:MAPT,pmod(Ph))
p(CONSO:"KXGS motif",pmod(Ac)) -> deg(p(HGNC:MAPT))

SET Evidence = "Based on recent evidence that HDAC6 regulates tau
acetylation on KXGS motifs, it is of particular interest
that, in a Drosophila model of tauopathy, loss of HDAC6
activity rescued tau-induced microtubule defects in both
neuronal and muscle cells [45]."
SET Species = "7227"
SET Cell = {"neuron","muscle cell"}
act(p(HGNC:HDAC6)) neg act(complex(GO:microtubule),ma(GO:"microtubule motor activity"))
p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation)) neg act(complex(GO:microtubule),ma(GO:"microtubule motor activity"))
UNSET Cell
UNSET Species

SET Evidence = "For instance, in a
mouse model of AD, genetic ablation of HDAC6 alleviated
cognitive impairment without impacting plaque burden,
which may suggest that beneficial consequences of loss
of HDAC6 expression are due to effects on endogenous
tau, though this has not yet been assessed in this model
[47]."
SET Species = "10090"
SET MeSHDisease = "Alzheimer Disease"
g(HGNC:HDAC6) pos bp(GO:cognition)
g(HGNC:HDAC6) cnc a(MESH:"Plaque, Amyloid")
UNSET MeSHDisease
UNSET Species

SET Evidence = "Deletion of HDAC6 in a mouse model of mutant
superoxide dismutase 1-linked amyotrophic lateral sclerosis
is also neuroprotective, as reflected by the extended
life span of mice and increased motor axon integrity [48]."
SET Species = "10090"
SET MeSHDisease = "Amyotrophic Lateral Sclerosis"
composite(p(HGNC:HDAC6),p(HGNC:SOD1,var("?"))) -| path(MESH:Neuroprotection)
composite(p(HGNC:HDAC6),p(HGNC:SOD1,var("?"))) -| bp(MESH:"Cell Survival")
composite(p(HGNC:HDAC6),p(HGNC:SOD1,var("?"))) -| bp(GO:"motor neuron axon guidance")
UNSET MeSHDisease
UNSET Species

SET Evidence = "The decrease in tubulin acetylation
and increased HDAC6 observed in patients with AD and
other tauopathies is indicative of a disrupted microtubular
network, which would be expected to contribute to the
pathophysiological changes associated with disease progression
[50,52,53]."
path(DOID:"Alzheimer's disease") -| p(INTERPRO:"Alpha tubulin",pmod(Ac))
path(DOID:"Alzheimer's disease") -> act(p(HGNC:HDAC6))
p(INTERPRO:"Alpha tubulin",pmod(Ac)) pos a(MESH:Microtubules)
act(p(HGNC:HDAC6)) neg a(MESH:Microtubules)
a(MESH:Microtubules) neg path(DOID:"Alzheimer's disease")

SET Evidence = "Another recent report identified a
decrease in microtubule stability in rTg4510 mice [54],
and also verified that treatment with the microtubule stabilizing
compound epothilone D (EpoD) decreased
tau burden and cognitive deficits [54]."
#rTg4510 mice
SET Species = "10090"
a(CHEBI:"epothilone D") pos a(MESH:Microtubules)
a(CHEBI:"epothilone D") pos bp(GO:cognition)
a(CHEBI:"epothilone D") -| p(HGNC:MAPT,pmod(CONSO:hyperphosphorylation))
UNSET Species

SET Evidence = "Zhang and colleagues
[55] also reported that treatment with EpoD
not only effectively decreased tau pathology in another
tau transgenic mouse model (PS19 mice), but also
increased axonal microtubular density."
#PS19 cells
SET Species = "10090"
a(CHEBI:"epothilone D") -> a(MESH:Microtubules,loc(GO:Axons))
UNSET Species

SET Evidence = "While tubulin acetylation is a marker of microtubule
stability [56], increased tau acetylation may allow
tau to dissociate from stabilized microtubules, providing
molecular motors greater access to microtubules and facilitating
axonal transport."
p(INTERPRO:"Alpha tubulin",pmod(Ac)) pos bp(GO:"microtubule cytoskeleton organization")
p(HGNC:MAPT,pmod(Ac)) -| path(EFO:"microtubule-associated protein tau")
p(HGNC:MAPT,pmod(Ac)) -> deg(complex(a(MESH:Microtubules),p(HGNC:MAPT)))
p(HGNC:MAPT,pmod(Ac)) -> a(MESH:Microtubules)
p(HGNC:MAPT,pmod(Ac)) -> bp(GO:"axonal transport")

SET Evidence = "Given that phosphorylation, which prevents
acetylation, within KXGS motifs has also been reported to
release tau from microtubules [36], this event would be
expected to uncouple the coordinated regulation of
tubulin and tau acetylation, further contributing to the
pathogenicity of this particular p-tau species."
p(CONSO:"KXGS motif",pmod(Ph)) -| p(CONSO:"KXGS motif",pmod(Ac))
p(CONSO:"KXGS motif",pmod(Ph)) -> deg(complex(a(MESH:Microtubules),p(HGNC:MAPT)))
deg(complex(a(MESH:Microtubules),p(HGNC:MAPT))) reg p(HGNC:MAPT,pmod(Ac))
deg(complex(a(MESH:Microtubules),p(HGNC:MAPT))) reg p(INTERPRO:"Alpha tubulin",pmod(Ac))
p(CONSO:"KXGS motif",pmod(Ph)) -- p(HGNC:MAPT,pmod(Ac))
p(CONSO:"KXGS motif",pmod(Ph)) -- p(INTERPRO:"Alpha tubulin",pmod(Ac))
p(CONSO:"KXGS motif",pmod(Ph)) -> path(DOID:tauopathy)

SET Evidence = "Wu and colleagues
[64] observe internalization of misfolded tau at the level
of both dendritic and axonal terminals in neurons, after
which pathologic tau species can be transported in either
the antero- or retrograde direction, thereby leading to
the spreading of pathology."
p(HGNC:MAPT,pmod(CONSO:misfolded),loc(GO:"dendrite terminus")) -> bp(GO:"anterograde dendritic transport")
bp(GO:"anterograde dendritic transport") -> tloc(p(HGNC:MAPT,pmod(CONSO:misfolded)),fromLoc(GO:cytoplasm),toLoc(MESH:Neurons))
p(HGNC:MAPT,pmod(CONSO:misfolded),loc(GO:"dendrite terminus")) -> bp(GO:"retrograde dendritic transport")
bp(GO:"retrograde dendritic transport") -> tloc(p(HGNC:MAPT,pmod(CONSO:misfolded)),fromLoc(GO:cytoplasm),toLoc(MESH:Neurons))
p(HGNC:MAPT,pmod(CONSO:misfolded),loc(GO:"axon terminus")) -> bp(GO:"anterograde axonal transport")
bp(GO:"anterograde axonal transport") -> tloc(p(HGNC:MAPT,pmod(CONSO:misfolded)),fromLoc(GO:cytoplasm),toLoc(MESH:Neurons))
p(HGNC:MAPT,pmod(CONSO:misfolded),loc(GO:"axon terminus")) -> bp(GO:"retrograde axonal transport")
bp(GO:"retrograde axonal transport") -> tloc(p(HGNC:MAPT,pmod(CONSO:misfolded)),fromLoc(GO:cytoplasm),toLoc(MESH:Neurons))
tloc(p(HGNC:MAPT,pmod(CONSO:misfolded)),fromLoc(GO:cytoplasm),toLoc(MESH:Neurons)) -> path(DOID:tauopathy)
p(HGNC:MAPT,pmod(CONSO:misfolded),loc(MESH:Neurons)) -> path(DOID:tauopathy)

SET Evidence = "In addition, injection of
brain material from mice that express human mutant
P301S tau into transgenic mice expressing human wildtype
tau (ALZ17 model) was sufficient to induce tau
pathology not only within, but also adjacent to, the injection
site along anatomically connected pathways [58]."
#ALZ17 cell line
SET Species = "10090"
SET MeSHAnatomy = "Brain"
p(HGNC:MAPT,var("p.Pro301Ser")) -> path(DOID:tauopathy)
UNSET MeSHAnatomy
UNSET Species

SET Evidence = "Our recent findings, which
demonstrate that acetylation within tau’s KXGS motifs
generates a tau species that fails to polymerize [11],
suggests that augmenting acetylation of the KXGS
motifs would also decrease tau seeding capacity."
p(CONSO:"KXGS motif",pmod(Ac)) -| p(CONSO:"Tau aggregates")
p(CONSO:"KXGS motif",pmod(Ac)) -| path(DOID:tauopathy)
