###############################
# Document Properties Section #
###############################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved"
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT Authors     = "Sepehr Golriz, Daniel Domingo-Fernández, Charles Tapley Hoyt"
SET DOCUMENT ContactInfo = "sepehr.golriz.khatami@scai-extern.fraunhofer.de"

SET DOCUMENT Name        = "Neuroimaging Biomarkers"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Description = "Biomarkers and neuroimaging features related to Alzheimer's disease"

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/conso
# --------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/3c93a65dc48e6786fe29ae55aa36cf9a902fe8c6/export/conso-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/go-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/conso/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/conso/d9d270e11aac480542c412d4222983a5f042b8ae/external/hp-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/conso/f02c6ad4a4791a8ed45448513b9de8c8f1b00c87/external/mesh-names.belns"
DEFINE NAMESPACE NIFT            AS URL "https://raw.githubusercontent.com/neurommsig/neurommsig-terminology/c45db68bb47b28023ec134049429320da6a9d631/terminologies/neuroimaging-feature-terminology.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP    AS PATTERN "^rs\d+$"

###############
# Annotations #
###############

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"

##############
# Statements #
##############

###################
SET Citation = {"PubMed", "25024322"}

SET Evidence = "We detected statistically significant differences between Prod-AD patients and controls.
 YKL-40 levels showed a significant correlation with t-tau and p-tau levels in the predementia AD
continuum and the Pre-AD group. We also observed significant correlations with the MMSE, FCSRT,
and M@T tests within the AD continuum, but not in iRBD subjects."

SET Confidence = "Medium"
SET Subgraph = {"Inflammatory response subgraph", "Tau protein subgraph"}
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:CHI3L1) positiveCorrelation p(HGNC:MAPT)
p(HGNC:CHI3L1) positiveCorrelation p(HGNC:MAPT, pmod(P))
p(HGNC:CHI3L1) positiveCorrelation a(CONSO:"CDR - Sum of Boxes")
p(HGNC:CHI3L1) positiveCorrelation a(NIFT:"Free and Cued Selective Reminding Test")
p(HGNC:CHI3L1) positiveCorrelation a(CONSO:"Mini-Mental Status Exam")
#M@T (memory association test, not found in CTO)

UNSET Confidence

UNSET ALL
##################
SET Citation = {"PubMed", "25865441"}

SET Evidence = "Cerebrospinal fluid YKL-40 has been described as a marker of glial inflammation. We aimed to study the relationship between YKL-40 and brain structure and its interactions with core Alzheimer's disease (AD) biomarkers. We measured cortical thickness (CTh) and cerebrospinal fluid biomarkers (amyloid-beta 1-42 [Abeta42], total tau, p-tau, and YKL-40) of 80 cognitively normal controls and 27 patients with amnestic mild cognitive impairment. Subjects were classified as Abeta42+ (<550 pg/mL) or Abeta42- (>550 pg/mL). CTh difference maps were derived from the interaction and correlation analyses in the whole sample and within clinical groups. There was a strong correlation between YKL-40 and markers of neurodegeneration (total tau and p-tau). In the whole sample, we found a negative correlation between YKL-40 and CTh in AD vulnerable areas in Abeta42+ subjects but not in Abeta42 participants. Our results suggest that YKL-40 could track the inflammatory processes associated to tau-related neurodegeneration in the presence of the AD pathophysiological process."

SET Confidence = "Medium"
SET Subgraph = "Inflammatory response subgraph"
p(HGNC:CHI3L1) biomarkerFor path(MESH:Inflammation)
p(HGNC:CHI3L1) positiveCorrelation p(HGNC:MAPT)
p(HGNC:CHI3L1) positiveCorrelation p(HGNC:MAPT, pmod(P))
p(HGNC:CHI3L1) negativeCorrelation a(NIFT:"Cortical Thickness")
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "25865441"}

SET Evidence = "There was a strong correlation between YKL-40 and markers of neurodegeneration (total tau and p-tau). In the whole sample, we found a negative correlation between YKL-40 and CTh in AD vulnerable areas in Abeta42+ subjects but not in Abeta42 participants. Our results suggest that YKL-40 could track the inflammatory processes associated to tau-related neurodegeneration in the presence of the AD pathophysiological process."

SET Confidence = "Medium"
SET Subgraph = "Inflammatory response subgraph"
p(HGNC:CHI3L1) positiveCorrelation p(HGNC:MAPT)
p(HGNC:CHI3L1) positiveCorrelation p(HGNC:MAPT, pmod(P))
p(HGNC:CHI3L1) negativeCorrelation a(NIFT:"Cortical Thickness")
p(HGNC:CHI3L1) -> bp(GO:"inflammatory response")
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "26876488"}

SET Evidence = "Higher levels of BDNF promoter methylation may be closely associated with the reduced cortical thickness among patients with MDD."

SET Subgraph = "Axonal transport subgraph"
p(HGNC:BDNF) -| a(NIFT:"Cortical Thickness")

#p(HGNC:BDNF) -- g(DBSNP:rs6265)

UNSET ALL
########################
SET Citation = {"PubMed", "26235064"}

SET Evidence = "The microtubule-associated protein tau plays a critical role in the pathogenesis of Alzheimer's disease (AD) and several related disorders collectively known as tauopathies.Development of tau pathology is associated with progressive neuronal loss and cognitive decline."

SET Confidence = "High"
SET Subgraph = "Tau protein subgraph"
p(HGNC:MAPT) -- bp(GO:"neuron death")
p(HGNC:MAPT) -- bp(GO:cognition)
####neuron death-> cortical thickness
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "25862420"}

SET Evidence = "In conclusion, TOMM40 VL variants are related to impairment in allocentric spatial navigation and reduced cortical thickness of specific brain regions among aMCI individuals with (LOAD neutral) APOE Îµ3/Îµ3 genotype. This may reflect a specific role of TOMM40 in the pathogenesis of LOAD."

SET Subgraph = "Mitochondrial translocation subgraph"
p(HGNC:TOMM40) -| a(NIFT:"Cortical Thickness")
g(DBSNP:rs10524523) -- a(NIFT:"Cortical Thinning")

UNSET Subgraph

UNSET ALL
########################
SET Citation = {"PubMed", "24479631"}

SET Evidence = "The current findings show that, similar to ApoE-ε4 non-carriers, BChE-K carriers are protected from the pathological detriments of AD that affect frontal cortical thickness and neuropsychiatric symptoms."

p(HGNC:BCHE) -> a(NIFT:"Cortical Thinning")

SET Evidence = "BChE wild-type carriers (BChE-W) showed more thinning in the left dorsolateral prefrontal cortex, including the lateral premotor regions and anterior cingulate cortex, than did BChE-K variant carriers (BChE-K). ApoE-ε4 carriers had a thinner left medial prefrontal cortex, left superior frontal cortex, and left posterior cingulate cortex than did ApoE-ε4 non-carriers. "

p(HGNC:BCHE) -> a(NIFT:"Cortical Thinning")

UNSET ALL
########################
SET Citation = {"PubMed", "24080518"}

SET Evidence = "APOE-4 carriers showed a thinner entorhinal cortex in the left hemisphere when compared with the right hemisphere across all participants. Non-carriers of the allele showed this asymmetry only in the patient group. Cortical thickness in the hippocampus did not vary between hemispheres among APOE-4 allele carriers and non-carriers."

p(HGNC:APOE) -- a(NIFT:"Cortical Thinning")

UNSET ALL
########################
SET Citation = {"PubMed", "22426017"}

SET Evidence = "These differences in Abeta(42) deposition were not significantly reflected in the brain structure, and CTh was only correlated with total tau. The lack of significant differences in relation to t-tau and p-tau levels and to the severity of CTh or grey matter loss suggests a similar level of neuronal injury despite higher Abeta(42) load in PSEN1."

SET Subgraph = {"Notch signaling subgraph", "Gamma secretase subgraph"}

SET Confidence = "Medium"
p(HGNC:PSEN1) -> bp(GO:"neuron death")
bp(GO:"neuron death") -> a(NIFT:"Cortical Thinning")
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "26027813"}

SET Evidence = "In addition, based on timing of changes in cortical thinning and Abeta deposition such as Abeta deposition after cortical thinning; cortical thinning after Abeta deposition, or concurrent Abeta deposition and cortical thinning, we identified three types of relationships between cortical thinning and Abeta deposition: (1) Abeta-associated cortical thinning; (2) Abeta-independent cortical thinning; and (3) Abeta deposition only without cortical thinning. Taken together, these findings suggest that Abeta-weighted cortical thickness values can be used as an objective biomarker of structural changes caused by amyloid pathology in the brain."
SET Confidence = "Medium"
SET Subgraph = "Amyloidogenic subgraph"
p(HGNC:APP) -> a(CHEBI:"amyloid-beta")
a(CHEBI:"amyloid-beta") -- a(NIFT:"Cortical Thickness")
a(NIFT:"Cortical Thinning") -> path(MESH:"Alzheimer Disease")
UNSET Confidence
UNSET Subgraph

UNSET ALL
########################
SET Citation = {"PubMed", "25570489"}

SET Evidence = "After false discovery rate correction, we observed 53 significant associations between SNPs and our imaging features, including associations of ventricular enlargement with SNPs on estrogen receptor 1 (ESR1) and sortilin-related VPS10 domain containing receptor 1 (SORCS1), hippocampal atrophy with SNPs on ESR1, and cerebral atrophy with SNPs on transferrin (TF) and amyloid beta precursor protein (APP"

p(HGNC:TF) -> a(NIFT:"Cerebral atrophy")

SET Evidence = "After false discovery rate correction, we observed 53 significant associations between SNPs and our imaging features, including associations of ventricular enlargement with SNPs on estrogen receptor 1 (ESR1) and sortilin-related VPS10 domain containing receptor 1 (SORCS1), hippocampal atrophy with SNPs on ESR1, and cerebral atrophy with SNPs on transferrin (TF) and amyloid beta precursor protein (APP)."

SET Subgraph = {"Apoptosis signaling subgraph", "Estrogen subgraph"}

SET Confidence = "High"
p(HGNC:ESR1) -- a(NIFT:"Ventricular expansion")
UNSET Confidence

UNSET Subgraph

SET Subgraph = "Low density lipoprotein subgraph"

SET Confidence = "High"
p(HGNC:SORCS1) -- a(NIFT:"Ventricular expansion")
UNSET Confidence

SET Subgraph = {"Apoptosis signaling subgraph", "Estrogen subgraph"}

SET Confidence = "High"
p(HGNC:ESR1) -- a(NIFT:"Hippocampal atrophy")
UNSET Confidence

UNSET Subgraph

p(HGNC:TF) -- a(NIFT:"Cerebral atrophy")

SET Subgraph = "Amyloidogenic subgraph"
SET Confidence = "High"
p(HGNC:APP) -- a(NIFT:"Cerebral atrophy")
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "3484831"}

SET Evidence = "There were significant correlations between cerebral atrophy as assessed by VBR and CSF AChE activity. Cognitive impairment on MMSE also correlated significantly with VBR. "

a(NIFT:"Cerebral atrophy") positiveCorrelation p(HGNC:ACHE)

UNSET ALL
########################
SET Citation = {"PubMed", "25027412"}

SET Evidence = "The absence of interset statistical differences allowed us to perform joint and conjunction analyses. Independent voxel-based morphometry analysis of the Spanish set as well as conjunction and joint analyses revealed substantial gray matter loss in orbitofrontal cortex and anterior cingulate cortex with relative preservation of parietal lobes in AD and/or mild cognitive impairment TREM2 p.R47H carriers, suggesting that TREM2 p.R47H variant is associated with certain clinical and neuroimaging AD features in addition to the increased TREM2 p.R47H atrophy in temporal lobes as described previously."

SET Confidence = "Medium"
SET Subgraph = "Inflammatory response subgraph"
p(HGNC:TREM2, sub(R,47,H)) -> a(NIFT:"Temporal lobe atrophy")
UNSET Subgraph
UNSET Confidence

g(DBSNP:rs75932628) -> a(NIFT:"Temporal lobe atrophy")

UNSET ALL
########################
SET Citation = {"PubMed", "23533697"}

SET Evidence = "The purpose of this paper is to discuss these recent developments including the potential role that TREM2 normally plays and how loss of function may contribute to AD pathogenesis by enhancing oxidative stress and inflammation within the CNS. In this context, an overview of the pathways linking beta-amyloid, neurofibrillary tangles (NFTs), oxidative stress, and inflammation will be discussed."

SET Confidence = "Medium"
SET Subgraph = {"Inflammatory response subgraph", "Response to oxidative stress"}
p(HGNC:TREM2) -| bp(GO:"response to oxidative stress")
p(HGNC:TREM2) -| path(MESH:Inflammation)
UNSET Confidence

SET Subgraph = "Response to oxidative stress"

SET Confidence = "High"
bp(GO:"response to oxidative stress") -| a(NIFT:"Cerebral blood flow")
UNSET Confidence
UNSET Subgraph

a(NIFT:"Cerebral blood flow") -> a(NIFT:"Glucose hypometabolism")
a(NIFT:"Glucose hypometabolism") -> a(NIFT:"Temporal lobe atrophy")

UNSET ALL
########################
SET Citation = {"PubMed", "23533697"}

SET Evidence = "Recently, a novel variant in the gene encoding the triggering receptor expressed on myeloid cells 2 (TREM2) has been identified that has refocused the spotlight back onto inflammation as a major contributing factor in AD. TREM2 is expressed on microglial cells, the resident macrophages in the CNS, and functions to stimulate phagocytosis on one hand and to suppress cytokine production and inflammation on the other hand. The purpose of this paper is to discuss these recent developments including the potential role that TREM2 normally plays and how loss of function may contribute to AD pathogenesis by enhancing oxidative stress and inflammation within the CNS."

SET Confidence = "Medium"
SET Subgraph = "Inflammatory response subgraph"
p(HGNC:TREM2) -- bp(GO:"inflammatory response")

SET MeSHAnatomy= {"Microglia", "Macrophages"}

p(HGNC:TREM2) -> bp(GO:phagocytosis)
p(HGNC:TREM2) -| bp(GO:"cytokine production")
p(HGNC:TREM2) -| bp(GO:"inflammatory response")

UNSET MeSHAnatomy

p(HGNC:TREM2) -> path(MESH:"Alzheimer Disease")

SET Subgraph = {"Inflammatory response subgraph", "Response to oxidative stress"}
p(HGNC:TREM2) -| bp(GO:"response to oxidative stress")
UNSET Subgraph
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "25027412"}

SET Evidence = "Independent voxel-based morphometry analysis of the Spanish set as well as conjunction and joint analyses revealed substantial gray matter loss in orbitofrontal cortex and anterior cingulate cortex with relative preservation of parietal lobes in AD and/or mild cognitive impairment TREM2 p.R47H carriers, suggesting that TREM2 p.R47H variant is associated with certain clinical and neuroimaging AD features in addition to the increased TREM2 p.R47H atrophy in temporal lobes as described previously. The high frequency of pathologic behavioral symptoms, combined with a preferential frontobasal gray matter cortical loss, suggests that frontobasal and temporal regions could be more susceptible to the deleterious biological effects of the TREM2 variant p.R47H."

SET Confidence = "Medium"
SET Subgraph = "Inflammatory response subgraph"
p(HGNC:TREM2, sub(R,47,H)) -> a(NIFT:"Temporal lobe atrophy")
p(HGNC:TREM2, sub(R,47,H)) -> a(NIFT:"Grey matter volume")
UNSET Confidence

UNSET Subgraph
SET Citation = {"PubMed", "25999473"}

SET Evidence = "beta level reduction by CALHM1 could be explained by an increase in extracellular Abeta degradation by insulin-degrading enzyme (IDE), extracellular secretion of which was strongly potentiated by CALHM1 activation."

SET Subgraph = {"Insulin signal transduction", "Calcium-dependent signal transduction"}
SET Confidence = "Medium"
p(HGNC:CALHM1) -> sec(p(HGNC:IDE))
p(HGNC:IDE) -> deg(a(CHEBI:"amyloid-beta"))
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "18585350"}

SET Evidence = "We show that CALHM1 encodes a multipass transmembrane glycoprotein that controls cytosolic Ca(2+) concentrations and Abeta levels. CALHM1 homomultimerizes, shares strong sequence similarities with the selectivity filter of the NMDA receptor, and generates a large Ca(2+) conductance across the plasma membrane. Importantly, we determined that the CALHM1 P86L polymorphism (rs2986017) is significantly associated with AD in independent case-control studies of 3404 participants (allele-specific OR = 1.44, p = 2 x 10(-10)). We further found that the P86L polymorphism increases Abeta levels by interfering with CALHM1-mediated Ca(2+) permeability. We propose that CALHM1 encodes an essential component of a previously uncharacterized cerebral Ca(2+) channel that controls Abeta levels and susceptibility to late-onset AD."

SET Subgraph = "Calcium-dependent signal transduction"
p(HGNC:CALHM1) -> path(MESH:"Alzheimer Disease")
UNSET Subgraph

g(DBSNP:rs2986017) -> bp(GO:"neuron death")
g(DBSNP:rs2986017) ->a(NIFT:"Hippocampal atrophy")
g(DBSNP:rs2986017) -> p(HGNC:APP)
g(DBSNP:rs2986017) -> bp(GO:"calcium ion homeostasis")

UNSET ALL
########################
SET Citation = {"PubMed", "20100581"}

SET Evidence = "Detailed image analyses of rs6463843 (flanking NXPH1) revealed reduced global and regional GM density across diagnostic groups in TT relative to GG homozygotes. Interaction analysis indicated that AD patients homozygous for the T allele showed differential vulnerability to right hippocampal GM density loss. NXPH1 codes for a protein implicated in promotion of adhesion between dendrites and axons, a key factor in synaptic integrity, the loss of which is a hallmark of AD. "

g(DBSNP:rs6463843) -- p(HGNC:NXPH1)
g(DBSNP:rs6463843) -| a(NIFT:"Grey matter volume")
a(NIFT:"Grey matter volume") -- path(MESH:"Alzheimer Disease")
### how to encode synaptic integrity ###
p(HGNC:NXPH1) -- path(MESH:"Alzheimer Disease")


SET Evidence = "In the present study, we report that administration of double-stranded RNA, a classical inducer of interferon-gamma (IFN-gamma), causes rapid and persistent activation of microglia and astrocytes, as well as induction of interleukin-1beta (IL-beta) and nitric oxide synthase. In close temporal succession to glial activation, there is neurodegeneration, with neuron loss involving apoptosis in selected brain regions including the septal nucleus, hippocampus, cortex and thalamus, along with hippocampal atrophy. This neuronal loss is accompanied by punctate deposits of material that are immunoreactive for amyloid precursor protein, beta-amyloid peptide (Abeta), and apolipoprotein E."

SET Subgraph = "Interferon signaling subgraph"

SET Confidence = "High"
p(HGNC:IFNG) -> bp(GO:"microglial cell activation")
p(HGNC:IFNG) -> bp(GO:"astrocyte activation")
UNSET Confidence

SET Subgraph = {"Nitric oxide subgraph", "Interferon signaling subgraph"}

SET Confidence = "High"
p(HGNC:IFNG) -- p(HGNC:NOS1)
UNSET Confidence

SET Subgraph = "Apoptosis signaling subgraph"

SET Confidence = "High"
bp(GO:"microglial cell activation") -- bp(GO:"apoptotic process")
bp(GO:"apoptotic process")-- a(NIFT:"Hippocampal atrophy")
p(HGNC:APP) -- bp(HP:Neurodegeneration)
UNSET Confidence

UNSET Subgraph

UNSET ALL
########################
SET Citation = {"PubMed", "24524964"}

SET Evidence = "Multiple linear regression analyses showed that plasma total homocysteine level was significantly associated with hippocampal volume even after controlling the degree of global cerebral beta amyloid deposition and vascular burden as well as other potential confounders including age, gender, education, and apolipoprotein E ε4 genotype."

a(CHEBI:"homocysteine") -- a(NIFT:"Hippocampus volume")

UNSET ALL
########################
SET Citation = {"PubMed", "21504134"}

SET Evidence = "Higher education and lower MMSE scores were correlated with reduced CTH in the AD group, whereas no significant correlation was found between CDR-SB scores or APOE genotype and CTH."

SET Disease = "Alzheimer's disease"

a(NIFT:"Mini mental state exam") positiveCorrelation a(NIFT:"Cortical Thinning")
#a(CTO:"Clinical Dementia Rating Scale sum of boxes") negativeCorrelation a(NIFT:"Cortical thickness")
p(HGNC:APOE) negativeCorrelation a(NIFT:"Cortical Thickness")

UNSET Disease

UNSET ALL
########################
SET Citation = {"PubMed", "10936883"}

SET Evidence = "Lipoprotein lipase (LPL) assists lipid transport by transferring lipids between lipoprotein particles and cells. LPL binds apolipoprotein E (apoE) lipoprotein particles and a major apoE receptor, low density lipoprotein receptor related protein (LRP). Because apoE and LRP polymorphisms alter Alzheimer's disease (AD) risk, and LPL itself is found in AD amyloid plaques, we examined whether LPL variants also affect AD risk."

p(HGNC:LPL) -> bp(GO:"lipid transport")
p(HGNC:LPL) -> complex(p(HGNC:APOE), p(HGNC:LRP1B))
g(HGNC:APOE) -- path(MESH:"Alzheimer Disease")
#SET Disease = {"Plaque, Amyloid"}
p(HGNC:LPL) -> path(MESH:"Alzheimer Disease")
#UNSET Disease

SET Evidence = "These trends are consistent with a putative protective effect of 447Ter and causative effect of 291Ser on AD. Furthermore, brains of AD patients with 447Ter showed trends toward fewer plaques, tangles, and glia, and more neurons and cortical thickness than AD patients without 447Ter. Hippocampal plaques were significantly reduced. LPL might affect hippocampal function and thus dementia via its role as supplier of membrane components or antioxidants to neurons. Alternatively, LPL may play a part in plaque formation through its interaction with apoE and LRP."

SET Confidence = "Medium"
p(HGNC:LPL, var("p.447Ter")) -| complex(GO:"neurofibrillary tangle")
p(HGNC:LPL, var("p.447Ter")) -> a(NIFT:"Cortical Thickness")
UNSET Confidence

# MESH:D006624 ! Hippocampus
p(HGNC:LPL) -> a(MESH:Hippocampus)
a(MESH:Hippocampus) -> path(MESH:"Dementia")
complex(p(HGNC:LPL), p(HGNC:APOE), p(HGNCGENEFAMILY:"Low density lipoprotein receptors")) -> path(MESH:"Plaque, Amyloid")


UNSET ALL
########################
SET Citation = {"PubMed", "20100581"}

SET Evidence = "Detailed image analyses of rs6463843 (flanking NXPH1) revealed reduced global and regional GM density across diagnostic groups in TT relative to GG homozygotes. Interaction analysis indicated that AD patients homozygous for the T allele showed differential vulnerability to right hippocampal GM density loss. NXPH1 codes for a protein implicated in promotion of adhesion between dendrites and axons, a key factor in synaptic integrity, the loss of which is a hallmark of AD. "

p(HGNC:NXPH1) -> a(NIFT:"Grey matter atrophy")
g(DBSNP:rs6463843) -> a(NIFT:"Grey matter atrophy")
p(HGNC:NXPH1) -> a(NIFT:"Grey matter atrophy")

UNSET ALL
########################
SET Citation = {"PubMed", "19592509"}

SET Evidence = "ur results suggest that the interaction between neuronal EphA4 and glial ephrin-A3 bidirectionally controls synapse morphology and glial glutamate transport, ultimately regulating hippocampal function."

p(HGNC:EPHA4) -> a(NIFT:"Grey matter volume")

UNSET ALL
########################
SET Citation = {"PubMed", "25678559"}

SET Evidence = "At baseline, amyloid-beta positive patients with mild cognitive impairment showed increased hippocampal activation, smaller hippocampal volumes, and a trend towards lower Mini-Mental State Examination scores and higher Clinical Dementia Ratings compared to amyloid-beta negative patients with mild cognitive impairment."

SET Confidence = "High"
SET Subgraph = "Amyloidogenic subgraph"
p(HGNC:APP) -> a(NIFT:"Hippocampal Formation")
p(HGNC:APP) negativeCorrelation a(NIFT:"Hippocampus volume")
p(HGNC:APP) negativeCorrelation a(NIFT:"Mini mental state exam")
p(HGNC:APP) positiveCorrelation a(NIFT:"Clinical dementia rating")
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "20065247"}

SET Evidence = "The strong correlation between hippocampal atrophy and beta-amyloid (Abeta) burden in the Pittsburgh compound B-positive healthy control group suggests that Abeta deposition in the inferior temporal neocortex is related to hippocampal synaptic and neuronal degeneration"

SET Subgraph = "Amyloidogenic subgraph"
SET Confidence = "High"
p(HGNC:APP) positiveCorrelation a(NIFT:"Hippocampal atrophy")
UNSET Confidence
UNSET Subgraph

UNSET ALL
########################
SET Citation = {"PubMed", "24028866"}

SET Evidence = "We also showed an association between high homocysteine levels and severe cortical-subcortical and hippocampal atrophy. The role of folate, which is inversely associated with the severity of brain atrophy, was confirmed. Our results also confirm the association between high homocysteine levels and severe cortical-subcortical and hippocampal atrophy. Auto-CM ANN is able to highlight associations sometimes visible only in longitudinal studies through intelligent data mining of a cross-sectional study."

a(CHEBI:"homocysteine") positiveCorrelation a(NIFT:"Hippocampal atrophy")

UNSET ALL
########################
SET Citation = {"PubMed", "21547229"}

SET Evidence = "In the EPHA4 gene, rs10932886 was correlated with gray matter density in the left precuneus and bilateral frontal regionsâ€”regions in which atrophy occurs in late AD."

p(HGNC:EPHA4) -> g(DBSNP:rs10932886)

g(DBSNP:rs10932886) positiveCorrelation a(NIFT:"Grey matter volume")
g(DBSNP:rs10932886) -> a(NIFT:"Grey matter atrophy")

UNSET ALL
########################
SET Citation = {"PubMed", "3122646"}

SET Evidence = "The prominence of white matter staining suggests that tau may be especially enriched in projection systems. Alzheimer's disease causes massive axonal disruption and the dislocation of tau protein from its usual axonal domain into neuronal cell bodies, dendrites, and presynaptic regions. The normal pattern of axonal staining in cortex is disrupted and white matter staining is reduced. Prominent abnormal tau-immunoreactive neuropil fibers are densely present even in cortical regions without classical neurofibrillary tangle and senile plaque formation. The striking neuropil abnormalities, revealed by the aberrant localization of tau protein, are likely to contribute to neuronal dysfunction in Alzheimer's disease."

SET Confidence = "High"
SET Subgraph = "Tau protein subgraph"
p(HGNC:MAPT) -- a(NIFT:"White matter")
SET MeSHAnatomy = "Neurons"
path(MESH:"Alzheimer Disease") -> tloc(p(HGNC:MAPT), GO:"axon", GO:"cell body")
UNSET Subgraph
UNSET MeSHAnatomy
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "19940480"}

SET Evidence = "A trend for larger brain and isthmus cingulate volumes and smaller hippocampal and ventricular volumes with increasing epsilon4 dose were seen."

SET Subgraph = "APOE subgraph"
p(HGNC:APOE, var(p.Cys130Arg)) pos a(NIFT:"Brain volume")
p(HGNC:APOE, var(p.Cys130Arg)) neg a(NIFT:"Hippocampus volume")
p(HGNC:APOE, var(p.Cys130Arg)) neg a(NIFT:"Ventricular volume")

UNSET Subgraph
UNSET Evidence

SET Evidence = "BChE wild-type carriers (BChE-W) showed more thinning in the left dorsolateral prefrontal cortex, including the lateral premotor regions and anterior cingulate cortex, than did BChE-K variant carriers (BChE-K). ApoE-ε4 carriers had a thinner left medial prefrontal cortex, left superior frontal cortex, and left posterior cingulate cortex than did ApoE-ε4 non-carriers. "

p(HGNC:BCHE) -> a(NIFT:"Cortical Thinning")

UNSET Evidence
UNSET Citation

UNSET ALL
########################
SET Citation = {"PubMed", "3484831"}

SET Evidence = "There were significant correlations between cerebral atrophy as assessed by VBR and CSF AChE activity. Cognitive impairment on MMSE also correlated significantly with VBR."

SET Subgraph = "Acetylcholine signaling subgraph"

SET Confidence = "High"
p(HGNC:ACHE) positiveCorrelation a(NIFT:"Cerebral atrophy")
UNSET Confidence

UNSET Subgraph

UNSET ALL
########################
SET Citation = {"PubMed", "22711244"}

SET Evidence = "e identified nominally significant associations between rs3748348 and expression levels of three genes (FLJ10357, RNASE2, and NDRG2). The strongest association was for FLJ10357 in cortex, which also had the most significant difference in expression between AD and non-AD brains, with greater expression in cortex of decedents with AD (p-value=7x10âˆ’7)."

p(HGNC:ARHGEF40) positiveCorrelation a(NIFT:"Cortical Thickness")

p(HGNC:ARHGEF40) -- g(DBSNP:rs3748348)


UNSET ALL
########################
SET Citation = {"PubMed", "10449594"}

SET Evidence = "In DAT patients, the number of regions with hypoperfusion correlated significantly with the score of the Mini Mental State Examination (MMSE, r = 0.33, p < 0.001). "

a(NIFT:Hypoperfusion) positiveCorrelation a(NIFT:"Mini mental state exam")


UNSET ALL
########################
SET Citation = {"PubMed", "16151770"}

SET Evidence = "On MP-SPECT, the AD group showed a significant rCBF reduction in the left parietal cortex in comparison with healthy elderly. At rest, no significant group differences were seen."

SET MeSHAnatomy = "Parietal Lobe"

path(MESH:"Alzheimer Disease") negativeCorrelation a(NIFT:"Cerebral blood flow")

UNSET MeSHAnatomy

UNSET ALL
########################
SET Citation = {"PubMed", "23300102"}

SET Evidence = "PET and SPECT studies have demonstrated decreased glucose metabolism and cerebral blood flow in the fronto-parietal cortex, particularly contralateral to the dominant symptoms in CBS patients. "

SET MeSHAnatomy = "Parietal Lobe"
path(MESH:"Alzheimer Disease") negativeCorrelation a(NIFT:"Cerebral blood flow")
path(MESH:"Alzheimer Disease") negativeCorrelation bp(GO:"glucose metabolic process")

UNSET MeSHAnatomy

UNSET ALL
########################
SET Citation = {"PubMed", "16340157"}

SET Evidence = "Consistent with this novel view, substantial evidences suggest that depressive illness is not a mere neurochemical disease, but is linked to gray matter atrophy due to the reduced number/size of neurons and glia in brain. "

path(MESH:Depression) positiveCorrelation a(NIFT:"Grey matter atrophy")
# MESH:D009474 ! Neurons
a(NIFT:"Grey matter atrophy") negativeCorrelation a(MESH:Neurons)
a(NIFT:"Grey matter atrophy") negativeCorrelation a(MESH:Neurons)

SET Evidence = "Chronic treatment of lithium, in agreement with the delayed-onset of mood-stabilizing effects of lithium, up-regulates cell survival molecules (e.g., Bcl-2, cyclic AMP-responsive element binding protein, brain-derived neurotrophic factor, Grp78, Hsp70, and beta-catenin). the neurogenesis is impaired by multiple not-fully defined factors (e.g., aging, chronic stress-induced increase of glucocorticoids, and excitotoxicity), accounting for brain atrophy in patients with depressive illness and neurodegenerative diseases."

SET Subgraph = "Chaperone subgraph"
a(CHEBI:"lithium atom") -> p(HGNC:HSPA5)
p(HGNC:HSPA5) -> bp(GO:"neurogenesis")
a(NIFT:"Grey matter atrophy") -| bp(GO:"neurogenesis")

UNSET ALL
########################
SET Citation = {"PubMed", "21673407"}

SET Evidence = "The 5-HT4 receptor may play a role in memory and learning and 5-HT4 receptor activation has been suggested to modulate acetylcholine release and to reduce amyloid-beta (Abeta) accumulation."

p(HGNC:HTR4) -- bp(GO:memory)
p(HGNC:HTR4) -- bp(GO:learning)
p(HGNC:HTR4) -- sec(a(CHEBI:acetylcholine))
p(HGNC:HTR4) -| p(HGNC:APP, frag(672_713))

SET Evidence = "The aim of this study was for the first time to investigate the in vivo cerebral 5-HT4 receptor binding in early Alzheimer disease (AD) patients in relation to cortical Abeta burden. Eleven newly diagnosed untreated AD patients (mean MMSE 24, range 19-27) and twelve age- and gender-matched healthy controls underwent a two-hour dynamic [11C]SB207145 PET scan to measure the binding potential of the 5-HT4 receptor. All AD patients and eight healthy controls additionally underwent a [11C]PIB PET scan to measure the cortical Abeta burden. When AD patients were defined on clinical criteria, no difference in cerebral 5-HT4 receptor binding between AD patients and healthy controls was found (p = 0.54). However, when individuals were reassigned to groups according to their amyloid status, the PIB-positive individuals had 13% higher 5-HT4 receptor levels than PIB-negative individuals (p = 0.02) and the importance of classification of groups is emphasized. The 5-HT4 receptor binding was a positively correlated to Abeta burden (p = 0.03) and negatively to MMSE score of the AD patients (p = 0.02). Our data suggests that cerebral 5-HT4 receptor upregulation starts at a preclinical stage of and continues while dementia is still at a mild stage, which contrasts other receptor subtypes. We speculate that this may either be a compensatory effect of decreased levels of interstitial 5-HT, an attempt to improve cognitive function, increase acetylcholine release or to counteract Abeta accumulation."

complex(a(NIFT:"Pittsburgh Compound B"),p(HGNC:HTR4)) positiveCorrelation p(HGNC:APP, frag(672_713))

UNSET ALL
########################
SET Citation = {"PubMed", "26836182"}

SET Evidence = "Microglial activation plays a role in clearance of cellular debris and beta-amyloid; however, the associated inflammatory cascade may also contribute to neural damage"

SET Disease = "Alzheimer's disease"
SET Subgraph = "Inflammatory response subgraph"
SET Confidence = "High"
bp(GO:"microglial cell activation") positiveCorrelation path(CONSO:neuroinflammation)
path(MESH:"Alzheimer Disease") positiveCorrelation path(CONSO:neuroinflammation)
UNSET Confidence

SET Evidence = "Higher CSF YKL-40 was associated with both higher CSF NFL and T-Tau."
# YKL-40 = CHI3L1
SET Confidence = "High"
SET Subgraph = {"Axonal transport subgraph", "Inflammatory response subgraph"}
p(HGNC:NEFL) positiveCorrelation p(HGNC:CHI3L1)
UNSET Confidence

SET Confidence = "High"
SET Subgraph = {"Tau protein subgraph", "Inflammatory response subgraph"}
p(HGNC:CHI3L1) positiveCorrelation p(HGNC:MAPT)
UNSET Confidence

SET Evidence = "In turn, YKL-40 is produced by activated microglia and has been previously associated with reduced cortical thickness and increased CSF total tau (T-Tau) protein in preclinical AD"

SET Subgraph = "Inflammatory response subgraph"
SET Confidence = "Medium"
bp(GO:"microglial cell activation") -> p(HGNC:CHI3L1)
p(HGNC:CHI3L1) negativeCorrelation a(NIFT:"Cortical Thickness")
p(HGNC:CHI3L1) positiveCorrelation p(HGNC:MAPT)
UNSET Confidence

SET Evidence = "Elevated neuroinflammation as shown by higher CSF YKL-40, was associated with higher CSF levels of NFL"

SET Confidence = "Medium"
SET Subgraph = {"Axonal transport subgraph", "Inflammatory response subgraph"}
p(HGNC:CHI3L1) positiveCorrelation path(CONSO:neuroinflammation)
p(HGNC:CHI3L1) -> p(HGNC:NEFL)
UNSET Confidence

SET Evidence = "monocyte chemoattractant protein-1 (MCP-1) in CSF were utilized as markers of neuroinflammation"
# MCP-1 = CCL2

SET Subgraph = {"Chemokine signaling subgraph", "Inflammatory response subgraph"}
SET Confidence = "High"
path(CONSO:neuroinflammation) positiveCorrelation p(HGNC:CCL2)
UNSET Confidence


UNSET ALL
########################
SET Citation = {"PubMed", "23183136"}

SET Evidence = "Thirty-two presenilin 1 (PSEN1) mutation carriers with AD, 27 presenilin 2 (PSEN2) mutation carriers with AD, 59 participants with late-onset AD (LOAD), and 168 autopsied subjects from a community-based cohort were genotyped for TOMM40 intron 6 poly-T (rs10524523) length using short tandem repeat assays. TOMM40 intron 6 poly-T length may explain some of the variation in age at onset in PSEN2 familial AD and may be associated with AD neuropathology in persons with APOE ε3/ε3."

SET Subgraph = "Gamma secretase subgraph"
p(HGNC:PSEN1, pmod(M,L,146)) -- g(DBSNP:rs10524523)
p(HGNC:PSEN1, pmod(M,L,146)) -| a(NIFT:"Cortical Thickness")


UNSET ALL
########################
SET Citation = {"PubMed", "22550605"}

SET Disease= "Alzheimer's disease"

SET Evidence = "In conclusion, influence of APOE on GM structural integrity has been consistently demonstrated in areas associated with preclinical AD."

SET Confidence = "Medium"
p(HGNC:APOE) -> a(NIFT:"Grey matter atrophy")
UNSET Confidence

SET Evidence = "In AD specifically, it has been hypothesized that mitochondria exert neurotoxic influence by allowing the influx of Abeta to the cell via the Tom40 import pore. Passage of Abeta through the Tom40 import pore increases Reactive Oxygen Species (ROS) within the organelle. This increase is detrimental for mitochondrial survival and energy production (ATP), ultimately resulting in apoptotic processes of the cell"

SET Subgraph = "AD T2DM SNPs"

SET Confidence = "Medium"
p(HGNC:TOMM40) -> tloc(p(HGNC:APP, frag(672_713)), fromLoc(GO:"extracellular region"), toLoc(GO:intracellular))
UNSET Confidence

p(HGNC:APP, frag(672_713)) -> a(CHEBI:"reactive oxygen species")

SET Evidence = "Overall, the studies that are available today point to a selective influence of TOMM40 polymorphisms on structural changes in AD vulnerable areas such as the HC, precuneus and posterior cingulate cortex."

SET Confidence = "Medium"
p(HGNC:TOMM40, var("?")) -- path(MESH:"Alzheimer Disease")
p(HGNC:TOMM40, var("?")) -- a(NIFT:"Hippocampus volume")
p(HGNC:TOMM40, var("?")) -- a(NIFT:Precuneus)
UNSET Confidence

UNSET Disease

UNSET ALL
########################
SET Citation = {"PubMed",  "26996954"}

SET Evidence = "Sortilin receptor 1 (SORL1) is involved in cellular trafficking of amyloid precursor protein and plays an essential role in amyloid-beta peptide generation in Alzheimer disease (AD). The major A allele in a SORL1 single nucleotide polymorphism (SNP), rs3824968, is associated with an increased AD risk. However, the role of SORL1 rs3824968 in the normal ageing process has rarely been examined in relation to brain structural morphology. This study investigated the association between SORL1 rs3824968 and grey matter (GM) volume in a nondemented Chinese population of 318 adults within a wide age range (21-92 years). Through voxel-based morphometry, we found that participants carrying SORL1 allele A exhibited significantly smaller GM volumes in the right posterior cingulate, left middle occipital, medial frontal, and superior temporal gyri. Considerable interaction between age and SORL1 suggested a detrimental and accelerated ageing effect of allele A on putamen. These findings provide evidence that SORL1 rs3824968 modulates regional GM volume and is associated with brain trajectory during the adult lifespan."

### cellular traffiking how to encode###

SET Subgraph = "Low density lipoprotein subgraph"

SET Confidence = "High"
p(HGNC:SORL1) -- p(HGNC:APP)
UNSET Confidence

SET Confidence = "Medium"
path(MESH:"Alzheimer Disease") -> p(HGNC:APP, frag(672_713))
UNSET Confidence

SET Confidence = "High"
p(HGNC:SORL1) -- g(DBSNP:"rs3824968")
p(HGNC:SORL1) -- a(NIFT:"Grey matter volume")
UNSET Confidence

SET Evidence = "SORL1 regulates and traps APP in the Golgi apparatus and reduces the production of the amyloid-beta peptide (Abeta), the main component of senile plaques in AD4. Cell biology experiments have indicated that SORL1 underexperssion leads to overproduction of Abeta3 , whereas SORL1 overexpression considerably reduces cellular APP and extracellular Abeta5. Although the underlying mechanisms of SORL1 in AD and neurodegenerative processes remain unclear, a recent comprehensive review on multiple functional domains of SORL1 suggested that SORL1 can assume various characters in AD pathogenesis"

SET CellStructure = "Golgi Apparatus"

SET Subgraph = "Low density lipoprotein subgraph"

SET Confidence = "High"
p(HGNC:SORL1) -| p(HGNC:APP)
p(HGNC:APP) -| p(HGNC:APP, frag(672_713))
UNSET Confidence

UNSET CellStructure

SET Confidence = "High"
p(HGNC:SORL1) -> p(HGNC:APP, frag(672_713))
p(HGNC:SORL1) -| p(HGNC:APP)
p(HGNC:APP) -| p(HGNC:APP, frag(672_713))
UNSET Confidence

SET Evidence = "SORL1 rs3824968 A allele carriers had lower GM volume in the left superior temporal, left medial frontal, right posterior cingulate, and left middle occipital gyri than did those carrying TT. A considerable interaction between age and the SORL1 genotypes was observed, showing an accelerated (downward) slope of GM volume differences in the right putamen of the participants carrying the AD-risk (A) allele compared with those carrying TT. Thus, our findings support the hypothesis that SORL1 rs3824968 is associated with regional GM volume differences in early adulthood and can influence ageing-related GM morphological changes during the nondemented adult lifespan."

SET MeSHAnatomy= {"Temporal Lobe", "Gyrus Cinguli", "Frontal Lobe", "Occipital Lobe"}

SET Subgraph = "Low density lipoprotein subgraph"

SET Confidence = "High"
g(HGNC:SORL1) -- g(DBSNP:rs3824968)
UNSET Confidence

SET Confidence = "High"
g(DBSNP:rs3824968) negativeCorrelation a(NIFT:"Grey matter volume")
UNSET Confidence

UNSET MeSHAnatomy

SET Evidence = "SORL1 rs3824968 variants may influence amyloid pathology during ageing. Greater amyloid burden has been shown to be positively associated with regional GM atrophy26,27,28, particularly in the superior temporal, medial frontal, and cingulate gyri in both structural and functional MRI studies29,30. In addition to the hypothesis of amyloid pathology, Cuenco et al.31 proposed another viewpoint that SORL1 increases dementia risk through effects on cerebrovascular abnormalities; SORL1 rs3824968 and its haplotypes were found to be associated with brain volume and WM hyperintensities in their study. This biological and neuroimaging evidence may suggest that SORL1 rs3824968 is associated with brain morphology in terms of A allele-related volumetric reduction caused by high Abeta load, cardiovascular disease, or both. However, additional studies are necessary to explore the potential mechanisms of SORL1 rs3824968 on the cellular and molecular determinants of brain structural differences in both demented and nondemented individuals."

SET Subgraph = "Low density lipoprotein subgraph"


SET Confidence = "High"
g(HGNC:SORL1) -- g(DBSNP:rs3824968)
UNSET Confidence

SET Confidence = "Medium"
g(DBSNP:rs3824968) -- path(MESH:"Amyloid Neuropathies")
a(CHEBI:"amyloid-beta") positiveCorrelation a(NIFT:"Grey matter atrophy")
UNSET Confidence

SET Confidence = "High"
g(HGNC:SORL1) -- path(MESH:"Cerebrovascular Disorders")
UNSET Confidence

SET Confidence = "High"
path(MESH:"Cerebrovascular Disorders") -> path(MESH:"Dementia")
UNSET Confidence

SET Confidence = "High"
g(DBSNP:rs3824968) -- a(NIFT:"Brain volume")
UNSET Confidence

SET Confidence = "High"
g(DBSNP:rs3824968) -- a(NIFT:"White Matter Hyperintensity")
UNSET Confidence

SET Confidence = "Medium"
g(DBSNP:rs3824968) -> act(a(CHEBI:"amyloid-beta"))
UNSET Confidence

SET Confidence = "High"
g(DBSNP:rs3824968) -- path(MESH:"Cerebrovascular Disorders")
UNSET Confidence

UNSET ALL
########################
SET Citation = {"PubMed", "21871088"}

SET Evidence = "Alzheimer's disease (AD) is a progressive neurodegenerative disease characterized by insidious cognitive decline and memory dysfunction. Synapse loss is the best pathological correlate of cognitive decline in AD and mounting evidence suggests that AD is primarily a disease of synaptic dysfunction. Soluble oligomeric forms of amyloid beta, the peptide that aggregates to form senile plaques in the brain of AD patients, have been shown to be toxic to neuronal synapses both in vitro and in vivo. amyloid beta oligomers inhibit long-term potentiation (LTP) and facilitate long-term depression (LTD), electrophysiological correlates of memory formation. Furthermore, oligomeric amyloid beta has also been shown to induce synapse loss and cognitive impairment in animals. The molecular underpinnings of these observations are now being elucidated, and may provide clear therapeutic targets for effectively treating the disease. Here, we review recent findings concerning AD pathogenesis with a particular focus on how amyloid beta impacts synapses."

SET Subgraph = "Synapse assembly subgraph"

SET Confidence = "Medium"
a(CONSO:"amyloid-beta oligomers") -| bp(GO:"long-term synaptic potentiation")
a(CONSO:"amyloid-beta oligomers") -> bp(GO:"long-term synaptic depression")
a(CONSO:"amyloid-beta oligomers") -| bp(GO:"synaptic signaling")
a(CONSO:"amyloid-beta oligomers") -> path(MESH:"Cognitive Dysfunction")
UNSET Confidence

SET Evidence = "Oligomeric amyloid beta has been shown to inhibit LTP and enhance LTD by modulating the activity of all of the above molecular pathways. Several studies have shown that oligomeric amyloid beta induces partial blockade of NMDA receptor currents, which leads to reduction of calcium influx into spines promoting LTD over LTP.VI. Synapse potentiation or depression depends on the rate of influx of calcium as well as the level of cytosolic calcium. LTP occurs when rapid and high levels of calcium influx occur through NMDA receptors, whereas LTD is favored when low level calcium influx through NMDA receptors occurs."

SET Subgraph = "Glutamatergic subgraph"

bp(GO:"long-term synaptic potentiation") -| act(p(HGNCGENEFAMILY:"Glutamate ionotropic receptor NMDA type subunits"))
act(p(HGNCGENEFAMILY:"Glutamate ionotropic receptor NMDA type subunits")) -> bp(GO:"calcium ion import")
bp(GO:"calcium ion import") -> bp(GO:"long-term synaptic depression")
bp(GO:"long-term synaptic depression") -- bp(GO:"calcium ion import")

SET Confidence = "Medium"
bp(GO:"long-term synaptic depression") -- bp(GO:"cellular calcium ion homeostasis")
UNSET Confidence

bp(GO:"long-term synaptic potentiation") -- bp(GO:"calcium ion import")

SET Confidence = "Medium"
bp(GO:"long-term synaptic potentiation") -- bp(GO:"cellular calcium ion homeostasis")
UNSET Confidence


SET Evidence = "Microglial MVs are neurotoxic and myelinotoxic in the presence of Amyloid beta . CSF myeloid MVs, mirroring microglia activation and MV release, are associated with WM damage in MCI and hippocampal atrophy in AD. This suggests that hippocampal microglia activation, in the presence of Amyloid beta in excess, produces neurotoxic and oligodendrotoxic oligomers that, through WM tract damage, spread disease to neighboring and connected areas, causing local microglia activation and propagation of disease through the same sequence of events"

SET MeSHAnatomy= {"Hippocampus", "Microglia"}

###Microglial MVs### positiveCorrealtion bp (GOBP = "microglial cell activation")
path(MESH:"Plaque, Amyloid") -> bp(GO:"microglial cell activation")
bp(GO:"microglial cell activation") -- a(NIFT:"Hippocampal atrophy")
bp(GO:"microglial cell activation") -- a(NIFT:"White matter lesion")

UNSET MeSHAnatomy

UNSET ALL
########################
SET Citation = {"PubMed", "21543606"}

SET Evidence = "There is a strong genetic risk for late-onset AlzheimerÃ¢â‚¬â„¢s disease (AD), but so far few gene variants have been identified that reliably contribute to that risk. A newly confirmed genetic risk allele C of the clusterin (CLU) gene variant rs11136000 is carried by approximately 88% of Caucasians. The C allele confers a 1.16 greater odds of developing late-onset AD than the T allele. AD patients have reductions in regional white matter integrity. We evaluated whether the CLU risk variant was similarly associated with lower white matter integrity in healthy young humans. Evidence of early brain differences would offer a target for intervention decades before symptom onset. We scanned 398 healthy young adults (mean age 23.6 Ã‚Â± 2.2 years) with diffusion tensor imaging (DTI), a variation of magnetic resonance imaging (MRI) sensitive to white matter integrity in the living brain. We assessed genetic associations using mixed model regression at each point in the brain to map the profile of these associations with white matter integrity. Each C allele copy of the CLU variant was associated with lower fractional anisotropy (FA) - a widely accepted measure of white matter integrity- in multiple brain regions, including several known to degenerate in AD. These regions included the splenium of the corpus callosum, the fornix, cingulum, and superior and inferior longitudinal fasciculi in both brain hemispheres. Young healthy carriers of the CLU gene risk variant showed a distinct profile of lower white matter integrity that may increase vulnerability to developing AD later in life."

SET Subgraph = "Low density lipoprotein subgraph"

SET Confidence = "High"
p(HGNC:CLU) -- g(DBSNP:rs11136000)
UNSET Confidence

SET Confidence = "High"
g(DBSNP:rs11136000) -- path(MESH:"Alzheimer Disease")
UNSET Confidence

SET Confidence = "High"
path(MESH:"Alzheimer Disease") -| a(NIFT:"White matter")
UNSET Confidence

SET Anatomy= {"splenium of the corpus callosum", "fasciculus of brain"}

SET Confidence = "High"
p(HGNC:CLU) -| a(NIFT:"Fractional anisotropy")
UNSET Confidence

SET Confidence = "High"
a(NIFT:"Fractional anisotropy") -| a(NIFT:"White matter")
UNSET Confidence

UNSET Anatomy

SET Evidence = "Clusterin is found in amyloid plaques and transports soluble Amyloid beta across the blood-brain barrier into brain parenchyma Amyloid beta then may damage the oligodendrocytes, which generate myelin, as reported in vitro .However, in our young healthy sample, lower FA may reflect variability in lipid processing as the lipid-rich myelin sheath develops; it is unlikely to be evidence of a disease mechanism or a biomarker of AD. Myelin abnormalities and axonal swelling may contribute to synaptic loss and precede amyloid deposition in AD .If so, the CLU risk variant could increase AD risk in two ways, an early-acquired vulnerability paired with suboptimal amyloid processing in later life"

SET MeSHAnatomy= {"Oligodendroglia", "Blood-Brain Barrier"}

SET Confidence = "High"
p(HGNC:CLU) -- p(HGNC:APP, frag(672_713))
UNSET Confidence

# D009836 = Oligodendroglia

SET Confidence = "High"
# MESH:D009836 ! Oligodendroglia
p(HGNC:APP, frag(672_713)) -| a(MESH:Oligodendroglia)
UNSET Confidence

SET Confidence = "High"
a(MESH:Oligodendroglia) -| bp(GO:"myelination")
UNSET Confidence

SET Confidence = "High"
a(NIFT:"Fractional anisotropy") -- bp(GO:"regulation of lipid metabolic process")
UNSET Confidence

SET Confidence = "High"
bp(GO:"regulation of lipid metabolic process")-- bp(GO:"myelination")
UNSET Confidence

SET Confidence = "High"
bp(GO:"axonal defasciculation") -- bp(GO:"regulation of synaptic plasticity")
UNSET Confidence

SET Confidence = "High"
bp(GO:"myelination") -- bp(GO:"regulation of synaptic plasticity")
UNSET Confidence


UNSET ALL
########################
SET Citation = {"PubMed", "14970312"}

SET Evidence = " Alzheimer's disease (AD) is an age-related disorder characterized by deposition of Amyloid beta and degeneration of neurons in brain regions such as the hippocampus, resulting in progressive cognitive dysfunction. The pathogenesis of AD is tightly linked to Amyloid beta deposition and oxidative stress, but it remains unclear as to how these factors result in neuronal dysfunction and death. We report alterations in sphingolipid and cholesterol metabolism during normal brain aging and in the brains of AD patients that result in accumulation of long chain ceramides and cholesterol. Membrane associated oxidative stress occurs in association with the lipid alterations, and exposure of hippocampal neurons to Amyloid beta induces membrane oxidative stress and the accumulation of ceramide species and cholesterol. Treatment of neurons with alpha-tocopherol or an inhibitor of sphingomyelin synthesis prevents accumulation of ceramides and cholesterol and protects them against death induced by Amyloid beta. Our findings suggest a sequence of events in the pathogenesis of AD in which Amyloid beta induces membrane-associated oxidative stress, resulting in perturbed ceramide and cholesterol metabolism which, in turn, triggers a neurodegenerative cascade that leads to clinical disease."

SET Subgraph = "Low density lipoprotein subgraph"

SET MeSHAnatomy = "Hippocampus"

SET Confidence = "High"
path(MESH:"Alzheimer Disease") -> p(HGNC:APP, frag(672_713))
UNSET Confidence

SET Confidence = "High"
path(MESH:"Alzheimer Disease") -> bp(GO:"neuron death")
UNSET Confidence

UNSET MeSHAnatomy

SET Confidence = "High"
path(MESH:"Alzheimer Disease") -- bp(MESH:"Oxidative Stress")
UNSET Confidence

SET Confidence = "High"
path(MESH:"Alzheimer Disease") -- bp(GO:"sphingolipid metabolic process")
UNSET Confidence

SET Confidence = "High"
path(MESH:"Alzheimer Disease") -- bp(GO:"cholesterol metabolic process")
UNSET Confidence

SET Confidence = "High"
bp(GO:"sphingolipid metabolic process") -> a(CHEBI:ceramide)
UNSET Confidence

SET Confidence = "High"
bp(GO:"cholesterol metabolic process") -> a(CHEBI:"cholesterol")
UNSET Confidence

SET Confidence = "High"
bp(MESH:"Oxidative Stress") -- bp(GO:"regulation of lipid metabolic process")
UNSET Confidence

SET MeSHAnatomy = "Hippocampus"

SET Confidence = "High"
p(HGNC:APP, frag(672_713)) -> bp(MESH:"Oxidative Stress")
UNSET Confidence

SET Confidence = "High"
p(HGNC:APP, frag(672_713)) -> a(CHEBI:ceramide)
UNSET Confidence

SET Confidence = "High"
p(HGNC:APP, frag(672_713)) -> a(CHEBI:"cholesterol")
UNSET Confidence

SET Confidence = "High"
a(CHEBI:"alpha-tocopherol") -| bp(GO:"regulation of sphingomyelin catabolic process")
UNSET Confidence

SET Confidence = "High"
bp(GO:"regulation of sphingomyelin catabolic process") -| a(CHEBI:ceramide)
UNSET Confidence

SET Confidence = "High"
bp(GO:"regulation of sphingomyelin catabolic process") -| a(CHEBI:"cholesterol")
UNSET Confidence

SET Evidence = "Perturbed membrane lipid metabolism in AD may result, at least in part, from increased Amyloid beta production/deposition, and that the increased ceramide production resulting from the oxidative stress induced by Amyloid beta may trigger the death of neurons."

SET Confidence = "High"
p(HGNC:APP, frag(672_713)) -- bp(GO:"regulation of lipid metabolic process")
UNSET Confidence

SET Confidence = "High"
p(HGNC:APP, frag(672_713)) -> bp(MESH:"Oxidative Stress")
UNSET Confidence

SET Confidence = "High"
bp(MESH:"Oxidative Stress") -> a(CHEBI:ceramide)
UNSET Confidence

SET Confidence = "High"
a(CHEBI:ceramide) -> bp(GO:"neuron death")
UNSET Confidence

SET Confidence = "High"
bp(GO:"neuron death") -> a(NIFT:"Hippocampal atrophy")
UNSET Confidence

SET Evidence = "High levels of free cholesterol can be toxic to cells as demonstrated by the ability of inhibitors of ACAT (an enzyme that converts free cholesterol to cholesterol esters) to induce apoptotic process. "

SET Confidence = "High"
a(CHEBI:"cholesterol") -| a(CHEBI:"acyl-CoA")
UNSET Confidence

SET Confidence = "Medium"
a(CHEBI:"acyl-CoA") -| bp(GO:"neuron apoptotic process")
UNSET Confidence

SET Confidence = "High"
bp(GO:"neuron death") -> a(NIFT:"Hippocampal atrophy")
UNSET Confidence


UNSET ALL
########################
SET Citation = {"PubMed", "24189376"}

SET Evidence = "Eleven AD patients and 7 healthy volunteers (HV) underwent combined [Â¹â�¸F]MK-9470 PET and [Â¹Â¹C]PIB PET scans to assess CBâ‚�R availability and amyloid deposition, respectively, and T1 volumetric MRI for partial volume correction. We found no difference in CBâ‚�R availability between AD and HV, VOI-based fractional uptake values (FUR) were 0.043Â±0.01 for AD and 0.045Â±0.01 for controls (p=0.9). CBâ‚�R availability did not correlate with neuropsychological test scores and was not modulated by ApoE genotype. As expected, global [Â¹Â¹C]PIB SUVR (standardized uptake value ratio) was increased in AD (SUVR 1.9Â±0.3) compared to HV (1.2Â±0.1) with p<0.001, but no correlation was found between amyloid beta (Abeta) deposition and CBâ‚�R availability."

p(HGNC:CNR1) causesNoChange p(HGNC:APP, frag(672_713))

UNSET ALL
########################
