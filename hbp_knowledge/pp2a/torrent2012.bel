#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright Â© 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "PP2A and Alzheimer Disease"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/cfd2ee89d1a71126da8b5f452544b7fe43be67e7/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/73688d6dc24e309fca59a1340dc9ee971e9f3baa/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/a5b84013a08880975ca84f40999e4404b14a97e2/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

# Add statements below this comment

SET Citation = {"PubMed", "Curr Alzheimer Res. 2012 Feb;9(2):248-56.","22299660"}

SET Evidence = "The excessive accumulation of phosphate groups in tau is
associated with its altered capacity in promoting microtubule
assembly  and  stability  [4-6]."
p(HGNC:MAPT, pmod(HBP:hyperphosphorylation)) -| bp(GO:"microtubule polymerization")

SET Evidence = "PP2A is mainly pre-
sent as a soluble protein in the cytosol but it is also encoun-
tered  in  the  nucleus,  mitochondria,  cytoskeleton,  and  mem-
branes. "
p(FPLX:PPP2) -- a(MESH:Cytosol)
p(FPLX:PPP2) -- a(MESH:"Cell Nucleus")
p(FPLX:PPP2) -- a(MESH:Mitochondria)
p(FPLX:PPP2) -- a(MESH:Cytoskeleton)
p(FPLX:PPP2) -- a(MESH:"Cell Membrane")

SET Evidence = "PP2A is composed of a catalytic, a scaffolding and a
regulatory subunit that integrate as a trimer [21]."
p(FPLX:PPP2) hasComponents list(p(FPLX:PPP2C), p(FPLX:"PPP2R_A"), p(FPLX:"PPP2R_B"))

SET Evidence = "Within the brain, ACB55 (B55) is the major PP2A B iso-
form  and  it  binds  to  tau  with  high  affinity  both  in  in  vitro
protein-protein  interaction  paradigms  and  in  cell  cultures
[36, 37]. "
p(HGNC:PPP2R2A) -- a(MESH:Brain)
complex(p(HGNC:PPP2R2A),p(HGNC:MAPT))

SET Evidence = "So far, the most explored pathologies in which PP2A is
implicated are cancer, and some viral and parasitic diseases
[44].  More  recently,  PP2A  has  been  investigated  in  FTLD
linked to mutations in the tau gene [45]. "
p(FPLX:PPP2) -- path(MESH:Neoplasms)
p(FPLX:PPP2) -- path(MESH:"Frontotemporal Dementia")

SET Evidence = " Studies  in  transgenic  mice  and  in  cell  cultures  have
shown a connection between PP2A loss of function and tau
hyper-phosphorylation  and  aggregation  into  PHF. "
SET Species = "10090"
act(p(FPLX:PPP2)) neg p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
act(p(FPLX:PPP2)) neg a(HBP:"paired helical filaments")
UNSET Species

SET Evidence = "Trans-
genic  mice  with  reduced  PP2A  activity  show  increased  tau
phosphorylation  at  Ser202/Thr205  and  Ser42  [46]. "
SET Species = "10090"
act(p(FPLX:PPP2)) -| p(HGNC:MAPT, pmod(Ph,Ser,202), pmod(Ph,Thr,205))
act(p(FPLX:PPP2)) -| p(HGNC:MAPT, pmod(Ph,Ser,242))
UNSET Species

SET Evidence = "Similar
findings have been observed in metabolically active rat brain
slices, where a selective inhibition of PP2A with OA results
in  an  aberrant  phosphorylation  of  tau  at  the  same  residues
seen in AD brains at serines (Ser) 198, 199, 202, 396, 404,
422 and 262 [11, 47, 48]."
a(CHEBI:"okadaic acid") -| act(p(FPLX:PPP2))
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT, pmod(Ph,Ser,189))
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT, pmod(Ph,Ser,199))
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT, pmod(Ph,Ser,202))
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT, pmod(Ph,Ser,396))
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT, pmod(Ph,Ser,404))
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT, pmod(Ph,Ser,422))
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT, pmod(Ph,Ser,262))
p(HGNC:MAPT, pmod(Ph,Ser,189)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph,Ser,199)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph,Ser,202)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph,Ser,396)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph,Ser,404)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph,Ser,422)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph,Ser,262)) pos path(MESH:"Alzheimer Disease")

SET Evidence = "Further experiments based on the
injection of a PP2A inhibitor in the rat hippocampus demon-
strated tau hyper-phosphorylation, and learning and memory
deficits  [49,  50]. "
SET Species = "10116"
SET MeSHAnatomy = "Hippocampus"
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT, pmod(HBP:hyperphosphorylation))
a(CHEBI:"okadaic acid") -| bp(GO:learning)
a(CHEBI:"okadaic acid") -| bp(GO:memory)
UNSET Species
UNSET MeSHAnatomy

SET Evidence = "Moreover,  some  tau  kinases  as  cyclin-
dependent  kinase  5  (cdk5)  and  TPKI  (thiamine  pyrophos-
phokinase 1)/GSK3 (glycogen synthase kinase 3) are acti-
vated following PP2A inhibition in starved mice [51]."
SET Species = "10090"
act(p(FPLX:PPP2)) -| act(p(HGNC:CDK5))
act(p(FPLX:PPP2)) -| act(p(HGNC:TPK1))
act(p(FPLX:PPP2)) -| act(p(HGNC:GSK3B))
UNSET Species

SET Evidence = "Several observations showing reduced PP2A activity
by 30% in the frontal cortex in AD [55], were followed by a
number of studies of PP2A mRNA and proteins."
SET MeSHAnatomy = "Frontal Lobe"
path(MESH:"Alzheimer Disease") neg act(p(FPLX:PPP2))
UNSET MeSHAnatomy

SET Evidence = " Decreased
mRNA  levels  of  PP2A  C  have  been  reported  in  the  CA3
region  of  AD  hippocampus  by  in  situ  RNA  hybridisation
[56].  Moreover,  microarray  RNA  analysis  carried  out  to
compare  the  expression  of  more  than  7,000  gene  in  the
amygdala,  cingulate  cortex,  striatum  and  cerebellum  dis-
closed down-regulation of the catalytic subunit PP2A C in
AD [57]. This has been further corroborated by quantitative
TaqMan PCR showing reduced PP2A C mRNA expression
levels  in  the  hippocampus,  but  not  in  the  frontal  cortex,  in
AD  cases  with  disease  progression  Fig.  (1)."
SET MeSHAnatomy = "CA3 Region, Hippocampal"
path(MESH:"Alzheimer Disease") neg r(HGNC:PPP2CA)
path(MESH:"Alzheimer Disease") neg r(HGNC:PPP2CB)
SET MeSHAnatomy = {"Amygdala", "Gyrus Cinguli", "Corpus Striatum", "Cerebellum"}
path(MESH:"Alzheimer Disease") neg r(HGNC:PPP2CA)
SET MeSHAnatomy = "Hippocampus"
path(MESH:"Alzheimer Disease") neg r(HGNC:PPP2CA)
UNSET MeSHAnatomy

SET Evidence = "Analyses of protein expression by using gel electrophore-
sis and western blotting have shown not only a reduction of
PP2A  C  expression  levels  but  also  a  marked  reduction  of
B55, thus indicating that PP2A impairment is the result of
combined  effects  of  different  subunits  [60]."
path(MESH:"Alzheimer Disease") neg p(FPLX:PPP2C)
path(MESH:"Alzheimer Disease") neg p(HGNC:PPP2R2A)

SET Evidence = "Decreased  of
PP2A, but not of other phosphatases, has also been observed
in Down syndrome correlating with increased tau pathology. "
path(MESH:"Down Syndrome") neg p(FPLX:PPP2)

SET Evidence = " Car-
boxyl  methylation  of  the  catalytic  subunit  is  required  for
efficient in vivo assembly of the trimer C, A, and B [63-65];
a process that is balanced by the opposite actions of methyl-
transferase  type  IV  (PPMT)  and  the  methylesterase  PME-1
[66-70]. "
#PPMT is ICMT
p(FPLX:PPP2C, pmod(M)) -> complex(p(FPLX:PPP2C), p(FPLX:"PPP2R_A"), p(FPLX:"PPP2R_B"))
p(HGNC:ICMT) -> p(FPLX:PPP2C, pmod(M))
p(HGNC:PPME1) -| p(FPLX:PPP2C, pmod(M))

SET Evidence = "The other important post-translational modi-
fication is phosphorylation of PP2A C at Tyr307 which in-
hibits PP2A activity [74-76]. "
p(FPLX:PPP2C, pmod(Ph,Tyr,307)) -> act(p(FPLX:PPP2))

SET Evidence = " PP2A  C
methylation  at  Leu309  is  reduced  in  AD  and  this  seems  to
contribute to PP2A C dysfunction by impairing the assembly
of the trimer [77, 78]. "
p(FPLX:PPP2C, pmod(M,Leu,309)) neg path(MESH:"Alzheimer Disease")
p(FPLX:PPP2C, pmod(M,Leu,309)) -| complex(p(FPLX:PPP2C), p(FPLX:"PPP2R_A"), p(FPLX:"PPP2R_B"))

SET Evidence = " Complementing these findings, immu-
nohistochemical  and  western  blot  studies  have  shown  re-
duced  expression  of  PPMT  [77],  and  up-regulation  of  the
PP2A inhibitors I 1  and I 2  in AD cases [79, 80]. "
#I1 is ANP32A, I2 is SET
p(HGNC:ICMT) neg path(MESH:"Alzheimer Disease")
p(HGNC:ANP32A) -| act(p(FPLX:PPP2))
p(HGNC:SET) -| act(p(FPLX:PPP2))
p(HGNC:ANP32A) pos path(MESH:"Alzheimer Disease")
p(HGNC:SET) pos path(MESH:"Alzheimer Disease")

SET Evidence = " High  levels  of  PP2A  C  phosphorylated  at  Tyr307  have
been  reported  in  the  entorhinal  cortex,  hippocampus  and
frontal cortex in AD compared to controls [81]."
SET MeSHAnatomy = {"Entorhinal Cortex","Hippocampus", "Frontal Lobe"}
p(FPLX:PPP2C, pmod(Ph,Tyr,307)) pos path(MESH:"Alzheimer Disease")
UNSET MeSHAnatomy

SET Evidence = "Furthermore,
phosphorylated  PP2A  C  decorates  neurofibrillary  tangles. "
p(FPLX:PPP2C, pmod(Ph)) -- a(MESH:"Neurofibrillary Tangles")

SET Evidence = "To sum up, PP2A
activity  is  decreased  in  brain  of  AD,  as  revealed  by  using
different  approaches  in  different  laboratories. "
SET MeSHAnatomy = "Brain"
act(p(FPLX:PPP2)) neg path(MESH:"Alzheimer Disease")
UNSET MeSHAnatomy
