#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Protein phosphatase 2A dysfunction in Alzheimer’s disease"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/cfd2ee89d1a71126da8b5f452544b7fe43be67e7/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/73688d6dc24e309fca59a1340dc9ee971e9f3baa/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/a5b84013a08880975ca84f40999e4404b14a97e2/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

# Add statements below this comment

SET Citation = {"PubMed","24653673"}

SET Evidence = "Proteinphosphatase 2A catalytic subunit is uniquely methylated
on Leu-309 by the dedicated leucine carboxyl methyltransferase-1 (LCMT-1; Lee et al.,1996; De Baere et al.,1999)."
p(HGNC:LCMT1) -> p(HGNC:PPP2CA,pmod(Me,Leu,309))
p(HGNC:LCMT1) -> p(HGNC:PPP2CB,pmod(Me,Leu,309))

SET Evidence = "Like all methyltransferases,LCMT1 activity depends on the supply of
the universal methyl donor, S-adenosylmethionine (SAM),and is inhibited by S-adenosylhomocysteine
(SAH; Leulliot et al.,2004; Sontag et al.,2007)."
a(CHEBI:"S-adenosyl-L-methionine") -> act(p(HGNC:LCMT1))
a(CHEBI:"S-adenosyl-L-homocysteine") -| act(p(HGNC:LCMT1))

SET Evidence = "Conversely, the PP2A-specific methylesterase PME-1 can directly bind to the active site of the catalytic subunit,
remove the methyl group and inactivate PP2A by evicting manganese ions required for phosphatase activity (Xing et al.,2008)."
p(HGNC:PPME1) -> complex(p(HGNC:PPME1),p(HGNC:PPP2CA))
complex(p(HGNC:PPME1),p(HGNC:PPP2CA)) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:PPME1) -> complex(p(HGNC:PPME1),p(HGNC:PPP2CB))
complex(p(HGNC:PPME1),p(HGNC:PPP2CB)) -| act(complex(GO:"protein phosphatase type 2A complex"))

SET Evidence = "Methylation is thought to play a critical role in the biogenesis of PP2A holoenzymes."
p(HGNC:PPP2CA,pmod(Me,Leu,309)) -> complex(GO:"protein phosphatase type 2A complex")
p(HGNC:PPP2CB,pmod(Me,Leu,309)) -> complex(GO:"protein phosphatase type 2A complex")

SET Evidence = "Significantly, downregulation of LCMT1 expression leads to a significant decrease of
PP2A methylation and concomitant loss of PP2A holoenzymes containing the regulatory Bα (or PPP2R2A) subunit (PP2A/Bα;
Lee and Pallas,2007; Sontag et al.,2008; MacKay et al.,2013)."
p(HGNC:LCMT1) -> p(HGNC:PPP2CA,pmod(Me,Leu,309))
p(HGNC:LCMT1) -> p(HGNC:PPP2CB,pmod(Me,Leu,309))
p(HGNC:PPP2CA,pmod(Me,Leu,309)) -> complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
p(HGNC:PPP2CB,pmod(Me,Leu,309)) -> complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))

SET Evidence = "PP2A enzymes can also become transiently inactivated following
tyrosine phosphorylation of the catalytic subunit at the putative
Tyr-307 site,via activation of src kinase, epidermal growth factor
receptor or insulin signaling (Chen et al.,1992)."
p(HGNC:SRC) -> p(HGNC:PPP2CA,pmod(Ph,Tyr,307))
p(HGNC:EGFR) -> p(HGNC:PPP2CA,pmod(Ph,Tyr,307))
bp(GO:"insulin receptor signaling pathway") -> p(HGNC:PPP2CA,pmod(Ph,Tyr,307))
p(HGNC:PPP2CA,pmod(Ph,Tyr,307)) -> act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:SRC) -> p(HGNC:PPP2CB,pmod(Ph,Tyr,307))
p(HGNC:EGFR) -> p(HGNC:PPP2CB,pmod(Ph,Tyr,307))
bp(GO:"insulin receptor signaling pathway") -> p(HGNC:PPP2CB,pmod(Ph,Tyr,307))
p(HGNC:PPP2CB,pmod(Ph,Tyr,307)) -> act(complex(GO:"protein phosphatase type 2A complex"))

SET Evidence = "Other modifications of PP2AC subunit include ubiquitination, which targets
PP2A for degradation (McConnelletal.,2010), and tyrosine nitration that increases PP2A activity
in endothelial cells (Wu and Wilson, 2009)."
p(HGNC:PPP2CA,pmod(Ub)) -> deg(complex(GO:"protein phosphatase type 2A complex"))
SET MeSHAnatomy = "Endothelial Cells"
p(HGNC:PPP2CA,pmod(HBP:nitration,Tyr)) -> act(complex(GO:"protein phosphatase type 2A complex"))
UNSET MeSHAnatomy

SET Evidence = "Adding another layer of complexity to the regulation
of PP2A holoenzymes, protein kinase A-mediated serine phosphorylation of selective PPP2R5A and PPP2R5D regulatory subunits
belonging to the B’family can also modulate PP2A catalytic activity (Ahn et al.,2007; Kirchhefer et al.,2014)."
p(FPLX:PKA) -> p(HGNC:PPP2R5A,pmod(Ph,Ser))
p(HGNC:PPP2R5A,pmod(Ph,Ser)) reg act(complex(GO:"protein phosphatase type 2A complex"))
p(FPLX:PKA) -> p(HGNC:PPP2R5D,pmod(Ph,Ser))
p(HGNC:PPP2R5D,pmod(Ph,Ser)) reg act(complex(GO:"protein phosphatase type 2A complex"))

SET Evidence = "A recent report also indicates the existence of regulated phosphorylation of the
scaffolding A subunit on Ser/Thr residues, which affects its binding to
the catalytic subunit and PP2A signaling in the heart (Kotlo et al.,2014)."
SET MeSHAnatomy = "Heart"
p(HGNC:PPP2R1A,pmod(Ph,Ser),pmod(Ph,Thr)) -| complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA))
p(HGNC:PPP2R1B,pmod(Ph,Ser),pmod(Ph,Thr)) -| complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB))
p(HGNC:PPP2R1A,pmod(Ph,Ser),pmod(Ph,Thr)) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:PPP2R1B,pmod(Ph,Ser),pmod(Ph,Thr)) -| act(complex(GO:"protein phosphatase type 2A complex"))
UNSET MeSHAnatomy

SET Evidence = "Natural toxins such as okadaic acid, calyculin ,and fostriecin
(Reviewed in Swingle et al., 2007), and endogenous nuclear inhibitors called I1
PP2A and I2 PP2A/SET (Li and Damuni, 1998), can directly bind to the catalytic
subunit and inhibit the phosphatase activity of the entire family of PP2A enzymes."
# Inhibitor 1 of PP2A: ANP32A
# Inhibitor 2 of PP2A: SET
a(CHEBI:"okadaic acid") -| act(complex(GO:"protein phosphatase type 2A complex"))
a(CHEBI:"okadaic acid") -> complex(a(CHEBI:"okadaic acid"),p(HGNC:PPP2CA))
complex(a(CHEBI:"okadaic acid"),p(HGNC:PPP2CA)) -| act(complex(GO:"protein phosphatase type 2A complex"))
a(CHEBI:"okadaic acid") -> complex(a(CHEBI:"okadaic acid"),p(HGNC:PPP2CB))
complex(a(CHEBI:"okadaic acid"),p(HGNC:PPP2CB)) -| act(complex(GO:"protein phosphatase type 2A complex"))
a(PUBCHEM:5311365) -| act(complex(GO:"protein phosphatase type 2A complex"))
a(PUBCHEM:5311365) -> complex(a(PUBCHEM:5311365),p(HGNC:PPP2CA))
complex(a(PUBCHEM:5311365),p(HGNC:PPP2CA)) -| act(complex(GO:"protein phosphatase type 2A complex"))
a(PUBCHEM:5311365) -> complex(a(PUBCHEM:5311365),p(HGNC:PPP2CB))
complex(a(PUBCHEM:5311365),p(HGNC:PPP2CB)) -| act(complex(GO:"protein phosphatase type 2A complex"))
a(PUBCHEM:6913994) -| act(complex(GO:"protein phosphatase type 2A complex"))
a(PUBCHEM:6913994) -> complex(a(PUBCHEM:6913994),p(HGNC:PPP2CA))
complex(a(PUBCHEM:6913994),p(HGNC:PPP2CA)) -| act(complex(GO:"protein phosphatase type 2A complex"))
a(PUBCHEM:6913994) -> complex(a(PUBCHEM:6913994),p(HGNC:PPP2CB))
complex(a(PUBCHEM:6913994),p(HGNC:PPP2CB)) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:SET) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:SET) -> complex(p(HGNC:SET),p(HGNC:PPP2CA))
complex(p(HGNC:SET),p(HGNC:PPP2CA)) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:SET) -> complex(p(HGNC:SET),p(HGNC:PPP2CB))
complex(p(HGNC:SET),p(HGNC:PPP2CB)) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:ANP32A) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:ANP32A) -> complex(p(HGNC:ANP32A),p(HGNC:PPP2CA))
complex(p(HGNC:ANP32A),p(HGNC:PPP2CA)) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:ANP32A) -> complex(p(HGNC:ANP32A),p(HGNC:PPP2CB))
complex(p(HGNC:ANP32A),p(HGNC:PPP2CB)) -| act(complex(GO:"protein phosphatase type 2A complex"))

SET Evidence = "Conversely, many endogenous small molecules, comprising metal cations,
ceramides and polyamines, can enhance the activity of PP2A enzymes (Reviewed in Voronkov et al.,2011)."
a(CHEBI:"metal cation") -> act(complex(GO:"protein phosphatase type 2A complex"))
a(CHEBI:ceramide) -> act(complex(GO:"protein phosphatase type 2A complex"))
a(CHEBI:polyamine) -> act(complex(GO:"protein phosphatase type 2A complex"))

SET Evidence = "For instance, the Bα subunit specifically and markedly facilitates dephosphory-
lation of tau by PP2A (Sontag et al.,1996; Xu et al.,2008)."
p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55") -> act(complex(GO:"protein phosphatase type 2A complex"))
p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55") -| p(HGNC:MAPT,pmod(Ph))
complex(GO:"protein phosphatase type 2A complex") -| p(HGNC:MAPT,pmod(Ph))

SET Evidence = "Furthermore, direct interaction of PP2A catalytic subunit with
specific regulatory proteins, including PME-1, LCMT1, the alpha4 subunit,
and the PP2A phosphatase activator PTPA, critically modulates PP2A biogenesis and stability"
complex(p(HGNC:LCMT1),p(HGNC:PPP2CA)) reg complex(GO:"protein phosphatase type 2A complex")
complex(p(HGNC:LCMT1),p(HGNC:PPP2CB)) reg complex(GO:"protein phosphatase type 2A complex")
complex(p(HGNC:PPME1),p(HGNC:PPP2CA)) reg complex(GO:"protein phosphatase type 2A complex")
complex(p(HGNC:PPME1),p(HGNC:PPP2CB)) reg complex(GO:"protein phosphatase type 2A complex")
complex(p(HGNC:IGBP1),p(HGNC:PPP2CA)) reg complex(GO:"protein phosphatase type 2A complex")
complex(p(HGNC:IGBP1),p(HGNC:PPP2CB)) reg complex(GO:"protein phosphatase type 2A complex")
complex(p(HGNC:PTPA),p(HGNC:PPP2CA)) reg complex(GO:"protein phosphatase type 2A complex")
complex(p(HGNC:PTPA),p(HGNC:PPP2CB)) reg complex(GO:"protein phosphatase type 2A complex")

SET Evidence = "Of particular relevance to the Alzheimer’s disease (AD) field, PP2A/Bα holoenzymes can
directly bind to the microtubule-associated protein tau (Sontag et al.,1999, 2012; Xu et al.,2008)."
SET MeSHDisease = "Alzheimer Disease"
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -> complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -> complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
UNSET MeSHDisease

SET Evidence = "PP2A enzymes can also associate with protein kinases that have been linked to AD, such as glycogen
synthase kinase 3β (GSK3β) and cyclin-dependent kinase 5 (cdk5; Plattner et al.,2006), and neuronal receptors, e.g., the NMDA
receptor (Chan and Sucher, 2001) and the metabotropic glutamate receptor 5 (Mao et al., 2005; Arif et al., 2014)."
complex(GO:"protein phosphatase type 2A complex") -> complex(a(GO:"protein phosphatase type 2A complex"),p(HGNC:GSK3B))
complex(GO:"protein phosphatase type 2A complex") -> complex(a(GO:"protein phosphatase type 2A complex"),p(HGNC:CDK5))
complex(GO:"protein phosphatase type 2A complex") -> complex(a(GO:"protein phosphatase type 2A complex"),a(MESH:"Receptors, N-Methyl-D-Aspartate"))
complex(GO:"protein phosphatase type 2A complex") -> complex(a(GO:"protein phosphatase type 2A complex"),p(HGNC:GRM5))
path(MESH:"Alzheimer Disease") -- p(HGNC:GSK3B)
path(MESH:"Alzheimer Disease") -- p(HGNC:CDK5)
path(MESH:"Alzheimer Disease") -- a(MESH:"Receptors, N-Methyl-D-Aspartate"))
path(MESH:"Alzheimer Disease") -- p(HGNC:GRM5)

SET Evidence = "For instance, some B’ subunits target PP2A to the nucleus (McCright et al., 1996) or the centrosome (Flegg
et al.,2010); Bα subunits can direct some PP2A pools to microtubules, which could serve as a cytoskeletal reservoir of inac-
tive enzymes (Sontag et al.,1995; Hiraga and Tamura,2000)."
# B' subunit: PPP2R5 also known as Protein phosphatase 2A, regulatory B subunit, B56
p(INTERPRO:"Protein phosphatase 2A, regulatory B subunit, B56") -> tloc(complex(GO:"protein phosphatase type 2A complex"),fromLoc(GO:cytoplasm),toLoc(GO:nucleus))
p(INTERPRO:"Protein phosphatase 2A, regulatory B subunit, B56") -> tloc(complex(GO:"protein phosphatase type 2A complex"),fromLoc(GO:cytoplasm),toLoc(GO:centrosome))
p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55") -> complex(a(GO:"protein phosphatase type 2A complex"),a(GO:microtubule))

SET Evidence = "For example, PME-1 stabilizes a nuclear pool of inactive PP2A enzymes (Longin et al., 2008), while methylation
by LCMT1 influences the amounts of PP2A enzymes bound to plasma membrane microdomains (Sontag et al.,2013)."
SET MeSHAnatomy = "Cell Nucleus"
p(HGNC:PPME1) neg act(complex(GO:"protein phosphatase type 2A complex"))
UNSET MeSHAnatomy
p(HGNC:LCMT1) -> p(HGNC:PPP2CA,pmod(Me,Leu,309))
p(HGNC:LCMT1) -> p(HGNC:PPP2CB,pmod(Me,Leu,309))
p(HGNC:LCMT1) reg complex(a(GO:"protein phosphatase type 2A complex"),a(MESH:"Membrane Microdomains"))

SET Evidence = "There is a significant decrease in total PP2A activity measured
in AD cortical and hippocampal brain homogenates (Gong et al.,1993; Gong et al.,1995; Sontag et al.,2004b)."
SET MeSHAnatomy = {"Hippocampus","Cerebral Cortex"}
path(MESH:"Alzheimer Disease") neg act(complex(GO:"protein phosphatase type 2A complex"))
UNSET MeSHAnatomy

SET Evidence = "Deficits in PP2A activity are in line with the reported down-regulation
of PP2A catalytic C subunit at the gene (Loring et al.,2001), mRNA (Vogelsberg-Ragaglia et al.,2001)
and protein (Sontag et al.,2004b) expression levels in AD."
act(complex(GO:"protein phosphatase type 2A complex")) -- p(HGNC:PPP2CA)
act(complex(GO:"protein phosphatase type 2A complex")) -- p(HGNC:PPP2CB)
path(MESH:"Alzheimer Disease") neg g(HGNC:PPP2CA)
path(MESH:"Alzheimer Disease") neg r(HGNC:PPP2CA)
path(MESH:"Alzheimer Disease") neg p(HGNC:PPP2CA)
path(MESH:"Alzheimer Disease") neg g(HGNC:PPP2CB)
path(MESH:"Alzheimer Disease") neg r(HGNC:PPP2CB)
path(MESH:"Alzheimer Disease") neg p(HGNC:PPP2CB)

SET Evidence = "In contrast, “PP2A” expression levels are increased in AD astrocytes (Pei et al., 1997)."
SET MeSHAnatomy = "Astrocytes"
path(MESH:"Alzheimer Disease") pos complex(GO:"protein phosphatase type 2A complex")
UNSET MeSHAnatomy

SET Evidence = "More specifically, decreased expression levels of PP2A regulatory Bγ
(or PPP2R2C) and B’ε (or PPP2R5E) subunit mRNAs in the hippocampus (Vogelsberg-Ragaglia et al.,2001),
and cortical Bα subunit (Sontag et al.,2004b) have been reported in AD."
SET MeSHAnatomy = "Hippocampus"
path(MESH:"Alzheimer Disease") neg r(HGNC:PPP2R2C)
path(MESH:"Alzheimer Disease") neg r(HGNC:PPP2R5E)
UNSET MeSHAnatomy
SET MeSHAnatomy = "Cerebral Cortex"
path(MESH:"Alzheimer Disease") neg p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")
UNSET MeSHAnatomy

SET Evidence = "Notably, the loss of neuronal PP2A/Bα holoenzymes correlates with the
down-regulation of PP2A methylation and severity of phosphorylated tau (P-tau) pathology
in AD-affected brain regions (Sontag et al.,2004 a,b)."
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) pos p(FPLX:PPP2,pmod(Me))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) pos p(FPLX:PPP2,pmod(Me))
SET MeSHDisease = "Alzheimer Disease"
path(MESH:Tauopathies) neg complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
UNSET MeSHDisease

SET Evidence = "Lastly, increased phosphorylation of PP2A at Tyr-307 has
been found in P-tau-rich, tangle-bearing neurons from post-mortem brain (Liu et al.,2008b)."
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = "Neurons"
p(FPLX:PPP2,pmod(Me,Tyr,307)) -- a(GO:"neurofibrillary tangle")
p(FPLX:PPP2,pmod(Me,Tyr,307)) -- p(HGNC:MAPT,pmod(Ph))
UNSET MeSHDisease
UNSET MeSHAnatomy

SET Evidence = "Significantly, down-regulation of LCMT1 protein expression
parallels the deficits in PP2A methylation observed in AD (Sontag et al.,2004a)."
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:LCMT1) -> p(FPLX:PPP2,pmod(Me))
path(MESH:"Alzheimer Disease") neg p(FPLX:PPP2,pmod(Me))
UNSET MeSHDisease

SET Evidence = "Up-regulation of I1 PP2A and I2 PP2A, and mislocalization and cleavage of I2
PP2A, could underlie the inactivation of PP2A in AD neocortical neurons (Tanimukai et al.,2005)."
# Inhibitor 1 of PP2A: ANP32A
# Inhibitor 2 of PP2A: SET
SET MeSHAnatomy = "Neocortex"
SET MeSHDisease = "Alzheimer Disease"
path(MESH:"Alzheimer Disease") pos p(HGNC:ANP32A)
p(HGNC:ANP32A) neg act(complex(GO:"protein phosphatase type 2A complex"))
path(MESH:"Alzheimer Disease") pos p(HGNC:SET)
p(HGNC:SET) neg act(complex(GO:"protein phosphatase type 2A complex"))
path(MESH:"Alzheimer Disease") pos p(HGNC:SET,frag("?"))
p(HGNC:SET,frag("?")) neg act(complex(GO:"protein phosphatase type 2A complex"))
UNSET MeSHAnatomy
UNSET MeSHDisease

SET Evidence = "Decreased expression levels of PTPA in AD brain tissue may also lead to inactivation of PP2A by indirectly increasing levels
of PP2A phosphorylated at the Tyr-307 site (Luo et al.,2013)."
SET MeSHDisease = "Alzheimer Disease"
path(MESH:"Alzheimer Disease") neg p(HGNC:PTPA)
p(HGNC:PTPA) -> act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:PTPA) -| p(FPLX:PPP2,pmod(Ph,Tyr,307))
p(FPLX:PPP2,pmod(Ph,Tyr,307)) neg act(complex(GO:"protein phosphatase type 2A complex"))
UNSET MeSHDisease

SET Evidence = "Lastly, increased calpain-mediated cleavage of alpha4, which critically
modulates PP2A stability, could be responsible for increased degradation
of PP2A catalytic subunit in AD (Watkins et al., 2012)."
p(FPLX:CAPN) -> p(HGNC:IGBP1,frag("?"))
p(HGNC:IGBP1,frag("?")) reg complex(GO:"protein phosphatase type 2A complex")
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:IGBP1,frag("?")) pos deg(p(HGNC:PPP2CA))
p(HGNC:IGBP1,frag("?")) pos deg(p(HGNC:PPP2CB))
UNSET MeSHDisease

SET Evidence = "Collectively, those studies point to a central role for PP2A dysfunction in AD pathogenesis"
path(MESH:"Alzheimer Disease") neg act(complex(GO:"protein phosphatase type 2A complex"))

SET Evidence = "In vivo use of phosphatase inhibitors such as okadaic acid has
been shown in many studies to induce cognitive impairment and
widespread neurotoxic effects that are reminiscent of the hallmark pathological
processes occurring in AD pathology, i.e., the accumulation of P-tau, amyloidogenesis, synapse loss and
neurodegeneration (Malchiodi-Albedi et al., 1997; Arendt et al., 1998; Sun et al.,2003; Kamat et al.,2013)"
a(CHEBI:"okadaic acid") -| act(complex(GO:"protein phosphatase type 2A complex"))
a(CHEBI:"okadaic acid") -| bp(GO:cognition)
a(CHEBI:"okadaic acid") -> p(HGNC:MAPT,pmod(Ph))
a(CHEBI:"okadaic acid") -> bp(HBP:"amyloidogenesis")
a(CHEBI:"okadaic acid") -| a(GO:synapse)
a(CHEBI:"okadaic acid") -> path(HBP:Neurodegeneration)

SET Evidence = "Not surprisingly, inhibition of total cellular PP2A activity ultimately leads to neuronal cell death."
act(complex(GO:"protein phosphatase type 2A complex")) -| bp(GO:"neuron death")

SET Evidence = "Knock-down of PP2A catalytic subunit (Kins et al.,2001) or PP2A B’δ (or PPP2R5D)
regulatory subunit (Louis et al.,2011), and expression of the methylation-site L309A C subunit mutant
(Schild et al.,2006) all induce AD-like tau phosphorylation in transgenic mice"
p(HGNC:PPP2CA) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP2CB) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP2R5D) -| p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP2CA,var("p.Leu309Ala")) -> p(HGNC:MAPT,pmod(Ph))
p(HGNC:PPP2CB,var("p.Leu309Ala")) -> p(HGNC:MAPT,pmod(Ph))

SET Evidence = "Moreover, expression of an I2 PP2A fragment can recapitulate AD-like pathology in rat brain (Wang et al.,2010)."
p(HGNC:SET,frag("?")) -> path(MESH:"Alzheimer Disease")

SET Evidence = "Specific PP2A inhibition has been proven to lead to in vivo deregulation of many major brain Ser/Thr kinases implicated
in AD, including GSK3β (Wang et al., 2010; Louis et al., 2011), cdk5 (Louis et al., 2011; Kimura et al., 2013), extracellular signal-
regulated kinase (ERK) and JNK (Kins et al., 2003)."
act(complex(GO:"protein phosphatase type 2A complex")) -> act(p(HGNC:GSK3B))
act(complex(GO:"protein phosphatase type 2A complex")) -> act(p(HGNC:CDK5))
act(complex(GO:"protein phosphatase type 2A complex")) -> act(p(FPLX:ERK))
act(complex(GO:"protein phosphatase type 2A complex")) -> act(p(FPLX:JNK))

SET Evidence = "Apart from affecting tau phosphorylation, abnormal activation of GSK3β, cdk5, and ERK has been linked to
cytoskeletal abnormalities (microtubules, neurofilaments), alterations in amyloid precursor protein (APP) phosphorylation and
processing, impairment of neurogenesis, alterations in synaptic plasticity and induction of apoptotic processes (Reviewed
in Crews and Masliah, 2010; Medina and Avila, 2013, 2014)."
act(p(HGNC:GSK3B)) neg a(GO:"microtubule cytoskeleton")
act(p(HGNC:GSK3B)) neg a(GO:"neurofilament cytoskeleton")
act(p(HGNC:GSK3B)) neg p(HGNC:APP,pmod(Ph))
act(p(HGNC:GSK3B)) neg bp(HBP:"APP processing")
act(p(HGNC:GSK3B)) neg bp(GO:neurogenesis)
act(p(HGNC:GSK3B)) pos bp(GO:"negative regulation of synaptic plasticity")
act(p(HGNC:GSK3B)) pos bp(GO:"apoptotic process")
act(p(HGNC:CDK5)) neg a(GO:"microtubule cytoskeleton")
act(p(HGNC:CDK5)) neg a(GO:"neurofilament cytoskeleton")
act(p(HGNC:CDK5)) neg p(HGNC:APP,pmod(Ph))
act(p(HGNC:CDK5)) neg bp(HBP:"APP processing")
act(p(HGNC:CDK5)) neg bp(GO:neurogenesis)
act(p(HGNC:CDK5)) pos bp(GO:"negative regulation of synaptic plasticity")
act(p(HGNC:CDK5)) pos bp(GO:"apoptotic process")
act(p(FPLX:ERK)) neg a(GO:"microtubule cytoskeleton")
act(p(FPLX:ERK)) neg a(GO:"neurofilament cytoskeleton")
act(p(FPLX:ERK)) neg p(HGNC:APP,pmod(Ph))
act(p(FPLX:ERK)) neg bp(HBP:"APP processing")
act(p(FPLX:ERK)) neg bp(GO:neurogenesis)
act(p(FPLX:ERK)) pos bp(GO:"negative regulation of synaptic plasticity")
act(p(FPLX:ERK)) pos bp(GO:"apoptotic process")

SET Evidence = "For instance, activated GSK3β has been reported to induce PP2A inactivation via several mechanisms:
phosphorylation of PP2A on Tyr307 (Yao et al.,2011); demethylation of PP2A on Leu309 through inhibition of LCMT1 and
up-regulation of PME1 (Yao et al.,2012); and accumulation of I2 PP2A (Liu et al.,2008a)."
act(p(HGNC:GSK3B)) -| act(complex(GO:"protein phosphatase type 2A complex"))
act(p(HGNC:GSK3B)) -> p(FPLX:PPP2,pmod(Ph,Tyr,307))
p(FPLX:PPP2,pmod(Ph,Tyr,307)) -| act(complex(GO:"protein phosphatase type 2A complex"))
act(p(HGNC:GSK3B)) -| p(HGNC:PPP2CA,pmod(Me,Leu,309))
act(p(HGNC:GSK3B)) -| p(HGNC:PPP2CB,pmod(Me,Leu,309))
p(HGNC:PPP2CA,pmod(Me,Leu,309)) -> act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:PPP2CB,pmod(Me,Leu,309)) -> act(complex(GO:"protein phosphatase type 2A complex"))
act(p(HGNC:GSK3B)) -| act(p(HGNC:LCMT1))
act(p(HGNC:GSK3B)) -> act(p(HGNC:PPME1))
p(HGNC:LCMT1) -> p(HGNC:PPP2CA,pmod(Me,Leu,309))
p(HGNC:LCMT1) -> p(HGNC:PPP2CB,pmod(Me,Leu,309))
act(p(HGNC:PPME1)) -| p(HGNC:PPP2CA,pmod(Me,Leu,309))
act(p(HGNC:PPME1)) -| p(HGNC:PPP2CA,pmod(Me,Leu,309))
act(p(HGNC:GSK3B)) -> p(HGNC:SET)
p(HGNC:SET) neg act(complex(GO:"protein phosphatase type 2A complex"))

SET Evidence = "Besides Ser/Thr kinases, the protein tyrosine kinase src promotes the phosphorylation of PP2A on Tyr-307, resulting in
PP2A inactivation and subsequent tau phosphorylation (Xiong et al.,2013; Arif et al.,2014)."
p(HGNC:SRC) -> p(HGNC:PPP2CA,pmod(Ph,Tyr,307))
p(HGNC:SRC) -> p(HGNC:PPP2CB,pmod(Ph,Tyr,307))
p(HGNC:PPP2CA,pmod(Ph,Tyr,307)) -| act(complex(GO:"protein phosphatase type 2A complex"))
p(HGNC:PPP2CB,pmod(Ph,Tyr,307)) -| act(complex(GO:"protein phosphatase type 2A complex"))
act(complex(GO:"protein phosphatase type 2A complex")) -| p(HGNC:MAPT,pmod(Ph))

SET Evidence = "While many PP2A holoenzymes have the potential to indirectly affect tau phosphorylation
by modulating key tau protein kinases (For example see Louis et al., 2011), biochemical and structural
studies have demonstrated that PP2A/Bα is the primary PP2A isoform that mediates tau dephosphorylation
(Sontag et al.,1996, 1999; Xu et al.,2008)."
act(complex(GO:"protein phosphatase type 2A complex")) -| p(HGNC:MAPT,pmod(Ph))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -| p(HGNC:MAPT,pmod(Ph))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -| p(HGNC:MAPT,pmod(Ph))

SET Evidence = "Specific inhibition of PP2A/Bα is associated with enhanced tau phosphorylation
at many AD-like phospho epitopes, and subsequent inability of tau to bind to and stabilize microtubules
(Sontag et al., 1996)."
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -| p(HGNC:MAPT,pmod(Ph))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -> complex(p(HGNC:MAPT),a(GO:microtubule))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -| p(HGNC:MAPT,pmod(Ph))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -> complex(p(HGNC:MAPT),a(GO:microtubule))

SET Evidence = "Deregulation of PP2A/Bα alone also affects microtubule stability (Nunbhakdi-Craig et al., 2007)
and neurite outgrowth (Sontag et al.,2010) in neuroblastoma cells"
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) reg a(GO:microtubule)
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) reg bp(MESH:"Neuronal Outgrowth")
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) reg a(GO:microtubule)
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) reg bp(MESH:"Neuronal Outgrowth")

SET Evidence = "As described earlier, it is especially significant that the biogenesis of PP2A/Bα
holoenzymes is intimately related to the methylation state of PP2A"
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -- p(FPLX:PPP2,pmod(Me))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -- p(FPLX:PPP2,pmod(Me))

SET Evidence = "It is noteworthy that PP2A/Bα can directly bind to tau via a
domain encompassing the microtubule-binding of tau; this interaction maximizes
the efficiency of tau dephosphorylation by PP2A (Sontag et al.,1999; Xu et al.,2008; Figure 3A)."
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -> complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -| p(HGNC:MAPT,pmod(Ph))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -> complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -| p(HGNC:MAPT,pmod(Ph))

SET Evidence = "Notably, tau missense mutations found in frontotemporal dementia with Parkinsonism
linked to chromosome 17 (FTDP-17; Goedert et al.,2000) and AD-mimicking tau phosphorylation in proline-rich motifs
(Eidenmuller et al.,2001) inhibit the association of tau with PP2A (Figure3B)."
p(HGNC:MAPT,var("?")) -| complex(a(GO:"protein phosphatase type 2A complex"),p(HGNC:MAPT))
p(HGNC:MAPT,pmod(Ph)) -| complex(a(GO:"protein phosphatase type 2A complex"),p(HGNC:MAPT))

SET Evidence = "Phosphorylation of the Thr-231 residue in this motif markedly decreases the affinity of tau for PP2A."
# phosphorylation of tau
p(HGNC:MAPT,pmod(Ph,Thr,231)) -| complex(a(GO:"protein phosphatase type 2A complex"),p(HGNC:MAPT))

SET Evidence = "This is potentially physiologically significant since phosphorylation of tau at Thr-231, a target site
for ERK2, GSK3β, and cdk5, occurs early in AD and can further inhibit the ability of PP2A/Bα to dephosphorylate other
major AD-tau phosphoepitopes (Landrieu et al.,2011)."
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ph,Thr,231))
p(HGNC:MAPT,pmod(Ph,Thr,231)) -| act(complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")))
p(HGNC:MAPT,pmod(Ph,Thr,231)) -| act(complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")))

SET Evidence = "Conversely, decreased PP2A methylation and PP2A/Bα levels in AD will disrupt normal PP2A-tau
interactions (Sontag et al., 2007), thereby preventing PP2A-mediated tau dephosphorylation while allowing
for enhanced binding of Fyn kinase or other regulators to the tau proteins."
path(MESH:"Alzheimer Disease") neg p(FPLX:PPP2,pmod(Me))
path(MESH:"Alzheimer Disease") neg complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
path(MESH:"Alzheimer Disease") neg complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -> complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
path(MESH:"Alzheimer Disease") neg complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -> complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
path(MESH:"Alzheimer Disease") neg complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -| p(HGNC:MAPT,pmod(Ph))
complex(p(HGNC:MAPT),p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55")) -| p(HGNC:MAPT,pmod(Ph))
path(MESH:"Alzheimer Disease") pos p(HGNC:MAPT,pmod(Ph))
path(MESH:"Alzheimer Disease") pos complex(p(HGNC:FYN),p(HGNC:MAPT))

SET Evidence = "The deregulation of PP2A methylation in AD is especially interesting, not only because it can lead to a loss of PP2A/Bα, a
major tau regulator, but also because PP2A methylation state is intimately linked to the integrity of one-carbon metabolism,
which regulates SAM supply (Reviewed in Fowler,2005)."
path(MESH:"Alzheimer Disease") neg p(FPLX:PPP2,pmod(Me))
path(MESH:"Alzheimer Disease") neg complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
path(MESH:"Alzheimer Disease") neg complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
p(FPLX:PPP2,pmod(Me)) pos complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CA),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
p(FPLX:PPP2,pmod(Me)) pos complex(p(HGNC:PPP2R1A),p(HGNC:PPP2CB),p(INTERPRO:"Protein phosphatase 2A regulatory subunit PR55"))
p(FPLX:PPP2,pmod(Me)) -- bp(GO:"one-carbon metabolic process")

SET Evidence = "Remarkably, impairment of one-carbon metabolism in animal models
can reproduce AD-like pathological features: accumulation of P-tau (Sontag et al.,2007; Zhang et al.,2008; Wei et al.,2011);
enhanced amyloidogenesis (Pacheco-Quinto et al.,2006; Zhang et al.,2009; Zhuo et al.,2010; Zhuo and Pratico,2010); increased
phosphorylation of APP at the regulatory Thr-668 site (Sontag et al.,2007; Zhang et al.,2009); increased sensitivity to
amyloid toxicity (Kruman et al.,2002); and cognitive impairment (Bernardo et al.,2007; Wei et al.,2011; Rhodehouse et al., 2013)."
bp(GO:"one-carbon metabolic process") -| p(HGNC:MAPT,pmod(Ph))
bp(GO:"one-carbon metabolic process") -| bp(HBP:"amyloidogenesis")
bp(GO:"one-carbon metabolic process") -| p(HGNC:APP,pmod(Ph,Thr,668))
bp(GO:"one-carbon metabolic process") -| bp(GO:cognition)
bp(GO:"one-carbon metabolic process") -| act(a(CHEBI:"amyloid-beta"))

SET Evidence = "Dietary folate and B-vitamin deficiency (Sontag et al.,2008; Nicolia et al.,2010)
and elevated homocysteine levels (Sontag et al.,2007, 2013; Zhang et al.,2008) lead to down-regulation of PP2A
methylation and concomitant phosphorylation of tau and/or APP in vivo"
a(CHEBI:folates) -| p(FPLX:PPP2,pmod(Me))
a(CHEBI:folates) -> p(HGNC:MAPT,pmod(Ph))
a(CHEBI:folates) -> p(HGNC:APP,pmod(Ph))
path(MESH:"Vitamin B Deficiency") -| p(FPLX:PPP2,pmod(Me))
path(MESH:"Vitamin B Deficiency") -> p(HGNC:MAPT,pmod(Ph))
path(MESH:"Vitamin B Deficiency") -> p(HGNC:APP,pmod(Ph))
a(CHEBI:homocysteine) neg p(FPLX:PPP2,pmod(Me))
a(CHEBI:homocysteine) pos p(HGNC:MAPT,pmod(Ph))
a(CHEBI:homocysteine) pos p(HGNC:APP,pmod(Ph))
p(FPLX:PPP2,pmod(Me)) neg p(HGNC:MAPT,pmod(Ph))
p(FPLX:PPP2,pmod(Me)) neg p(HGNC:APP,pmod(Ph))

SET Evidence = "In cultured cells, deregulation of PP2A methylation also affects APP processing (Sontag et al.,2007), neurite outgrowth
(Sontag et al.,2010) and tau distribution (Sontag et al.,2013)."
p(FPLX:PPP2,pmod(Me)) reg bp(HBP:"APP processing")
p(FPLX:PPP2,pmod(Me)) reg bp(MESH:"Neuronal Outgrowth")
p(FPLX:PPP2,pmod(Me)) reg p(HGNC:MAPT)
