##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Name        = "APP processing in Alzheimer's disease"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Description = "APP processing in Alzheimer's disease"
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

##############
# Namespaces #
##############
DEFINE NAMESPACE CHEBI   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20180106.belns"
DEFINE NAMESPACE GO      AS URL "https://raw.githubusercontent.com/pharmacome/terminology/73688d6dc24e309fca59a1340dc9ee971e9f3baa/external/go-names.belns"
DEFINE NAMESPACE HGNC    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc/hgnc-20180215.belns"
DEFINE NAMESPACE MESHC   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-chemicals/mesh-chemicals-20170725.belns"
DEFINE NAMESPACE MESHCS  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-cell-structures/mesh-cell-structures-20170511.belns"
DEFINE NAMESPACE MESHD   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-diseases/mesh-diseases-20170725.belns"
DEFINE NAMESPACE MESHPP  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-processes/mesh-processes-20170725.belns"
DEFINE NAMESPACE SCOMP   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE HBP     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/bd0996a28201cad363557315043c6392e31abf58/export/hbp.belns"
DEFINE NAMESPACE FPLX    AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/1b7e14ec0fd02ee7ed71514c6e267f57d5641a4b/export/famplex.belns"
DEFINE NAMESPACE UNIPROT AS PATTERN ".*"

###############
# Annotations #
###############
DEFINE ANNOTATION CellStructure   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION MeSHAnatomy     AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease     AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Species         AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"
DEFINE ANNOTATION Confidence      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION PublicationType AS LIST {"Review", "Research", "Comment"}

##############
# Statements #
##############
SET Citation = {"PubMed", "21214928"}
SET PublicationType = "Review"

SET Evidence = "Abeta is generated from b-amyloid precursor protein (APP)
through sequential cleavages first by beta-secretase and then by gamma-secretase complex"
SET Confidence = "Medium"
p(HGNC:APP) -> a(CHEBI:"amyloid-beta")
p(HGNC:BACE1) -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))
complex(SCOMP:"gamma Secretase Complex") -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))
a(MESHC:"Amyloid Precursor Protein Secretases") -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))

SET Evidence = "Alternatively, APP can be
cleaved by alpha-secretase within the Abeta domain to release soluble APPa and preclude Abeta generation"
SET Confidence = "Medium"
p(HGNC:ADAM10) -> p(HBP:HBP00067)
p(HGNC:ADAM10) -| a(CHEBI:"amyloid-beta")

SET Evidence = "Cleavage of
APP by caspases may also contribute to AD pathologies"
SET Confidence = "Medium"
a(MESHC:Caspases) -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))
a(MESHC:Caspases) -> path(MESHD:"Alzheimer Disease")

SET Evidence = "The neurofibrillary tangles (NFTs) consist
largely of hyperphosphorylated twisted filaments of
the microtubule-associated protein tau [4,5]"
SET Confidence = "High"
p(HGNC:MAPT,pmod(Ph)) -> a(GO:"neurofibrillary tangle")

SET Evidence = "Extracellular
neuritic plaques are deposits of differently sized
small peptides called beta-amyloid (Abeta) that are derived via
sequential proteolytic cleavages of the b-amyloid precursor
protein (APP) [6]"
SET Confidence = "High"
a(CHEBI:"amyloid-beta") -> path(MESHD:"Plaque, Amyloid")

SET Evidence = "The APP gene is located on chromosome 21 in humans
with three major isoforms arising from alternative
splicing [3]. These are APP695, APP751 and APP770
(containing 695, 751, and 770 amino acids, respectively)"
SET Confidence = "High"
bp(HBP:HBP00077) -> p(HBP:HBP00063)
bp(HBP:HBP00077) -> p(HBP:HBP00064)
bp(HBP:HBP00077) -> p(HBP:HBP00065)

SET Evidence = "APP751 and APP770 are expressed in most tissues and
contain a 56 amino acid Kunitz Protease Inhibitor (KPI)
domain within their extracellular regions"
SET Confidence = "High"
p(HBP:HBP00064) -- p(UNIPROT:A0A293M1A7)
p(HBP:HBP00065) -- p(UNIPROT:A0A293M1A7)

SET Evidence = "There are reports showing that the protein
and mRNA levels of KPI-containing APP isoforms are
elevated in AD brain and associated with increased Ab
deposition [9]; and prolonged activation of extrasynaptic
NMDA receptor in neurons can shift APP expression
from APP695 to KPI-containing APP isoforms, accompanied
with increased production of Ab [10]"
SET Confidence = "Medium"
p(HBP:HBP00064) -> path(MESHD:"Plaque, Amyloid")
p(HBP:HBP00065) -> path(MESHD:"Plaque, Amyloid")
path(MESHD:"Alzheimer Disease") positiveCorrelation r(HBP:HBP00064)
path(MESHD:"Alzheimer Disease") positiveCorrelation r(HBP:HBP00064)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HBP:HBP00065)
path(MESHD:"Alzheimer Disease") positiveCorrelation p(HBP:HBP00065)
a(MESHC:"Receptors, N-Methyl-D-Aspartate") -> p(HBP:HBP00064)
a(MESHC:"Receptors, N-Methyl-D-Aspartate") -> p(HBP:HBP00065)
a(MESHC:"Receptors, N-Methyl-D-Aspartate") -> a(CHEBI:"amyloid-beta")

SET Evidence = "A role for APP has been suggested
in neurite outgrowth and synaptogenesis, neuronal
protein trafficking along the axon, transmembrane
signal transduction, cell adhesion, calcium metabolism,
etc, all requiring additional in vivo evidence (reviewed
in [19])"
SET Confidence = "High"
p(HGNC:APP) -- bp(MESHPP:"Neuronal Outgrowth")
p(HGNC:APP) -- bp(GO:"synapse assembly")
p(HGNC:APP) -- bp(GO:"signal transduction")
p(HGNC:APP) -- bp(GO:"cell adhesion")
p(HGNC:APP) -- bp(GO:"calcium ion homeostasis")
SET MeSHAnatomy = {"Neurons","Axons"}
p(HGNC:APP) -- bp(GO:"protein transport")

SET Evidence = "Indeed, several APP
ligands have been identified, such as Abeta [20], F-spondin
[21] and nectrin-1 [22]."
SET Confidence = "High"
complex(p(HGNC:APP),a(CHEBI:"amyloid-beta"))
complex(p(HGNC:APP),p(HGNC:SPON1))
complex(p(HGNC:APP),p(HGNC:NTN1))

SET Evidence = "APP was found to colocalize with beta1 intergrins in neural cells [23]"
SET MeSHAnatomy = "Neurons"
SET Confidence = "High"
p(HGNC:APP) -- p(HGNC:ITGB1)

SET Evidence = "APP undergoes rapid anterograde transport in neurons"
SET MeSHAnatomy = "Neurons"
SET Confidence = "High"
p(HGNC:APP) -- bp(GO:"anterograde axonal protein transport")

SET Evidence = "During its transport, APP was found to interact
with kinesin-I and functions as a kinesin-I membrane
receptor to mediate axonal transport of beta-secretase
(BACE1) and PS1 [26,27]"
SET Confidence = "High"
complex(p(HGNC:APP),p(HGNC:KIF5B)) reg act(p(HGNC:BACE1))
complex(p(HGNC:APP),p(HGNC:KIF5B)) reg act(p(HGNC:PSEN1))

SET Evidence = "APP was also
found to interact with high-affinity choline transporter
(CHT) through the C-terminal domain and APP deficiency
affected CHT endocytosis [33]"
SET Confidence = "Medium"
complex(p(HGNC:APP),p(HGNC:SLC5A7))

SET Evidence = "Overall, most studies suggest that APP plays some role in regulating protein trafficking"
SET Confidence = "High"
p(HGNC:APP) reg bp(GO:"protein transport")

SET Evidence = "From the TGN, APP can be transported in TGN-derived
secretory vesicles to the cell surface where it is
either cleaved by alpha-secretase to produce a soluble molecule,
sAPPalpha [37], or re-internalized via an endosomal/
lysosomal degradation pathway [38,39]"
SET Confidence = "High"
p(HGNC:ADAM10) -> rxn(reactants(p(HGNC:APP)),products(a(HBP:HBP00067)))

SET Evidence = "While Abeta is neurotoxic, studies
suggest that sAPPalpha is neuroprotective, making the
subcellular distribution of APP an important factor in
neurodegeneration [42-44]"
SET Confidence = "Medium"
a(CHEBI:"amyloid-beta") -> bp(HBP:HBP00061)
a(HBP:HBP00067) -| bp(HBP:HBP00061)

SET Evidence = "Cleavage of APP by alpha-secretase precludes Abeta generation
as the cleavage site is within the Abeta domain (at the Lys16-
Leu17 bond), and releases a large soluble ectodomain of
APP called sAPPalpha"
SET Confidence = "Medium"
p(HGNC:ADAM10) -> rxn(reactants(p(HGNC:APP)),products(a(HBP:HBP00067)))
p(HGNC:ADAM10) -> rxn(reactants(p(HGNC:APP)),products(p(HGNC:APP, frag("1_16")), p(HGNC:APP, frag("17_*"))))
p(HGNC:ADAM10) -| a(CHEBI:"amyloid-beta")

SET Evidence = "Manipulation of ADAM17 can alter alpha-cleavage of APP
and Abeta generation, with regulated alpha-cleavage abolished
in ADAM17-deficient cells, suggesting that ADAM17 is
likely the alpha-secretase responsible for regulated APP cleavage [47]"
SET Confidence = "Medium"
p(HGNC:ADAM17) -> rxn(reactants(p(HGNC:APP)),products(a(HBP:HBP00067)))

SET Evidence = "Co-expression of ADAM9 with APP promoted sAPPalpha
production upon phorbol ester treatment, suggesting
that ADAM9 possesses alpha-secretase activity [51]"
SET Confidence = "High"
composite(p(HGNC:ADAM9),p(HGNC:APP),a(CHEBI:"phorbol ester")) -> a(HBP:HBP00067)

SET Evidence = "A dramatically
reduced ADAM10 protein level in the platelets of
sporadic AD patients was also found to correlate with
the significantly decreased sAPPalpha levels found in their
platlets and cerebrospinal fluid [55] and the reduced alpha-secretase
activity in the temporal cortex homogenates of AD patients [56]"
SET Confidence = "Medium"
SET MeSHAnatomy = "Blood Platelets"
path(MESHD:"Alzheimer Disease") negativeCorrelation p(HGNC:ADAM10)
SET MeSHAnatomy = {"Blood Platelets", "Cerebrospinal Fluid"}
path(MESHD:"Alzheimer Disease") negativeCorrelation a(HBP:HBP00067)
SET MeSHAnatomy = "Temporal Lobe"
path(MESHD:"Alzheimer Disease") negativeCorrelation act(p(HGNC:ADAM10))

SET Evidence = "In contrast to Abeta, sAPPalpha has an important role in
neuronal plasticity/survival and is protective against
excitotoxicity [42,43]. sAPPalpha also regulates neural stem
cell proliferation and is important for early CNS development
[57,58]"
SET Confidence = "High"
a(HBP:HBP00067) -- bp(GO:"regulation of neuronal synaptic plasticity")
a(HBP:HBP00067) reg bp(GO:"central nervous system formation")
a(HBP:HBP00067) -| bp(HBP:HBP00068)
SET MeSHAnatomy = "Neurons"
a(HBP:HBP00067) reg bp(GO:"stem cell proliferation")

SET Evidence = "We and others have also found that
sAPPalpha can inhibit stress-induced CDK5 activation and
participate in various neuroprotective reagent-mediated
excitoprotection [44,59-61]"
SET Confidence = "High"
a(HBP:HBP00067) -| act(p(HGNC:CDK5))
a(HBP:HBP00067) -| bp(HBP:HBP00068)

SET Evidence = "The first step in Abeta generation is cleavage of APP by the
beta-secretase"
SET Confidence = "High"
p(HGNC:BACE1) -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))

SET Evidence = "These results provide convincing
evidence that BACE1 is the beta-secretase involved in
APP metabolism [63-67]; and BACE1 activity is thought
to be the rate-limiting factor in Abeta generation from APP"
SET Confidence = "High"
p(HGNC:BACE1) -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))

SET Evidence = "Some studies found that BACE1 can interact with
reticulon/Nogo proteins, whose increased expression
can block BACE1 in the ER with a neutral pH environment
and thus inhibit BACE1 activity in Abeta generation
[73-75]"
SET CellStructure = "Endoplasmic Reticulum"
SET Confidence = "High"
complex(p(HGNC:BACE1),p(HGNC:RTN4)) -| rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))

SET Evidence = "On the other hand, Golgi-localized gamma-ear-containing
ARF-binding (GGA) proteins have been found to
interact with BACE1 and regulate its trafficking between
the late Golgi and early endosomes; and depletion of
GGA proteins increases the accumulation of BACE1 in
acidic early endosomes for enhanced BACE1 stability
and cleavage of APP [76-78]"
SET Confidence = "High"
SET CellStructure = "Endosomes"
complex(p(HGNC:GGA1),p(HGNC:BACE1)) -| rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))

SET Evidence = "BACE1 deficiency in AD model mice
have been shown to rescue cholinergic dysfunction, neuronal
loss and memory deficits, correlating with a dramatic
reduction in Abeta40/42 levels [79-81]"
SET Confidence = "Medium"
p(HGNC:BACE1) -> bp(GO:"neuron death")
p(HGNC:BACE1) -| bp(GO:"synaptic transmission, cholinergic")
p(HGNC:BACE1) -| bp(GO:memory)
p(HGNC:BACE1) -> p(HGNC:APP, frag(672_713))
p(HGNC:BACE1) -> p(HGNC:APP, frag(672_711))

SET Evidence = "As DS also results in Abeta accumulation, the genes location
suggests a link between BACE2 and APP processing"
SET Confidence = "Medium"
path(MESHD:"Down Syndrome") -> a(HBP:HBP00018)
p(HGNC:BACE2) -- rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))

SET Evidence = "Indeed, BACE2 cleaves beta-secretase substrates such as
wild-type and Swedish mutant APP, similar to BACE1,
in enzymatic In vitro assays [89]"
SET Confidence = "High"
p(HGNC:BACE2) -> rxn(reactants(p(HGNC:APP)),products(a(CHEBI:"amyloid-beta")))

SET Evidence = "Inhibition of cathepsin B has been found to reduce Abeta production
both in vivo and in vitro [92,93]"
SET Confidence = "High"
p(HGNC:CTSB) -> a(CHEBI:"amyloid-beta")

SET Evidence = "Although sAPPbeta only
differs from sAPPalpha by lacking the Abeta1-16 region at its
carboxyl-terminus, sAPPbeta was reported to function as a
death receptor 6 ligand and mediate axonal pruning and
neuronal cell death [94]"
SET Confidence = "High"
complex(a(HBP:HBP00042),p(HGNC:TNFRSF21)) -- bp(GO:"synapse pruning")
complex(a(HBP:HBP00042),p(HGNC:TNFRSF21)) -- bp(GO:"neuron death")

SET Evidence = "A recent report found that
sAPPbeta can rescue gene expression of transthyretin and
Klotho, which is decreased in APP/APLP2 deficient
mice, but cannot rescue the lethality and neuromuscular
synapse defects of these mice, suggesting a gene expression
regulation function for sAPPbeta that is independent
of developmental APP functions [95]"
SET Confidence = "High"
a(HBP:HBP00042) -> p(HGNC:TTR)
a(HBP:HBP00042) -> p(HGNC:KL)

SET Evidence = "However, overexpression of APP betaCTF
was found to be cytotoxic and cause neuronal degeneration,
perhaps by perturbing APP signal transduction [96,97]"
SET Confidence = "High"
a(HBP:HBP00024) -> bp(HBP:HBP00027)

SET Evidence = "It is also possible that APP betaCTF’s cytotoxic
effect is actually mediated by the end products of
gamma- and/or caspase-cleavage including APP intracellular
domain (AICD), C31 and Jcasp which are cytotoxic (see
below)"
SET Confidence = "Low"
a(MESHC:Caspases) -> rxn(reactants(p(HBP:HBP00071)),products(a(HBP:HBP00069),a(HBP:HBP00070)))

SET Evidence = "APP alphaCTF and betaCTF are further cleaved by gamma-secretase
to generate p83 and Abeta, respectively"
SET Confidence = "Medium"
p(FPLX:"Gamma_secretase") -> rxn(reactants(a(HBP:HBP00024)),products(a(CHEBI:"amyloid-beta"),a(HBP:HBP00072)))

SET Evidence = "One study showed that the ectodomain of
Nicastrin binds to APP and Notch and can recruit them
into the g-secretase complex, suggesting that Nicastrin
may act as the gamma-secretase receptor [108]"
SET Confidence = "Medium"
complex(p(HGNC:NCSTN),p(HGNC:APP),p(HGNC:NOTCH1)) -> complex(complex(SCOMP:"gamma Secretase Complex"),p(HGNC:APP))

SET Evidence = "Downregulation of CD147 increases Abeta production but its overexpression
has no effect on Abeta generation [113]"
SET Confidence = "Low"
#contrary results
p(HGNC:BSG) -| a(CHEBI:"amyloid-beta")
p(HGNC:BSG) causesNoChange a(CHEBI:"amyloid-beta")

SET Evidence = "However, another study failed to confirm the binding of TMP23/p21 to
gamma-secretase, but rather suggested that TMP21/p23, which belongs to the p24 cargo family involved in vesicular
trafficking regulation, influences APP trafficking and thus Abeta generation [116]"
SET Confidence = "High"
p(HGNC:TMED10) reg a(CHEBI:"amyloid-beta")

SET Evidence = "Recently, a novel gamma-secretase
activating protein (GSAP) was identified and GSAP
was found to selectively increase Abeta production through
interaction with both gamma-secretase and the APP CTF substrate [117]"
SET Confidence = "Medium"
complex(p(HGNC:GSAP),p(FPLX:"Gamma_secretase"),a(HBP:HBP00024)) -> a(CHEBI:"amyloid-beta")

SET Evidence = "In addition to cleaving APP CTFs, gamma-secretase
cleaves a series of functionally important transmembrane
proteins, including Notch [120], cadherin [114],
tyrosinase [121], ErbB4 [79], CD44 [70], etc.) (see review [122])"
SET Confidence = "Medium"
p(FPLX:"Gamma_secretase") -> p(HBP:HBP00024, frag("?"))
p(FPLX:"Gamma_secretase") -> p(HGNC:NOTCH1, frag("?"))
p(FPLX:"Gamma_secretase") -> p(FPLX:Cadherin, frag("?"))
p(FPLX:"Gamma_secretase") -> p(HGNC:TYR, frag("?"))
p(FPLX:"Gamma_secretase") -> p(HGNC:ERBB4, frag("?"))
p(FPLX:"Gamma_secretase") -> p(HGNC:CD44, frag("?"))

SET Evidence = "In a similar fashion, released AICD has been
shown to possess transactivation activity and can regulate
transcription of multiple genes including APP, GSK-
3b, KAI1, neprilysin, BACE1, p53, EGFR, and LRP1
[127-132]"
SET Confidence = "Medium"
tscript(a(HBP:HBP00071)) reg p(HGNC:APP)
tscript(a(HBP:HBP00071)) reg p(HGNC:GSK3B)
tscript(a(HBP:HBP00071)) reg p(HGNC:CD82)
tscript(a(HBP:HBP00071)) reg p(HGNC:MME)
tscript(a(HBP:HBP00071)) reg p(HGNC:BACE1)
tscript(a(HBP:HBP00071)) reg p(HGNC:EGFR)
tscript(a(HBP:HBP00071)) reg p(HGNC:LRP1)

SET Evidence = "In addition, free AICD can induce apoptosis
and may play a role in sensitizing neurons to toxic stimuli
[133,134]"
SET Confidence = "High"
a(HBP:HBP00071) -> bp(GO:"apoptotic process")

SET Evidence = "In addition to secretases, caspases (predominantly caspase-
3) can directly cleave APP at position Asp664
(based on the APP695 sequence) within the cytoplasmic
tail during apoptosis to release a fragment containing
the last 31 amino acids of APP (called C31)"
SET Confidence = "High"
a(MESHC:Caspases) -> rxn(reactants(p(HGNC:APP)),products(a(HBP:HBP00069)))

SET Evidence = "Additional
gamma-cleavage further generates the fragment (called Jcasp)
containing the region between gamma- and caspase-cleavage
sites"
SET Confidence = "High"
a(MESHC:Caspases) -> rxn(reactants(p(HGNC:APP)),products(a(HBP:HBP00070)))

SET Evidence = "One
possible mechanism for C31’s toxicity is that C31 complexes
with APP to recruit the interacting partners that
initiate the signals related to cellular toxicity [136]"
SET Confidence = "High"
complex(p(HGNC:APP),a(HBP:HBP00069)) -> bp(HBP:HBP00061)

SET Evidence = "Multiple
lines of evidence demonstrate that overproduction of Abeta
results in a neurodegenerative cascade leading to synaptic
dysfunction, formation of intraneuronal fibrillary tangles
and eventually neuron loss in affected areas of the brain [6,142]"
SET Confidence = "High"
a(CHEBI:"amyloid-beta") -> bp(HBP:HBP00027)
a(CHEBI:"amyloid-beta") -| bp(GO:"regulation of synapse structure or activity")
a(CHEBI:"amyloid-beta") -> bp(GO:"neurofibrillary tangle assembly")
a(CHEBI:"amyloid-beta") -> bp(GO:"neuron death")

SET Evidence = "There are two main toxic species, Ab40
and Ab42, with Abeta42 more hydrophobic and more
prone to fibril formation while only making up about
10% of the Abeta peptide produced [143]"
SET Confidence = "Medium"
p(HGNC:APP, frag(672_713)) -> a(HBP:HBP00038)

SET Evidence = "Studies done on
familial AD (FAD) mutations consistently show
increases in the ratio of Abeta42/40 [105,144], suggesting
that elevated levels of Abeta42 relative to Abeta40 is critical
for AD pathogenesis, probably by providing the core for
Abeta assembly into oligomers, fibrils and amyloidogenic
plaques [145,146]"
SET Confidence = "High"
p(HGNC:APP, frag(672_713)) -> path(MESHD:"Alzheimer Disease")
p(HGNC:APP, frag(672_713)) -> a(HBP:HBP00022)
p(HGNC:APP, frag(672_713)) -> a(HBP:HBP00038)
p(HGNC:APP, frag(672_713)) -> path(MESHD:"Plaque, Amyloid")

SET Evidence = "In Down Syndrome (DS) patients, the accumulation of
intracellular Abeta precedes extracellular plaque formation
[148] and the level of intraneuronal Ab decreases as the
extracellular Abeta plaques accumulate [149]"
SET MeSHDisease = "Down Syndrome"
SET Confidence = "High"
a(CHEBI:"amyloid-beta") -> path(MESHD:"Plaque, Amyloid")

SET Evidence = "Intraneuronal Abeta can also
impair amygdala-dependent emotional responses by
affecting the ERK/MAPK signaling pathway [153]"
SET MeSHAnatomy = "Neurons"
SET Confidence = "High"
a(CHEBI:"amyloid-beta") -- bp(GO:"MAPK cascade")
a(CHEBI:"amyloid-beta") -- bp(GO:"ERK1 and ERK2 cascade")

SET Evidence = "Inhibition of dynamin-mediated but not clathrin-mediated
Abeta internalization was also found to reduce Abeta-induced neurotoxicity [154]"
SET Confidence = "High"
complex(p(HGNC:DNM1),a(CHEBI:"amyloid-beta")) -| bp(HBP:HBP00061)

SET Evidence = "Although excessive Abeta causes synaptic dysfunction and synapse
loss [142], low levels of Abeta increase hippocampal longterm
potentiation and enhances memory, indicating a novel positive, modulatory role on neurotransmission
and memory [158,159]"
SET Confidence = "High"
a(CHEBI:"amyloid-beta") -| bp(GO:"regulation of synapse structure or activity")
a(CHEBI:"amyloid-beta") -| a(MESHCS:Synapses)
a(CHEBI:"amyloid-beta") -| bp(GO:"memory")
a(CHEBI:"amyloid-beta") -- bp(MESHPP:"Synaptic Transmission")
SET MeSHAnatomy = "Hippocampus"
a(CHEBI:"amyloid-beta") -| bp(GO:"long-term synaptic potentiation")

SET Evidence = "Picomolar levels of Abeta can also
rescue neuronal cell death induced by inhibition of Abeta
generation (by exposure to inhibitors of beta- or gamma-scretases)
[160], possibly through regulating the potassium ion channel expression, hence affecting neuronal excitability
[161]"
SET Confidence = "Low"
#low levels different to no levels. result because it has a different funtion when present in low amounts or due to missing activity of downregulation?
a(CHEBI:"amyloid-beta") -- a(MESHC:"Potassium Channels")
a(CHEBI:"amyloid-beta") -> bp(GO:"cell death")

SET Evidence = "For example, the
APP C-terminus has been found to interact with all
three mint (X11) family members (mint1, mint2, and
mint3) involved in trafficking regulation [163-165]"
SET Confidence = "High"
complex(p(HGNC:APP),p(HGNC:APBA1)) reg bp(GO:"protein transport")
complex(p(HGNC:APP),p(HGNC:APBA2)) reg bp(GO:"protein transport")
complex(p(HGNC:APP),p(HGNC:APBA3)) reg bp(GO:"protein transport")

SET Evidence = "APP
interaction with mint proteins has been shown to affect
APP processing by stabilizing cellular APP, altering both
sAPPalpha and Abeta generation and secretion [166]"
SET Confidence = "High"
complex(p(HGNC:APP),p(HGNC:APBA1)) -- a(HBP:HBP00067)
complex(p(HGNC:APP),p(HGNC:APBA1)) -- sec(a(HBP:HBP00067))
complex(p(HGNC:APP),p(HGNC:APBA1)) -- a(CHEBI:"amyloid-beta")
complex(p(HGNC:APP),p(HGNC:APBA1)) -- sec(a(CHEBI:"amyloid-beta"))
complex(p(HGNC:APP),p(HGNC:APBA2)) -- a(HBP:HBP00067)
complex(p(HGNC:APP),p(HGNC:APBA2)) -- sec(a(HBP:HBP00067))
complex(p(HGNC:APP),p(HGNC:APBA2)) -- a(CHEBI:"amyloid-beta")
complex(p(HGNC:APP),p(HGNC:APBA2)) -- sec(a(CHEBI:"amyloid-beta"))
complex(p(HGNC:APP),p(HGNC:APBA3)) -- a(HBP:HBP00067)
complex(p(HGNC:APP),p(HGNC:APBA3)) -- sec(a(HBP:HBP00067))
complex(p(HGNC:APP),p(HGNC:APBA3)) -- a(CHEBI:"amyloid-beta")
complex(p(HGNC:APP),p(HGNC:APBA3)) -- sec(a(CHEBI:"amyloid-beta"))

SET Evidence = "Rab6, a member of the GTP-binding protein family of membrane
trafficking regulators, is implicated in protein
transport along biosynthetic and endocytic pathways
and has also been found to affect APP processing"
SET Confidence = "High"
p(HGNC:RAB6A) -- bp(HBP:HBP00073)

SET Evidence = "Moreover, internalization of APP from the cell surface
for endosomal/lysosomal degradation can be mediated
by clathrin"
SET Confidence = "High"
bp(GO:"clathrin-dependent endocytosis") -> deg(p(HGNC:APP))

SET Evidence = "We and others have shown that
PS1 can also regulate the intracellular trafficking of APP"
SET Confidence = "High"
p(HGNC:PSEN1) reg p(HGNC:APP)

SET Evidence = "This PS1-PLD1 interaction recruits PLD1 to the Golgi/TGN
and thus potentially alters APP trafficking as PLD1 overexpression
promotes budding of vesicles from the TGN containing APP and increases cell surface levels of APP [176,177]"
SET Confidence = "High"
complex(p(HGNC:PLD1),p(HGNC:PSEN1)) -> p(HGNC:APP)

SET Evidence = "Recently it was found that SorLA/
LR11 overexpression redistributed APP to the Golgi,
decreasing Abeta generation, while SorLA/LR11 knockout
mice have increased levels of Abeta, as found in AD patients [182]"
SET Confidence = "High"
p(HGNC:SORL1) -| a(CHEBI:"amyloid-beta")

SET Evidence = "Low-density lipoprotein receptor-related protein (LRP)
is a SorLA/LR11-related protein that binds to APP through Fe65, a cytoplasmic adaptor protein [184]"
SET Confidence = "High"
complex(p(HGNC:APBB1),p(HGNC:LRP1),p(HGNC:APP))

SET Evidence = "Antagonizing the
extracellular interaction between cell-surface APP and
LRP increased the level of cell surface APP while
decreasing Abeta generation [187]"
SET Confidence = "Medium"
complex(p(HGNC:LRP1),p(HGNC:APP)) -> a(CHEBI:"amyloid-beta")
complex(p(HGNC:LRP1),p(HGNC:APP)) -| p(HGNC:APP)

SET Evidence = "An LRP-related protein 1B (LRP1B) has a similar effect, binding APP at
the plasma membrane, preventing APP internalization, and leading to decreased Abeta generation and increased
sAPPalpha secretion [189]"
SET Confidence = "High"
SET CellStructure = "Cell Membrane"
complex(p(HGNC:LRP1B),p(HGNC:APP)) -| a(CHEBI:"amyloid-beta")
complex(p(HGNC:LRP1B),p(HGNC:APP)) -> sec(a(HBP:HBP00067))

SET Evidence = "We have found that estrogen may reduce Abeta levels by
stimulating the alpha-secretase pathway and thereby inhibit Abeta generation"
SET Confidence = "High"
a(CHEBI:estrogen) -| a(CHEBI:"amyloid-beta")

SET Evidence = "Interestingly, the stimulation of
sAPPalpha secretion by estrogen can be blocked by a PKC
inhibitor, suggesting the involvement of a PKC-dependent pathway [200]"
SET Confidence = "High"
a(CHEBI:estrogen) -> sec(a(HBP:HBP00067))
act(p(HGNC:PRKCA)) -> sec(a(HBP:HBP00067))

SET Evidence = "Indeed, phorbol ester’s effect
on sAPPalpha secretion and Abeta generation though activation
of protein kinase C (PKC) has been known for a long
time [201-203]"
SET Confidence = "High"
a(CHEBI:"phorbol ester") -- sec(a(HBP:HBP00067))
a(CHEBI:"phorbol ester") -- a(CHEBI:"amyloid-beta")
a(CHEBI:"phorbol ester") -> act(p(HGNC:PRKCA))

SET Evidence = "PKC stimulates sAPPalpha secretion, reducing
Abeta levels, even when the phosphorylation sites on
APP are mutated or the entire cytoplasmic domain is
deleted [204]"
SET Confidence = "High"
p(HGNC:PRKCA) -> sec(a(HBP:HBP00067))
p(HGNC:PRKCA) -| a(CHEBI:"amyloid-beta")

SET Evidence = "While PKC can directly phosphorylate
APP Ser655 [205], it appears to affect APP metabolism
by phosphorylating a different target"
SET Confidence = "High"
p(HGNC:PRKCA) => p(HGNC:APP,pmod(Ph,Ser,655))

SET Evidence = "In support of this, protein kinase A (PKA) has
similar effects on reducing Abeta generation and stimulating
the budding of APP-containing vesicles from the TGN
[207]"
SET Confidence = "High"
p(HGNC:PRKACA) -| a(CHEBI:"amyloid-beta")

SET Evidence = "Additionally, estrogen has been found to facilitate binding of
Rab11 to the TGN membrane and a dominant negative Rab11 mutant abolishes the estrogen-regulated change in
APP trafficking, leading to increased Abeta formation [197]"
SET Confidence = "High"
p(HGNC:RAB11A) -| a(CHEBI:"amyloid-beta")

SET Evidence = "However, a recent study blocking the conversion of testosterone
to estrogen found an estrogen-independent improvement in cognitive function and lowering of plaque
formation along with a decrease in BACE1 mRNA, protein level, and activity [211]. In addition, testosterone
may also reduce the protein level of PS1 [196]"
SET Confidence = "Medium"
a(CHEBI:testosterone) -| path(MESHD:"Plaque, Amyloid")
a(CHEBI:testosterone) -| r(HGNC:BACE1)
a(CHEBI:testosterone) -| p(HGNC:BACE1)
a(CHEBI:testosterone) -| act(p(HGNC:BACE1))
a(CHEBI:testosterone) -| p(HGNC:PSEN1)
