#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Anti-aggregant tau mutant promotes neurogenesis"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/bd0996a28201cad363557315043c6392e31abf58/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/01c9daa61012b37dd0a1bc962521ba51a15b38f1/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET STATEMENT_GROUP = "Sandra"

# Add statements below this comment

SET Citation = {"PubMed", "Mol Neurodegener. 2017 Dec 4;12(1):88. doi: 10.1186/s13024-017-0230-8", "29202785"}

#pro-aggregant (TauRDΔK): p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"))
#anti-aggregant (TauRDΔKPP): p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro"))

SET Species = "10090"

SET Evidence = "This may be due to the strong Tau pathology of the toxic
pro-aggregant TauRDΔK leading to Tau aggregation, loss of synapses and loss of neurons"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> a(HBP:"Tau aggregates")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(GO:synapse)
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(MESH:Neurons)


SET Evidence = "Microscopic analysis
revealed a remarkable overall increase (30%) in the
size of the hippocampus of anti-aggregant TauRDΔKPP
OHSCs, compared to controls and the age-matched proaggregant
TauRDΔK slices (Fig. 2a)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:"Hippocampus")

SET Evidence = "This revealed an impressive increase in mature
neurons in all regions of the hippocampus (47% in CA1;
69% in CA3 and 81% in DG) in the anti-aggregant
TauRDΔKPP slices compared to age-matched controls
(Fig. 2c, bars 2, 5 and 8)"
SET MeSHAnatomy = "Hippocampus"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:Neurons)
UNSET MeSHAnatomy

SET Evidence = "By contrast, the pro-aggregant
TauRDΔK slices showed a pronounced reduction in neuronal
numbers, particularly in the CA1 (-44%), CA3
(-33%) and DG (-22%) regions compared to controls (Fig.
2c, bars 3, 6 and 9)"
SET MeSHAnatomy = {"CA1 Region, Hippocampal","CA3 Region, Hippocampal","Dentate Gyrus"}
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(MESH:Neurons)
UNSET MeSHAnatomy

SET Evidence = "Thus, pro-aggregant TauRDΔK causes
neurodegeneration, whereas anti-aggregant TauRDΔKPP
leads to neurogenesis, even in regions outside the DG"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> path(HBP:Neurodegeneration)
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> bp(GO:neurogenesis)

SET Evidence = "In controls and anti-aggregant
TauRDΔKPP slices, the microglia were mainly in the ramified
form, in contrast to the pro-aggregant TauRDΔK
slices where microglia were more of the reactive form
(Fig 3b)"
#ramified microglia: unactive microglia
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) neg act(a(MESH:Microglia))
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) pos act(a(MESH:Microglia))

SET Evidence = "Total numbers of Iba1 positive microglial cells
were reduced to 50% in anti-aggregant TauRDΔKPP slices
when compared to controls (Fig. 3c, bar 2)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -| a(MESH:Microglia)

SET Evidence = "The opposite
result was observed in the pro-aggregant TauRDΔK slices
where the microglial number was increased by 100%
compared to controls (Fig. 3c, bar 3)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> a(MESH:Microglia)

SET Evidence = "The antiaggregant
TauRDΔKPP slices had ramified form of microglia
with 6-7 branches on an average indicating that the
microglial cells were in their normal physiologically active
form and there is no sign of inflammation (Fig 3d,
bars 1 and 2)."
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) cnc act(a(MESH:Microglia))
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) cnc path(MESH:Inflammation)

SET Evidence = "On the contrary, microglia in proaggregant
TauRDΔK slices, were increased in number and
were also observed with 2-3 branches on an average
compared to age-matched controls and also the antiaggregant
TauRDΔKPP slices (Fig. 3d, bar 3). This indicates
that in the pro-aggregant TauRDΔK slices the
microglia are in a reactive form, indicating that there is
also enhanced inflammation"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> path(MESH:Inflammation)
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) pos act(a(MESH:Microglia))

SET Evidence = "On the contrary the proaggregant
TauRDΔK slices showed hypertrophic astrocytes
with prominent cell bodies with 1-3 processes ranging from
3-5μm in length, which can be compared to the degrading
forms of astrocytes (Fig. 4a3)"
SET Cell = "astrocyte"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> path(MESH:Hypertrophy)
UNSET Cell

SET Evidence = "In the controls and the antiaggregant
TauRDΔKPP slices there was a uniform axonal
distribution of Tau in the hippocampus (Fig. 5, A1 and 2)"
SET MeSHAnatomy = "Hippocampus"
SET CellStructure = "Axons"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> p(MGI:Mapt)
UNSET MeSHAnatomy
UNSET CellStructure

SET Evidence = "The pro-aggregant TauRDΔK slices showed intense
mislocalization of Tau in the somato-dendritic compartment,
the most affected being the CA3 pyramidal neurons (Fig. 5, A3)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> tloc(p(MGI:Mapt),fromLoc(GO:axon),toLoc(GO:"somatodendritic compartment"))

SET Evidence = "Surprisingly, there was an ~80%
increase in the endogenous mouse Tau level in slices obtained
from pups expressing anti-aggregant TauRDΔKPP
(Fig. 5b lane 2 and 5c bar 2), compared with age
matched non-transgenic controls and pro-aggregant
TauRDΔK (Fig. 5b lane 1 and 3; and 5c lane 1 and 3)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> p(MGI:Mapt)

SET Evidence = "In
spite of this increase, there was little mislocalization of
Tau (endogenous or exogenous) into the somatodendritic
compartment in the anti-aggregant TauRDΔKPP (Fig. 5, A2)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> tloc(p(MGI:Mapt),fromLoc(GO:axon),toLoc(GO:"somatodendritic compartment"))

SET Evidence = "By contrast, pro-aggregant TauRDΔK slices
showed no overall increase in endogenous mouse Tau
(rather a 20% decrease), yet pronounced mislocalization (Fig. 5, A3)."
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) cnc p(MGI:Mapt)
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> tloc(p(MGI:Mapt),fromLoc(GO:axon),toLoc(GO:"somatodendritic compartment"))

SET Evidence = "From DIV15 until
DIV30 the anti-aggregant slices were treated with DOX
to switch off the expression of the anti-aggregant Tau.
This lead to a strong reduction (~70%) of the endogenous
mouse Tau (Fig. 10c, lane3)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> p(MGI:Mapt)

SET Evidence = "However, in case of the anti-aggregant
TauRDΔKPP slices there was a remarkable 30% increase in
the number of BrdU positive cells in the CA1 and CA3 regions
and almost 100% increase in DG (Fig. 6b, bar 2, 5, and 8)"
SET MeSHAnatomy = {"CA1 Region, Hippocampal","CA3 Region, Hippocampal","Dentate Gyrus"}
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:"Stem Cells")
UNSET MeSHAnatomy

SET Evidence = "This was in striking contrast to the pro-aggregant
TauRDΔK slices where there was no significant difference
in the number of BrdU positive cells compared to the age
matched controls (Fig. 6b, bar 3, 6, and 9)"
SET MeSHAnatomy = {"CA1 Region, Hippocampal","CA3 Region, Hippocampal","Dentate Gyrus"}
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) cnc a(MESH:"Stem Cells")
UNSET MeSHAnatomy

SET Evidence = "In particular in the CA3 and DG regions almost 70% and in CA1 almost 50% of the new born cells got differentiated
into NeuN positive neurons in the anti-aggregant TauRDΔKPP slices (Additional file 2: Figure S1A, bars
4, 8, 12).This reveals an increase in proliferation followed by an increase in neuronal differentiation in
the anti-aggregant TauRDΔKPP slices"
SET MeSHAnatomy = {"CA1 Region, Hippocampal","CA3 Region, Hippocampal","Dentate Gyrus"}
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> bp(GO:"neuron differentiation")
UNSET MeSHAnatomy

SET Evidence = "This also resulted in a 30% increase
in the rate of differentiation in the CA1 region of the
pro-aggregant TauRDΔK slices (Additional file 2: Fig. S1B,
bars 2 and 4)"
SET MeSHAnatomy = "CA1 Region, Hippocampal"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -> bp(GO:"neuron differentiation")
UNSET MeSHAnatomy

SET Evidence = "Notably, after switch-off of antiaggregant
TauRDΔKPP there was a change in the number
of BrdU stained cells in all regions of the hippocampus
(CA1 region 32%, CA3 region 22% and DG 33% reduction)
compared to switch-ON conditions (Fig. 7a, bars 4, 8 and 12)"
SET MeSHAnatomy = {"CA1 Region, Hippocampal","CA3 Region, Hippocampal","Dentate Gyrus"}
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:Neurons)
UNSET MeSHAnatomy

SET Evidence = "Similarly in the number of NeuN positive
cells, there was a 22% reduction in CA1, 33% in CA3
and 37% reduction in DG compared to switch-On conditions
(Fig. 7b, bars 4, 8 and 12)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:Neurons)

SET Evidence = "BrdU positive cells were present in
CA1, CA3 and DG in both the controls and antiaggregant
TauRDΔKPP groups (Fig. 8a, b), but their numbers
were increased strongly by 80% only in the CA3
region of the anti-aggregant TauRDΔKPP mice (Fig. 8b,
bar 4), with 20% change in the CA1 and no change in
the DG (Fig. 8b, bar 2 and 6)"
SET MeSHAnatomy = {"CA1 Region, Hippocampal","CA3 Region, Hippocampal","Dentate Gyrus"}
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:Neurons)
UNSET MeSHAnatomy

SET Evidence = "Additionally,
there was an increase by 25% of the hippocampal volume
in anti-aggregant TauRDΔKPP mice, compared to
controls at P8 (Fig. 8c, bar 2), presumably due to the increased
number of neurons"
SET MeSHAnatomy = "Hippocampus"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:Neurons)
UNSET MeSHAnatomy
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:"Hippocampus")

SET Evidence = "The anti-aggregant TauRDΔKPP mice had an increased
hippocampal volume of ~15% compared to agematched
controls but it was not a significant increase as
analyzed by bonferroni post hoc test (Fig. 9a, bar 2) in
contrast mice expressing pro-aggregant TauRDΔK had a
25% reduced hippocampal volume (Fig. 9a, bar 3)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:"Hippocampus")
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(MESH:"Hippocampus")

SET Evidence = "Surprisingly, the anti-aggregant TauRDΔKPP mice had
an increased neuronal number significantly in the
CA3 region (20%, Fig. 9b, bar 5), in contrast to the
pro-aggregant TauRDΔK mice where neuronal loss (e.g.
CA1 ~50%, CA3 ~10%, DG ~25%) was observed in
all regions of the hippocampus (Fig. 9b, bar 3, 6, 9)"
SET MeSHAnatomy = "CA3 Region, Hippocampal"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:Neurons)
SET MeSHAnatomy = "Hippocampus"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del")) -| a(MESH:Neurons)
UNSET MeSHAnatomy

SET Evidence = "Surprisingly there was a 50% reduction
in the amount of Wnt5a protein in anti-aggregant
TauRDΔKPP slices compared to controls (Fig. 10a, lane 2, bar 2)."
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -| p(MGI:Wnt5a)

SET Evidence = "However, when the expression of the antiaggregant
TauRDΔKPP was switched off by DOX, there was
an even more pronounced reduction in Wnt5a level
(Fig. 10a, lane 3, bar 3)"
a(CHEBI:doxycycline) -| p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro"))
a(CHEBI:doxycycline) -| p(MGI:Wnt5a)

SET Evidence = "Unexpectedly the
Wnt3 levels in the anti-aggregant TauRDΔKPP slices were
increased substantially (up to 85%) compared to the agematched
control slices (Fig. 10b, lane 2, bar 2)"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> p(MGI:Wnt3)

SET Evidence = "Remarkably, this caused a 40% reduction of the
Wnt3 level compared to control slices (Fig. 10b, lane 3, bar 3)"
#this refers to doxycycline
a(CHEBI:doxycycline) -| p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro"))
a(CHEBI:doxycycline) -| p(MGI:Wnt3)
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> p(MGI:Wnt3)

SET Evidence = "This suggests that the expression
of anti-aggregant TauRDΔKPP is needed for the increased
proliferation of newborn neurons, and since these new
born neurons need endogenous mouse Tau for their migration,
differentiation, and maturation, there is enhanced
expression of endogenous mouse Tau"
p(HBP:"Tau isoform F (441 aa)", var("p.Lys280del"),var("p.Ile277Pro"),var("p.Ile308Pro")) -> a(MESH:Neurons)
p(MGI:Mapt) -> bp(GO:"neuron migration")
p(MGI:Mapt) -> bp(GO:"neuron differentiation")
p(MGI:Mapt) -> bp(GO:"neuron maturation")

UNSET Species

##########################################################


UNSET STATEMENT_GROUP
