#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Incidence and extent of TDP-43 accumulation in aging human brain"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Yojana Gadiya"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
 DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
# DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
 DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
 DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
 DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
 DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
# DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
 DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
 DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
 DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
 DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
 DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
 DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
 DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
 DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
 DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
 DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
 DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
 DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
 DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
 DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
 DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
 DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
 DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
 DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "26091809"}

SET Evidence = "The transactivation response element DNA-binding
protein 43 kDa (TDP-43) is a major component of the
ubiquitin-positive and tau-negative inclusions in frontotemporal
lobar degeneration (FTLD-TDP) and sporadic
amyotrophic lateral sclerosis (ALS) [1–3]."
SET Confidence = "High"
SET MeSHDisease = {"Amyotrophic Lateral Sclerosis", "Frontotemporal Lobar Degeneration"}
p(MESH:"Ubiquitin", loc(GO:"inclusion body")) -> p(HGNC:"TARDBP")
p(HGNC:"MAPT", loc(GO:"inclusion body")) -| p(HGNC:"TARDBP")
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "One such study demonstrated that TDP-43 deposition
increased with age and did not occur in individuals
younger than 65 years [12]."
SET Confidence = "Medium"
#Age 65 and above
path(MESH:"Aging") pos p(HGNC:"TARDBP")
UNSET Confidence

SET Evidence = "Compared with
the control elderly group, the mean age at death was
higher for AD, LBD, and AGD groups (Table 3)."
SET Confidence = "High"
#AD : Alzheimer’s disease
#LBD : Lewy body disease
#AGD : argyrophilic grain disease, type of Dementia
path(MESH:"Alzheimer Disease") -> path(MESH:"Mortality")
path(MESH:"Lewy Body Disease") -> path(MESH:"Mortality")
path(MESH:"Dementia") -> path(MESH:"Mortality")
UNSET Confidence

SET Evidence = "In control elderly brains, TDP-43
immunoreactive GCIs and NCIs were rare. In fact,
GCIs were observed only in a 94-year-old woman’s
entorhinal cortex and NCIs only in a 98-year-old
woman’s subiculum and pyramidal layer (Table 5)."
SET Confidence = "High"
#Neuronal cytoplasmic inclusions (NCIs)
#glial cytoplasmic inclusions (GCIs)
SET Gender = "Female"
#Age 94 yrs
SET MeSHAnatomy = "Entorhinal Cortex"
p(HGNC:"TARDBP") -> p(HGNC:"TARDBP", loc(GO:"glial cytoplasmic inclusion"))
UNSET MeSHAnatomy
#subiculum component of Hippocampus
#Age 98 yrs
SET MeSHAnatomy = {"Pyramidal Cells", "Hippocampus"}
SET Cell = "neuron"
p(HGNC:"TARDBP") -> p(HGNC:"TARDBP", loc(GO:"inclusion body"))
UNSET Cell
UNSET MeSHAnatomy
UNSET Gender
UNSET Confidence

SET Evidence = "TDP-43–positive structures were found in 21/29
(72.2 %) AD brains and were widely distributed in the
amygdala and hippocampus (Tables 4, 5). These structures
were composed of NCIs (17/29 cases, 58.6 %)
and GCIs (18/29 cases, 62.1 %) as well as DNs (19/29
cases, 65.5 %) (Fig. 3b, c)."
SET Confidence = "High"
#dystrophic neurites (DNs)
SET MeSHDisease = "Alzheimer Disease"
SET MeSHAnatomy = {"Amygdala", "Hippocampus"}
p(HGNC:"TARDBP") -> p(HGNC:"TARDBP", loc(GO:"glial cytoplasmic inclusion"))
SET Cell = "neuron"
p(HGNC:"TARDBP") -> p(HGNC:"TARDBP", loc(GO:"inclusion body"))
UNSET Cell
SET CellStructure = "Neurites"
p(HGNC:"TARDBP") -> p(HGNC:"TARDBP", loc(GO:"inclusion body"))
UNSET CellStructure
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Confidence

SET Evidence = "DNs were observed in the
uncus of the anterior hippocampus (11/28 cases,
39.3 %; in one case, the anterior hippocampus was unavailable)
as well as in the amygdala (14/29 cases,
48.3 %). In 7/29 cases (24.1 %), TDP-43 immunoreactive
NIIs were also present in the amygdala and hippocampus
(Fig. 3d)."
SET Confidence = "High"
#uncus of the anterior hippocampus segment of Parahippocampal Gyrus
SET MeSHAnatomy = {"Parahippocampal Gyrus", "Amygdala"}
SET CellStructure = "Neurites"
p(HGNC:"TARDBP") -> p(HGNC:"TARDBP", loc(GO:"inclusion body"))
UNSET CellStructure
UNSET MeSHAnatomy
#neuronal intranuclear inclusions (NIIs)
SET MeSHAnatomy = {"Amygdala", "Hippocampus"}
p(HGNC:"TARDBP") -> path(MESH:"Intranuclear Inclusion Bodies")
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "In the lumbar spinal cord, we found
TDP-43 immunoreactive DNs in the white matter in an
85-year-old male."
SET Confidence = "High"
#Age 85 yrs
SET Gender = "Male"
SET MeSHAnatomy = {"Lumbar Vertebrae", "Spinal Cord", "White Matter"}
SET CellStructure = "Neurites"
p(HGNC:"TARDBP") -> p(HGNC:"TARDBP", loc(GO:"inclusion body"))
UNSET CellStructure
UNSET MeSHAnatomy
UNSET Gender
UNSET Confidence

SET Evidence = "The mean age at death was significantly higher in cases
with TDP-43 immunoreactive structures than in those
without TDP-43 immunoreactive structures in control
elderly and AGD (Table 4)."
SET Confidence = "High"
p(HGNC:"TARDBP") -> path(MESH:"Mortality")
UNSET Confidence
