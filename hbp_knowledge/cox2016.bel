#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Analysis of Isoform-specific Tau Aggregates Suggests a Common Toxic Mechanism Involving Similar Pathological Conformations and Axonal Transport Inhibition"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/7e4be528f12abd28be768b62402fba6e083eaf9e/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "27574109"}

# 'Right angle laser light scattering' used to measure aggregation

SET Evidence = "Right angle laser light scattering showed significantly greater scattered light
intensity in all 4R tau isoforms when compared to 3R isoforms (one-way ANOVA with Holm-Sidak
post-hoc, F(5, 18) = 60.22, p < 0.0001; Fig. 2A)"
a(HBP:"3R tau") positiveCorrelation path(MESH:"Protein Aggregation, Pathological")
a(HBP:"4R tau") positiveCorrelation path(MESH:"Protein Aggregation, Pathological")

SET Evidence = "hT40 showed the highest amount of light scattering compared to
other 4R isoforms, and there were no differences between the different 3R isoforms"
a(HBP:"Tau isoform F (441 aa)") positiveCorrelation path(MESH:"Protein Aggregation, Pathological")

SET Evidence = "Similar results were seen in the ThS assay, where the 4R isoforms were
significantly higher than 3R isoforms (one-way ANOVA with Holm-Sidak
post-hoc, F(5, 18) = 19.99, p < 0.0001; Fig. 2B), and no differences were
found in comparisons between the individual 4R isoforms or between the 3R isoforms"
a(HBP:"3R tau") positiveCorrelation path(MESH:"Protein Aggregation, Pathological")
a(HBP:"4R tau") positiveCorrelation path(MESH:"Protein Aggregation, Pathological")

SET Evidence = "A mixture of long, intermediate and short filaments, as well as globular
oligomers were present in 4R isoform reactions (Fig. 2C–E)"
a(HBP:"4R tau") positiveCorrelation bp(GO:"positive regulation of protein oligomerization")

SET Evidence ="In contrast, 3R isoforms were primarily composed of globular oligomers and only very rare long filaments were found (Fig. 2F–H)"
a(HBP:"3R tau") positiveCorrelation bp(GO:"positive regulation of protein oligomerization")

# PAD exposure (TNT1 reactivity)

SET Evidence = "Comparisons between isoform monomers showed that hT39 monomer signal was significantly higher
than hT24 and hT23 monomers (Kruskal-Wallis ANOVA with Dunn’s post-hoc, H = 18.4, p = 0.0025)"
# Tau isoform D (383 aa): htau24
# Tau isoform C (410 aa): htau39
# Tau isoform Fetal-tau (352 aa): htau23
a(HBP:"Tau isoform Fetal-tau (352 aa)") -- a(HBP:"phosphatase-activating domain")
a(HBP:"Tau isoform D (383 aa)") -- a(HBP:"phosphatase-activating domain")
a(HBP:"Tau isoform C (410 aa)") -- a(HBP:"phosphatase-activating domain")

SET Evidence = "The hT24 aggregates showed the highest TNT1 signal, which reached significance compared to
hT40, hT39, hT37 and hT23 aggregates (one-way ANOVA with Holm-Sidak post-hoc, F(5, 18) = 19.11, p < 0.0001)"
# Tau isoform Fetal-tau (352 aa): htau40
# Tau isoform B (381 aa): htau37
p(HBP:"Tau isoform D (383 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform C (410 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform Fetal-tau (352 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform B (381 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform F (441 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")

SET Evidence = "Aggregates of all six tau isoforms showed significant increases in TNT1 reactivity when compared
to their respective monomer samples (Fig. 3A; Mann-Whitney test, for all comparisons p = 0.029)"
# 6 isoforms: hT40, hT34, hT24, hT39, hT37, hT23
# HBP:"Tau isoform E (412 aa)": htau34
p(HBP:"Tau isoform D (383 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform C (410 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform Fetal-tau (352 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform B (381 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform F (441 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform E (412 aa)",pmod(HBP:"protein aggregation")) positiveCorrelation a(HBP:"phosphatase-activating domain")

# determine the levels of tau oligomers (TOC1 reactivity)

SET Evidence = "Comparisons between isoform monomers showed that hT39 monomer signal was
significantly higher than hT24 and hT23 monomers (Kruskal-Wallis ANOVA with Dunn’s post-hoc, H = 18.6, p = 0.0023)"
a(HBP:"Tau isoform C (410 aa)") -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform D (383 aa)") -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform Fetal-tau (352 aa)") -> a(HBP:"Tau oligomers")

SET Evidence = "The hT24 aggregates showed the highest TOC1 signal, which reached significance compared to
hT40, hT39, hT37 and hT23 aggregates, while hT34 aggregates were significantly different from hT39, hT37 and
hT23 aggregates, and both hT40 and hT39 aggregates are significantly higher than hT37 and hT23
(one-way ANOVA with Holm-Sidak post-hoc, F(5, 18) = 50.77, p < 0.0001)"
p(HBP:"Tau isoform D (383 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform C (410 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform Fetal-tau (352 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform B (381 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform F (441 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform E (412 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")

SET Evidence = "Aggregated samples for all six isoforms showed significant increases in TOC1 reactivity
when compared to their respective monomer samples (Fig. 3B; Mann-Whitney tests, for all comparisons p = 0.029)"
p(HBP:"Tau isoform D (383 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform C (410 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform Fetal-tau (352 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform B (381 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform F (441 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
p(HBP:"Tau isoform E (412 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")

SET Evidence = "As expected, monomer and aggregated samples of all six tau isoforms showed equal reactivity
for TNT1 and TOC1 when the samples were denatured because this exposes the epitopes making them equally
accessible (Student’s t-tests, for all comparisons p > 0.05; Fig. 3C–H)"
p(HBP:"Tau isoform D (383 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform D (383 aa)") -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform D (383 aa)") positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform C (410 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform C (410 aa)") -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform C (410 aa)") positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform Fetal-tau (352 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform Fetal-tau (352 aa)") -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform Fetal-tau (352 aa)") positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform B (381 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform B (381 aa)") -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform B (381 aa)") positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform F (441 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform F (441 aa)") -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform F (441 aa)") positiveCorrelation a(HBP:"phosphatase-activating domain")
p(HBP:"Tau isoform E (412 aa)",pmod(HBP:"protein aggregation")) -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform E (412 aa)") -> a(HBP:"Tau oligomers")
a(HBP:"Tau isoform E (412 aa)") positiveCorrelation a(HBP:"phosphatase-activating domain")

# effect of isoform-specific tau aggregates on fast axonal transport

SET MeSHAnatomy = {"Axons","Cytoplasm"}

SET Evidence = "Perfusion of hT40, hT34 and hT24 aggregates into squid axoplasms
significantly impaired anterograde transport (Fig. 4A) when compared to the respective monomers (all at 2 μM)."
p(HBP:"Tau isoform E (412 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")
p(HBP:"Tau isoform F (441 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")
p(HBP:"Tau isoform D (383 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")

SET Evidence = "Similarly, perfusion of squid axoplasms with hT39, hT37 and hT23 aggregates significantly
impaired anterograde FAT (Fig. 4A) when compared to the respective monomers (all at 2 μM)"
p(HBP:"Tau isoform C (410 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")
p(HBP:"Tau isoform B (381 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")
p(HBP:"Tau isoform Fetal-tau (352 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")

UNSET MeSHAnatomy

SET Evidence = "Pairwise comparisons within tau species showed that hT24 aggregates produced
significantly more inhibition of anterograde FAT when compared to hT34 and hT39 aggregates."
p(HBP:"Tau isoform D (383 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")
p(HBP:"Tau isoform C (410 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")
p(HBP:"Tau isoform E (412 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"anterograde axonal transport")

SET Evidence = "hT40, hT34, hT24, hT37 and hT23 aggregates did not significantly impair retrograde FAT
when compared to the respective monomers, but hT39 aggregates elicited a mild inhibitory effect on retrograde FAT (Fig. 4B)"
p(HBP:"Tau isoform C (410 aa)",pmod(HBP:"protein aggregation")) -| bp(GO:"retrograde axonal transport")
p(HBP:"Tau isoform F (441 aa)",pmod(HBP:"protein aggregation")) cnc bp(GO:"retrograde axonal transport")
p(HBP:"Tau isoform E (412 aa)",pmod(HBP:"protein aggregation")) cnc bp(GO:"retrograde axonal transport")
p(HBP:"Tau isoform D (383 aa)",pmod(HBP:"protein aggregation")) cnc bp(GO:"retrograde axonal transport")
p(HBP:"Tau isoform B (381 aa)",pmod(HBP:"protein aggregation")) cnc bp(GO:"retrograde axonal transport")
p(HBP:"Tau isoform Fetal-tau (352 aa)",pmod(HBP:"protein aggregation")) cnc bp(GO:"retrograde axonal transport")

SET Evidence = "Collectively, these studies indicate that inhibition of anterograde FAT represents a toxic effect common
to all tau aggregates, regardless of isoform composition"
a(HBP:"Tau aggregates") -| bp(GO:"anterograde axonal transport")

# antibodies: TNT1 (PAD exposure), TOC1 (tau oligomers) and R1 (pan-tau marker)

SET MeSHAnatomy = "Hippocampus"

SET Evidence = "Indeed, early pre-tangle neurons within the hippocampus were labeled with
 all antibodies in Braak I-II cases (Fig. 5A–D)"
a(HBP:pretangles) -- a(HBP:"Tau oligomers")
a(HBP:pretangles) -- a(HBP:"phosphatase-activating domain")
a(HBP:pretangles) -- p(HGNC:MAPT)
a(HBP:"Braak_Stage I") -- a(HBP:"Tau oligomers")
a(HBP:"Braak_Stage I") -- a(HBP:"phosphatase-activating domain")
a(HBP:"Braak_Stage I") -- p(HGNC:MAPT)
a(HBP:"Braak_Stage II") -- a(HBP:"Tau oligomers")
a(HBP:"Braak_Stage II") -- a(HBP:"phosphatase-activating domain")
a(HBP:"Braak_Stage II") -- p(HGNC:MAPT)

SET Evidence = "In severe AD cases (i.e. Braak stage V-VI), all markers continue to colocalize in classic NFTs within the
hippocampus that characterize AD tau pathology (Fig. 5E–H)"
SET MeSHDisease = "Alzheimer Disease"
a(MESH:"Neurofibrillary Tangles") -- a(HBP:"Tau oligomers")
a(MESH:"Neurofibrillary Tangles") -- a(HBP:"phosphatase-activating domain")
a(MESH:"Neurofibrillary Tangles") -- p(HGNC:MAPT)

UNSET MeSHDisease

SET MeSHAnatomy = "Frontal Lobe"

SET Evidence = "In CBD, the characteristic astrocytic pathology (e.g. astrocytic plaques) showed extensive
co-localization between TNT1, TOC1 and R1 in the frontal cortex (Fig. 5I–L)."
a(HBP:"Corticobasal Degeneration") -- a(HBP:"Tau oligomers")
a(HBP:"Corticobasal Degeneration") -- a(HBP:"phosphatase-activating domain")
a(HBP:"Corticobasal Degeneration") -- p(HGNC:MAPT)

SET Evidence = "Similarly, the characteristic Pick bodies in the frontal cortex were well labeled by TNT1, TOC1 and R1 in PiD tissue (Fig. 5M–P)"
SET MeSHDisease = "Pick Disease of the Brain"
a(GO:"Pick body") -- a(HBP:"Tau oligomers")
a(GO:"Pick body") -- a(HBP:"phosphatase-activating domain")
a(GO:"Pick body") -- p(HGNC:MAPT)
UNSET MeSHDisease

UNSET MeSHAnatomy

SET Evidence = "In general, the remarkable co-localization between TNT1, TOC1 and R1 in all tauopathies confirms that
PAD exposure and tau oligomerization occur simultaneously in cells displaying tau pathology, irrespective of isoform composition"
path(MESH:Tauopathies) positiveCorrelation a(HBP:"Tau oligomers")
path(MESH:Tauopathies) positiveCorrelation a(HBP:"phosphatase-activating domain")

SET Evidence = "The band patterns in the immunoblots showed that the AD cases contained a mixture of isoforms, the PiD cases clearly
contained 3R isoforms but also some 4R isoforms, while the vast majority of pathology in CBD cases were comprised of 4R tau isoforms"
path(MESH:"Alzheimer Disease") positiveCorrelation a(HBP:"3R tau")
path(MESH:"Alzheimer Disease") positiveCorrelation a(HBP:"4R tau")
path(MESH:"Pick Disease of the Brain") positiveCorrelation a(HBP:"3R tau")
path(MESH:"Pick Disease of the Brain") positiveCorrelation a(HBP:"4R tau")
a(HBP:"Corticobasal Degeneration") positiveCorrelation a(HBP:"4R tau")

SET Evidence = "Total tau levels in the soluble fractions were similar for AD, CBD and PiD, as
indicated by the Tau5 sandwich ELISA (Fig. 6B; one-way ANOVA, F(2,9) = 3.283, p = 0.085)"
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT)
path(MESH:"Pick Disease of the Brain") positiveCorrelation p(HGNC:MAPT)
a(HBP:"Corticobasal Degeneration") positiveCorrelation p(HGNC:MAPT)

SET Evidence = "In contrast, AD soluble tau displayed the highest level of TNT1 followed
by CBD, with PiD having the lowest levels (Fig. 6C; one-way ANOVA with Holm-Sidak post-hoc, F(2,9) = 24.87, p = 0.0002)."
path(MESH:"Alzheimer Disease") positiveCorrelation a(HBP:"phosphatase-activating domain")
path(MESH:"Pick Disease of the Brain") positiveCorrelation a(HBP:"phosphatase-activating domain")
a(HBP:"Corticobasal Degeneration") positiveCorrelation a(HBP:"phosphatase-activating domain")

SET Evidence = "Similarly, the soluble fraction from AD contained the greatest level of TOC1 reactivity, followed by CBD
and then PiD had the lowest signal (Fig. 6D; one-way ANOVA with Holm-Sidak post-hoc, F(2,9) = 16.57, p = 0.001)"
path(MESH:"Alzheimer Disease") positiveCorrelation a(HBP:"Tau oligomers")
path(MESH:"Pick Disease of the Brain") positiveCorrelation a(HBP:"Tau oligomers")
a(HBP:"Corticobasal Degeneration") positiveCorrelation a(HBP:"Tau oligomers")

SET Evidence = "Total tau levels in the insoluble fractions, as detected by Tau5, were highest in AD, followed by
CBD and PiD contained the least (Fig. 6E; one-way ANOVA with Holm-Sidak post-hoc, F(2,9) = 25.93, p = 0.0002)"
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT)
path(MESH:"Pick Disease of the Brain") positiveCorrelation p(HGNC:MAPT)
a(HBP:"Corticobasal Degeneration") positiveCorrelation p(HGNC:MAPT)

SET Evidence = "TNT1 detected significantly more PAD exposed tau in AD compared to PiD, and more in CBD when compared to
PiD, but AD and CBD were not different (Fig. 6F; one-way ANOVA with Holm-Sidak post-hoc, F(2,9) = 12.07, p = 0.0028)"
path(MESH:"Alzheimer Disease") positiveCorrelation composite(p(HGNC:MAPT),a(HBP:"phosphatase-activating domain"))
path(MESH:"Pick Disease of the Brain") positiveCorrelation composite(p(HGNC:MAPT),a(HBP:"phosphatase-activating domain"))
a(HBP:"Corticobasal Degeneration") positiveCorrelation composite(p(HGNC:MAPT),a(HBP:"phosphatase-activating domain"))

SET Evidence = "TOC1 detected significantly more oligomeric tau in AD compared to CBD and PiD and more in
CBD compared to PiD (Fig. 6G; one-way ANOVA with Holm-Sidak post-hoc, F(2,9) = 35.32, p < 0.0001)"
path(MESH:"Alzheimer Disease") positiveCorrelation a(HBP:"Tau oligomers")
path(MESH:"Pick Disease of the Brain") positiveCorrelation a(HBP:"Tau oligomers")
a(HBP:"Corticobasal Degeneration") positiveCorrelation a(HBP:"Tau oligomers")
