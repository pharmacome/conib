#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "The autism/neuroprotection-linked ADNP/NAP regulate the excitatory glutamatergic synapse"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
#DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
#DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
#DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
#DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "30664622"}

SET Species = "10090"

SET Evidence = "Figure 1a shows enhanced brain/body bioavailability
in the presence of the newly tested vehicle, chlorobutanol over time."
# penetration of NAP into the brain/body
a(CHEBI:chlorobutanol) -> tloc(a(PUBCHEM:9832404),fromLoc(GO:"extracellular region"),toLoc(MESH:Brain))
a(CHEBI:chlorobutanol) -> tloc(a(PUBCHEM:9832404),fromLoc(GO:"extracellular region"),toLoc(MESH:"Cell Body"))

SET Evidence = "Fig. 1b displays enhanced brain bioavailability by picture evaluation. Fig. 1c shows
quantitative assessment of three independent experiments, revealing the dramatic 4-fold increase in specific brain bioavailability."
# in the presence of chlorobutanol
# penetration of NAP into the brain/body
a(CHEBI:chlorobutanol) -> tloc(a(PUBCHEM:9832404),fromLoc(GO:"extracellular region"),toLoc(MESH:Brain))

SET Evidence = "Further results showed no significant brain penetration of Evans blue ( < 0.1 ng/ml,
which was the limit of detection), and no difference between chlorobutanol and saline."
# penetration of NAP into the brain/body
a(CHEBI:"Evans blue") cnc tloc(a(PUBCHEM:9832404),fromLoc(GO:"extracellular region"),toLoc(MESH:Brain))

SET Evidence = "Representative pictures show penetration of the Evans blue dye in the periphery (Fig. 1d), but
not in the brain (Fig. 1e), with brain concentration values < 0.03 ng/ml (Supplemental Fig. S1)"
# penetration of NAP into the brain/body
a(CHEBI:"Evans blue") -> tloc(a(PUBCHEM:9832404),fromLoc(GO:"extracellular region"),toLoc(GO:"cell periphery"))
a(CHEBI:"Evans blue") cnc tloc(a(PUBCHEM:9832404),fromLoc(GO:"extracellular region"),toLoc(MESH:Brain))

SET Evidence = "These findings provide clear evidence that the blood brain barrier
remained intact in the presence of chlorobutanol"
a(CHEBI:chlorobutanol) cnc a(MESH:"Blood-Brain Barrier")

SET Evidence = "Adnp+/− mice compared with Adnp+/+ mice spent significantly shorter time periods in exploring the new
objects, indicative of impaired memory, with intranasal NAP-CB treatment completely ameliorating this impairment (Fig. 2a, b)."
# NAP-CB: NAP-chlorobutanol
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -| bp(GO:memory)
p(HGNC:ADNP) -> bp(GO:memory)
composite(a(CHEBI:chlorobutanol),a(PUBCHEM:9832404)) -> bp(GO:memory)

SET Evidence = "While no effect was observed in the Adnp+/− male mouse social recognition, with preference to mice rather
than objects (Fig. 2c), Adnp haploinsufficiency showed significantly inhibited social memory, which was completely
ameliorated by NAP-CB treatment (Fig. 2d)."
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -| bp(GO:"social behavior")
p(HGNC:ADNP) -> bp(GO:"social behavior")
composite(a(CHEBI:chlorobutanol),a(PUBCHEM:9832404)) -> bp(GO:"social behavior")

SET Evidence = "We have previously shown essentially no significant NAP effects on behavior in the Adnp+/+ mice"
a(PUBCHEM:9832404) cnc bp(GO:behavior)

SET Evidence = "We have also extended the experiments to female mice, and interestingly, in the
social recognition test CB- or NAP-treated females displayed significant preference to mice rather than objects
(Supplemental Fig. S2C), unlike previous findings with the “DD” formulation."
# “DD” formulation: NAP-benzaokonium
SET Gender = "Female"
a(CHEBI:chlorobutanol) -> bp(GO:"social behavior")
a(PUBCHEM:9832404) -> bp(GO:"social behavior")
composite(a(PUBCHEM:59557),a(PUBCHEM:9832404)) -| bp(GO:"social behavior")
UNSET Gender

SET Evidence = "Furthermore, in both sexes, Adnp haploinsufficiency showed significantly
inhibited social memory, which was completely ameliorated by NAP treatment (Fig. 2d, males and Supplemental Fig. S2D, females)."
SET Gender = {"Female","Male"}
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -| bp(GO:"social behavior")
a(PUBCHEM:9832404) -> bp(GO:"social behavior")
UNSET Gender

SET Evidence = "As social behavior depends on olfaction, this was also measured showing intact odor discrimination ability in
CB-treated males (Fig. 2e) and a sex difference, with no preference for a specific olfactory cue in CB-treated
females (Supplemental Fig. S2E), thus corroborating previous findings observed for DD-treated mice"
bp(MESH:Smell) reg bp(GO:"social behavior")
SET Gender = "Male"
a(CHEBI:chlorobutanol) cnc path(MESH:Smell)
SET Gender = "Female"
a(CHEBI:chlorobutanol) -| path(MESH:Smell)
UNSET Gender

SET Evidence = "Interestingly, in CB-treated males, Adnp haploinsufficiency
showed a reduction in the total time spent with the different odors, which was significantly increased upon NAP
treatment (Fig. 2e, inset)."
SET Gender = "Male"
composite(a(CHEBI:chlorobutanol),p(HGNC:ADNP,pmod(MESH:Haploinsufficiency))) -| path(MESH:Smell)
composite(a(PUBCHEM:9832404),p(HGNC:ADNP,pmod(MESH:Haploinsufficiency))) -> path(MESH:Smell)
UNSET Gender

SET Evidence = "As opposed to this, in females, CB-treated Adnp haploinsufficient mice showed significant
increased total odor sniffing time, which was significantly reduced by NAP treatment (Supplemental Fig. S2E, inset)."
SET Gender = "Female"
composite(a(CHEBI:chlorobutanol),p(HGNC:ADNP,pmod(MESH:Haploinsufficiency))) -> path(MESH:Smell)
composite(a(PUBCHEM:9832404),p(HGNC:ADNP,pmod(MESH:Haploinsufficiency))) -| path(MESH:Smell)
UNSET Gender

SET Evidence = "Given the fact that children carrying ADNP mutations (ADNP syndrome children) exhibit motor impairments,
we also utilized the hanging wire test to measure potential impairments and amelioration by NAP. Results showed a
significant impairment due to Adnp haploinsufficiency and amelioration by NAP treatment (Fig. 2f)"
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -> path(MESH:"Motor Skills Disorders")
a(PUBCHEM:9832404) -| path(MESH:"Motor Skills Disorders")
p(HGNC:ADNP) -| path(MESH:"Motor Skills Disorders")

SET Evidence = "Here, Adnp haploinsufficiency in male mice has led to a significant increased MD in the hippocampus (Fig. 3a)."
# Increased MD is indicative of increased brain tissue damage
SET MeSHAnatomy = "Hippocampus"
SET Gender = "Male"
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -> path(MESH:"Brain Injuries")
UNSET MeSHAnatomy
UNSET Gender

SET Evidence = "Furthermore, FA which is used to characterize the organization of white matter fibers, was
shown to be significantly increased in Adnp+/− mice, thus implying of structural impairment. Importantly, this DTI
observed structural impairment was ameliorated by NA-PCB treatment (Fig. 3b)."
SET MeSHAnatomy = "White Matter"
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -| a(MESH:"Nerve Fibers, Myelinated")
p(HGNC:ADNP) -> a(MESH:"Nerve Fibers, Myelinated")
composite(a(CHEBI:chlorobutanol),a(PUBCHEM:9832404)) -> a(MESH:"Nerve Fibers, Myelinated")
UNSET MeSHAnatomy

SET Evidence = "Our original cell culture results showed NAP protection against excitotoxicity over a broad concentration range,
suggesting an involvement of the glutamatergic system in ADNP/NAP activity"
a(PUBCHEM:9832404) -| path(HBP:Excitotoxicity)
a(GO:"glutamatergic synapse") -- act(a(PUBCHEM:9832404))
a(GO:"glutamatergic synapse") -- act(p(HGNC:ADNP))

SET Evidence = "Also, as our previous experiments, labeling dendritic spines
in vivo, showed reduction in spine density in the hippocampus and the cortex as a consequence of Adnp deficiency
and protection by NAP injection, we now sought to determine possible genotype/treatment/sex effects on
the vesicular glutamate transporter VGLUT1"
SET MeSHAnatomy = {"Hippocampus","Cerebral Cortex"}
p(HGNC:ADNP) -> a(GO:"dendritic spine")
UNSET MeSHAnatomy

SET Evidence = "VGLUT1 is both necessary and sufficient for uptake and storage of
glutamate, and thus comprises the sole determinant for an excitatory glutamatergic phenotype"
p(HGNC:SLC17A7) reg bp(GO:"glutamate homeostasis")
p(HGNC:SLC17A7) -> bp(GO:"L-glutamate import")

SET Evidence = "In females, hippocampal VGLUT1 was not affected by the Adnp genotype or NAP treatment (Supplemental Fig. S4A)"
SET MeSHAnatomy = "Hippocampus"
SET Gender = "Female"
a(PUBCHEM:9832404) cnc p(HGNC:SLC17A7)
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) cnc p(HGNC:SLC17A7)
UNSET MeSHAnatomy
UNSET Gender

SET Evidence = "In the cerebral cortex, female Adnp+/− mice exhibited significantly reduced VGLUT1 expression, with
no effect for NAP (Supplemental Fig. S4B)."
SET MeSHAnatomy = "Cerebral Cortex"
SET Gender = "Female"
a(PUBCHEM:9832404) cnc p(HGNC:SLC17A7)
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -| p(HGNC:SLC17A7)
UNSET MeSHAnatomy
UNSET Gender

SET Evidence = "In males, complementing DTI data (Fig. 3) our results (Fig. 4a) revealed that in Adnp+/− mice, Slc17a7
(VGLUT1) gene expression was significantly decreased in the hippocampus, and completely reversed by NAP
treatment (Fig. 4a), while in the cerebral cortex, NAP treatment resulted in a small, albeit significant decrease in
the VGLUT1 transcript (Fig. 4b)."
SET Gender = "Male"
SET MeSHAnatomy = "Hippocampus"
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -| r(HGNC:SLC17A7)
a(PUBCHEM:9832404) -> r(HGNC:SLC17A7)
SET MeSHAnatomy = "Cerebral Cortex"
a(PUBCHEM:9832404) -| r(HGNC:SLC17A7)
UNSET MeSHAnatomy
UNSET Gender

SET Evidence = "At the protein level, a significant reduction in VGLUT1 was observed in both
the hippocampus (Fig. 4c–e, immunohistochemistry, 4FG, densitometry) and cerebral cortex (Fig. 4h, i, densitometry)."
# in Adnp+/− mice
SET MeSHAnatomy = {"Hippocampus","Cerebral Cortex"}
p(HGNC:ADNP,pmod(MESH:Haploinsufficiency)) -| p(HGNC:SLC17A7)
UNSET MeSHAnatomy

SET Evidence = "When using area counting, NAP treatment was shown to provide full protection against VGLUT1
decreases in both the hippocampus and the cerebral cortex (Fig. 4f, h), whereas in terms of intensity changes in
VGLUT1 expression, NAP effect was significant in the hippocampus (Fig. 4g), and exhibited a trend of
improvement in the cortex (Fig. 4i)."
SET MeSHAnatomy = {"Hippocampus","Cerebral Cortex"}
a(PUBCHEM:9832404) -> p(HGNC:SLC17A7)
UNSET MeSHAnatomy

SET Evidence = "In this respect, our previous data associated
postsynaptic density protein 95 (PSD95, also known as DLG4) with ADNP/NAP activity"
p(HGNC:DLG4) -- act(a(PUBCHEM:9832404))
p(HGNC:DLG4) -- act(p(HGNC:ADNP))

UNSET Species
