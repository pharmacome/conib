#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Extracellular Monomeric and Aggregated Tau Efficiently Enter Human Neurons through Overlapping but Distinct Pathways"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Authors     = "Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE HBP            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/export/hbp-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE CTO            AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE SCOMP          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM           AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "29590627"}

# monomeric tau: p(HGNC:MAPT,var("p.P301S"))

SET Evidence = "We used the tau P301S variant, an autosomal
dominant mutation that causes early onset FTD with
high penetrance (Bugiani et al., 1999; Guo et al., 2017)"
p(HGNC:MAPT,var("p.Pro301Ser")) -> path(MESH:"Frontotemporal Dementia")

SET Evidence = "After extensive
washing, monomeric and aggregated tau-Dylight were both detected
within cells expressing the neuron-specific microtubule-associated
protein MAP2, confirming that both forms of tau enter neurons"
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
a(HBP:"Tau aggregates") -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "Tau-Dylight was found predominantly within the somatic
compartment of neurons (Figure 1A)"
#monomeric and aggregated tau-Dylight
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(GO:"cell body"))
a(HBP:"Tau aggregates") -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(GO:"cell body"))

SET Evidence = "After a 4-hr incubation
with extracellular tau, flow cytometry analysis (Figures 1B
and 1C) revealed that 83% and 73% of dissociated cells
contained monomeric or aggregated tau-Dylight, respectively,
demonstrating that extracellular tau efficiently enters human
neurons in culture"
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
a(HBP:"Tau aggregates") -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "Internalization of monomeric tau (P301S)
and wild-type tau was comparable and concentration dependent
(Figure S3A), confirming that the P301S mutation does not confer the ability to efficiently enter
neurons, nor is this form of tau likely to aggregate in extracellular media during
the 3- to 4-hr incubation period"
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
p(HGNC:MAPT) -> tloc(p(HGNC:MAPT),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "Incubation of
tau-pHrodo with human neurons at a range of concentrations
from 2.5 to 25 nM (0.12–1.2 µg.mL-1, diluted in culture medium)
showed that tau entry to neurons is rapid, as visualized by live imaging"
#tau P301S
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "We found that FLAG-tagged tau enters
neurons efficiently and that internalized tau persists at detectable
levels within neurons for at least 4 days (Figure S4A)"
#tau P301S
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET MeSHAnatomy = {"Neurons","Neurites"}

SET Evidence = "Intracellular fluorescent punctae were observed within the first
10 min of exposure of neurons to monomeric tau-pHrodo (Figure
2A; Video S1). Tau-pHrodo-positive structures increased in
size and intensity over the 4-hr course of the assay. These structures
were present within neurites and accumulated in the cell bodies of neurons"
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(GO:"cell body"))

SET Evidence = "In the presence of 15 and 25 nM monomeric
tau-pHrodo, the number of tau-pHrodo-positive objects approached
a plateau (>90% of final measurement) after approximately 1 hr (Figure 2C)"
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(GO:"cell body"))

SET Evidence = "Internalization of aggregated tau-pHrodo (Figure 2D) was
also found to be concentration dependent (Figure 2E)"
a(HBP:"Tau aggregates") -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(GO:"cell body"))

SET Evidence = "These kinetics of
aggregated tau-pHrodo entry are similar to that of both lower
concentrations of monomeric tau (2.5 nM) and of low-molecular
weight (10-kDa) dextran-pHrodo (same molarity as monomeric
tau samples; Figures S5A–S5C"
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(GO:"cell body"))
a(HBP:"Tau aggregates") -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(GO:"cell body"))
a(CHEBI:dextran) -> tloc(a(CHEBI:dextran)),fromLoc(GO:"extracellular region"),toLoc(GO:"cell body"))

UNSET MeSHAnatomy

SET Evidence = "We confirmed that heparin within the aggregated tau preparations
did not contribute to tau entry into neurons, finding that
uptake of both monomeric and aggregated tau was unaffected
in the presence of heparin (Figures S4C and S4D"
a(CHEBI:heparin) cnc tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
a(CHEBI:heparin) cnc tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "In agreement with the monomeric tau-pHrodo experiments,
monomeric tau-Dylight was rapidly taken up into neurons (Figure 3C"
p(HGNC:MAPT,var("p.Pro301Ser")) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "As early as 1 hr after the addition of extracellular tau, monomeric
and aggregated tau-Dylight were co-localized in EEA1+ early endosomes"
p(HGNC:MAPT,var("p.Pro301Ser")) -- a(GO:"early endosome")
a(HBP:"Tau aggregates") -- a(GO:"early endosome")

SET Evidence = "Monomeric and aggregated tau-Dylight were
also detected in LAMP1+late endosomes and lysosomes, consistent
with endocytosed proteins first reaching early endosomes,
before late endosomes and lysosomes"
p(HGNC:MAPT,var("p.Pro301Ser")) -- a(GO:"late endosome")
p(HGNC:MAPT,var("p.Pro301Ser")) -- a(GO:lysosome)
a(HBP:"Tau aggregates") -- a(GO:"late endosome")
a(HBP:"Tau aggregates") -- a(GO:lysosome)

SET Evidence = "Thus, monomeric and aggregated tau both efficiently enter neurons
via the endosome/lysosome system, and they are actively trafficked
within vesicles over long distances within neurons over several hours"
bp(GO:endocytosis) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
a(GO:lysosome) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
bp(GO:endocytosis) -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
a(GO:lysosome) -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "The amount of monomeric tau entering neurons, as measured
by total fluorescent intensity of intracellular monomeric tau-pHrodo
vesicles, was significantly reduced by dynamin inhibition,
as shown at 1 and 3 hr after the addition of extracellular
tau (Figure 4B)"
# dynamin inhibition via Dynasore
a(CHEBI:dynasore) -| tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
act(p(HGNC:DNM1)) -> tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "The effect of dynamin inhibition on the entry of aggregated tau
was more pronounced than on monomeric tau (Figures 4D–4F).
The total fluorescent intensity of intracellular aggregated taupHrodo
was consistently reduced by more than 70% at 1 and
3 hr after tau addition (Figure 4E), and the number of taupHrodo-
positive objects was reduced by 95% (Figure 4F)"
a(CHEBI:dynasore) -| tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
act(p(HGNC:DNM1)) -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "These data indicate that, at the concentrations studied, aggregated tau
enters neurons almost entirely via endocytosis"
bp(GO:endocytosis) -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "Inhibition of actin
polymerization with Cytochalasin D disrupts several clathrin-independent
endocytic processes, including bulk endocytosis/
macropinocytosis (Mooren et al., 2012)"
a(CHEBI:"cytochalasin D") -| bp(GO:"actin filament polymerization")
a(CHEBI:"cytochalasin D") negativeCorrelation bp(GO:"bulk synaptic vesicle endocytosis")
bp(GO:"actin filament polymerization") positiveCorrelation bp(GO:"bulk synaptic vesicle endocytosis")
a(CHEBI:"cytochalasin D") negativeCorrelation bp(GO:macropinocytosis)
bp(GO:"actin filament polymerization") positiveCorrelation bp(GO:macropinocytosis)

SET Evidence = "Disruption of actin polymerization
has previously been shown to inhibit entry of fibrils
formed of the tau repeat domain (Holmes et al., 2013)"
bp(GO:"actin filament polymerization") -> tloc(a(HBP:"Tau fibrils"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "Entry of monomeric tau was markedly reduced in the presence
of 1 mM Cytochalasin D, as reflected in the 95% reduction in the
number of monomeric tau-pHrodo-positive objects after 3-hr
incubation in the presence of 1 mMCytochalasinD (Figure 5C)"
a(CHEBI:"cytochalasin D") -| tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "In
contrast, disruption of actin polymerization with Cytochalasin D
had little effect on the entry of aggregated tau (total fluorescent
intensity and number of objects; Figures 5D–5F; Figure S5)"
a(CHEBI:"cytochalasin D") cnc tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))


SET Evidence = "These independent assays confirmed
the same differential effects of the two inhibitors observed by
live imaging of pHrodo-tau: at the 3-hr assay point, dynamin inhibition
had no effect on the number of monomeric tau-Dylightpositive
punctae within neurons, whereas inhibition of actin polymerization
reduced the amount of intracellular tau by over half (Figure S6)."
composite(a(CHEBI:dynasore),p(HGNC:MAPT,var("p.Pro301Ser"))) cnc tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
composite(a(CHEBI:"cytochalasin D"),p(HGNC:MAPT,var("p.Pro301Ser"))) -| tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "Conversely, dynamin inhibition significantly reduced
the entry of aggregated tau, with no significant effects of Cytochalasin
D (Figure S6) at this relatively high molar concent of aggregated tau"
a(CHEBI:dynasore) -| tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
act(p(HGNC:DNM1)) -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
a(CHEBI:"cytochalasin D") cnc tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "Pre-incubation with the polyclonal (anti-taupAb) tau antibody
reduced the amount of monomeric tau that entered neurons as assessed by the number of monomeric tau-pHrodo-containing
vesicles (Figure 6B; Figure S7)"
#anti-tau polyclonal antibody to the C-terminal half of tau
#composite(a(HBP:"tau polyclonal antibody"),p(HGNC:MAPT,var("p.Pro301Ser"))) -| tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "Tau antibodies also reduced entry of aggregated tau uptake
into human neurons (Figure 6C)"
#composite(a(HBP:"tau polyclonal antibody"),a(HBP:"Tau aggregates")) -| tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "In the presence of anti-taupAb,
the number of aggregated tau-pHrodo vesicles was reduced
by more than half (Figure 6D; Figure S7)"
#composite(a(HBP:"tau polyclonal antibody"),p(HGNC:MAPT,var("p.Pro301Ser"))) -| composite(p(HGNC:MAPT,var("p.Pro301Ser")),a(MESH:"Transport Vesicles"))

SET Evidence = "These independent assays confirmed that tau-specific antibodies
significantly reduced the amount of tau-Dylight entering
neurons (Figure S7)"
#a(HBP:"tau polyclonal antibody") -| tloc(p(HGNC:MAPT,var("p.Pro301Ser")),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))

SET Evidence = "To distinguish between
these possibilities, we performed additional assays using preformed
aggregated tau-Dylight-antibody complexes. After 3 hr
of incubation, antibodies were detected in the fixed neurons
with a secondary antibody specific to the host species (antirabbit
polyclonal; Figure 7) and also imaged for tau-Dylight."
#complex(a(HBP:"tau polyclonal antibody"),a(HBP:"Tau aggregates")) -> tloc(a(HBP:"Tau aggregates"),fromLoc(GO:"extracellular region"),toLoc(MESH:Neurons))
