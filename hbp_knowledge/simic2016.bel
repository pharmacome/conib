#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "Private Ownership to Fraunhofer Institute SCAI"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Tau Protein Hyperphosphorylation and Aggregation in Alzheimer’s Disease and Other Tauopathies, and Possible Neuroprotective Strategies"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Kristian Kolpeja"
SET DOCUMENT Description = ""

###############
# Definitions #
###############

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP    AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY AS PATTERN "^\d+$"
DEFINE NAMESPACE CL       AS PATTERN "^\d{7}$"

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE MESH     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mesh-names.belns"
DEFINE NAMESPACE CHEBI    AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE   AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
DEFINE NAMESPACE NCBIGENE AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE GFAM     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE HGNC     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE MIRBASE  AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mirbase-names.belns"
DEFINE NAMESPACE HBP      AS URL "https://raw.githubusercontent.com/pharmacome/terminology/cf4d8bb88754f036b943b4d94ad96e103dcb7149/export/hbp-names.belns"
DEFINE NAMESPACE PFAM     AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel
# --------------------------------------------------------------------
DEFINE NAMESPACE CTO      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/clinical-trial-ontology/clinical-trial-ontology-1.0.0.belns"
DEFINE NAMESPACE DO       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/disease-ontology/disease-ontology-20170725.belns"
DEFINE NAMESPACE HP       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"
DEFINE NAMESPACE MGI      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170725.belns"
DEFINE NAMESPACE SCOMP    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170725.belns"
DEFINE NAMESPACE SFAM     AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170725.belns"
DEFINE NAMESPACE INTERPRO AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/interpro/interpro-20170731.belns"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

DEFINE ANNOTATION Species AS PATTERN "\d+"

# Locally defined annotations
# ---------------------------
DEFINE ANNOTATION Tau_Antibody AS LIST {"Tau-5", "Tau-12", "Tau-13","9G3", "pY18","TN1","TOC1","Anti-pS422","TauC3"}
DEFINE ANNOTATION Braak_Stage AS LIST {"Stage I", "Stage II", "Stage III", "Stage IV", "Stage V", "Stage VI"}

##############
# Statements #
##############


SET Citation = {"PubMed", "26751493"}
SET Evidence = "Using the same tau-1 monoclonal antibody and electron microscopy with colloidal gold-labeled secondary antibodies, tau was also found in very low amounts in astrocytes [16]
and oligodendrocytes [17], and this was confirmed by tau mRNA expression analysis in the mouse brain [18]."
SET Species = "10090"
r(MGI:Mapt) partOf a(CL:"0000127")
r(MGI:Mapt) partOf a(CL:"0000128")
UNSET Species

SET Evidence = "3. Putative phosphorylation sites on tau protein and epitopes specific for major tau
antibodies. Red color denotes amino acids phosphorylation in AD brain."
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:MAPT, pmod(Ph, Y, 18)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 68)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, T, 69)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, T, 71)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 113)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, T, 123)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, T, 153)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, T, 175)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, T, 184)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 185)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 191)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, Y, 197)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 208)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 210)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 214)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 237)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 238)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 258)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 262)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 289)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 356)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, Y, 394)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, T, 403)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 409)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 435)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, T, 427)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 433)) pos path(MESH:"Alzheimer Disease")
p(HGNC:MAPT, pmod(Ph, S, 422)) pos path(MESH:"Alzheimer Disease")
UNSET MeSHDisease

SET Evidence = "Apart from binding to MT, the repeat domains of tau also bind to tubulin deacetylase, histone deacetylase 6 (HDAC6) [68] and apolipoprotein E (apoE)
more with the "3 than the "4 isovariant [69])."

p(HBP:"microtubule-binding region") -- p(HGNC:HDAC6)
p(HBP:"tubulin-binding repeat 1") -- g(HBP:"APOE e3")

p(HBP:"tubulin-binding repeat 1") partOf p(HBP:"microtubule-binding region")
p(HBP:"microtubule-binding region") partOf p(HGNC:MAPT)

SET Evidence = "The projection domain of tau may be involved in cell signaling that occurs through the interaction with Lck, Fgr and cSrc (Src-family kinases),
growth factor receptor-bound protein 2 (Grb2), phospholipase C- [70], phosphatidylinositol and phosphatidylinositol bisphosphate [71,72], peptidyl-prolyl cis/trans isomerase Pin 1,
and many others (for review see [73]), making them potential therapeutic targets in tauopathies [74]."

p(HBP:"projection domain") -- p(HGNC:LCK)
p(HBP:"projection domain") -- p(HGNC:FGR)
p(HBP:"projection domain") -- p(HGNC:SRC)
p(HBP:"projection domain") -- p(HGNC:GRB2)
p(HBP:"projection domain") -- p(FPLX:PLC)
p(HBP:"projection domain") -- a(CHEBI:phosphatidylinositol)
p(HBP:"projection domain") -- a(CHEBI:"phosphatidylinositol bisphosphate")
p(HBP:"projection domain") -- p(HGNC:PIN1)

p(HBP:"projection domain")  partOf p(HGNC:MAPT)

SET Evidence = "In synapses, the projection domain of tau interacts with protein kinase Fyn (plays an important role during myelination [75]),
postsynaptic density protein 95 (PSD-95) [76], and N-methyl-D-aspartate receptors (NMDAR)."
p(HBP:"projection domain") -- p(HGNC:FYN)
p(HBP:"projection domain") -- p(HGNC:DLG4)
p(HBP:"projection domain") -- a(MESH:"Receptors, N-Methyl-D-Aspartate")

SET Evidence = "An additional “knot” of tau being entangled in epigenetic landscape of neurodegeneration comes from
the finding that by acting as a HDAC6 inhibitor, tau is being indirectly involved in both (dys)regulation
of transcriptional activity and impairment of autophagic clearance by the ubiquitin proteasome system [81,82]."

p(HGNC:MAPT) -| act(p(HGNC:HDAC6))
p(HGNC:MAPT) -| bp(GO:autophagy)
p(HGNC:MAPT) neg act(complex(GO:"proteasome complex"))

SET Evidence = "On the other side, sarkosyl extracts from the filaments of PSP [129], corticobasal
degeneration (CBD; [130]), argyrophilic grain disease (AgD; [131]), and some cases of FTDP-17, contain
tau protein that separates as doublets of 64 and 69 kDa and are predominantly composed of tau
isoforms with 4R (class II tauopathies), whereas sarkosyl extracts from filaments of Pick’s disease
are characterized by the presence of pathological tau doublets of 60 and 64 kDa and contain mainly
3R tau isoforms (class III tauopathy)."

p(HBP:"4R tau") pos path(HBP:"Corticobasal Degeneration")
p(HBP:"4R tau") pos path(HBP:"Argyrophilic Grain Disease")
p(HBP:"4R tau") pos path(MESH:"Supranuclear Palsy, Progressive")

p(HBP:"3R tau") pos path(MESH:"Niemann-Pick Diseases")

SET Evidence = "Poorkaj et al. reported two exonic mutations (P301L and
V337M) in two families with FTDP-17 [139], while Hutton et al. reported six different mutations in
10 families: three of these mutations (G272V, P301L and R406W) were missense mutations in exons,
while the other three were in the 5' splice site of exon 10 [140]."

p(HGNC:MAPT, var("p.Pro301Leu")) pos path(MESH:"Frontotemporal Dementia")
p(HGNC:MAPT, var("p.Val337Met")) pos path(MESH:"Frontotemporal Dementia")
p(HGNC:MAPT, var("p.Gly272Val")) pos path(MESH:"Frontotemporal Dementia")
p(HGNC:MAPT, var("p.Arg406Trp")) pos path(MESH:"Frontotemporal Dementia")

SET Evidence = "A PSEN1 mutation causes a Pick’s disease phenotype including FTD tau pathology
without deposition of Abeta [145]; some MAPT single nucleotide polymorphisms have also been linked to
sporadic Parkinson’s disease (PD, [146]);"

p(HGNC:PSEN1, var("p.Gly183Val")) pos path(MESH:"Frontotemporal Dementia")
p(HGNC:PSEN1, var("p.Gly183Val")) pos path(MESH:"Niemann-Pick Diseases")
p(HGNC:PSEN1, var("p.Gly183Val")) cnc a(CHEBI:"amyloid-beta")

g(DBSNP:rs2471738) -- path(MESH:"Parkinson Disease")
g(DBSNP:rs242557) -- path(MESH:"Parkinson Disease")

SET Evidence = "The tau fragment first isolated from the PHF core is approximately 100 amino acids in length.
Its N-terminus was defined by sequence analysis [30,56], and its C-terminus was defined by epitope
mapping of MN423. Immunoreactivity was shown to depend on a specific C-terminal trunctation at Glu391 [33,150]."

p(HGNC:MAPT, trunc(Glu391)) -- p(HBP:"paired helical filaments")
p(HBP:"paired helical filaments") partOf a(GO:"neurofibrillary tangle")

SET Evidence = "In a prospective study three different synaptic protein (synaptophysin, SNAP-25 and syntaxin) were found
to be progressively decreased in neocortex at Braak stages III-VI [158], NFT-bearing neurons demonstrating, for example, a 35%–57% reduction in synaptophysin mRNA in AD brain [159]."
SET Braak_Stage = {"Stage III", "Stage VI"}
r(HGNC:SYP) neg a(GO:"neurofibrillary tangle")
p(HGNC:SNAP25) neg a(GO:"neurofibrillary tangle")
p(PFAM:Syntaxin) neg a(GO:"neurofibrillary tangle")
UNSET Braak_Stage

SET Evidence = "The most recent data obtained indicate that tau pathology indeed may be induced and propagated after the injection of tau oligomers or
aggregates in either wild-type or mutated MAPT transgenic mice [164], and that tau aggregates can be transferred from cell to cell in vitro [164,165] and in vivo [166,167]."
a(HBP:"Tau aggregates") pos tloc(a(HBP:"Tau aggregates"), fromLoc(MESH:Neurons), toLoc(MESH:Neurons))

SET Evidence = "In the case of soluble monomeric or small oligomeric tau protein, the endocytosis appears to be clathrin-dependent (reviewed in [169]).
In contrast, larger aggregates of tau could bind heparin in the extracellular matrix and be internalized through macropinocytosis [170].
As a result of exocytosis and endocytosis, the spreading of tau can occur in various neurodegenerative diseases (tauopathies) including AD.
Three plausible mechanisms of tau spreading are shown schematically in Figure 6. Additionally, it appea
rs that
microglial cells may facilitate tau propagation by phagocytosis and exocytosis of tau protein [171]."

p(HGNC:MAPT) pos bp(GO:"clathrin-dependent endocytosis")
p(HBP:"Tau aggregates") -- a(CHEBI:heparin)
p(HBP:"Tau aggregates") -> complex(p(HBP:"Tau aggregates"), a(CHEBI:heparin))
a(CHEBI:heparin) partOf complex(p(HBP:"Tau aggregates"), a(CHEBI:heparin))
complex(p(HBP:"Tau aggregates"), a(CHEBI:heparin)) pos bp(GO:macropinocytosis)

bp(GO:macropinocytosis) isA tloc(a(HBP:"Tau aggregates"), fromLoc(MESH:Neurons), toLoc(MESH:Neurons))
bp(GO:"clathrin-dependent endocytosis") isA tloc(a(HBP:"Tau aggregates"), fromLoc(MESH:Neurons), toLoc(MESH:Neurons))
bp(GO:"cell-cell signaling via exosome") isA tloc(a(HBP:"Tau aggregates"), fromLoc(MESH:Neurons), toLoc(MESH:Neurons))
a(CL:"0000129") -> tloc(a(HBP:"Tau aggregates"), fromLoc(MESH:Neurons), toLoc(MESH:Neurons))

SET Evidence = "Green coffee, a non-toxic small molecule, found to be an inhibitor of protein
phosphatase 2A methylesterase, was shown to improve cognitive and motor performance in mouse models with tau pathology"
a(CHEBI:"trans-5-O-caffeoyl-D-quinic acid") -| act(p(ECCODE:"3.1.1.89"))
a(CHEBI:"trans-5-O-caffeoyl-D-quinic acid") pos act(complex(GO:"respiratory chain complex I"))

SET Evidence = "A quite different strategy is to target tau clearance—e.g., by rapamycin that induces
macroautophagy [175], inhibitors of Hsp90 chaperone protein that binds to misfolded proteins or by
immunotherapeutic approaches [176]."
a(CHEBI:sirolimus) -> bp(GO:macroautophagy)
bp(GO:macroautophagy) -| p(HGNC:MAPT)
p(HBP:"Tau antibody, RG7345") -| p(HGNC:MAPT, pmod(Ph, S, 422))
p(HGNC:MAPT, pmod(Ph, S, 422)) partOf p(HBP:"Tau aggregates")

SET Evidence = "MB is a phenothiazine that crosses the blood brain barrier and acts as a redox cycler. Moreover, besides its beneficial properties as being able to improve
energy metabolism and to act as an antioxidant, it is also able to reduce tau protein aggregation"
a(CHEBI:"methylene blue") -| p(HBP:"Tau aggregates")
a(CHEBI:"methylene blue") -| act(p(HGNC:CASP1))
a(CHEBI:"methylene blue") -| act(p(HGNC:CASP3))

SET Evidence = "Recently, it has been proposed that tau protein acetylation may be responsible for tau aggregation in AD. Grinberg and collaborators
detected tau acetylation at Lys274 in all tauopathies (both primary and secondary), except in AgD"
p(HGNC:MAPT, pmod(Ac, Lys, 274)) pos p(HBP:"Tau aggregates")

SET Evidence = "Other possible inhibitors of tau aggregation are rhodanine-based inhibitors, phenylthiazolyl-hydrazide inhibitors, N-phenylamines, phenothiazines
and benzothiazoles, and polyphenols and anthraquinones"
a(CHEBI:rhodanine) -| p(HBP:"Tau aggregates")
a(HBP:thiohydantoin) -| p(HBP:"Tau aggregates")
a(HBP:oxazolidinedione) -| p(HBP:"Tau aggregates")
a(HBP:thioxooxazolidine) -| p(HBP:"Tau aggregates")

a(CHEBI:thiostrepton) -| p(HBP:"Tau aggregates")
a(HBP:sudoxicam) -| p(HBP:"Tau aggregates")
a(CHEBI:isoniazide) -| p(HBP:"Tau aggregates")
a(CHEBI:Iproniazid) -| p(HBP:"Tau aggregates")
a(CHEBI:iproclozide) -| p(HBP:"Tau aggregates")
a(HBP:isocarboxazide) -| p(HBP:"Tau aggregates")

a(CHEBI:myricetin) -| p(HBP:"Tau aggregates")

a(CHEBI:emodin) -| p(HBP:"Tau aggregates")
a(CHEBI:daunorubicin) -| p(HBP:"Tau aggregates")
a(CHEBI:doxorubicin) -| p(HBP:"Tau aggregates")
a(CHEBI:quinalizarin) -| p(HBP:"Tau aggregates")
