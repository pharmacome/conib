#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Neuronal Nicotinic Acetylcholine Receptor Structure and Function and Response to Nicotine"
SET DOCUMENT Authors     = "Esther Wollert"
SET DOCUMENT Version     = "1.0.1"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/drugbank-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/ec-code.belns"
# DEFINE NAMESPACE FB           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/80171ae62cf43aa1fc8a6c326b94537ab342458c/external/fb-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/501ceccdc9a27d97edbdc48a89ebe8e1dd3626e9/export/conso-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/3074b85b858455d8eeb76cfcdef685ced19bbe11/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/mgi-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/b46b65c3da259b6e86026514dfececab7c22a11b/external/entrez.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/8ccfed235e418e4c8aa576f9a5ef0f838e794c7f/external/pfam-names.belns"
DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/f2f993e599694ab5ce989cc39d789a499f75db99/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/e8ae9926ff95266032cb74f77973c84939bffbeb/export/famplex.belns"

# Namespaces hosted on https://arty.scai.fraunhofer.de/artifactory/bel/namespace
# ------------------------------------------------------------------------------
DEFINE NAMESPACE HP             AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hp/hp-20171108.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DNSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"
###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET STATEMENT_GROUP = "Esther"

# Add statements below this comment

SET Citation = {"PubMed","Int Rev Neurobiol. 2015;124:3-19. doi: 10.1016/bs.irn.2015.07.001. Epub 2015 Aug 21.","26472524"}

SET Evidence = " For example, those that contain the α 7 subunit ( α 7 * ) as a
homomeric or heteromeric receptor most commonly also have accompany-
ing characteristics. They bind α -bungarotoxin, have relatively low affinity
for nicotine and have relatively fast kinetics."
SET Confidence = "Medium"
p(CONSO:"alpha-7-containing nAChR") -> act(p(HGNC:CHRNA7))
composite(p(HGNC:CHRNA7), p(CONSO:"alpha-Bungarotoxin")) -> complex(p(HGNC:CHRNA7), p(CONSO:"alpha-Bungarotoxin"))
#Evidence suggests binding of alpha-bungarotoxin to CHRNA7
UNSET Confidence

SET Evidence = " Those that contain β 2 ( β 2 * )
commonly have high affinity for nicotine, desensitize to low agonist con-
centrations, have relatively slow kinetics, and do not bind α -bungarotoxin."
SET Confidence = "Medium"
composite(p(HGNC:CHRNB2), a(CHEBI:nicotine)) -> complex(p(HGNC:CHRNB2), a(CHEBI:nicotine))
#Evidence suggests binding of CHRNB2 to nicotine
a(CHEBI:nicotine) -> act(p(HGNC:CHRNA7))
UNSET Confidence

SET Evidence = "All the mammalian neuronal nAChR subtypes do share the general func-
tional property of being permeable to small monovalent and divalent cations.
The main conducting species under biological conditions are Na+, K+, and Ca2+. "
SET Confidence = "High"
p(FPLX:CHRN) -> tloc(a(CHEBI:"sodium(1+)"), MESH:"Extracellular Space", MESH:"Intracellular Space")
p(FPLX:CHRN) -> tloc(a(CHEBI:"sodium(1+)"), MESH:"Intracellular Space", MESH:"Extracellular Space")
p(FPLX:CHRN) -> tloc(a(CHEBI:"potassium(1+)"), MESH:"Extracellular Space", MESH:"Intracellular Space")
p(FPLX:CHRN) -> tloc(a(CHEBI:"potassium(1+)"), MESH:"Intracellular Space", MESH:"Extracellular Space")
p(FPLX:CHRN) -> tloc(a(CHEBI:"calcium(2+)"), MESH:"Extracellular Space", MESH:"Intracellular Space")
p(FPLX:CHRN) -> tloc(a(CHEBI:"calcium(2+)"), MESH:"Intracellular Space", MESH:"Extracellular Space")
UNSET Confidence

SET Evidence = "Mammalian nAChRs are cation selective, being permeable to small
monovalent and divalent cations that can fit through the narrowest hydro-
philic region of the open pore (Albuquerque et al., 2009; Dani, 1989;
Dani & Bertrand, 2007; Dani & Eisenman, 1987)."
SET Confidence = "High"
p(FPLX:CHRN) -> tloc(a(MESH:"Cations, Monovalent"), MESH:"Extracellular Space", MESH:"Intracellular Space")
p(FPLX:CHRN) -> tloc(a(MESH:"Cations, Monovalent"), MESH:"Intracellular Space", MESH:"Extracellular Space")
p(FPLX:CHRN) -> tloc(a(MESH:"Cations, Divalent"), MESH:"Extracellular Space", MESH:"Intracellular Space")
p(FPLX:CHRN) -> tloc(a(MESH:"Cations, Divalent"), MESH:"Intracellular Space", MESH:"Extracellular Space")
UNSET Confidence

SET Evidence = " While nAChR
activity causes depolarization, the divalent cation permeability plays an impor-
tant physiological role by supplying ionic signals, including calcium (Bertrand,
Galzi, Devillers-Thiery, Bertrand, & Changeux, 1993b; Dani & Bertrand,
2007; Decker & Dani, 1990; Gray, Rajan, Radcliffe, Yakehiro, & Dani,
1996; McGehee, Heath, Gelber, Devay, & Role, 1995; Vernino et al.,
1992)."
SET Confidence = "High"
act(p(FPLX:CHRN)) -> bp(GO:"membrane depolarization")
tloc(a(MESH:"Cations, Divalent"), MESH:"Extracellular Space", MESH:"Intracellular Space") -> bp(GO:"membrane depolarization")
tloc(a(CHEBI:"calcium(2+)"), MESH:"Extracellular Space", MESH:"Intracellular Space") -> bp(GO:"membrane depolarization")
UNSET Confidence

SET Evidence = "Although calcium modulation can act intracellularly,
nAChRs also are allosterically modulated by extracellular calcium, leading
to dramatic changes in the channel opening probability (Amador & Dani,
1995; Mulle, Lena, & Changeux, 1992; Vernino et al., 1992)."
SET Confidence = "High"
SET MeSHAnatomy = "Extracellular Space"
a(CHEBI:"calcium(2+)") reg act(p(FPLX:CHRN))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = " Although
many areas of the brain participate, nicotinic receptors of the midbrain dopa-
mine (DA) area are particularly important during the initiation of the addic-
tion process (Dani et al., 2014; De Biasi & Dani, 2011)."
SET Confidence = "High"
SET Cell = "midbrain dopaminergic neuron"
#path(MESH:"Tobacco Smoking") -> act(p(FPLX:CHRN))
act(p(FPLX:CHRN)) -> path(MESH:"Behavior, Addictive")
UNSET Cell
UNSET Confidence

SET Evidence = " Activation of the presynaptic
nAChRs (commonly but not exclusively α 7 * nAChRs) enhances the release
of glutamate (Dani et al., 2000; Mansvelder & McGehee, 2000, 2002)."
SET Confidence = "High"
SET MeSHAnatomy = "Presynaptic Terminals"
act(p(FPLX:CHRN)) -> sec(a(CHEBI:"glutamate(2-)"))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence = "Simultaneously, activity of postsynaptic (and somatic) α 4 β 2 * nAChRs
depolarizes DA neurons leading to enhanced action potential firing
(Zhang et al., 2009)."
SET Confidence = "High"
SET Cell = "dopaminergic neuron"
SET MeSHAnatomy = "Post-Synaptic Density"
act(p(CONSO:"alpha-4 beta-2 nAChR")) -> bp(GO:"depolarization of postsynaptic membrane")
bp(GO:"depolarization of postsynaptic membrane") -> bp(GO:"regulation of action potential firing rate")
UNSET MeSHAnatomy
UNSET Cell
UNSET Confidence

SET Evidence = " This depolarization and firing of the DA neurons helps
to relieve the divalent cation block of NMDA receptors and, thus, enables
the NMDA receptors to participate in long-term synaptic potentiation of
glutamatergic afferents onto midbrain dopamine neurons."
SET Confidence = "Low"
SET Cell = "dopaminergic neuron"
bp(GO:"depolarization of postsynaptic membrane") -| a(MESH:"Cations, Divalent")
a(MESH:"Cations, Divalent") -| act(p(MESH:"Receptors, N-Methyl-D-Aspartate"), ma(GO:"NMDA glutamate receptor activity"))
UNSET Cell
SET Cell = "glutamatergic neuron"
act(p(MESH:"Receptors, N-Methyl-D-Aspartate")) -> bp(GO:"long-term synaptic potentiation")
UNSET Cell
UNSET Confidence

UNSET STATEMENT_GROUP
