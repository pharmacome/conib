#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright Â© 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Extracellular Tau and Its Potential Role in the Propagation of Tau Pathology"
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Lingling Xu, Esther Wollert"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
# DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
# DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
# DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
# DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
# DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/501ceccdc9a27d97edbdc48a89ebe8e1dd3626e9/export/conso-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
# DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
# DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
# DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mgi-names.belns"
# DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
# DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
# DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
# DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
# DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
# DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
# DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
# DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
# DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
# DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
# DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
# DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
# DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
# DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
# DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
# DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"
##############
# Statements #
##############
SET Citation = {"PubMed", "29238289"}

SET Evidence ="Tau is well-recognized for its role in assembling/stabilizing
microtubules although tau single knockout mice do not show major phenotypic
changes in neuronal microtubule stability because of a putative functional
redundancy "
SET Confidence = "Medium"
p(HGNC:MAPT) -- bp(GO:"microtubule polymerization")
p(HGNC:MAPT) -- a(MESH:Microtubules)
UNSET Confidence

SET Evidence ="Subsequent studies have shown that injecting tau fibrils into
the brain also induces trans- neuronal propagation in vivo "
SET Confidence = "Medium"
SET MeSHAnatomy = "Brain"
p(CONSO:"Tau fibrils") -> tloc(p(HGNC:MAPT))
UNSET MeSHAnatomy

SET Evidence ="the application of extracellular tau increases the electrical
activity of iPSC-derived or primary cortical neurons"
SET Confidence = "High"
p(HGNC:MAPT,loc(GO:"extracellular region")) -> act(a(MESH:Neurons))
UNSET Confidence

SET Evidence ="Moreover, extracellular tau may elicit cellular toxicity under certain conditions."
SET Confidence = "Medium"
p(HGNC:MAPT,loc(GO:"extracellular region")) -> path(MESH:"Cytotoxicity, Immunologic")
UNSET Confidence

SET Evidence ="The addition of tau into the cell culture media binds to
muscarinic acetylcholine receptors and increases intracellular calcium,
eventually leading to cell death"
SET Confidence = "Medium"
complex(p(HGNC:MAPT),p(MESH:"Receptors, Muscarinic")) -> a(MESH:Calcium)
complex(p(HGNC:MAPT),p(MESH:"Receptors, Muscarinic")) -> bp(GO:"apoptotic process")
UNSET Confidence

SET Evidence ="Furthermore, extracellular oligomeric tau impairs memory and long-term
potentiation (LTP) in mice"
SET Confidence = "Medium"
SET Species = "10090"
SET MeSHAnatomy = "Extracellular Space"
a(CONSO:"Tau oligomers") -| path(MESH:Memory)
a(CONSO:"Tau oligomers") -| bp(MESH:"Long-Term Potentiation")
UNSET Species
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="starvation or lysosomal dysfunction increases tau release"
SET Confidence = "Medium"
path(MESH:"Starvation") -> sec(p(HGNC:MAPT))
act(a(MESH:Lysosomes)) -| sec(p(HGNC:MAPT))
UNSET Confidence

SET Evidence ="In addition, stimulating neuronal activity in either cultured
neurons or in vivo also enhances tau release"
SET Confidence = "Medium"
act(a(MESH:Neurons)) -> sec(p(HGNC:MAPT))
UNSET Confidence

SET Evidence ="this observation suggests that the activity-dependent release
of tau participates in a positive feedback loop on neuronal activity."
SET Confidence = "Medium"
act(a(MESH:Neurons)) -> sec(p(HGNC:MAPT))
sec(p(HGNC:MAPT)) -> act(a(MESH:Neurons))
UNSET Confidence

SET Evidence ="Furthermore, caspase-3 cleavage at D421 increases tau release"
SET Confidence = "Medium"
act(p(HGNC:CASP3)) -> reaction(reactants(p(HGNC:MAPT)),products(p(HGNC:MAPT, frag("1_421")),p(HGNC:MAPT,frag("421_*"))))
reaction(reactants(p(HGNC:MAPT)),products(p(HGNC:MAPT, frag("1_421")),p(HGNC:MAPT,frag("421_*")))) -> sec(p(HGNC:MAPT))
UNSET Confidence

SET Evidence =" Tau from the CSF (Saman et al., 2012) and blood of patients with AD (Fiandaca et al., 2015) is associated with exosomes"
SET Confidence = "Medium"
SET MeSHAnatomy ={"Cerebrospinal Fluid","Blood"}
SET MeSHDisease = "Alzheimer Disease"
p(HGNC:MAPT) -- act(a(MESH:Exosomes))
UNSET MeSHAnatomy
UNSET MeSHDisease
UNSET Confidence

SET Evidence =" N2a cells over-expressing tau (Wang et al., 2017) and microglia
also release tau in an exosome-dependent manner "
SET Confidence = "Medium"
a(MESH:Microglia) -> sec(p(HGNC:MAPT))
UNSET Confidence

SET Evidence ="For example, DnaJ/Hsc70 chaperone complexes control the release
of tau and knockdown of SNAP23, a SNARE protein involved in vesicle-mediated
exocytosis, blocks tau release "
SET Confidence = "Medium"
complex(p(INTERPRO:"Chaperone DnaJ"),p(HGNC:HSPA8)) reg sec(p(HGNC:MAPT))
p(HGNC:SNAP23) -> sec(p(HGNC:MAPT))
p(HGNC:SNAP23) isA p(MESH:"SNARE Proteins")
p(MESH:"SNARE Proteins") -> bp(GO:"exocytosis")
UNSET Confidence

SET Evidence ="Rab GTPase regulates various steps of intracellular vesicle
trafficking and membrane fusion"
SET Confidence = "High"
p(MESH:"rab GTP-Binding Proteins") reg act(a(MESH:"Transport Vesicles"))
p(MESH:"rab GTP-Binding Proteins") reg bp(MESH:"Membrane Fusion")
UNSET Confidence

SET Evidence ="Rab7a (Rodriguez et al., 2017) and Rab1a (Mohamed et al., 2017)
have been recently implicated in tau release, further suggesting that tau release
involves vesicle transport."
SET Confidence = "Medium"
p(HGNC:RAB7A) -- sec(p(HGNC:MAPT))
p(HGNC:RAB1A) -- sec(p(HGNC:MAPT))
sec(p(HGNC:MAPT)) -- a(MESH:"Transport Vesicles")

SET Evidence ="Tau also interacts with plasma membrane via amino terminal projection domain "
SET Confidence = "High"
complex(p(HGNC:MAPT),a(MESH:"Cell Membrane"))
UNSET Confidence

SET Evidence =" Anti-tau antibodies facilitate microglial uptake of tau aggregates"
SET Confidence = "Medium"
SET MeSHAnatomy = "Microglia"
p(HGNC:MAPT) -| tloc(p(CONSO:"Tau aggregates"), fromLoc(MESH:"Extracellular Space"), toLoc(MESH:"Intracellular Space")))
UNSET MeSHAnatomy
UNSET Confidence

SET Evidence ="Elevated CSF tau is a diagnostic biomarker in AD patients"
SET Confidence = "High"
SET MeSHAnatomy ="Cerebrospinal Fluid"
p(HGNC:MAPT) biomarkerFor path(MESH:"Alzheimer Disease")
UNSET Confidence

SET Evidence ="CSF tau in APP transgenic mice is increased in an age- dependent manner without a global neuronal loss"
SET Confidence = "Medium"
SET Species = "10090"
SET MeSHAnatomy ="Cerebrospinal Fluid"
bp(GO:aging) -> p(HGNC:MAPT)
p(HGNC:MAPT) cnc bp(GO:"neuron death")
UNSET Confidence
