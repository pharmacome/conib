#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2018 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Neuronal uptake and propagation of a rare phosphorylated high-molecular-weight tau derived from Alzheimer’s disease brain"
SET DOCUMENT Version     = "1.0.2"
SET DOCUMENT Authors     = "Esther Wollert, Sandra Spalek"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
#DEFINE NAMESPACE CHEBI          AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/chebi-names.belns"
#DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/drugbank-names.belns"
#DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
#DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/ec-code.belns"
#DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/fb-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/501ceccdc9a27d97edbdc48a89ebe8e1dd3626e9/export/conso-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hgnc-names.belns"
#DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hgnc.genefamily-names.belns"
#DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/go-names.belns"
#DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mesh-names.belns"
DEFINE NAMESPACE MGI            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
#DEFINE NAMESPACE MIRBASE        AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mirbase-names.belns"
#DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/ncbigene.belns"
#DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/pfam-names.belns"
#DEFINE NAMESPACE RGD            AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
#DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
#DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
#DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
#DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
#DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
#DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
#DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
#DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
#DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
#DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
#DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
#DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
#DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
#DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############

SET Citation = {"PubMed","26458742"}

SET Evidence = "Importantly, neuronal uptake of HMW tau occurred in vivo as
well; human tau uptake in neurons was detected in young
rTg4510 (pre-tangle stage) (Fig. 1j–l) and WT (Supplementary
Fig. 6) mice injected with the HMW SEC fraction of Tg4510
(12 months) brain extract, but not in those injected with the
LMW fractions."
SET Confidence = "Medium"
SET Species = "10090"
# rTg4510 mice
a(CONSO:"Tau aggregates") -> tloc(p(MGI:Mapt),MESH:"Extracellular Space",MESH:Neurons)
UNSET Confidence
UNSET Species

SET Evidence = " The PBS-
extractable tau species from rTg4510 brain had higher levels
of phosphorylation compared with the tau species obtained
from rTg21221, especially those associated with some speciﬁc
phosphorylation sites such as pT205, pS262, pS400, pS404, pS409
and pS422."
SET Confidence = "Medium"
SET Species = "10090"
SET MeSHAnatomy = "Brain"
# rTg4510 (expressing aggregating P301L tau (0N4R))
p(CONSO:"Tau isoform D (383 aa)", var("p.Pro301Leu")) pos p(CONSO:"Tau isoform D (383 aa)", pmod(Ph,T,205))
p(CONSO:"Tau isoform D (383 aa)", var("p.Pro301Leu")) pos p(CONSO:"Tau isoform D (383 aa)", pmod(Ph,S,262))
p(CONSO:"Tau isoform D (383 aa)", var("p.Pro301Leu")) pos p(CONSO:"Tau isoform D (383 aa)", pmod(Ph,S,400))
p(CONSO:"Tau isoform D (383 aa)", var("p.Pro301Leu")) pos p(CONSO:"Tau isoform D (383 aa)", pmod(Ph,S,404))
p(CONSO:"Tau isoform D (383 aa)", var("p.Pro301Leu")) pos p(CONSO:"Tau isoform D (383 aa)", pmod(Ph,S,409))
p(CONSO:"Tau isoform D (383 aa)", var("p.Pro301Leu")) pos p(CONSO:"Tau isoform D (383 aa)", pmod(Ph,S,422))
UNSET MeSHAnatomy
UNSET Confidence
UNSET Species

SET Evidence = "SEC analysis of the molecular weight distribution of
tau demonstrated that rTg21221 brain extracts (PBS-soluble,
3,000g) contained primarily LMW species and very low levels of
HMW tau species, whereas rTg4510 brain extract showed both
HMW and LMW peaks (Fig. 2e,f). "
SET Confidence = "Medium"
SET Species = "10090"
SET MeSHAnatomy = "Brain"
p(CONSO:"Tau isoform D (383 aa)", var("p.Pro301Leu")) -> a(CONSO:"Tau aggregates")
UNSET MeSHAnatomy
UNSET Confidence
UNSET Species

SET Evidence = " However, the AD brain extract
(3,000g) contained signiﬁcantly higher levels of phosphorylated
tau (Fig. 6h,i,m) when compared with the control brain, especially
those associated with some speciﬁc phosphorylation sites such as
pS199, pS396 and pS404 (Fig. 6i)."
SET Confidence = "High"
SET MeSHAnatomy = "Brain"
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT, pmod(Ph))
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT, pmod(Ph,S,199))
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT, pmod(Ph,S,396))
path(MESH:"Alzheimer Disease") positiveCorrelation p(HGNC:MAPT, pmod(Ph,S,404))
UNSET Confidence
UNSET MeSHAnatomy

SET Evidence = "The HMW tau species from the AD brain were
highly phosphorylated compared with those from control brain
(Fig. 6m)."
SET Confidence = "Medium"
SET MeSHAnatomy = "Brain"
p(HGNC:MAPT, pmod(CONSO:hyperphosphorylation)) -> a(CONSO:"Tau aggregates")
UNSET Confidence
UNSET MeSHAnatomy

SET Evidence = " Phosphatase treatment dephosphorylated tau in
rTg4510 brain extract (Fig. 7d) without changing HMW tau levels
(Fig. 7e), resulting in a signiﬁcant reduction of cellular uptake of
tau (Fig. 7f). "
# rTg4510 mice
SET Confidence = "Medium"
SET MeSHAnatomy = "Brain"
SET Species = "10090"
p(MGI:Mapt, pmod(CONSO:hyperphosphorylation)) -> tloc(p(MGI:Mapt),fromLoc(MESH:"Extracellular Space"),toLoc(MESH:"Intracellular Space"))
UNSET MeSHAnatomy
UNSET Species
UNSET Confidence

SET Evidence = " There was no difference in
cell viability between tau-aggregate positive and negative cells for
up to 4 days (Supplementary Fig. 10)."
SET Confidence = "Medium"
a(CONSO:"Tau aggregates") causesNoChange bp(GO:"cell death")
UNSET Confidence
