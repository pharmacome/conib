#######################
# Document Properties #
#######################
SET DOCUMENT Copyright   = "Copyright © 2019 Fraunhofer Institute SCAI, All rights reserved."
SET DOCUMENT Licenses    = "CC BY 4.0"
SET DOCUMENT ContactInfo = "charles.hoyt@scai.fraunhofer.de"

SET DOCUMENT Name        = "Impairment of chaperone-mediated autophagy affects neuronal homeostasis through altered expression of DJ-1 and CRMP-2 proteins "
SET DOCUMENT Version     = "1.0.0"
SET DOCUMENT Authors     = "Trusha Adeshara"
SET DOCUMENT Description = ""

##############
# Namespaces #
##############

# Namespaces hosted on https://github.com/pharmacome/terminology
# --------------------------------------------------------------
DEFINE NAMESPACE CHEBI         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/chebi-names.belns"
DEFINE NAMESPACE DRUGBANK       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/drugbank-names.belns"
DEFINE NAMESPACE DOID           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c0c576e3d3ab9b25a3982251a4ee2fd7c49e1679/external/doid-names.belns"
DEFINE NAMESPACE ECCODE         AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ec-code.belns"
DEFINE NAMESPACE FB             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/fb-names.belns"
DEFINE NAMESPACE CONSO          AS URL "https://raw.githubusercontent.com/pharmacome/conso/501ceccdc9a27d97edbdc48a89ebe8e1dd3626e9/export/conso-names.belns"
DEFINE NAMESPACE HGNC           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc-names.belns"
DEFINE NAMESPACE HGNCGENEFAMILY AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hgnc.genefamily-names.belns"
DEFINE NAMESPACE HP             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/hp-names.belns"
DEFINE NAMESPACE GO             AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/go-names.belns"
DEFINE NAMESPACE INTERPRO       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/interpro-names.belns"
DEFINE NAMESPACE MESH           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mesh-names.belns"
DEFINE NAMESPACE MGI           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/mgi-names.belns"
DEFINE NAMESPACE MIRBASE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/mirbase-names.belns"
DEFINE NAMESPACE NCBIGENE       AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/ncbigene.belns"
DEFINE NAMESPACE PFAM           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/c328ad964c08967a0417a887510b97b965a62fa5/external/pfam-names.belns"
DEFINE NAMESPACE RGD           AS URL "https://raw.githubusercontent.com/pharmacome/terminology/efc856fb009a39e4d284269a6801f79ed3d3cf56/external/rgd-names.belns"

# Namespaces hosted on https://github.com/sorgerlab/famplex
# ---------------------------------------------------------
DEFINE NAMESPACE FPLX           AS URL "https://raw.githubusercontent.com/sorgerlab/famplex/d9ec0526c20795146a9a6aef17496efe1a36cac6/export/famplex.belns"

# Namespaces defined with regular expressions
# -------------------------------------------
DEFINE NAMESPACE DBSNP          AS PATTERN "rs[0-9]+"
DEFINE NAMESPACE TAXONOMY       AS PATTERN "^\d+$"
DEFINE NAMESPACE UNIPROT        AS PATTERN "^([A-N,R-Z][0-9]([A-Z][A-Z, 0-9][A-Z, 0-9][0-9]){1,2})|([O,P,Q][0-9][A-Z, 0-9][A-Z, 0-9][A-Z, 0-9][0-9])(\.\d+)?$"
DEFINE NAMESPACE PUBCHEM        AS PATTERN "^\d+$"

###############
# Annotations #
###############

# Annotations defined with regular expressions
# --------------------------------------------
DEFINE ANNOTATION Species       AS PATTERN "^\d+$"

# Annotations hosted on https://arty.scai.fraunhofer.de/artifactory/bel/annotation
# --------------------------------------------------------------------------------
DEFINE ANNOTATION Anatomy       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/anatomy/anatomy-20170511.belanno"
DEFINE ANNOTATION CellLine      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION CellStructure AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-structure/cell-structure-20170511.belanno"
DEFINE ANNOTATION Cell          AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell/cell-20170511.belanno"
DEFINE ANNOTATION Confidence    AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/confidence/confidence-1.0.0.belanno"
DEFINE ANNOTATION Disease       AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/disease/disease-20170511.belanno"
DEFINE ANNOTATION Gender        AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/gender/gender-1.0.0.belanno"
DEFINE ANNOTATION MeSHAnatomy   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-anatomy/mesh-anatomy-20170511.belanno"
DEFINE ANNOTATION MeSHDisease   AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/mesh-diseases/mesh-diseases-20170511.belanno"
DEFINE ANNOTATION Subgraph      AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"
DEFINE ANNOTATION TextLocation  AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/text-location/text-location-1.0.1.belanno"

##############
# Statements #
##############
SET Citation = {"PubMed", "30562574"}

SET Evidence = "Chaperone-mediated autophagy (CMA) is a selective pathway for lysosomal degradation of long-lived cytosolic proteins, and differs from the other two modes of cytosol-to-lysosome targeting, macro- and micro-autophagy, in that it 1) does not involve vesicle formation, and 2) that specific interactions with a complex of molecular chaperones must occur prior to direct translocation of the substrate protein through the lysosomal membrane (Cuervo 2011)."
SET Confidence = "High"
bp(GO:"chaperone-mediated autophagy") -> deg(a(MESH:Proteins,loc(GO:lysosome)))
bp(GO:macroautophagy) -> tloc(a(MESH:"Cytoplasmic Vesicles"),fromLoc(GO:cytoplasm),toLoc(GO:lysosome))
bp(GO:"lysosomal microautophagy") -> tloc(a(MESH:"Cytoplasmic Vesicles"),fromLoc(GO:cytoplasm),toLoc(GO:lysosome))
composite(a(MESH:Proteins),a(MESH:"Molecular Chaperones")) -> tloc(a(MESH:Proteins),fromLoc(GO:cytoplasm),toLoc(GO:lysosome))
UNSET Confidence

SET Evidence = "Once bound by the chaperone complex, substrates are directly unfolded and translocated through the lysosomal membrane by a multimeric channel consisting of the lysosome-associated membrane protein type 2A (LAMP2A)"
SET Confidence = "Medium"
composite(a(MESH:Proteins),a(MESH:"Molecular Chaperones")) -> path(MESH:"Protein Unfolding")
p(HGNC:LAMP2,loc(GO:"lysosomal membrane")) -> tloc(a(MESH:Proteins),fromLoc(GO:cytoplasm),toLoc(GO:lysosome))
UNSET Confidence


SET Evidence = " Furthermore, LAMP2A acts as the rate-limiting step for the activity of the CMA pathway, with rate of the assembly / disassembly of the LAMP2A complex in the lysosomal membrane being paramount for its function (Cuervo and Dice 1996;Bandyopadhyay et al. 2008)."
SET Confidence = "Low"
p(HGNC:LAMP2) reg bp(GO:"chaperone-mediated autophagy")
#Can you let me know which statement shows about the next two Statements
p(HGNC:LAMP2,loc(GO:cytoplasm)) -| bp(GO:"chaperone-mediated autophagy")
p(HGNC:LAMP2,loc(GO:"lysosomal membrane")) -> bp(GO:"chaperone-mediated autophagy")
UNSET Confidence

SET Evidence = "In vivo evidence shows that age-related decline in CMA function coincides with reduced levels of LAMP2A (Cuervo and Dice 2000b), and that restoration of LAMP2A abundance in the aged liver is sufficient to rescue CMA function in aged animals (Zhang and Cuervo 2008). Although it is maximally activated upon prolonged starvation (Cuervo et al. 1995), CMA activity has been also found specifically upregulated in response to oxidative stress (Hubbi et al. 2013; Kiffin 2004), genotoxic insult (Park et al. 2015), and - in a compensatory fashion - upon impaired macroautophagy (Koga et al. 2011a)"
SET Confidence = "High"
bp(GO:aging) neg p(HGNC:LAMP2)
bp(GO:aging) -| bp(GO:"chaperone-mediated autophagy")
path(MESH:Starvation) -> bp(GO:"chaperone-mediated autophagy")
bp(GO:"cellular response to oxidative stress") -> bp(GO:"chaperone-mediated autophagy")
bp(GO:macroautophagy) neg bp(GO:"chaperone-mediated autophagy")
UNSET Confidence

SET Evidence = " Importantly, CMA dysfunction has been linked to a variety of neurodegenerative conditions, most notably Parkinson’s Disease (PD), as well as to the ageing process, as part of the generalized decline of the activity of the protein degradation systems (Xilouri and Stefanis 2016; Xilouri and Stefanis 2015). "
SET Confidence = "High"
bp(GO:"chaperone-mediated autophagy") -- path(MESH:"Neurodegenerative Diseases")
bp(GO:"chaperone-mediated autophagy") -- path(DOID:"Parkinson's disease")
bp(GO:"chaperone-mediated autophagy") -- bp(GO:aging)
bp(GO:"chaperone-mediated autophagy") isA bp(GO:"cellular protein catabolic process")
UNSET Confidence

SET Evidence = "a recent in vivo study identified significant changes to large portions of the proteome upon LAMP2A knock-down and consequent CMA dysfunction in the aging liver (Schneider et al. 2014)."
SET Confidence = "Medium"
SET Anatomy = "liver"
p(HGNC:LAMP2) reg a(MESH:Proteome)
a(MESH:Proteome) reg bp(GO:"chaperone-mediated autophagy")
UNSET Anatomy
UNSET Confidence

SET Evidence = "We herein report 44 proteins to be significantly altered by our interventions, amongst them the Parkinson’s Disease (PD)-related anti-oxidant PARK7/DJ-1 (Bonifati 2002; Taipa et al. 2016), and the schizophrenia-related pro-neuronal differentiation factor dihydropyrimidinase-related protein 2 (DPYSL2/CRMP-2) - critical in the neurotrophic response following CNS injury (Nagai et al. 2016), and in general neuritic outgrowth and stability (Mita et al. 2016)"
SET Confidence = "Medium"
p(HGNC:PARK7) -- path(MESH:"Parkinson Disease")
p(HGNC:PARK7) isA a(MESH:"Antioxidants")
p(HGNC:DPYSL2) -- path(MESH:"Schizophrenia")
p(HGNC:DPYSL2) isA a(MESH:"Neuregulins")
p(HGNC:PARK7) -> path(MESH:"Neuronal Outgrowth")
p(HGNC:DPYSL2) -> path(MESH:"Neuronal Outgrowth")
p(HGNC:PARK7) -> bp(GO:"neuron projection development")
p(HGNC:DPYSL2) -> bp(GO:"neuron projection development")
UNSET Confidence

SET Evidence = " LAMP2A down-regulation decreased total lysosomal proteolysis (NH4Cl-sensitive) as well as CMA-dependent proteolysis [(NH4Cl-3MA)-sensitive], without affecting macroautophagy-dependent (3MA-sensitive) proteolysis, further verifying the importance of LAMP2A as the ratelimiting step of the CMA pathway in this culture system (Suppl. Fig. 1A-C). LC3 lipidation and macroautophagic substrate SQSTM1/p62 abundance was similarly unchanged (Suppl. Fig. 1D)."
SET Confidence = "Medium"
#not stated in evidence/annotation
#SET Cell = "CNS neuron (sensu Vertebrata)"
p(HGNC:LAMP2) -> bp(GO:"lysosomal protein catabolic process")
p(HGNC:LAMP2) -> bp(GO:"chaperone-mediated autophagy")
p(HGNC:LAMP2) cnc bp(GO:macroautophagy)
p(HGNC:LAMP2) cnc p(HGNC:MAP1LC3A,pmod(GO:"protein lipidation"))
p(HGNC:LAMP2) cnc p(HGNC:SQSTM1)
UNSET Confidence

SET Evidence = "Moreover, assessment of cell survival by nuclear counts assay carried out at the same time-point, revealed no detectable cytotoxicity stemming from LAMP2A down-regulation (Fig. 1C). To exclude any aberrant effects that might precede cell death per se, but might still confound our analysis, we measured gross levels of two established neuronal markers: the axon-associated tubulin beta-3 chain (TUBB3/TUJ-1) (Fig. 1D), and the dendritic marker microtubule-associated protein 2(MAP2) (Fig. 1E) - both of which showed no significant changes."
SET Confidence = "Medium"
p(HGNC:LAMP2) cnc path(MESH:"Cytotoxicity, Immunologic")
p(HGNC:LAMP2) cnc bp(GO:"cell death")
p(HGNC:LAMP2) cnc p(HGNC:TUBB3)
p(HGNC:LAMP2) cnc p(HGNC:MAP2)
UNSET Confidence

#UNSET Cell

SET Evidence = "From these, we selected the PD-associated antioxidant PARK7/DJ-1 and the schizophrenia-associated microtubule-binding pro-neuronal differentiation factor CRMP-2 for further analysis, given their reported connections to human neuropsychiatric diseases. Other proteins with higher overall accumulation reported in the MS/MS-analysis following LAMP2A downregulation, including canonical CMA substrate glyceraldehyde 3-phosphate dehydrogenase (GAPDH) and prohibitin (PHB), were also investigated at the biochemical level for assay validation purposes (Suppl. Fig. 3A)."
SET Confidence = "High"
p(HGNC:PARK7) -- path(DOID:"Parkinson's disease")
p(HGNC:PARK7) isA a(CHEBI:antioxidant)
p(HGNC:DPYSL2) -- path(DOID:"schizophrenia")
p(HGNC:PARK7) -- path(MESH:"Neurodegenerative Diseases")
p(HGNC:DPYSL2) -- path(MESH:"Neurodegenerative Diseases")
p(HGNC:LAMP2) -| p(HGNC:GAPDH)
p(HGNC:LAMP2) -| p(HGNC:PHB)
UNSET Confidence

SET Evidence = "As Park7/DJ1 mRNA levels were unchanged this stemmed from accelerated protein clearance (Fig. 3B). When we assessed the impact of LAMP2A overexpression on PARK7/DJ-1 abundance in the same cellular system, we found a significant increase(50±15%) of PARK7/DJ-1 protein levels compared to the control EGFP-treated cultures (Fig. 3C)"
SET Confidence = "High"
deg(p(HGNC:LAMP2)) cnc r(HGNC:PARK7)
deg(p(HGNC:LAMP2)) -> deg(p(HGNC:PARK7))
p(HGNC:LAMP2) -> p(HGNC:PARK7)
UNSET Confidence

SET Evidence = "PARK7/DJ-1 is primarily implicated in PD pathogenesis through mitochondrial destabilization and increased susceptibility to oxidative stress. We sought to investigate whether decreased abundance of this protein could sensitize neuronal cultures to the effects of mitochondrial toxin 1-methyl-4-phenylpyridinium (MPP+)(Langston et al. 1984; Javitch and Snyder 1984) following LAMP2A downregulation, and - as both we and others have shown primary cortical neurons to be sensitive to MPP+ toxicity (Xilouri et al. 2011; Wang et al. 2016; Rosa et al. 2017) ... , a significant, 34±12% reduction of survival in GFP+ MPP+-treated cultures post LAMP2A-shRNA transfection that was completely rescued by coexpression of WT PARK7/DJ-1 (Fig. 3F)."
SET Confidence = "Medium"
p(HGNC:PARK7) -- path(DOID:"Parkinson's disease")
p(HGNC:PARK7) -- bp(GO:"mitochondrial depolarization")
p(HGNC:PARK7) -> bp(GO:"cellular response to oxidative stress")
SET Anatomy = "cortex"
SET Cell = "primary neuron"
act(a(MESH:"1-Methyl-4-phenylpyridinium")) -> bp(GO:"cell death")
complex(p(HGNC:LAMP2), p(HGNC:PARK7)) -| bp(GO:"cell death")
complex(p(HGNC:LAMP2), p(HGNC:PARK7)) -| act(a(MESH:"1-Methyl-4-phenylpyridinium"))
UNSET Cell
UNSET Anatomy
UNSET Confidence

SET Evidence = "We biochemically verified the reduction of CRMP-2 protein levels (37±8%) upon LAMP2A downregulation (Fig. 4A), which was accompanied by no detectable changes in Dpysl2/Crmp2 mRNA levels (Fig. 4B)."
SET Confidence = "High"
deg(p(HGNC:LAMP2)) cnc r(HGNC:DPYSL2)
deg(p(HGNC:LAMP2)) -> deg(p(HGNC:DPYSL2))
UNSET Confidence

SET Evidence = "we also investigated the protein levels of CRMP-2 following AV-mediated LAMP2A-overexpression and observed a significant increase of 62±17% relative to that of the EGFP-transduced control cultures (Fig. 4C)"
SET Confidence = "High"
p(HGNC:LAMP2) -> p(HGNC:DPYSL2)
UNSET Confidence

SET Evidence = " Indeed, DIV0 LAMP2A downregulation evoked a significant, 38±13% reduction of MAP2 protein levels (Fig. 4E) which coincided with a significant reduction of CRMP-2 protein levels (32±14%), comparable to what we had already seen in mature cultures. No change in TUJ-1 abundance was detected (Fig. 4F), suggesting that there was a specific effect on dendritic, and not axonal, projections."
SET Confidence = "Medium"
p(HGNC:LAMP2) -> p(HGNC:MAP2)
p(HGNC:LAMP2) -> p(HGNC:DPYSL2)
p(HGNC:LAMP2) cnc p(HGNC:TUBB3)
act(p(HGNC:LAMP2)) reg a(GO:dendrite)
#act(p(HGNC:LAMP2)) cnc a(GO:axon)
SET CellStructure = "Axons"
act(p(HGNC:LAMP2)) cnc bp(GO:"neuron projection development")
UNSET CellStructure
UNSET Confidence

SET Evidence = "We have recently established in vivo knockdown of LAMP2A in the rat dopaminergic system as a viable model of PD-like neurodegeneration in the adult rat substantia nigra ... Four (4) weeks post
transduction, we could observe a significant, 67±3% reduction of CRMP-2
immunoreactivity within GFP+ shRNA-LAMP2A (L2)-transduced ventral midbrain
neurons compared to the GFP+ shRNA-scr controls (Fig. 5A)."
SET Confidence = "High"
SET Species = "10116"
SET Anatomy = "dopaminergic system"
p(HGNC:LAMP2) -> p(HGNC:DPYSL2)
UNSET Species
UNSET Anatomy
UNSET Confidence
